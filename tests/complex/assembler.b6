шифр 419999 зс5^
лен 41(2048)^
лен 50(1001-ЗП)^
eeв1а3
*name МКБ-ассемблер
*call yesmemory
*     no list
*     system
*     full list
*
*     17/02/88  21.48.53    МКБ-АССЕМБЛЕР: ВЕРСИЯ 1.00.
*     15/03/89  ПРАВКИ ДЛЯ ПЕЧАТИ ЧАСТОТЫ УПОТР.КОМАНД
*     17/05/89  ВВЕДЕНЫ КОМАНДЫ ВТОРОЙ СТРУКТУРЫ
*      9/08/89  ХЕШИРОВАНИЕ ПРИ ПОИСКЕ  К О П  ИЛИ МАКРО
*     26/10/89  БИТЫ РЕГИСТРА РЕЖИМА МОЖНО ЗАДАВАТЬ СПИСКОМ ИМЕН
*
*ftn
      SUBROUTINE MACGEN(IPAR)
      COMMON /NUMKOP/ INDEX
      COMMON /НТАВ/ КОРС(2)
      COMMON /STRING/ КАР(14),SCAL,СН(8),MIR,
     *  NUM(4),LАВ(2),КОР,MADR(12),КС(8)
      DАТА МАХ/500/, МАХ1/100/, IFILE/310000В/,
     *  ILIB/600000В/, KMNEW/0/
      DIMENSION KBEG(14,3),МТ(14,500),MD(3,100)
      DАТА KBEG/'*NО LOAD LIST  ',11(' '),
     *      '*MAIN МКВ/ASS',11(' '),
     *      '*EXECUTE',12(' ') /
      DIMENSION MSTAT(12),IDENT(2)
      DАТА ККОМ/0/, MSTAT/
     * 'ОПР.МАКРО - 1     ВЫЗ.МАКРО - 1      КОМАНД -   1        ',
     * ' ',' '/
С---------------------------------------------
С  КМ=ЧИСЛО МАКРОСОВ,     МТ=ТЕКСТЫ МАКРОСОВ
С  JМТ=ТЕК.ИНДЕКС ПО МТ,  NСМ=НОМЕР ВЫЗОВА
С  MD=ДЕСКРИПТОР: 1-ИМЯ, 2-НАЧ.В МТ, 3-ДЛИНА
С  КОРС=<Н-ТАБЛ.КОП>: 1-ИМЯ, 2-ХАРАКТЕРИСТИКА:
С   25-36:НОМЕР МАКРО ИЛИ 4095(КОМ) ИЛИ 0(ПСЕВ)
С    1-24:ЧАСТОТА ПОЯВЛЕНИЯ
С---------------------------------------------
      LISTF=0 $ IF(IPAR.NЕ.' ') LISTF=4
      CALL DEFINP('NEW',IFILE)
      CALL WBEGIN(IFILE)
      DО 1 J=1,3
  1   CALL WRCARD(KBEG(1,J))
      DО 1111 I=1,1000 $ LЕТ=0
      IDENT(1)=КОРС(2*I-1) $ IDENT(2)=КОРС(2*I)
      IF(IDENT(1).EQ.0В) GОТО 1111
      IF(IDENT(1).EQ.' <END>') GОТО 1112
      LЕТ=IDENT(2).SHIFT.47.OR.0
      IF(LЕТ.NЕ.0) LЕТ=4095
 1111 КОРС(2*I)=LЕТ.SHIFT.-24.OR.0
 1112 NСМ=0 $ JОВ=1 $ JМТ=1
С         СТАРЫЕ МАКРОСЫ - В ТАБЛИЦУ
      CALL EXLIB(ILIB) $ CALL ELMOVE(' MACR ')
      CALL ELREAD(КМ,1) $ IF(КМ.EQ.0) GОТО 2
      CALL ELREAD(JМТ,1) $CALL ELREAD(МТ,14*JМТ)
      CALL ELREAD(MD,3*КМ) $ DО 1113 I=1,КМ
      IDENT(1)=MD(1,I) $ IDENT(2)=I.SHIFT.-24.OR.0
 1113 J=KHPUT(IDENT)
С
С...   ЧТЕНИЕ ВХОДНОЙ СТРОКИ, ЕЕ РАСПОЗНАВАНИЕ
С
  2   MSKIP = -1
  10  IF(МС(1).EQ.'*END F') GОТО 999
      READ 11,КАР
  11  FORMAT(13А6,А2)
      IF(КАР.EQ.'*SKIP'.AND.IPAR.EQ.':NOSKI') GОТО 10
      IF(КАР.EQ.'*SKIP') MSKIP=MSKIP+1
      IF(MSKIP) 12,10,2
С...   НА ОЧНУЮ СТАВКУ К ТОВ.ДАВЫДОВУ
  12  CALL SYN
      MER=SCAL.SHIFT.47.OR.0
      МСОМ=SCAL.SHIFT.38.AND.1.OR.0
      IF(JОВ.EQ.2) GОТО 200
      IF(МСОМ.NЕ.0) GОТО 9
      IF(КОР.NЕ.'MACRO') GОТО 100
С...   МАКРООПРЕДЕЛЕНИЕ. ПРОБА НА ПОВТОР.
      IDENT = LАВ
      KMNEW=KMNEW+1 $ IDENT(2)=7777 0000 0000В
      INDEX=KHGET(IDENT) $ NМАС=IDENT(2).SHIFT.24.AND.7777В.OR.0
      IF(INDEX.NЕ.0.AND.NМАС.LЕ.МАХ1) GОТО 15
С       НОВОЕ МАКРО ВНОСИМ В ТАБЛИЦУ
  14  КМ=КМ+1 $ NМАС=КМ
      IDENT(2)=NМАС.SHIFT.-24.OR.0
      INDEX=KHPUT(IDENT)
      IF(КМ.GТ.МАХ1) PRINT 1000
      IF(КМ.GТ.МАХ1) SТОР
 1000 FORMAT(' СЛИШКОМ МНОГО МАКРО.')
      IF(KMNEW.EQ.1.AND.LISTF.NЕ.0) CALL DATAPR
  15  MD(1,NМАС)=LАВ $ MD(2,NМАС)=JМТ
      MD(3,NМАС)=0 $ JОВ=2 $ GОТО 220
С...   ПРОБА КОНЦА МАКРООПРЕДЕЛЕНИЯ
 200  IF(КОР.EQ.'ENDM') JОВ=1
      IF(КОР.EQ.'ENDM') GОТО 220
С...   ПРИЕМ ОЧЕРЕДНОЙ СТРОКИ МАКРООПРЕДЕЛЕНИЯ
      IF(JМТ.GТ.МАХ) PRINT 1000
      IF(JМТ.GТ.МАХ) SТОР
      IF(МСОМ.NЕ.0) GОТО 210
      ASSIGN 204 ТО JLAB $ GОТО 300
С...   МОДИФИКАТОР, КОП, АДРЕС, КОММЕНТАРИЙ
 204  КАР(3)=MIR $ КАР(4)=КОР
      DО 206 I=1,8 $ LЕТ=MADR(I)
 206  КАР(I+4)=LЕТ
С...   НУМЕРАЦИЯ ТЕЛА МАКРО
 210  КАР(13)=MD(1,NМАС)
      MD(3,NМАС)=MD(3,NМАС)+1
      NSTR=MD(3,NМАС)
      IF(NSTR.GТ.99) NSTR=0
      ENCODE(6,1001,КАР(14))  NSTR
 1001 FORMAT(I2,'    ')
      DО 211 I=1,14
 211  МТ(I,JМТ)=КАР(I)
      JМТ=JМТ+1
      IF(МСОМ.EQ.0) GОТО 220
С...   ";/..." - ВНУТРЕННИЙ КОММЕНТАРИЙ
      IF(IGETS(КАР,1).NЕ.47) GОТО 220
      JМТ=JМТ-1 $ MD(3,NМАС)=MD(3,NМАС)-1
 220  IF(LISTF.NЕ.0) CALL PRNTS(КАР)
      GОТО 10
С
С...   ЗАПИСЬ СТРОКИ-РЕЗУЛЬТАТА
С
  9   CALL WRCARD(КАР) $ GОТО 2
С
С...   МАКРОВЫЗОВ ИЛИ ОБЫЧНАЯ КОМАНДА
С
 100  IF(INDEX.EQ.0) GОТО 9
      КОРС(INDEX+1)=КОРС(INDEX+1)+1
      NМАС=КОРС(1+INDEX).SHIFT.24.AND.7777В.OR.0
      ККОМ=ККОМ+1 $ IF(NМАС.EQ.4095) GОТО 9
      IF(NМАС.LТ.1.OR.NМАС.GТ.МАХ1) GОТО 9
      NСМ=NСМ+1 $ ККОМ=ККОМ-1
      КАР(1)=';  МАС' $ КАР(2)='RО>'
      КАР(3)=LАВ(1) $ КАР(5)=КОР
      КАР(4)=(LАВ(2).AND.7777 7777 0000 0000В).OR.(MIR.AND.7777 7777В)
      DО 190 I=6,12
 190  КАР(I)=MADR(I-5)
      КАР(13)=' ' $ КАР(14)=' '
      DО 191 I=12,72
      LЕТ=IGETS(КАР,I)
      IF(LЕТ.LТ.32) LЕТ=32
 191  CALL IPUTS(LЕТ,КАР,I-1)
      CALL WRCARD(КАР)
      IА=MD(2,NМАС) $ NSTR=MD(3,NМАС)
      LABEL='Ь0000Ь' $ CALL Ь(LABEL,NСМ)
      IF(LАВ(1).EQ.' '.AND.LАВ(2).EQ.'  :') LАВ=LABEL
С...   МЕТКУ СЛИВАЕМ ОТДЕЛЬНО
      ASSIGN 104 ТО JLAB
С---
 300  КАР(1)=LАВ(1) $ КАР(2)=LАВ(2)
      IF(КАР.EQ.' ') GОТО 303
      DО 301 I=1,12
 301  КАР(I)=' '
      DО 302 I=1,8 $ LЕТ=IGETS(LАВ,I-1)
      IF(LЕТ.EQ.32) GОТО 303
      CALL IPUTS(LЕТ,КАР,I-1)
 302  CALL IPUTS(58,КАР,I)
 303  GОТО JLAB,(204,104)
С---
 104  IF(КАР.NЕ.' ') CALL WRCARD(КАР)
      CALL MADREG(КОР,MADR)
 105  IF(NSTR.EQ.0) GОТО 10
С...    ПОИСК КОНЦА АДРЕСНОЙ ЧАСТИ
      DО 107 I=1,48 $ NS=I-1
      LЕТ=IGETS(MADR,NS)
      IF(LЕТ.LЕ.32.OR.LЕТ.EQ.59) GОТО 108
 107  CONTINUE
С...    ПОДСТАНОВКА ТЕЛА МАКРО
 108  IF(NS.EQ.0) MADR=' '
      IF(NS.EQ.0) NS=1
      DО 199 ISTR=1,NSTR
      DО 106 I=1,14
 106  КАР(I)=МТ(I,IА)
      IА=IА+1
      CALL Ь(КАР,NСМ)
      IF(КАР(3).EQ.'    / ') КАР(3)=MIR
      CALL Ь(КАР(5),NСМ)
      LЕТ=КАР(5).SHIFT.40
      IF(LЕТ.NЕ.57В) GОТО 199
С...   ЗАМЕНА "/" В АДРЕСНОЙ ЧАСТИ
      DО 109 I=1,8
      КС(I)=КАР(I+4)
 109  КАР(I+4)=MADR(I)
      DО 110 I=NS,47
 110  CALL IPUTS(IGETS(КС,I-NS+1),КАР(5),I)
 199  CALL WRCARD(КАР)
      GОТО 10
С...   КОНЕЦ ВХОДНОГО ФАЙЛА
 999  DО 998 I=1,14
 998  КАР(I)=' '
      КАР(1)='*END F'
      CALL WRCARD(КАР)
      КАР(1)='*READ'
      КАР(2)='OLD'
      CALL WRCARD(КАР)
      CALL WRIEND
      CALL JMPINP('NEW')
      IF(KMNEW.EQ.0) GОТО 900
      CALL ELMOVE(' MACR ')
      CALL ELWRIT(КМ,1) $ CALL ELWRIT(JМТ,1)
      CALL ELWRIT(МТ,14*JМТ) $ CALL ELWRIT(MD,3*КМ)
  900 CALL ELCLOS
      IF(LISTF.EQ.0) GОТО 909
      CALL PRNTS(КАР(3))
      CALL PUTG(MSTAT(3),КМ,0,6,0,IР)
      CALL PUTG(MSTAT(6),NСМ,0,6,0,IР)
      CALL PUTG(MSTAT(9),ККОМ,0,6,0,IР)
      CALL PRNTS(MSTAT)
      CALL PRNTS(КАР(3))
      N=0 $ J=-1
 901  J=J+2 $ LАВ=КОРС(J)
      IF(LАВ.EQ.0В) GОТО 901
      IF(LАВ.EQ.' <END>') GОТО 902
      IND=КОРС(J+1)
      I=IND.SHIFT.24.AND.7777В.OR.0
      IF(I.NЕ.0) IND=IND.OR.777 0000 0000В
      N=N+1 $ КОРС(2*N)=LАВ
      КОРС(2*N-1)=IND $ GОТО 901
 902  КОРС(2*N+1)=6401 0000 0000 0000В
      КОРС(2*N+2)=' КОМ=>'
      КОРС(2*N+3)=6400 7776 0000 0000В
      КОРС(2*N+4)=' МАК=>'
      КОРС(2*N+5)=6400 0001 0000 0000В
      КОРС(2*N+6)=' ПСЕ=>' $ N=N+3
      DО 906 I=1,6
      КОРС(2*N+1)=0 $ КОРС(2*N+2)=' '
  906 N=N+1
      CALL SHSORT(КОРС,N,2,0)
      IND=N+1
  904 DО 903 I=1,5 $ J=2*(IND-I)-1
      КОРС(J)=КОРС(J).AND.7777 7777В.OR.0
      CALL PUTG(КАР(2*I),КОРС(J),0,6,0,IР)
      IF(КОРС(J).EQ.0) КАР(2*I)=' '
  903 КАР(2*I-1)=КОРС(J+1)
      CALL PRNTS(КАР) $ IND=IND-5
      IF(IND.GТ.5) GОТО 904
      CALL PRNT81
  909 CALL EXIT
      END
      SUBROUTINE MADREG(КОР,MADR)
С*
С***   ОБРАБОТКА СПИСКА БИТОВ РЕЖИМА
С*     НАПРИМЕР:  SETR РОА+БПНП+БПИНТ...
С*
      DIMENSION MADR(12)
      INTEGER SCAL,LЕХ(10),RBNAM(20),LISCOP(4)
      DАТА NСОР/4/
      DАТА LISCOP/'SETR  CLRR  SMON  CMON  '/
      DАТА RBNAM/
     1  'ББЧ   БППУ  РД    БЧОП  БПТЧ  ',
     2  'БЧС   БПИНТ БВП   БПТЗ  ???   ',
     3  'РШ    ПОП   БЗЗ   БЗО   РОА   ',
     4  'БПНП  БП    ПИА   ППК   ППУ   '/
С-----------------------------------------
      DО 1 IС=1,NСОР $ JС=IС.AND.1
      IF(КОР.EQ.LISCOP(IС)) GОТО 2
   1  CONTINUE
      RETURN
   2  CALL SCANER(MADR,LЕХ,0,IТУР,KOD,IPOZ)
      SCAL=0 $ GОТО 4
   3  CALL SCANER(0,LЕХ,0,IТУР,KOD,IPOZ)
   4  IF(IТУР.NЕ.4) RETURN
      DО 5 I=1,20 $ J=I
      IF(LЕХ.EQ.RBNAM(I)) GОТО 6
   5  CONTINUE
      RETURN
   6  SCAL=1.SHIFT.(1-J).OR.SCAL
      IF(KOD.NЕ.32.AND.KOD.NЕ.59) GОТО 3
      IF(JС.EQ.0) SCAL=LEXOR(SCAL,7777 7777В)
      DО 7 I=2,12
   7  MADR(I)=' '
      MADR(1)='@'
      DО 8 I=1,5
      J=SCAL.SHIFT.16.AND.17В.OR.0
      IF(J.GТ.9) J=J+7
      CALL IPUTS(J+48,MADR,I)
   8  SCAL=SCAL.SHIFT.-4
      RETURN
      END
      SUBROUTINE PRICOD(LCOD)
      INTEGER IС(65)
      CALL ELMOVE(0)
      DО 10 I=1,LCOD,65
      J=65
      IF (LCOD.LТ.I+65) J=LCOD-I+1
      CALL ELREAD(IС,J)
      CALL PRNTO2(IС,J)
10    CONTINUE
      CALL TEST1
      RETURN
      END
      SUBROUTINE PRNTO2(IS,N)
      DIMENSION IS(N),IТ(11)
      COMMON // IF(16)
      L=LOR(N,0)
      ENCODE (63,11,IТ)
      CALL PRNTS(IТ)
11    FORMAT(10Х,'.   G  .  ',
     *'А В .С . D W .Н. Т. N К  IR КОП .АДРЕС.',4Х)
      DО 1 I=1,L
      CALL GETF(IS(I))
      ENCODE(63,10,IТ) IF
10    FORMAT(4(О4,1Х),О1,1Х,О1,1Х,О4,1Х,О1,1Х,О1,1Х,О3,1Х,
     *О2,1Х,О1,1Х,О1,2Х,О2,1Х,О3,1Х,О7,4Х)
      CALL PRNTS(IТ)
1     CONTINUE
      RETURN
      END
      SUBROUTINE SORCOP( MNEM, JХАР )
      DIMENSION MNEM(2),JХАР(2),КТАВ(3,512)
      DIMENSION MLET(2),КТ(2)
      EQUIVALENCE (КТ,КТАВ)
      DАТА МАХ/499/
      DАТА MLET/'CPRDSJM:'/
С--------
      N=0 $ DО 1 I=1,512
      IF(MNEM(I).EQ.0В) GОТО 2
      J=JХАР(I).SHIFT.47.OR.0
      IF(J.EQ.0) GОТО 1
      IF(N.LЕ.МАХ) N=N+1
      КТАВ(1,N)=MNEM(I)
      J=JХАР(I).SHIFT.20.AND.377В.OR.0
      CALL PUTO(КТАВ(2,N),J,0,3,0,IР)
      КТАВ(3,N)=' ' $ NS=0
      LЕТ=JХАР(I).SHIFT.39.AND.7.OR.0
      ASSIGN 3 ТО LRET
      IF(LЕТ.EQ.0) GОТО 3
      LЕТ=IGETS(' INU',LЕТ)
  9   CALL IPUTS(LЕТ,КТАВ(3,N),NS)
      IF(NS.LТ.5) NS=NS+1
      GОТО LRET,(3,4,5)
С---
  3   LЕТ=JХАР(I).SHIFT.36.AND.7.OR.0
      ASSIGN 4 ТО LRET $ IF(LЕТ.EQ.0) GОТО 4
      LЕТ=IGETS(' XHQBF',LЕТ) $ GОТО 9
С---
  4   J=JХАР(I).SHIFT.19.AND.1
      J=JХАР(I).SHIFT.31.AND.36В.OR.J
      J=JХАР(I).SHIFT.37.AND.340В.OR.J
      DО 5 JJ=1,12
      LЕТ=J.AND.1 $ IF(LЕТ.EQ.0В) GОТО 5
      LЕТ=IGETS(MLET,JJ-1)
      ASSIGN 5 ТО LRET $ GОТО 9
  5   J=J.SHIFT.1
  1   CONTINUE
С---
  2   N3 = 3*N
      DО 20 JТАВ=1,2 $ CALL SHSORT(КТ,N,3,0)
      MER=' ' $ IF(N.GТ.МАХ) MER='МНОГО!'
      DО 21 I=2,N
      IF(КТАВ(1,I-1).NЕ.КТАВ(1,I)) GОТО 21
      MER='БДИ !!'
      КТАВ(1,I)=КТАВ(1,I).OR.(53В.SHIFT.-8)
  21  CONTINUE
      PRINT 101,N,MER
 101  FORMAT(1Н1,'  В С Е Г О   ',I4,'   КОМАНД.    ',А6)
      PRINT 102,(КТ(I),I=1,N3)
 102  FORMAT(99(/6(4Х,А6,А5,А6)))
      DО 22 I=1,N
      J=КТАВ(1,I) $ КТАВ(1,I)=КТАВ(2,I)
  22  КТАВ(2,I)=J.AND.7777 7777 7776 0377В
  20  CONTINUE
      RETURN
      END
      SUBROUTINE TEST1
      COMMON /TENTRY/ W1(2),TENTRY(128) /EXTERN/ W2,ЕХТ(1022)
      COMMON /TDEF/ TDEF(1040),TDEF1(1025),TDM(15),TDEF2(1024)
      COMMON /TSS/ TSS(1536)
      COMMON /SUBCH/ NSTR,ATENTR,АЕХТ,ADEF,AVAR,ACONST,АСОМ,ABDEF
     *,ABLIT,LSUBP,ATSS
      COMMON /STRING/ ISTR(18),NLINE,NERR,CHAR,DEF1,DEF2,TIR,NUMBER(4)
     *,ТМТ(2),ТСОР,TADS(12),IDERR
      INTEGER TENTRY,ЕХТ,TDEF,TDEF1,TDEF2,TSS,TDM
     *,ATENTR,АЕХТ,ADEF,АСОМ,ABDEF,ABLIT,ATSS
     *,CHAR,DEF1,DEF2,TIR,ТМТ,ТСОР,TADS
     *,SUBCH,AVAR,ACONST
      DIMENSION IS(6),SUBCH(11),IТРС(30)
      EQUIVALENCE (NSTR,SUBCH)
      EQUIVALENCE (TIR,IТРС)
С
С   IFIRST=1 - НЕ ПЕЧАТАТЬ ПРОМЕЖ. КОД
С
      IFIRST=1
      PRINT 900
900   FORMAT(28Х,'TENTRY')
      DО 10 I=1,121,4
      IF(TENTRY(I).EQ.0В)GОТО 15
      II=I+3
      К=1
      DО 12 J=I,II,2
      CALL TEXISO(TENTRY(J),IS(К))
      IS(К+2)=TENTRY(J+1)
12    К=К+3
      PRINT 800,IS
800   FORMAT(2(3Х,А6,А2,1Х,О16))
10    CONTINUE
15    CONTINUE
      PRINT 901
901   FORMAT(28Х,'Т ЕХТ')
      DО 20 I=1,121,4
      IF(ЕХТ(I).EQ.0В)GОТО 25
      II=I+3
      К=1
      DО 22 J=I,II,2
      CALL TEXISO(ЕХТ(J),IS(К))
      IS(К+2)=ЕХТ(J+1)
22    К=К+3
      PRINT 800,IS
20    CONTINUE
25    CONTINUE
С
С     ПЕЧАТЬ ТАБЛИЦЫ ОПИСАНИЙ
С
      PRINT 902
902   FORMAT(21Х,'ТАБЛИЦА  ОПИСАНИЙ')
      DО 30 I=1,16
      IF (TDM(I).EQ.0В) GОТО 30
      PRINT 801,I,TDM(I)
801   FORMAT(10Х,'IR',I2,2Х,О16)
30    CONTINUE
      DО 40 I=2,512
      I0=I-1
      IF(TDEF(I).EQ.0В)GОТО 50
      CALL TEXISO(TDEF(I),IS(1))
      IS(3)=TDEF1(I)
      IS(4)=TDEF2(I)
      PRINT 802,I0,(IS(J),J=1,4)
802   FORMAT(I5,3Х,А6,А2,2Х,2О17)
40    CONTINUE
50    CONTINUE
С
С     ПЕЧАТЬ ТАБЛИЦЫ ССЫЛОК
С
      PRINT 905
905   FORMAT(23Х,'ТАБЛИЦА ССЫЛОК')
      JJ=LOR(ATSS,0)
      DО 55 I=1,JJ,2
      I0=I-1
      I1=I+1
      PRINT 805,I0,TSS(I),I,TSS(I1)
805   FORMAT(2(3Х,О4,': ',О16))
55    CONTINUE
60    CONTINUE
      PRINT 806,(SUBCH(I),I=1,9)
806   FORMAT(' NSTR   AENTR  АЕХТ   ADEF   AVAR   ACONST АСОМ   ABDEF ',
     *' ABLIT'/9(1Х,О6))
      PRINT 807, (SUBCH(I),I=10,11)
807   FORMAT(' LSUBP  ATSS'/2(1Х,О6))
      IF(IFIRST.EQ.1)GОТО 120
      IFIRST=1
      PRINT 809
809   FORMAT(20Х,'ПРОМЕЖУТОЧНЫЙ КОД')
      J=LOR(NSTR,0)
      DО 110 JJ=1,J
      IFE=IРС(0)
      DО 101 I=1,30
      IF(IТРС(I).NЕ.0В) GОТО 101
      IТРС(I)=1Н
101   CONTINUE
      IF(NERR.EQ.0В)GОТО 102
      PRINT 810,NERR
810   FORMAT(' ERROR ',О3)
102   CONTINUE
      IF(ТСОР.NЕ.1Н .OR.ТМТ(1).NЕ.1Н )GОТО 105
      PRINT 811,TADS
811   FORMAT(' ;',12А6)
      GОТО 107
105   CONTINUE
      PRINT 812,NLINE,ТМТ,TIR,ТСОР,(TADS(I),I=1,6),
     *CHAR,DEF1,DEF2
812   FORMAT(1Х,О5,3Х,А6,А3,А3,А6,6А6/1Х,3О17)
107   IF(IFE.EQ.0В)GОТО 120
110   CONTINUE
      PRINT 820
820   FORMAT(' ИСЧЕРПАН СЧЕТЧИК СТРОК!')
120   CONTINUE
      RETURN
      END
      SUBROUTINE TSTSYN
      COMMON /STRING/ ISTR(14),ISCH,LADS,LСОММ
     *,ISCW,NLINE,NERR(4),ITIR,ISRV(4),IТМТ(2),
     *IТСОР,ITADS(12),IТСОММ(12)
      DIMENSION IМТ(3,10),IТРС(30)
      EQUIVALENCE (ITIR,IТРС)
      DАТА(IМТ=
     * 18HERROR BIT SСН
     *,18НПРИНЯТЬ МЕТКУ
     *,18НВ МЕТКЕ АДРЕС
     *,18НП О М Е Ч Е Н О
     *,18НЕСТЬ МОДИФИКАТОР
     *,18Н3-СИМВОЛЬНЫЙ КОП
     *,18НВ КОП 4-5 СИМВ.
     *,18НОБРАБОТАТЬ ЛИТЕРАЛ
     *,18НАДРЕСНОЕ ВЫРАЖЕНИЕ
     *,18НВСЕ - КОММЕНТАРИЙ
     *)
10    CONTINUE
      PRINT 99
      WRITE (54,99)
99    FORMAT(' .....')
      READ(54,100)ISTR
100   FORMAT(13А6,А2)
      PRINT 101,ISTR
101   FORMAT(1Х,13А6,А2)
      IF(ISTR(1).EQ.3HEND)GОТО 77
      IF(ISYNT(0).NЕ.0В) GОТО 12
      PRINT 102
      WRITE(54,102)
102   FORMAT(' ERROR!')
12    CONTINUE
      DО 13 I=1,30
      IF(IТРС(I).EQ.0В)IТРС(I)=6Н
13    CONTINUE
      LADS=LOR(LADS,0)
      LСОММ=LOR(LСОММ,0)
      PRINT 105,IТМТ,ITIR,IТСОР,LADS,LСОММ
      WRITE (54,105) IТМТ,ITIR,IТСОР,LADS,LСОММ
      IF (LADS.EQ.0) GОТО 16
      PRINT 106,(ITADS(I),I=1,LADS)
      WRITE (54,106)(ITADS(I),I=1,LADS)
16    IF(LСОММ.EQ.0) GОТО 17
      PRINT 107,(IТСОММ(I),I=1,LСОММ)
      WRITE (54,107)(IТСОММ(I),I=1,LСОММ)
105   FORMAT(' МТ:',2А6,'IR:',А6,'КОП:',А6,'  LADS:',I2,'  LСОММ:',I2)
106   FORMAT('  АДР.:',12А6)
107   FORMAT(' КОММ.:',12А6)
17    IF(LSHIFT(ISCH,-10).EQ.0В) GОТО 14
      PRINT 104,ISCH
      WRITE (54,104) ISCH
104   FORMAT(' SСН: ',О16)
14    CONTINUE
      DО 20 I=1,10
      IF(LAND(ISCH,LSHIFT(4000 0000 0000 0000В,I-1)).EQ.0В)GОТО 20
      PRINT 101,(IМТ(J,I),J=1,3)
      WRITE(54,100) (IМТ(J,I),J=1,3)
20    CONTINUE
      GОТО 10
77    WRITE (54,78)
78    FORMAT(' ДО СВИДАНИЯ!')
      SТОР
      END
*full list
*MADLEN:L=0.
 ****:,NАМЕ,
 MKBSTX:,SТЕХТ,МКВ-ASSEMBLERS SТЕХТ
С
С      О П И С А Н И Я
С
 BITS:,MACRO,
 ****:LС,BLOCK,BITS
 ,CONT,В48,В47,В46,В45,В44,В43
 ,CONT,В42,В41,В40,В39,В38,В37
 ,CONT,В36,В35,В34,В33,В32,В31
 ,CONT,В30,В29,В28,В27,В26,В25
 ,CONT,В24,В23,В22,В21,В20,В19
 ,CONT,В18,В17,В16,В15,В14,В13
 ,CONT,В12,В11,В10,В9,В8,В7
 ,CONT,В6,В5,В4,В3,В2,В1
 ,CONT,FULWOR
 ,CONT,CIR,СКОР,CADR(0),В20/01
 ,CONT,CIRCOP(0),В32/21
 ,CONT,В48/25,В36/01
 ,CONT,В32/01
 ,CONT,В48/37
 ,CONT,В29/21,В44/33(0),MTDEF1
 ,CONT,MATD1,В16/01,В41/01
 ,CONT,В32/20,В24/01
 ,CONT,С3,С5,С6,С7,С17
 ,CONT,С37,С77,С377
 ,CONT,С777,С7777,С77777
 ,CONT,USPACE  .КОД ПОДЧЕРКА
 BITS:,ENDM,
С
С
С
С
С
С.....    МАКРООПРЕДЕЛЕНИЯ
С
 WRITM:,MACRO,В,L
 ,ХТА,L
 ,ARX,LCOD
 ,АТХ,LCOD
 ,NTR,6
С  14,VТМ,В
С  ,ITS,14
С  14,VТМ,L
С  ,ITS,14
С  ,CALL,PRNTO2
 14,VТМ,В
 ,ITS,14
 14,VТМ,L
 ,ITS,14
 ,CALL,ELWRIT
 ,NTR,7
  WRITM:,ENDM,
 CALLS:,MACRO,N,Р
 ,IRP,
 13,VТМ,Р
 ,ITS,13
 ,IRP,
 ,CALL,N
 ,NTR,7
 CALLS:,ENDM,
 IDENT:,MACRO,Т
 14,VТМ,Т
 ,CALL,TRIDENT
 IDENT:,ENDM,
 GIVH:,MACRO,
 ,AUX,=6Н'17''17''17''17''17''17'
 15,АТХ,
 ,ARX,=6Н'166''166''166''166''166''166'
 ,ААХ,=6Н'160''160''160''160''160''160'
 ,ASN,64+4
 ,АЕХ,=6Н'67''67''67''67''67''67'
 15,ARX,
 GIVH:,ENDM,
 DEFLC:,MACRO,
 *STRING*:LС,BLOCK,STR(14).ВХОДНАЯ СТРОКА
 ,CONT,SСН     .СИНТ. Х-КА
 ,CONT,LADS    .ДЛИНА АДР.ЧАСТИ В СЛОВАХ
 ,CONT,LCOMMENT.ДЛИНА КОММЕНТАРИЯ В СЛОВАХ
 ,CONT,SCWORDS .ШКАЛА СЛОВ ПРОМЕЖУТ. КОДА
 ,CONT,BEGPC(0).НАЧАЛО ПРОМЕЖУТ. КОДА
 ,CONT,NLINE   .НОМЕР ТЕК.СТРОКИ ДЛЯ ПЕЧАТИ
 ,CONT,NERR    .НОМЕР ОШИБКИ
 ,CONT,CHAR    .Х-КА МНЕМОКОДА
 ,CONT,DEF1    .ОПИСАНИЕ МЕТКИ
 ,CONT,DEF2    .ОПИСАНИЕ АДРЕСА
 ,CONT,TIR     .IR (ISO)
 ,CONT,NUMBER(4).НОМЕР СТРОКИ
 ,CONT,ТМТ(2)  .МЕТКА + IR (ISO)
 ,CONT,ТСОР    .КОП (ISO)
 ,CONT,TADS(0) .АДР.ЧАСТЬ +
 ,CONT,TCOMMENT(12).КОММЕНТАРИЙ
 ,CONT,ERRIDENT.ОШ. ИДЕНТИФ.
 ,CONT,ENDPC(0).КОНЕЦ ПРОМЕЖУТ. КОДА
 *SUBCH*:LС,BLOCK,NSTR    .НОМЕР П/К
 ,CONT,ATENTRY,ATEXTERN
 ,CONT,ADEF    .АДР. ПО "ЧИСЛАМ"
 ,CONT,AVAR    .АДР. ПО ПЕРЕМ.
 ,CONT,ACONST  .АДР. ПО КОНСТ.
 ,CONT,АСОМ    .АДР. ПО КОМАНДАМ
 ,CONT,ABDEF   .НАЧ. ОПИСАНИЙ
 ,CONT,ABLIT   .НАЧ. ЛИТЕРАЛОВ
 ,CONT,LSUBP   .ДЛИНА П/П
 ,CONT,ATSS    .СВОБ. АДРЕС В TSS
 ,CONT,TYPDEF  .ТИП ЧИСЕЛ:     0=VAR; 1=CONST
 ,CONT,TYPSS   .ДЛЯ TSS
 ,CONT,TYPTD   .ДЛЯ TDEF1
 ,CONT,NLI     .N СТРОКИ ПЕЧ.
 ,CONT,LLIT    .К-ВО ЛИТЕРАЛОВ
 ,CONT,LARGE   .СТ.РАЗРЯДЫ
 ,CONT,ADEFLD  .АДР. ПО EXTD
 ,CONT,WDEFLD  .РАБ.ЯЧ В EXTD
 ,CONT,LCOD    .СЧ.СЛОВ ТЕЛА П/П
 ,CONT,ERRORS  .БЫЛИ ОШИБКИ
 ,CONT,LISTFL  .LIST/NО LIST
 ,CONT,JTD3    .УК. ПО TDEF3
 ,CONT,F/TWO   .ВТОРОЙ ПРОХОД
 *TDEF*:LС,BLOCK,TDEF(1040).ИМЕНА
 ,CONT,TDEF1(1040).ОПИСАНИЕ-1
 ,CONT,TDEF2(1024).ОПИСАНИЕ-2
 *PARAM:LС,BLOCK,СТАВ.КОД ПОСЛЕДНЕГО СИМВОЛА
 DEFLC:,ENDM,
 *1:,BLOCK,М1,М2,М3,М4,М5,М6,М7
 ,CONT,М8,М9,МА,МВ,МС,MD
 LTENTRY:,EQU,130.64*2+2
 LTEXTERN:,EQU,1023.511*2+1
 LTSS:,EQU,3000В.4608/3
 LTDEF:,EQU,6040В.1024*3+32
 LSTRI*:,EQU,70В
 LSUBCH:,EQU,30В
С
С
С.....      КОНСТАНТЫ
С
 ,ОСТ,
 ,ОСТ,4
 ,ОСТ,2
 ,ОСТ,1
 ,ОСТ,04
 ,ОСТ,02
 ,ОСТ,01
 ,ОСТ,004
 ,ОСТ,002
 ,ОСТ,001
 ,ОСТ,0004
 ,ОСТ,0002
 ,ОСТ,0001
 ,ОСТ,0000 4
 ,ОСТ,0000 2
 ,ОСТ,0000 1
 ,ОСТ,0000 04
 ,ОСТ,0000 02
 ,ОСТ,0000 01
 ,ОСТ,0000 004
 ,ОСТ,0000 002
 ,ОСТ,0000 001
 ,ОСТ,0000 0004
 ,ОСТ,0000 0002
 ,ОСТ,0000 0001
 ,LOG,4000 0000
 ,LOG,2000 0000
 ,LOG,1000 0000
 ,LOG,400 0000
 ,LOG,200 0000
 ,LOG,100 0000
 ,LOG,40 0000
 ,LOG,20 0000
 ,LOG,10 0000
 ,LOG,4 0000
 ,LOG,2 0000
 ,LOG,1 0000
 ,LOG,4000
 ,LOG,2000
 ,LOG,1000
 ,LOG,400
 ,LOG,200
 ,LOG,100
 ,LOG,40
 ,LOG,20
 ,LOG,10
 ,LOG,4
 ,LOG,2
 ,LOG,1
 ,LOG,7777 7777 7777 7777
 ,LOG,360 0000 0000
 ,LOG,17 7400 0000
 ,LOG,377 7777
 ,LOG,377 7400 0000
 ,ОСТ,7777 7777
 ,LOG,7777 7777 7777
 ,LOG,377 7777 7777
 ,ОСТ,7777
 ,ОСТ,0000 0037 74
 ,ОСТ,0377 74
 ,LOG,7400 0377 7777 7777
 ,LOG,17 7777
 ,LOG,0037 7777 7777 7777
 ,LOG,377 7600 0000
 ,LOG,7777 7777
 ,LOG,3
 ,LOG,5
 ,LOG,6
 ,LOG,7
 ,LOG,17
 ,LOG,37
 ,LOG,77
 ,LOG,377
 ,LOG,777
 ,LOG,7777
 ,LOG,77777
 ,ОСТ,01
 ,END,****

*MADLEN:L=0,S=MKBSTX.                                                  J
  *НТАВ*:,NАМЕ,
  :,BSS,1022
  :,ISO,6Н <END>
  :,LOG,
  ,END,

 *STRUCT:,NАМЕ,
С*
С*               СТРУКТУРА
С*     ТАБЛИЦЫ ОПИСАНИЙ ТРАНСЛЯТОРА
С*
С
С        ТРИ СЛОВА НА ИДЕНТИФИКАТОР
С
С
С         П Е Р В О Е   С Л О В О
С
С 48:01 - ИДЕНТИФИКАТОР В КОДЕ ТЕХТ
С
С         В Т О Р О Е   С Л О В О
С
С 48:45 - ТИП АДРЕСА:
С         0 - АБСОЛЮТНЫЙ АДРЕС
С         1 - СМЕЩЕНИЕ В ОБЛАСТИ КОМАНД
С         2 - СМЕЩЕНИЕ В ОБЛАСТИ ПЕРЕМЕННЫХ
С         3 - ССЫЛКА ПО EXTD - ВНЕШНЕЙ  ТАБ-
С             ЛИЦЕ ОПИСАНИЙ.
С         4 - СМЕЩЕНИЕ В ОБЛАСТИ КОНСТАНТ
С         5 - ПРОСТАЯ  ВНЕШНЯЯ   ЭКВИВАЛЕНТ-
С             НОСТЬ
С         6 - ПРОСТАЯ ВНУТРЕННЯЯ ЭКВИВАЛЕНТ-
С             НОСТЬ
С         7 - ЛИТЕРАЛ
С         8 - ЭКВИВАЛЕНТНОСТЬ,   НЕ  ДАЮЩАЯ
С             ЗНАЧЕНИЕ НА ПЕРВОМ ПРОХОДЕ.
С         9 - НЕОПРЕДЕЛЕННЫЙ ИДЕНТИФИКАТОР
С   44  - 'ENTRY'
С   43  - 'IR'
С   42  - 'МОЖНО ПЕРЕОПРЕДЕЛЯТЬ'
С   41  - 'НЕОПРЕДЕЛЕН'
С 40:33 - РЕЗЕРВ
С 32:01 - ПОЛЕ АДРЕСА :
С
С    ДЛЯ ПРОСТЫХ ВНЕШНИХ ЭКВИВАЛЕНТНОСТЕЙ:
С 32:30 - РЕЗЕРВ
С 29:21 - НОМЕР ОБ'ЕКТА В TEXTERN
С 20:01 - СМЕЩЕНИЕ В ОБ'ЕКТЕ
С
С    ДЛЯ ПРОСТЫХ ВНУТРЕННИХ ЭКВИВАЛЕНТНОС-
С    ТЕЙ:
С 32:21 - АДРЕС ПО TDEF НАЧАЛА БЛОКА
С 20:01 - СМЕЩЕНИЕ ОТ НАЧАЛА
С
С    ДЛЯ ЛИТЕРАЛА:
С 32:21 - РЕЗЕРВ
С 20:01 - СМЕЩЕНИЕ ПО TLT
С
С
С        Т Р Е Т Ь Е   С Л О В О
С
С 48:37 - УКАЗАТЕЛЬ НА КОНЕЦ СПИСКА ССЫЛОК
С 36:25 - УКАЗАТЕЛЬ НА НАЧАЛО СПИСКА ССЫЛОК
С 25:13 - УК. НА НАЧАЛО ЛОКАЛЬНОГО СПИСКА
С 12:01 - УК. НА КОНЕЦ ЛОКАЛЬНОГО СПИСКА
С
С*
С*      СТРУКТУРА  ТАБЛИЦЫ  ССЫЛОК
С*
С   48  - =1: ЗАПОЛНЕНО
С 47:46 - ВСЕГДА =0
С 45:31 - ЭЛЕМЕНТ СПИСКА ССЫЛОК
С 30:16 - ЭЛЕМЕНТ СПИСКА ССЫЛОК
С 15:01 - ССЫЛКА НА ПРОДОЛЖЕНИЕ СПИСКА
 ,END,

 CHEXIT:,NАМЕ,
 MONCARD*:,LС,1
 МКВ/ASS:,SUBP,
 EXIT:,SUBP,
 SТОР*:,SUBP,
 PRINT8:,SUBP,
  *ERRORS*:,LС,1
С--------
 ,UТС,MONCARD*
 ,ХТА,
 ,ASN,64+40
 ,ATI,14
 14,UТМ,-52В
 14,V1М,МКВ/ASS
 EXITTO:,ХТА,*ERRORS*
  ,UZA,EXIT
  14,VТМ,ТЕХТ
  ,ITS,14
  14,UТМ,4
  ,ITS,14
  14,VТМ,4
  ,ITS,14
  13,VТМ,SТОР*
  ,UJ,PRINT8
  ТЕХТ:,ISO,30НБЫЛИ ОШИБКИ ПРИ ТРАНСЛЯЦИИ !!!
 ,END,

 СНК*ADR:,NАМЕ,
 *MNCHAR*:,SUBP,
С      ПРОВЕРКА НА ДЛИННЫЙ АДРЕС
С      И14=КОП,  АСС=АДРЕС
  8,BASE,*
  ,ААХ,=377 7600 0000
  13,UZA,.ПОЛОЖ.АДР.
  ,АТХ,А
  ,ААХ,=377 7400 0000
  13,UZA,.НЕРАЗМН.ЗНАК
  ,UТС,*MNCHAR*
  14,ХТА,
  ,ASN,64+36
  ,ААХ,=7
  ,ATI,14
  ,ХТА,А
  ,ААХ,=200 0000
  :,UZA,СНР
  ,ХТА,А
  14,ААХ,S
  14,АЕХ,S
  13,UJ,
  СНР:,ХТА,А
  14,ААХ,S
  13,UJ,
  S:,LOG,
  :,LOG,377 7400 0000.W
  :,LOG,000 0400 0000.Н
  :,LOG,000 1400 0000.Q
  :,LOG,000 3400 0000.В
  :,LOG,003 7400 0000.F
  :,LOG,
  :,LOG,
  А:,BSS,1
  ,END,

 DEFINER:1,NАМЕ,***
С*
С*     ДООПРЕДЕЛЕНИЕ ИДЕНТИФИКАТОРОВ.
С*
С*     ПРИ ОБРАЩЕНИИ  АСС  СОДЕРЖИТ  СЛОВО
С*   ИЗ  TDEF, ПОДЛЕЖАЩЕЕ ДООПРЕДЕЛЕНИЮ.
С*
С*
С*     ОТВЕТ:
С*   I14.EQ.0 - ХОРОШО, АСС СОДЕРЖИТ ГОТОВОЕ
С*     СЛОВО ДЛЯ TDEF1.
С*
С*   I14.NЕ.0 - ERROR, В I14 НАХОДИТСЯ НОМЕР
С*     ОШИБКИ.
С*
С*   ЕСЛИ ССЫЛКА НА TDEF1 ДАЕТ НОВУЮ ССЫЛКУ,
С* DEFINER РЕКУРСИВНО ОБРАЩАЕТСЯ САМ К СЕБЕ,
С* И ДООПРЕДЕЛЯЕТ ЭТУ ССЫЛКУ,  ЗАПИСЫВАЯ  В
С* TDEF1(!). ПРИ ДООПРЕДЕЛЕНИИ ПРОСТОЙ ВНЕШ-
С* НЕЙ ЭКВИВАЛЕНТНОСТИ  ПРОИЗВОДИТСЯ ЗАПИСЬ
С* ПОСЛЕДОВАТЕЛЬНОСТИ  КОМАНД  ЗАГРУЗЧИКА В
С* ОБ'ЕКТНЫЙ МОДУЛЬ.
С*
 ,BITS,
 ,DEFLC,
 WDEF:,EQU,-1.РАБ.ЯЧ.
 LBL:,EQU,1.К-ВО РАБ.ЯЧ.
 ,АТХ,W
 ,IТА,13
 ,ITS,12
 ,XTS,
 15,UТМ,LBL
 ,ХТА,W
 15,АТХ,WDEF
 ,ARX,
 ,UZA,ERR14
 ,ASN,64-4
 ,УТА,
 ,ATI,14
 15,ХТА,WDEF
 14,UJ,*+1
 :14,VТМ,.0
 ,UJ,RЕТ.АБСОЛЮТ
 :14,VТМ,.1
 ,UJ,RЕТ.СМЕЩ. В КОМ.
 :14,VТМ,.2
 ,UJ,RЕТ.СМЕЩ. В ПЕРЕМ.
 :14,VТМ,.3
 ,UJ,RЕТ.ВНЕШН. ОБ'ЕКТ
 :14,VТМ,.4
 ,UJ,CONST.СМЕЩ. В КОНСТ.
 :14,VТМ,.5
 ,UJ,EXTEQ.ВНЕШН. ЭКВИВ.
 :14,VТМ,.6
 ,UJ,INTEQ.ВНУТР. ЭКВИВ.
 :14,VТМ,.7
 ,UJ,LIT.ЛИТЕРАЛ
 CONST:,BSS,
 ,ARX,ABDEF
 ,АЕХ,=:24
 ,UJ,RЕТ
 LIT:,ARX,ABLIT
 ,АЕХ,=:3
 RЕТ:,АТХ,W
 15,UТМ,-LBL
 ,SТХ,
 ,STI,12
 ,ATI,13
 ,ХТА,W
 13,UJ,
 ERR14:14,VТМ,14
 ,UJ,RЕТ
 INTEQ:,BSS,
С    ПРОСТАЯ ВНУТРЕННЯЯ ЭКВИВАЛЕНТНОСТЬ
 15,АТХ,
 ,ASN,64+20
 ,ААХ,С7777
 ,STI,12
 ,ААХ,В20/01
 15,АТХ,WDEF.СМЕЩЕНИЕ
 12,ХТА,TDEF1
 ,ААХ,=:6
 ,UZA,DFN.ОПРЕДЕЛЕНО
С   ССЫЛКА НА НЕДООПРЕДЕЛЕННЫЙ ИДЕНТИФИКАТОР
 ,ARX,
 ,UZA,UNDEF.=> ERR12
 12,ХТА,TDEF1
 ,CALL,DEFINER
 14,VIM,RЕТ
 12,XTS,TDEF1
 ,ААХ,=:03.В44*43
 15,АОХ,
 12,АТХ,TDEF1
 DFN:12,ХТА,TDEF1
 ,ААХ,=:14
 ,АЕХ,=:14
 ,UZA,IEXTEQ
 12,ХТА,TDEF1
 15,ARX,WDEF
 ,UJ,RЕТ
 UNDEF:,BSS,
С  ПРИ ДООПРЕДЕЛЕНИИ ИДЕНТИФИКАТОРА,
С  РАНЕЕ ОПИСАННОГО В ИНСТРУКЦИИ BLOCK,
С  ОКАЗАЛОСЬ, ЧТО НЕ ОПИСАН ИДЕНТИФ. В
С  ПОЛЕ МЕТКИ ЭТОЙ ИНСТРУКЦИИ.
 12,ХТА,TDEF.ЕГО ИМЯ
 14,VТМ,12
 ,UJ,RЕТ
 IEXTEQ:,BSS,
 12,ХТА,TDEF1
 ,ААХ,MATD1
 ,АТХ,W+1
 ,UJ,G/ADD
 EXTEQ:,BSS,
С    ЭТА ВЕТКА НЕ ЗАНИМАЕТСЯ РЕКУРСИЕЙ,
С    НО ЗАПИСЫВАЕТ В ОМ.
 ,ААХ,В32/01
 ,ASN,64+20.FADR=NEXTERN
 ,АОХ,=:14.ТА=3,ТО=0
 ,АТХ,W+1
 G/ADD:,BSS,
 15,ХТА,WDEF
 ,ААХ,В20/01.FADR=СМЕЩЕНИЕ
 ,АТХ,W+2.ТА=0,ТО=0
 ,UZA,Z/ADD.=> ВСЕ ЕСТЬ
 ,ХТА,ADEFLD
 ,АОХ,=1400 0000 0000 0022
 ,АТХ,W
 ,IТА,11
 ,ITS,10
 ,ITS,9
 ,ITS,8
 ,XTS,
 ,WRITM,W,С3
 ,SТХ,
 ,STI,8
 ,STI,9
 ,STI,10
 ,ATI,11
 ,ХТА,ADEFLD
 ,ASN,64+6
 ,АОХ,=:14
 ,XTS,ADEFLD
 ,ARX,В7
 ,SТХ,ADEFLD
 14,VТМ,
 ,UJ,RЕТ.
 Z/ADD:,ХТА,W+1
 ,UJ,RЕТ
 W:,BSS,3
 ,END,DEFINER

 FLIST/:1,NАМЕ,***
С*
С*     РАЗБОР АДРЕСНОГО СПИСКА ИНСТРУКЦИЙ
С*
С*      INT, INTH, INTQ, INTB, НЕХ
С*
С*     ПРИ ОБРАЩЕНИИ:
С*
С*  М4 - ТИП ЭЛЕМЕНТА СПИСКА:
С*     0 - INTH, INTQ, INTB
С*     1 - INT
С*     2 - НЕХ
С*
С*  М5 - ДЛИНА ФОРМИРУЕМОГО ФРАГМЕНТА
С*       (ДЛЯ  М4.EQ.0):
С*     0 - 32 РАЗРЯДА
С*     1 - 16 РАЗРЯДОВ
С*     2 - 8 РАЗРЯДОВ
С*
С*  НА СУММАТОРЕ - АДРЕС П/П
С*                       "ДАЙПРОДОЛЖЕНИЕ"
С*
С*
С*     FLIST/  САМОСТОЯТЕЛЬНО ЗАПИСЫВАЕТ
С*  ФОРМИРУЕМЫЕ СЛОВА В ОБ'ЕКТНЫЙ МОДУЛЬ.
С*
С*
С*     ОТВЕТ:
С*
С*  I14.EQ.0 - СПИСОК РАЗОБРАН БЕЗ ОШИБОК,
С*             СЛЕДУЮЩАЯ СТРОКА ПРОМЕЖУТОЧ-
С*             НОГО КОДА ПРОЧИТАНА.
С*  I14.NЕ.0 - ERROR,   I14  СОДЕРЖИТ НОМЕР
С*             ОШИБКИ.
С*
 PUTHEX:,SUBP,
 PUTINT:,SUBP,
 TABSYM:,SUBP,
 ,BITS,
 ,DEFLC,
 BL:I,BLOCK,W64(2)
 ,CONT,WW(8)
 ,CONT,W
 ,CONT,SI4
 ,CONT,SUBP.ДАЙПРОДОЛЖЕНИЕ
 ,CONT,SI13
 ,CONT,ENDBL(0)
 LBL:,EQU,14
 ЬLBL:,EQU,ENDBL-BL
 ,АТХ,SUBP
 ,IТА,13
 ,АТХ,SI13
 М5,WТС,LLP
 М3,VТМ,
 ,IТА,М4
 ,АТХ,SI4
 BEGIN:М9,VТМ,TADS
 МА,VТМ,
 MLOOP:М4,UJ,*+1
 :,UJ,INT*.INTH,INTQ,INTB
 :М6,VТМ,НЕХ/INT.INT
 ,UJ,PUTINT
 :М6,VТМ,НЕХ/INT.НЕХ
 ,UJ,PUTHEX
 INT*:,ХТА,
 ,АТХ,W64
 :М6,CALL,TRAD/
 14,UJ,*+1
 :,UZA,ERR8.ОШИБКА
 ,UJ,ERR12
 :,UJ,ISINT.УДАЧНО
 :М5,VIM,ERR8.НЕВЫЧИСЛИМО
 М8,MTJ,М7
 М8,VZM,WR**
 М8,UТМ,-1
 М8,VIM,ERR8
 WR**:,BSS,
 ,АТХ,W
 ,ХТА,STR
 ,АОХ,WDEFLD
 ,АОХ,=140 0001.БУДЕТ FD=3,FА=3
 ,АТХ,STR
 М6,VТМ,EMLOOP
 ,UJ,WRM32
 ISINT:,BSS,
 М8,MTJ,М7.ТИП РАЗДЕЛИТЕЛЯ
 М8,VZM,WR*.=> К.С.
 М8,UТМ,-1
 М8,VIM,ERR8.=> НЕ ","
 WR*:,BSS,
 ,АТХ,W64+1
 М5,VZM,WRH.ЗАП. ПОЛУСЛОВА
 ,ААХ,=:74
 ,U1А,ERR8.НЕ АБС.
 ,ХТА,W64+1
 ,UТС,MQ/В-1
 М5,ААХ,
 ,UZA,WRF
 ,UТС,BQ/В-1
 М5,ARX,
 ,ААХ,В33
 ,UZA,ERR8
 WRF:,ХТА,W64+1
 М5,UТС,М/INT*
 ,ААХ,
 ,UТС,WW+7
 М3,АТХ,
 М3,VLM,EMLOOP
 М5,WТС,LLP
 М3,VТМ,
 М6,VТМ,EMLOOP
 SWF:М5,UJ,*
 :,UJ,WRF16
 :,UJ,WRF08
 WRWORD:,BSS,
 ,IТА,М9
 ,ITS,МА
 ,ITS,
 ,WRITM,W64,В2
 ,STI,
 ,STI,МА
 ,ATI,М9
 М6,UJ,
 WRH:,BSS,
 ,ААХ,=:74
 ,АЕХ,=:14
 ,UZA,ЕХТ1.ВНЕШНИЙ
 ,ХТА,W64+1
 ,ААХ,М/INT*
 WRF32:,АТХ,STR
 ,ХТА,В1
 ,АТХ,W
 М6,VТМ,EMLOOP
 WRM32:М3,UТМ,1.СЧЕТ.ПОЛУСЛОВ
 ,IТА,М9
 ,ITS,МА
 ,ITS,
 ,WRITM,STR,W
 ,STI,
 ,STI,МА
 ,ATI,М9
 М6,UJ,
 ЕХТ1:,BSS,
С    ОДИН ВНЕШНИЙ АДРЕС
 ,ХТА,W64+1
 ,ASN,64-6
 ,АОХ,=:1400 0000 0140 0001
 ,UJ,WRF32
 EMLOOP:,BSS,
 ,WТС,SI4
 М4,VТМ,
 М7,VIM,MLOOP.НЕ К.С.
С       КОНЕЦ  СТРОКИ
 ,WТС,SUBP
 М2,VJM,
 ,UZA,BEGIN.ПРОДОЛЖЕНИЕ
С      КОНЕЦ  ОПЕРАТОРА
 М4,VIM,RЕТ
 М5,VZM,ENDH
 М5,WТС,LРР-1
 М3,UТС,
 14,VТМ,
 14,VZM,RЕТ.=>БЕЗ ДОКРУТКИ
С     ДОПОЛНЕНИЕ ОСТАВШИХСЯ ДО КОНЦА
С     СЛОВА  ФРАГМЕНТОВ
 ,ХТА,
 *1:М3,UТС,WW+7
 ,АТХ,
 М3,VLM,*1
 М6,VJM,SWF
 RЕТ:14,VТМ,
 ERROR:М2,VТМ,
 ,WТС,SI13
 ,UJ,
 ENDH:,BSS,
 ,IТА,М3
 ,ААХ,В1
 ,UZA,RЕТ.=>БЕЗ ДОКРУТКИ
 ,АТХ,W
 ,ХТА,
 ,АТХ,STR
 М6,VТМ,RЕТ
 ,UJ,WRM32
 WRF08:,BSS,
 12,VТМ,WW
 13,VТМ,-1
 LF08:14,VТМ,-2
 12,ХТА,
 12,UТМ,1
 *F08:,ASN,64-8
 12,АОХ,
 12,UТМ,1
 14,VLM,*F08
 ,UТС,W64+1
 13,АТХ,
 13,VLM,LF08
 ,UJ,WRWORD
 WRF16:,BSS,
 ,ХТА,WW+4
 ,ASN,64-16
 ,АОХ,WW+5
 ,АТХ,W64
 ,ХТА,WW+6
 ,ASN,64-16
 ,АОХ,WW+7
 ,АТХ,W64+1
 ,UJ,WRWORD
 НЕХ/INT:,BSS,
 14,VIM,ERROR
 ,АТХ,W64+1
 ,ХТА,LARGE
 ,АТХ,W64
 М8,WТС,TABSYM
 М7,VТМ,-12
 М6,VТМ,EMLOOP
 М7,VZM,WRWORD.КОНЕЦ СТРОКИ
 М7,MTJ,14
 14,UТМ,12-4
 14,VZM,WRWORD.ЗАПЯТАЯ
 ERR8:14,VТМ,8
 ,UJ,ERROR
 ERR12:14,VТМ,12
 ,UJ,ERROR
 LLP:,Z00,
 ,Z00,.Н
 :,Z00,
 ,Z00,-3.Q
 :,Z00,
 ,Z00,-7.В
 LРР:,BSS,
 ,LOG,3.Q
 ,LOG,7.В
 MQ/В:,LOG,377 7760 0000.В32/17
 ,LOG,377 7777 7400.В32/09
 BQ/В:,LOG,20 0000.В17
 ,LOG,400.В09
 М/INT*:,LOG,1400 0377 7777 7777
 ,LOG,17 7777.В16/01
 ,LOG,377.В08/01
 BL:,BSS,LBL
 ,END,

 FORMTX:1,NАМЕ,***
С*
С*   РАЗБОР ТЕКСТОВОЙ КОНСТАНТЫ В АДРЕСНОЙ
С*   ЧАСТИ ИНСТРУКЦИИ.
С*
С*     РАБОТА НАЧИНАЕТСЯ С ТЕКУЩЕЙ ПОЗИЦИИ
С*   PARAM, КОТОРАЯ ДОЛЖНА УКАЗЫВАТЬ НА ПЕР-
С*   ВЫЙ СИМВОЛ ПОСЛЕ ОТКРЫВАЮЩЕЙ КАВЫЧКИ.
С*
С*     ПРИ ОБРАЩЕНИИ АСС СОДЕРЖИТ:
С*  В48    - ПРИЗНАК ISOZ
С*  В15:01 - АДРЕС  ПРОЦЕДУРЫ  "ДАЙПРОДОЛЖЕ-
С*           НИЕ", К КОТОРОЙ БУДЕТ ОБРАЩЕНИЕ
С*             2,VJM,....
С*
С*     ОТВЕТ:
С*
С*   I14.EQ.0 - УДАЧНО. СФОРМИРОВАННАЯ КОНС-
С*     ТАНТА НАХОДИТСЯ В  WСВ,  ПО  32 Р. В
С*     СЛОВЕ.  ДЛИНА ЭТОЙ КОНСТАНТЫ  В 64-Р
С*     СЛОВАХ - НА СУММАТОРЕ.
С*
С*   I14.NЕ.0 - ОШИБКА, I14 СОДЕРЖИТ ЕЕ НО-
С*     МЕР.
С*
С*
С*     ВОЗВРАТ ПО I13.
С*     ПОРТЯТСЯ РЕГИСТРЫ М8-МС.
С*
 ,BITS,
 ,DEFLC,
 TABSYM:,SUBP,
 WСВ:,LС,256
 AW:,EQU,М3
 LW:,EQU,М4
 N:,EQU,М5
 ,АТХ,ТУРТ
 ,IТА,13
 ,ITS,AW
 ,ITS,LW
 ,ITS,N
 ,XTS,
 AW,VТМ,WСВ.АДРЕС СЛОВА ЗАП.
 LW,VТМ,-3.ЦИКЛ ПО СЛОВУ ЗАП.
 N,VТМ,.СЧ.СИМВ.
 AW,АТХ,
 NEWS:,BSS,
 ,CALL,SYMBOL
 14,UТС,TABSYM+40В
 ,ХТА,
 ,ААХ,В35
 ,U1А,SPS.АПОСТРОФ
 14,UТС,TABSYM+40В
 ,ХТА,
 ,ASN,64+24
 ,ААХ,С377
 ,U1А,WRITS
 14,UТМ,40В
 ,IТА,14
 WRITS:,BSS,
 N,UТМ,1
 AW,АОХ,
 LW,VLM,*1
 AW,АТХ,
 ,ХТА,
 AW,АТХ,1
 LW,VТМ,-3
 AW,VLM,NEWS
 *1:,ASN,64-8
 AW,АТХ,
 ,UJ,NEWS
 SPS:,BSS,.АПОСТРОФ
 14,UТМ,40В-47В
 14,VZM,PUTCOD
С     СИМВОЛ " - ЛИБО КОНЕЦ, ЛИБО  ""
С
С     ЗАГЛЯНЕМ ДАЛЬШЕ.
 ,CALL,SYMBOL
 14,UТМ,40В-42В
 14,VZM,WRITS.АСС=42В.
С    ЭТО БЫЛА ЗАКРЫВАЮЩАЯ КАВЫЧКА.
 ,АТХ,СТАВ.СОХРАНИМ СЛЕД. СИМВОЛ.
 ,WТС,ТУРТ
 М2,VJM,.*****
 ,U1А,END.КОНЕЦ ИНСТРУКЦИИ
С    ПОЛУЧИЛИ СТРОКУ-ПРОДОЛЖЕНИЕ.
 ,CALL,S/SYMBOL
 14,UТМ,40В-42В
 14,VZM,NEWS
 ERROR:14,VТМ,8.ОШ. В АДРЕСЕ
 ,UJ,RЕТ
 END:,BSS,
С    КОНЕЦ ОПЕРАТОРА
 М2,VТМ,
 ,ХТА,ТУРТ
 ,ARX,
 ,UZA,ISOZ
 ,IТА,N
 ,ААХ,С7
 ,UZA,ALL.ДОПОЛНЕНО
 ,IТА,LW
 ,ASN,64-3.*8
 ,ATI,14
 AW,ХТА,
 14,ASN,64
 LW,UТС,TABS+3
 ,АОХ,
 AW,АТХ,
 ,IТА,N
 ,ААХ,В3
 ,U1А,ALL
С    ФОРМИРУЕМ ВТОРОЕ ПОЛУСЛОВО
 ,ХТА,TABS
 AW,АТХ,1
 ALL:,IТА,N
 ,ARX,С7
 ,ASN,64+3
 ,АТХ,ТУРТ.ДЛИНА В СЛОВАХ
 14,VТМ,
 RЕТ:,BSS,
 ,SТХ,
 ,STI,N
 ,STI,LW
 ,STI,AW
 ,ATI,13
 ,ХТА,ТУРТ
 13,UJ,
 ISOZ:,BSS,
С    КОНЕЦ ТЕКСТА ISOZ
 N,UТМ,1
 ,IТА,LW
 ,ASN,64-3
 ,ATI,14
 AW,ХТА,
 14,ASN,64
 AW,АТХ,
 ,ХТА,
 AW,АТХ,1
 ,UJ,ALL
 PUTCOD:,BSS,
С    ВВОД КОДА СИМВОЛА
 ,CALL,PARAM
 14,UJ,*+1
 :,UJ,ERROR.ОШ
 :,UJ,PUTID.ИДЕНТ
 :,UJ,COD.ЧИСЛО
 WRICOD:,BSS,.ПУСТО
 ,WТС,СТАВ
 14,VТМ,-47В
 14,VIM,ERROR.В КОНЦЕ НЕ '
 ,UJ,WRITS
 COD:,BSS,
 ,АТХ,W
 ,ASN,64+8
 ,U1А,ERROR.СЛИШКОМ МНОГО
 ,ХТА,W
 ,UJ,WRICOD
 PUTID:,BSS,
С    КОД СИМВОЛА ЗАДАН ИДЕНТИФИКАТОРОМ
 М4,MTJ,МС
 ,CALL,CHID
 М4,MTJ,14
 МС,MTJ,М4
 14,VZM,ERROR.НЕ ОПИСАН
 14,ХТА,TDEF1
 ,ААХ,=7400 0377 7777 7777
 ,UJ,COD
С===========================================
С
 ТУРТ:,BSS,1
 W:,BSS,1
 TABS:,ISO,6Н''''
 ,ISO,6Н''''''
 ,ISO,6Н''''''''
 ,ISO,6Н''''''''''
 ,LOG,0
 ,END,FORMTX

 GETF:,NАМЕ,
С*    ВСПОМОГАТЕЛЬНАЯ П/П ПЕЧАТИ КОДА
 **:LС,BLOCK,F(16)
 N:,EQU,16
 NL:,EQU,N-1
 12,BASE,*
 ,STI,14
 14,ХТА,
 ,АТХ,W
 9,VТМ,-NL
 LL:,BSS,
 9,ХТА,М+NL
 ,АСХ,
 ,АЕХ,=77777
 ,ARX,=1
 ,ATI,8
 ,ХТА,W
 9,АРХ,М+NL
 8,ASN,64
 ,УТА,
 9,UТС,F+NL
 ,АТХ,
 9,VLM,LL
 13,UJ,
 М:,BSS,
 ,ОСТ,7777
 ,ОСТ,0000 7777
 ,LOG,7777 0000
 ,LOG,7777
 ,LOG,7.А
 ,LOG,70.В
 ,LOG,37 7700.С
 ,LOG,140 0000.D
 ,LOG,200 0000.W
 ,ОСТ,0000 0377.Н
 ,ОСТ,0000 0037.Т
 ,ОСТ,0000 004.N
 ,ОСТ,0000 03.К
 ,ОСТ,0000 036.IR
 ,LOG,17 7400 0000.КОП
 ,LOG,377 7777.АДР
 W:,BSS,1
 ,END,

  HCLE:,NАМЕ,
  LНТАВ:,EQU,512
  HMASK:,EQU,=1776
  MARKEND:,EQU,=6Н <END>
  LТ2:,EQU,LНТАВ+LНТАВ
  *НТАВ*:,LС,LТ2
  А/I0:,LС,1
С
С        SUBROUTINE HCLE    - ОЧИСТКА ТАБЛ.
С
  ,ХТА,
  14,VТМ,1-LТ2
  :,UТС,*НТАВ*
  14,АТХ,LТ2-1
  14,VLM,*-1
  ,UТС,MARKEND
  ,ХТА,
  ,UТС,*НТАВ*
  ,АТХ,LТ2-2
  13,UJ,
С
С       FUNCTION KHPUT(КЕУ)   - ВНЕСЕНИЕ
С       КЛЮЧА (КЕУ) И ЕГО ХАР-КИ (КЕУ+1)
С
С       FUNCTION KHGET(КЕУ)   - ПОЛУЧЕНИЕ
С       В КЕУ+1 ХАР-КИ КЛЮЧА КЕУ.
С
С       РЕЗУЛЬТАТ = ИНДЕКС В *НТАВ* ИЛИ 0
С
С
  KHPUT:,ENTRY,
  ,UТС,PUT-GЕТ
  KHGET:,ENTRY,
  14,VТМ,
  ,ATI,12
  12,ХТА,
  15,АТХ,1
  ,ASN,64+11
  15,ARX,1
  ,ASN,64+11
  15,ARX,1
  ,ASN,64+11
  15,ARX,1
  ,ASN,64+3
  ,UJ,КЕУ
  NКЕУ:11,UТМ,2
  ,IТА,11
  КЕУ:,UТС,HMASK
  ,ААХ,
  ,ATI,11
  ,UТС,*НТАВ*
  11,ХТА,
  14,UZA,GЕТ. ИЛИ  PUT !
  15,АЕХ,1
  ,UIA,NКЕУ
  14,VIM,PUT
  ,UТС,*НТАВ*
  11,ХТА,1
  12,АТХ,1
  POZR:11,UТМ,1
  ,IТА,11
  ,АОХ,А/I0
  13,UJ,
  PUT:12,ХТА,
  ,UТС,*НТАВ*
  11,АТХ,
  12,ХТА,1
  ,UТС,*НТАВ*
  11,АТХ,1
  ,UJ,POZR
  GЕТ:,BSS,
  NEGR:,ХТА,А/I0
  13,UJ,
  ,END,

 IРС:,NАМЕ,
С*
С*          FUNCTION IРС(0)
С*
С*   ЧТЕНИЕ СТРОКИ ПРОМЕЖУТОЧНОГО КОДА.
С*
С*  ОТВЕТ:  0В,  ЕСЛИ   END
С*
 ,DEFLC,
 LРС:,EQU,ENDPC-BEGPC
 FRPC:,LС,1
 PAGE:,РС,1024
 NР:,Р/Р,(PAGE)16
 12,BASE,*
 ,UТС,FRPC
 ,ХТА,
 ,UZA,BEGIN
 ,WТС,S/10
 10,VТМ,
 CONT:14,VТМ,1-LРС
 ,ХТА,
 :,UТС,BEGPC
 14,АТХ,LРС-1
 14,VLM,*-1
 11,VJM,RDW
 ,АТХ,SС
 L:,ХТА,SС
 ,UZA,ENDL
 ,ANX,
 ,ATI,9
 ,УТА,
 9,ASN,64
 ,АТХ,SС
 11,VJM,RDW
 9,АТХ,BEGPC-1
 ,UJ,L
 ENDL:,IТА,10
 ,АТХ,S/10
 ,ХТА,CHAR
 ,АЕХ,=:7
 ,ААХ,=:7
 13,UJ,
 BEGIN:10,VТМ,-1023
 ,*70,IW70
 ,ХТА,IW70
 ,ARX,=1
 ,АТХ,IWW
 ,UТС,FRPC
 ,АТХ,
 ,UJ,CONT
 RDW:10,UТС,PAGE+1023
 ,ХТА,
 11,UТС,
 10,VLM,
 10,VТМ,-1023
 ,ITS,13
 15,АТХ,
 ,*70,IWW
 ,ХТА,IWW
 ,CALL,DRUMTAP*
 ,SТХ,IWW
 ,STI,13
 11,UJ,
 IW70:,010,NР
 ,010,
 IWW:,BSS,1
 S/10:,BSS,1
 SС:,LOG,
 ,END,

 ISYNT:,NАМЕ,.ДЛЯ Т/SYNTAX
 ,DEFLC,
 SYNTAX:,SUBP,
 ,IТА,13
 ,АТХ,R
 14,VТМ,-6
 *2:14,IТА,7
 14,АТХ,В+6
 14,VLM,*2
 13,VJM,SYNTAX
 ,ХТА,SСН
 ,ASN,64-1
 ,УТА,
 ,АЕХ,=1
 ,ATI,12
 14,VТМ,-6
 *3:14,ХТА,В+6
 14,ATI,7
 14,VLM,*3
 ,IТА,12
 ,WТС,R
 ,UJ,
 В:,BSS,7
 R:,BSS,1
 ,END,

 LETBI:,NАМЕ,.ВЫДРАНО ИЗ TASK41
С
С    FUNCTION LETBI(STRING)
С    ПОЛУЧЕНИЕ БИТОВОЙ ШКАЛЫ ИЗ
С    ТЕКСТОВОЙ СТРОКИ STRING :
С    В47 - "А", В46 - "В"   И Т.Д.
С
  8,BASE,*
  ,ATI,14
  ,ХТА,
  15,АТХ,2
  LВ1:14,ХТА,
  12,VТМ,-5
  LВ2:,ASN,64-8
  15,АТХ,3
  ,УТА,
  ,ATI,11
  ,ASN,64+6
  ,UZA,LВ3
  ,IТА,11
  ,ААХ,=40
  :,UZA,*+1
  11,UТМ,-40В
  :,ХТА,=:4
  11,ASN,
  15,АОХ,2
  15,АТХ,2
  15,ХТА,3
  12,VLM,LВ2
  14,VLM,LВ1
  LВ3:15,ХТА,2
  13,UJ,
 ,END,

 LITERAL:1,NАМЕ,***
С*
С*          ФОРМИРОВАНИЕ  ЛИТЕРАЛА
С*          И  ЗАПИСЬ  ЕГО  В  TLT
С*
С*    РАЗБОР АДРЕСНОЙ ЧАСТИ НАЧИНАЕТСЯ С ТЕ-
С*  КУЩЕЙ ПОЗИЦИИ П/П SYMBOL, КОТОРАЯ ДОЛЖНА
С*  УКАЗЫВАТЬ НА СИМВОЛ, СЛЕДУЮЩИЙ ЗА "=".
С*
С*     ОТВЕТ:
С*
С*   I14=0 - ХОРОШО;  АСС СОДЕРЖИТ СЛОВО
С*     ДЛЯ TDEF1.
С*     ЕСЛИ ПРИ ЭТОМ  МС.NЕ.0 - ПРОЧИТАНА
С*     СЛЕДУЮЩАЯ СТРОКА ТЕКСТА П/П.
С*
С*   I14.NЕ.0 - ОШИБКА.  I14 СОДЕРЖИТ ЕЕ НО-
С*     МЕР.
С*
С*   ВОЗВРАТ ПО I13.
С*   ПОРТЯТСЯ РЕГИСТРЫ М8-МС.
С*
 ,BITS,
 ,DEFLC,
 LTLT:,EQU,2048
 ILTLT:,EQU,В12  .2048
 TLT:,LС,LTLT
 WСВ:,LС,256
 WRPC:,SUBP,
 BL:I,BLOCK,LL.ДЛИНА ЛИТ.
 ,CONT,SI13
 LBL:,EQU,2
 ,IТА,13
 ,АТХ,SI13
 PUTS:,CALL,SYMBOL
 14,VZM,PUTS
 14,UТМ,40В-111В.I
 14,VZM,INT
 14,UТМ,111В-42В."
 14,VZM,ISO
 14,UТМ,42В-132В.Z
 14,VZM,ISOZ
 14,UТМ,132В-110В.Н
 14,VZM,НЕХ
 14,UТМ,110В-117В.О
 14,VZM,ОСТ
 14,UТМ,117В-122В.R
 14,VZM,REAL
 14,UТМ,122В-104В.D
 14,VZM,DREAL
 14,UТМ,104В-125В.U
 14,VZM,U/INT
 0,UТС,-1
 :,CALL,SYMBOL
 :14,VZM,*-1
 ,UJ,INT
 ERROR:14,VТМ,8.ОШ. В АДР.
 ,UJ,RЕТ
 ERR12:14,VТМ,12.
 ,UJ,RЕТ
 ERR13:14,VТМ,13.
 ,UJ,RЕТ
 ОСТ:,BSS,
 REAL:,BSS,
 DREAL:,BSS,
 14,VТМ,11.НЕРЕАЛИЗОВАНО
 ,UJ,RЕТ
 ISO:,BSS,
 14,VТМ,WRPC
 ,IТА,14
 PUTTX:,CALL,FORMTX
 14,VIM,RЕТ.ОШИБКА
 ,UТС,1
 WRL:МС,VТМ,
С     ПОИСК СВОЕГО ЛИТЕРАЛА В TLT.
С     АСС = ДЛИНА ЛИТЕРАЛА В 64-Р. СЛОВАХ.
 ,АТХ,LL.ДЛИНА
 ,АЕХ,В20/01.1-<ДЛИНА
 ,ARX,В2.   ИСКОМОЙ
 ,ATI,М9.   ПОСЛЕДОВ.>
 ,ХТА,LLIT
 ,UZA,NОТ.ПЕРВЫЙ ЛИТЕРАЛ
 ,ASN,64-1
 ,АЕХ,В20/01
 ,ARX,В1
 ,ATI,MD.ДЛИНА TLT
 М8,VТМ,
 CHL:,BSS,
 М9,MTJ,МВ
 МА,VТМ,
 СОМР:М8,UТС,TLT
 МА,ХТА,
 ,UТС,WСВ
 МА,АЕХ,
 ,U1А,NО
 М8,UТС,TLT
 МА,ХТА,1
 ,UТС,WСВ
 МА,АЕХ,1
 ,U1А,NО
 МВ,VLM,YES
 ,UJ,IS
 YES:МА,UТМ,2
 МА,UТС,
 М8,UТС,
 14,VТМ,
 MD,J+М,14
 14,VIM,СОМР
С    ВСЕ ПРОСМОТРЕНО, НУЖНОЙ КОНСТАНТЫ НЕТ.
 ,UJ,NОТ
 NО:М8,UТМ,2
 М8,MTJ,14
 MD,J+М,14
 14,V1М,CHL.ЕЩЕ НЕ ВСЕ
 NОТ:,BSS,
С    ЗАПИСЬ НОВОГО ЛИТЕРАЛА В TLT
 ,ХТА,LLIT
 ,ASN,64-1
 ,ATI,М8.НАЧ. ЗАПИСИ
 14,VТМ,.СТРЕЛКА
 М9,MTJ,МВ
С    МВ = 1 - <К-ВО ЗАПИСЫВАЕМЫХ 64-Р. СЛОВ>
 ,ХТА,LL.А НЕ
 ,ARX,LLIT.ПЕРЕПОЛНИМ-ЛИ
 ,Х-А,ILTLT
 ,U1А,ERR13.ПЕРЕПОЛНИМ!
 LWR:,BSS,
 14,UТС,WСВ
 ,ХТА,
 14,UТС,TLT
 М8,АТХ,
 14,UТС,WСВ
 ,ХТА,1
 14,UТС,TLT
 М8,АТХ,1
 14,UТМ,2
 МВ,VLM,LWR
 ,ХТА,LL
 ,ARX,LLIT
 ,АТХ,LLIT
  IS:,IТА,М8
 ,ASN,64+1
 ,АОХ,=:34.ПРИЗНАК: ЛИТЕРАЛ
 14,VТМ,
 RЕТ:,BSS,
 ,WТС,SI13
 ,UJ,
 ISOZ:,BSS,
 ,CALL,SYMBOL
 14,UТМ,40В-42В
 14,V1М,ERROR
 14,VТМ,WRPC
 ,IТА,14
 ,АОХ,В48
 ,UJ,PUTTX
 INT:,BSS,
 М6,CALL,PUTINT
 14,VIM,RЕТ
 ,UJ,ISWORD
 НЕХ:,BSS,
 М6,CALL,PUTHEX
 14,VIM,RЕТ
 ISWORD:,АТХ,WСВ+1
 ,ХТА,LARGE
 ,АТХ,WСВ
 ,ХТА,В1
 ,UJ,WRL
 U/INT:,BSS,
 М6,CALL,TRAD*
 14,UJ,*+1
 :,UZA,ERROR.ОШИБКА
 ,UJ,ERR12
 :,UJ,ISINT.УДАЧНО
 :,UJ,ERROR.НЕВЫЧИСЛИМО
 ISINT:,BSS,
 ,АТХ,WСВ+1
 ,ААХ,В32
 ,U1А,NEG
 WRLINT:,АТХ,WСВ
 ,ХТА,В1
 ,UJ,WRL
 NEG:,ХТА,В32/01
 ,UJ,WRLINT
 BL:,BSS,LBL
  ,END,LITERAL

 LLIST:1,NАМЕ,***
С*
С*    ПОДСЧЕТ  КОЛИЧЕСТВА  ЭЛЕМЕНТОВ  СПИСКА
С*    ТИПА INT (INTH, INTQ, INTB, НЕХ, ...).
С*
С*
С*        ОБРАЩЕНИЕ:
С*
С*  НА СУММАТОРЕ - АДРЕС П/П "ДАЙПРОДОЛЖЕНИЕ"
С*
С*        ОТВЕТ:
С*
С*  I14.EQ.0 - ХОРОШО.   АСС  СОДЕРЖИТ  К-ВО
С*     ЭЛЕМЕНТОВ  СПИСКА.  СЛЕДУЮЩАЯ  СТРОКА
С*     ТЕКСТА П/П ПРОЧИТАНА. (!)
С*
С*  I14.NЕ.0 - ERROR, I14 СОДЕРЖИТ ЕЕ НОМЕР.
С*
С*
С*     ВОЗВРАТ ПО I13.
С*     ПОРТЯТСЯ РЕГИСТРЫ  М9, МА, МС.
С*
 ,BITS,
 ,DEFLC,
 TABSYM:,SUBP,
 BL:I,BLOCK,SI13,W,SВР
 LBL:,EQU,3
 ,АТХ,SВР
 ,IТА,13
 ,АТХ,SI13
 МС,VТМ,.СЧЕТЧИК
 BLP:М9,VТМ,
 МА,VТМ,
 MLP:,CALL,SYMBOL
 14,UТС,TABSYM+40В
 ,ХТА,
 ,ААХ,=:0000 14.В33*34
 ,U1А,ENDSTR.КОНЕЦ СТРОКИ
 14,UТМ,40В-54В.,
 14,VIM,MLP.НЕ ЗАПЯТАЯ
С        ЗАПЯТАЯ
 МС,UТМ,1
 ,UJ,MLP
 ENDSTR:МС,UТМ,1
 ,IТА,МС
 ,АТХ,W
 ,WТС,SВР
 2,VJM,.ЗА НОВОЙ СТРОКОЙ
 2,VТМ,
 ,WТС,W
 МС,VТМ,
 ,UZA,BLP
С      КОНЕЦ СПИСКА.
 ,ХТА,W
 ,WТС,SI13
 ,UJ,
 BL:,BSS,LBL
 ,END,

 МКВ/ASS:,NАМЕ,
С*
С*     РАСПРЕДЕЛИТЕЛЬ ПАМЯТИ
С*
 ****:,SUBP,
 *ERRORS*:,LС,1
 *МСН:,SUBP,.ТАБЛ.МНЕМОКОДОВ
С---------  НАЧАЛО ОБНУЛЯЕМОГО
 *STRING*:,LС,LSTRI*.
 *SUBCH*:,LС,LSUBCH.Х-КИ П/П
 *TENTRY*:,LС,LTENTRY.ТАБЛИЦА ВХОДОВ
 *TDEF*:,LС,LTDEF.ТАБЛИЦА ОПИСАНИЙ
 *TSS*:,LС,LTSS.ТАБЛ.ССЫЛОК
 *EXTERN*:,LС,LTEXTERN
 FRPC:,LС,1
 *PARAM:,LС,1
С---------  КОНЕЦ ОБНУЛЯЕМОГО
 ЫР1:,РС,0
 МКВ/ASS*:,SUBP,
 DEFINER:,SUBP,
 SYNTAX:,SUBP,
 TABSYM:,SUBP,
 ЫР2:,РС,0
 МКВ/ASS/:,SUBP,
 TRAD:,SUBP,
 TRAD*:,SUBP,
 TRIDENT:,SUBP,
 WRITID:,SUBP,
 S/PARAM:,SUBP,
 TBLOCK:,SUBP,
 LITERAL:,SUBP,
 FORMTX:,SUBP,
 LLIST:,SUBP,
 FLIST/:,SUBP,
 PUTHEX:,SUBP,
 REFER:,SUBP,
С-----      НАЧАЛО ОБНУЛЯЕМОГО
 WСВ:,LС,256
 TLT:,LС,2048
 TDEF3:,LС,1024
 PAGE:,РС,1024
С--------------------
 14,VТМ,=600000
 ,ITS,14
 ,CALL,EXLIB
 14,VТМ,*STRING*-*PARAM
 ,ХТА,
 :,UТС,*PARAM
 14,АТХ,
 14,VLM,*-1
 14,VТМ,WСВ-PAGE
 :,UТС,PAGE
  14,АТХ,
  14,VLM,*-1
 ,UJ,МКВ/ASS*
 ,END,

 МКВ/ASS*:1,NАМЕ,***
 ,LIST,MS
 XR1:,MACRO,Х
 ,ХТА,Х
 ,ARX,В1
 ,АТХ,Х
 XR1:,ENDM,
 READ*:,SUBP,
  NOLISFL*:LС,BLOCK,NOLIST
 FRPC:,LС,1
 SYNTAX:,SUBP,
 TABSYM:,SUBP,
 PAGE:,РС,1024
 МКВ/ASS/:,SUBP,
 TRAD:,SUBP,
 NР:,Р/Р,(PAGE)16
 ,BITS,
 ,DEFLC,
 *TENTRY*:LС,BLOCK,(2),TENTRY
 *EXTERN*:LС,BLOCK,(1),TEXTERN
 *TSS*:LС,BLOCK,TSS
 BL:I,BLOCK,
 ,CONT,NSISO   .К-ВО СИМВ В ISO
 ,CONT,W       .РАБ.ЯЧ
 ,CONT,W2      .РАБ.ЯЧ
 ,CONT,SOST    .СОСТОЯНИЕ
 ,CONT,SI13,S/МВ,IW70
 ,CONT,F/MTNEXT
 ,CONT,MOZY
 LBL:,EQU,9
 LРС:,EQU,ENDPC-BEGPC
 TYPTDVAR:,EQU,В46
 TYPTDCNT:,EQU,В47
 Т/CONT:,EQU,=6Н    ,
С
С   В ТАБЛИЦЕ ССЫЛОК КАЖДАЯ ССЫЛКА СОДЕРЖИТ
С   В РАЗРЯДАХ 15:14 ТИП АДРЕСА:
С
С     0 - АДР. КОМАНДЫ
С     1 - АДР. КОНСТАНТЫ
С     2 - АДР. ПЕРЕМЕННОЙ
С     3 - НОМЕР СТРОКИ
С
С
С   ......  СОСТОЯНИЯ  ......
С
С     1 - НАЧАЛО РАБОТЫ
С     2 - ВХОДЫ
С     3 - ВНЕШНИЕ ИМЕНА
С     4 - ПРИЕМ МАКРО
С     5 - ПОСЛЕ  ENDM
С     6 - ОПИСАНИЯ
С     7 - ДАННЫЕ ДЛЯ РАССЫЛКИ
С     8 - КОМАНДЫ РАССЫЛКИ
С     9 - КОМАНДЫ ПРОГРАММЫ
С
С   .........................
С
С
С     TYPDEF - ТИП  "ЧИСЕЛ":
С    0 - ПЕРЕМЕННЫЕ
С    1 - КОНСТАНТЫ
С
С
С     DEF1:  ИНФОРМАЦИЯ ДЛЯ ВТОРОГО
С     ПРОХОДА:
С
С  48:45 - НОМЕР НОВОГО СОСТОЯНИЯ
С  22    - ТИП BSS (0=BSS, 1=BSSZ)
С  20:01 - ДЛИНА BSS
С
С     ДЛЯ ЛИТЕРАЛОВ В КОМАНДАХ:
С
С  44:25 - СМЕЩЕНИЕ ПО TLT ДЛЯ АДРЕСА
С          ПРЕДЫДУЩЕЙ(!) КОМАНДЫ.
С
 М1,VТМ,20000В
 М2,VТМ,
 ,ХТА,
 14,VТМ,1-LBL
 *1:14,UТС,BL+LBL-1
  ,АТХ,
  14,VLM,*1
 ,NTR,7.СТАНДАРТНО !
 ,ХТА,NOLIST
 ,АТХ,LISTFL.LIST
С   НУЛЕВУЮ ЯЧЕЙКУ TSS НЕ ИСПОЛЬЗУЕМ
 ,ХТА,В1
 ,АТХ,ATSS
 ,ХТА,В15
 ,АТХ,TYPSS
 14,VТМ,-1023
 ,IТА,14
 ,АТХ,S/МВ
 ,ХТА,В1
 ,АТХ,JTD3
 ,АТХ,SOST
 ,IТА,13
 ,АТХ,SI13
 ,ХТА,=6 0000.В15*14
 ,АТХ,NLI
 ,ХТА,MI70
 ,АТХ,IW70
 NEWCARD:,BSS,
 ,XR1,NSTR
 RCARD:,BSS,
 14,VТМ,STR
 ,ITS,14
 ,CALL,READ*
  ,ХТА,STR+12.НОМЕР ВХОДНОЙ
  ,АТХ,NUMBER.СТРОКИ - НА
  ,ХТА,STR+13.СВОЕ МЕСТО
  ,АТХ,NUMBER+1.(ДЛЯ ПЕЧАТИ)
 ,CALL,SYNTAX
 ,UZA,RCARD
 М2,VZM,CONTINUE
 ,ХТА,ТСОР
 ,АЕХ,Т/CONT
 М2,UJ,
 CONTINUE:,BSS,
 ,ХТА,SСН
 ,ARX,
 ,UZA,ERRSYNT
 ,ААХ,В39.КОММЕНТАРИЙ
 ,U1А,COMMENT.= ДА
 ,WТС,SOST
 М5,VТМ,
 М5,UJ,*
 :,ХТА,В2.1
 ,UJ,IFNAME
 :,ХТА,ТСОР.2
 ,UJ,PUTENTRY
 :,UJ,SELECT.3
 :,UJ,SELECT.4
 :,UJ,SELECT.5
 :,UJ,SELECT.6
 :,UJ,SELECT.7
 :,UJ,SELECT.8
 :,UJ,SELECT.9
 IFNAME:,АТХ,SOST
 ,ХТА,ТСОР
 ,АЕХ,=6HNAME
 ,U1А,ERR1
 ,XR1,NLI
 ,АТХ,NLINE
 ,IDENT,ТМТ
 ,UZA,ERR4
 ,АТХ,TENTRY
 М6,VJM,EXPAR.РАЗБОР АДРЕСА
 ,АОХ,В47
 ,АТХ,TENTRY+1
 ,ХТА,В2
 ,АТХ,ATENTRY
 ,ХТА,В46
 ,АТХ,CHAR
 ,UJ,WRPC
 PUTENTRY:,BSS,.ПРИЕМ ENTRY
 ,АЕХ,=6HENTRY
 ,U1А,SELECT
 ,XR1,NLI
 ,АТХ,NLINE
 ,IDENT,ТМТ
 ,UZA,ERR4
 ,WТС,ATENTRY
 М3,VТМ,
 М3,АТХ,TENTRY
 ,CALL,WRITID
 14,V1М,ERR2
 М6,VJM,EXPAR
 ,ITS,М4
 15,АОХ,
 М3,АТХ,TENTRY+1
 ,ХТА,=:462.В44,В42=1; Т=9
 М4,АТХ,TDEF1
 М3,UТМ,2
 ,IТА,М3
 ,АТХ,ATENTRY
 ,ХТА,В46
 ,АТХ,CHAR
 ,UJ,WRPC
С-------------------------------------------
 SELECT:,BSS,
С
С     ГЛАВНЫЙ ПЕРЕКЛЮЧАТЕЛЬ ПО СОСТОЯНИЯМ
С
  ,ХТА,ТСОР
  12,VТМ,W
  12,АТХ,
  ,IТА,12
  ,CALL,KHGET
  ,ATI,14
  ,ХТА,W+1
  ,АТХ,CHAR
  14,VZM,IFEND
 ,ARX,
 ,U1А,SSW
С   КОМАНДА:  У НЕЕ СПЕЦИАЛЬНЫЙ КЛЮЧ.
 ,ХТА,CHARCOM.ВОТ ОН
  SSW:,АТХ,W
 М5,UТС,М/SOST-2
 ,АРХ,
 ,ASN,64-2
 ,УТА,
 ,ATI,14
 14,UJ,*+1
 :,UJ,ERR7.НЕВЕРНО
 :,UJ,JUMP.ПЕРЕХОД
 :М5,UJ,SWSOST-3.СВОЕ СОСТОЯНИЕ
 :М5,UJ,SWSOST-3.НА ВСЯК.СЛУЧАЙ
 IFEND:,ХТА,ТСОР
 ,АЕХ,=6HEND
 ,UZA,/END
 ,ХТА,ТСОР
 ,АЕХ,=6Н
 ,U1А,ERR3.ОШ. В КОП
С     ПУСТЫШКА: У НЕЕ ТОЖЕ ЕСТЬ
С     СПЕЦИАЛЬНЫЙ КЛЮЧ.
 ,ХТА,CHARSPAC.
 ,АТХ,CHAR
 ,UJ,SSW
 JUMP:,ХТА,W
 ,ASN,64+15
 ,ААХ,С17
 ,ATI,М5
 ,АТХ,SOST
 ,ASN,64+4
 ,УТА,
 ,АОХ,DEF1
 ,АТХ,DEF1
 М5,UJ,SWSBEG-3.
С    ПЕРЕКЛЮЧАТЕЛЬ НАЧАЛЬНЫХ УСТАНОВОК ПРИ
С    ПЕРЕХОДЕ В НОВОЕ СОСТОЯНИЕ.
 SWSBEG:,UJ,BEGEXT.3
 :,UJ,BEGMAC.4
 :,UJ,ENDMAC.5
 :,UJ,BEGDEF.6
 :,UJ,BEGDAT.7
 :,UJ,BEGSET.8
 :,UJ,BEGCMD.9
С  . . . . . . . . . . . . . . . . . . . . .
 SWSOST:,BSS,
С    ПЕРЕКЛЮЧАТЕЛЬ ПО СОСТОЯНИЯМ
 :,UJ,PUTEXT.3
 :,UJ,PUTMACRO.4
 :,033,5.НЕ БЫВАЕТ
 ,SJ,
 :,UJ,PUTDEF.6
 :,UJ,PUTDAT.7
 :,UJ,PUTSET.8
 :,UJ,PUTCMD.9
 BEGEXT:,BSS,
 PUTEXT:,BSS,
 ,XR1,NLI
 ,АТХ,NLINE
 ,ХТА,W
 ,ААХ,С17
 ,АЕХ,В3
 ,UZA,SWEQ
 ,IDENT,ТМТ
 ,UZA,ERR4
 ,WТС,ATEXTERN
 М3,VТМ,
 М3,UТС,TEXTERN
 ,АТХ,
 ,CALL,WRITID
 14,V1М,ERR5
 ,IТА,М3
 ,ASN,64+1
 ,АОХ,=:14.3
 М4,АТХ,TDEF1
 ,IТА,М3
 ,ARX,В2
 ,АТХ,ATEXTERN
С    РАЗБОР АДРЕСНОЙ ЧАСТИ ИНСТРУКЦИЙ
С    SUBP, LСВ, РСВ, ССВ, РССВ
 ,ХТА,CHAR
 ,ААХ,С17
 ,АЕХ,В1
 ,U1А,ERR11
 ,ХТА,CHAR
 ,ASN,64+6
 ,ААХ,С7
 ,U1А,EXBLOCK
С    ИНСТРУКЦИЯ  SUBP,  В АДРЕСЕ
С    ДОЛЖНА БЫТЬ КОНСТРУКЦИЯ ХХ/N
 М6,VТМ,SВР
 EXPAR:,BSS,.М6,VJM,EXPAR
 0,UТС,-1
 EXPAR/:М7,VТМ,
 ,ХТА,
 ,АТХ,W
 М8,VТМ,-1
 LSBA:,BSS,
 М7,UТС,
 :,CALL,SYMBOL
 14,VZM,*-1
 М7,VТМ,
 ,АОХ,W
 ,ASN,64-8
 ,АТХ,W
 14,UТС,TABSYM+40В
 ,ХТА,
 ,ААХ,В42
 ,UZA,ERR8.НЕ БУКВА
 М8,VLM,LSBA
 ,ХТА,W
 ,ААХ,=6Н'''''''240''240'''
 ,U1А,ERR8.ТОЛЬКО БОЛЬШИЕ   ЛАТИНСКИЕ
 ,ХТА,W
 ,АРХ,=6Н'''''''37''37'''
 ,ASN,64+3
 ,АТХ,W2
 :,CALL,SYMBOL
 14,VZM,*-1
 14,UТМ,40В-57В
 14,V1М,NONPR.НЕ /
С    ПРОШЕЛ  /
 ,CALL,PARAM.ТЯНЕМ  N
 14,UТМ,-2
 14,VIM,IFXX/*.НЕ ЧИСЛО
 ,ASN,64+8
 ,АТХ,W
 ,УТА,
 ,ASN,64+13
 WRNPR:,АОХ,W2
 ,АТХ,W2
 ,ХТА,W
 ,U1А,ERR8.МНОГО ПАРАМ.
 ,ХТА,W2
 М6,UJ,
 IFXX/*:14,UТМ,2-3
 14,V1М,ERR8.НЕ ПУСТО
 ,UТС,СТАВ
 ,ХТА,
 ,АЕХ,=52
 ,U1А,ERR8
 ,АТХ,W
 ,ХТА,=:0000 377
 ,UJ,WRNPR
 NONPR:,BSS,
 14,UТС,TABSYM+57В
 ,ХТА,
 ,ААХ,=:0000 14."КС",";"
 ,UZA,ERR8
С    П/П  БЕЗ ПАРАМЕТРОВ
 ,ХТА,W2
 М6,UJ,
 SВР:,UТС,TEXTERN
 М3,АТХ,1
 ,UJ,WRPC
С
С  . . . . . . . . . . . . . . . . . .
С
 EXBLOCK:,BSS,
С       LСВ,  РСВ,  ССВ,  РССВ
 ,ATI,14
 14,UТС,JTEXTERN-1
 ,ХТА,
 ,АТХ,W2
 ,IТА,М3
 ,ASN,64-19
 ,АОХ,=:24.5
 ,CALL,TBLOCK
 14,VIM,ERROR
 ,АОХ,W2.ДОБАВИМ
 ,UТС,TEXTERN.ДЛИНУ
 М3,АТХ,1.БЛОКА
 ,UJ,CONTINUE
 BEGMAC:,BSS,
 ENDMAC:,BSS,
 BEGDAT:,BSS,
 BEGSET:,BSS,
 PUTDAT:,BSS,
 PUTSET:,BSS,
 PUTMACRO:,BSS,
С     ПУСТЫШКА
 ,033,1
 ,SJ,
 BEGDEF:,BSS,
 13,VJM,BEGVAR
 PUTDEF:,BSS,
 ,ХТА,ADEF
 ,АОХ,TYPSS
 ,АТХ,NLINE
 ,ХТА,CHAR
 ,ААХ,С17
 ,АЕХ,В3.4
 ,UZA,SWEQN
С
С     ОБРАБОТКА МЕТКИ
С
 ,ХТА,SСН
 ,ASN,64-1
 ,ААХ,=:6
 ,UZA,SWDEF.БЕЗ МЕТКИ
 ,ARX,
 ,U1А,ERR4.ЧИСЛО В МЕТКЕ
 ,IDENT,ТМТ
 ,UZA,ERR4
 ,CALL,WRITID
 14,V1М,IFENTRY1
 WRDT:,ХТА,ADEF
 ,АОХ,TYPTD
 М4,АОХ,TDEF1
 М4,АТХ,TDEF1
С
С    СОСТОЯНИЕ  6 ,  ПРИЕМ ПЕРЕМЕННЫХ
С    И КОНСТАНТ.
С
 SWDEF:,BSS,
 ,ХТА,CHAR
 ,ASN,64+6
 ,ААХ,С37
 ,ATI,14
 14,UJ,*+1
 :,UJ,/VAR.0
 :,UJ,/CONST.1
 :,UJ,/ISO.2
 :,UJ,/ISOZ.3
 :М5,VТМ,-3.4 НЕХ
 ,UJ,LISTD
 :М5,VТМ,-3.5 ОСТ
 ,UJ,LISTD
 :М5,VТМ,-3.6 INT
 ,UJ,LISTD
 :М5,VТМ,-2.7 INTH
 ,UJ,LISTD
 :М5,VТМ,-1.8 INTQ
 ,UJ,LISTD
 :М5,VТМ,0.9 INTB
 ,UJ,LISTD
 :М5,VТМ,-3.10 REAL
 ,UJ,LISTD
 :М5,VТМ,-4.11 DREAL
 ,UJ,LISTD
 :,ХТА,   .12 BSS
 ,UJ,/BSS
 :,ХТА,В22.13 BSSZ
 ,UJ,/BSS
 :,UJ,ERR11.DАТА
 :,UJ,ERR11.SЕТ
 :,UJ,ERR11.CMD
 IFENTRY1:,BSS,
 М4,ХТА,TDEF1
 ,ААХ,В41
 ,U1А,ENTRY1
 М4,ХТА,TDEF
 ,UJ,ERR5.ДВАЖДЫ ОП. ИД.
 ENTRY1:М4,ХТА,TDEF1.ГАСИМ ПРИЗНАК
 ,ААХ,В44.НЕОПРЕД.
 М4,АТХ,TDEF1
 ,UJ,WRDT
 SWEQN:,ХТА,ADEF
 ,U1А,SWEQ
 ,XR1,NLI
 ,АТХ,NLINE
 SWEQ:,BSS,
 ,ХТА,CHAR
 ,ААХ,В14
 ,U1А,/BLOCK
 ,ХТА,SСН
 ,ASN,64-1
 ,ААХ,=:6
 ,UZA,SWG4
 ,ARX,
 ,U1А,ERR4.ЧИСЛО В МЕТКЕ
 ,IDENT,ТМТ
 ,UZA,ERR4
 ,CALL,WRITID
 14,VIM,IFREQ
 SWG4:,ХТА,CHAR
 ,ASN,64+6
 ,ААХ,С17
 ,ATI,14
 14,UJ,*+1
 :,UJ,/SРАСЕ.ПУСТО
 :,UJ,/BLOCK
 :,UJ,/EQU
 :,UJ,/REQ
 IFREQ:,ХТА,CHAR
 ,ААХ,В15.REQ
 ,UZA,ERR5
 М4,ХТА,TDEF1
 ,ААХ,В42
 ,UZA,ERR5.НЕЛЬЗЯ
 ,UJ,/REQ
 /CONST:,ХТА,ADEF
 ,WТС,TYPDEF
 ,АТХ,AVAR
 ,ХТА,В1
 ,АТХ,TYPDEF
 ,ХТА,В14
 ,АТХ,TYPSS
 ,ХТА,ACONST
 ,АТХ,ADEF
 ,ХТА,TYPTDCNT
 ,АТХ,TYPTD
 ,UJ,WRPC
 /VAR:,ХТА,ADEF
 ,WТС,TYPDEF
 ,АТХ,AVAR
 13,VТМ,WRPC
 BEGVAR:,ХТА,
 ,АТХ,TYPDEF
 ,ХТА,В15
 ,АТХ,TYPSS
 ,ХТА,AVAR
 ,АТХ,ADEF
 ,ХТА,TYPTDVAR
 ,АТХ,TYPTD
 13,UJ,
 /BSS:,BSS,
 ,АТХ,W2
 ,ХТА,SСН
 ,ASN,64-7
 ,UZA,WRPC.ПУСТОЙ BSS
 ,ARX,
 ,UZA,ERR8
 ,ХТА,TYPTD
 ,АОХ,ADEF
 ,CALL,TBLOCK
 14,V1М,ERROR
 ,АТХ,W
 ,ASN,64+19
 ,U1А,ERR8
 ,ХТА,W
 ,АОХ,W2
 ,АОХ,DEF1
 ,АТХ,DEF1
 ,ААХ,В20/01
 ,ARX,ADEF
 ,АТХ,ADEF
 ,UJ,CONTINUE
 /REQ:,UТС,В42
 /EQU:,ХТА,
 М4,АТХ,TDEF1
 ,ХТА,SСН
 ,ААХ,В47
 ,UZA,ERR9
 ,IТА,М4
 ,АТХ,DEF2
 ,ХТА,SСН
 ,ААХ,В40
 ,UZA,IFLEQ
С
С    В АДРЕСНОЙ ЧАСТИ EQU - ВЫРАЖЕНИЕ
 М9,VТМ,TADS.УСТАНОВИМ
 МА,VТМ,.УКАЗАТЕЛИ PARAM
 М6,CALL,TRAD*.И ПОПРОБУЕМ     ВЫЧИСЛИТЬ
 14,UJ,*+1
 :,UZA,ERR8.ОШИБКА
 ,UJ,ERR12
 :,UJ,WTD
 :,ХТА,В48.НЕВЫЧИСЛИМО
 WTD:,WТС,DEF2
 М4,VТМ,
 М4,АОХ,TDEF1
 М4,АТХ,TDEF1
 ,ААХ,В48
 ,АОХ,DEF2
 ,АТХ,DEF2
С
С     ДЛЯ ЭКВИВАЛЕНТНОСТЕЙ DEF2 СОДЕРЖИТ:
С
С  48    - НАДО ВЫЧИСЛЯТЬ АДРЕСНОЕ ВЫРАЖЕ-
С          НИЕ НА ВТОРОМ ПРОХОДЕ
С  12:01 - АДРЕС ПО TDEF
С
 ,UJ,WRPC
 IFLEQ:,BSS,
 ,ХТА,SСН
 ,ААХ,В41
 ,UZA,ERR8
 :,CALL,S/SYMBOL
 14,VZM,*-1
 ,CALL,LITERAL
 14,VIM,ERROR
 М4,АОХ,TDEF1
 М4,АТХ,TDEF1
 МС,VIM,CONTINUE
 ,UJ,WRPC
 /SРАСЕ:,ХТА,ADEF
 ,АОХ,TYPTD
 М4,АТХ,TDEF1
 ,UJ,WRPC
 /BLOCK:,BSS,
С
С    ДЛЯ ИНСТРУКЦИИ  BLOCK  DEF2 СОДЕРЖИТ:
С
С  48    - НА ВТОРОМ ПРОХОДЕ НАДО ПРОВЕРИТЬ
С          ОПРЕДЕЛЕННОСТЬ ИДЕНТИФИКАТОРА
С  12:01 - ЕГО АДРЕС ПО TDEF.
С
 ,ХТА,SСН
 ,ААХ,=:3
 ,UZA,ERR9
 ,ASN,64-1
 ,ARX,
 ,U1А,NUM/BL.ЧИСЛО В МЕТКЕ
С    BLOCK-ЭКВИВАЛЕНТНОСТЬ ИДЕНТИФИКАТОРУ.
 ,IDENT,ТМТ
 ,UZA,ERR4
 ,АТХ,W
 ,CALL,CHID
 М4,VIM,HBL
С    В ПОЛЕ МЕТКИ - НЕОПРЕДЕЛЕННЫЙ ИДЕНТИФ.
 ,ХТА,W
 ,CALL,WRITID
 ,ХТА,=:4420.В41=1,Т=9
 М4,АТХ,TDEF1
 ,IТА,М4
 ,АОХ,В48
 ,АТХ,DEF2
 ,UJ,INTEQBL.ПРОСТ.ВНУТР.EQ
 HBL:,BSS,
 М4,ХТА,TDEF1
 ,ASN,64-4
 ,УТА,
 ,ATI,14
 14,UJ,*+1
 :,UJ,ABSBL.АБС. АДРЕС
 :,UJ,ERR4.КОМАНДА
 :,UJ,ABSBL.ПЕРЕМЕННАЯ
 :,UJ,EXEQBL.ВНЕШН.ССЫЛКА
 :,UJ,ABSBL.КОНСТАНТА
 :,UJ,EXEQS.ИЗ ВНЕШН. БЛОКА
 :,IТА,М4
 ,UJ,INTEQBL.ИЗ ВНУТР. БЛОКА
 :,UJ,ERR14.ЛИТЕРАЛ
 :,IТА,М4.НЕОПР. ЭКВИВАЛ.
 ,UJ,INTEQBL
 :,IТА,М4.НЕОПР. ИДЕНТ.
 ,UJ,INTEQBL.
 EXEQBL:,BSS,
 М4,ХТА,TDEF1
 ,ААХ,С7777
 ,ASN,64-20
 ,АОХ,=:24
 ,UJ,MBLOCK
 INTEQBL:,BSS,
 ,ASN,64-20
 ,АОХ,=:3
 MBLOCK:,CALL,TBLOCK
 14,VIM,ERROR
 ,UJ,CONTINUE
 EXEQS:,BSS,
 ABSBL:М4,ХТА,TDEF1
 ,ААХ,MATD1
 ,UJ,MBLOCK
 NUM/BL:,BSS,
 М9,VТМ,ТМТ.ИЗ МЕТКИ
 МА,VТМ,.НАЧ УСТАНОВКА
 ,CALL,PARAM
 14,UJ,*+1
 :,UJ,ERR4.ОШ В МЕТКЕ
 :,UJ,ERR11.АВАРИЯ
 :,UJ,ISNBL.ЧИСЛО
 :М8,UТМ,-3.ПУСТО
 М8,VIM,ERR4.=> НЕ МИНУС
С       ЗНАК МИНУС
 ,CALL,PARAM
 14,UТМ,-2
 ,VIM,ERR4
 ,АЕХ,В32/01
 ,ARX,В1
 ISNBL:,ААХ,В32/01
 ,АТХ,W
 ,UJ,MBLOCK
 /ISOZ:,UТС,В48
 /ISO:,ХТА,
 ,АТХ,W
 0,UТС,-1
 :,CALL,SYMBOL
 14,VZM,*-1
 14,UТМ,40В-42В
 14,VIM,ERR8.НЕ "
 14,VТМ,WRPC
 ,IТА,14
 ,АОХ,W
 ,CALL,FORMTX
 14,VIM,ERROR
 ADDL:,ARX,ADEF
 ,АТХ,ADEF
 ,UJ,CONTINUE
 LISTD:,BSS,
 14,VТМ,WRPC
 ,IТА,14
 ,CALL,LLIST
 М5,ASN,64
 ,ARX,С7
 ,ASN,64+3
 ,UJ,ADDL
 BEGCMD:,BSS,
 ,ХТА,
 ,АТХ,АСОМ
 ,АТХ,NLINE
 ,АТХ,F/MTNEXT
 ,ХТА,ADEF
 ,WТС,TYPDEF
 ,АТХ,AVAR
 ,ХТА,CHAR
 ,АЕХ,=2003.CMD
 ,ААХ,С7777
 ,UZA,WRPC
С
С       ПРИЕМ   КОМАНД
С
 PUTCMD:,BSS,
 ,ХТА,CHAR
 ,ARX,
 ,UZA,COMMAND.КОМАНДА
 ,ААХ,С17
 ,АЕХ,В3.4
 ,U1А,ERR14.АВАРИЯ
 ,ХТА,CHAR
 ,ASN,64+6
 ,ААХ,С17
 ,ATI,14
 14,UJ,*+1
 :,UJ,CSPACE.ПУСТОЙ КОП
 :,UJ,/BLOCK.BLOCK
 :,UJ,SWEQ.EQU
 :,UJ,SWEQ.REQ
 IFENTRYC:М4,ХТА,TDEF1
 ,ААХ,В41
 ,U1А,ENTRYC
 М4,ХТА,TDEF
 ,UJ,ERR5.ДВАЖДЫ ОП.ИД.
 ENTRYC:М4,ХТА,TDEF1
 ,ААХ,В44
 М4,АТХ,TDEF1
 ,ХТА,В47
 ,АОХ,NLINE
 ,АТХ,NLINE
 ,UJ,WRIDC
 CSPACE:,BSS,
 ,ХТА,В45
 ,АТХ,F/MTNEXT
 ,ХТА,АСОМ
 ,ARX,В1
 ,ASN,64+1
 ,АТХ,W
 ,АОХ,В48
 ,АТХ,NLINE
 ,ХТА,SСН
 ,ААХ,=:34
 ,UZA,ERR14
 ,ANX,
 ,ATI,14
 14,UJ,*-1
 :,UJ,PUTIDC.2
 :,UJ,ERR4.3
 :,UJ,WRPC.4 (БЕЗ МЕТКИ)
 COMMAND:,BSS,
 ,ХТА,
 ,АТХ,NLINE
 ,ХТА,SСН
 ,АОХ,F/MTNEXT
 ,АТХ,SСН
 ,ААХ,В45
 ,UZA,ADDAC.НЕ ПОМЕЧЕНО
 ,ХТА,SСН
 ,ANX,
 ,ATI,14
 14,UJ,*-1
 :13,VТМ,PUTIDC.2
 ,UJ,NEWWORD
 :,UJ,ERR4.3
 :13,VТМ,IFADDAC.4
 NEWWORD:,BSS,
С
С  DEF2 : ИНФОРМАЦИЯ ДЛЯ ВТОРОГО ПРОХОДА:
С     В48 - ПЕРЕД ТЕКУЩЕЙ КОМАНДОЙ ВСТАВИТЬ
С           "UТС"
С     В45 - ПОМЕЧЕНО
С     В41 - В АДРЕСЕ - ЛИТЕРАЛ
С     В40 - ЛИТЕРАЛ НА ОДНОЙ СТРОКЕ
С     В20:01 - АДРЕС ЛИТЕРАЛА ПО TLT (64-Р)
С              (ДЛЯ НЕТЕКСТОВЫХ ЛИТЕРАЛОВ).
С
 ,ХТА,АСОМ
 ,ААХ,В1
 ,ASN,64+1
 ,УТА,
 ,АОХ,В45
 ,АТХ,DEF2
 ,ХТА,АСОМ
 ,ARX,В1
 ,ASN,64+1
 ,АТХ,W
 ,ASN,64-1
 ,АТХ,АСОМ
 13,UJ,
 PUTIDC:,BSS,
 ,IDENT,ТМТ
 ,CALL,WRITID
 14,VIM,IFENTRYC
 WRIDC:,BSS,
 ,ХТА,В45
 ,АОХ,W
 М4,АОХ,TDEF1
 М4,АТХ,TDEF1
 IFADDAC:,ХТА,CHAR
 ,ААХ,В13
 ,U1А,WRPC
 ADDAC:,BSS,
 ,ХТА,АСОМ
 ,ASN,64+1
 15,АТХ,
 ,УТА,
 ,АЕХ,В48
 15,АОХ,
 ,АОХ,NLINE
 ,АТХ,NLINE
 ,ХТА,АСОМ
 ,ARX,В1
 ,АТХ,АСОМ
 ,ААХ,В1
  ,UZA,NOMOZY
  ,ХТА,MOZY
  ,UZA,NOMOZY
  ,ARX,NLINE
  ,ASN,64-28
  ,AUX,=6Н'17''17''17''17''17'''
  15,АТХ,
  ,ARX,=6Н'166''166''166''166''166'''
  ,ААХ,=6Н'160''160''160''160''160'''
  ,ASN,64+4
  ,АЕХ,=6Н77777-
  15,ARX,
  ,АТХ,NUMBER
  NOMOZY:,BSS,
  ,ХТА,CHAR
  ,ASN,64-20
  ,AUX,=6Н'17''17'''''''''
  15,АТХ,
  ,ARX,=6Н'166''166'''''''''
  ,ААХ,=6Н'160''160'''''''''
  ,ASN,64+4
  ,АЕХ,=6Н77
  15,ARX,
  ,АТХ,NUMBER+1
С-----------------
 ,ХТА,CHAR.ПОМЕЧЕННОСТЬ
 ,ААХ,В45.СЛЕД. КОМАНДЫ
 IFLAC:,АТХ,F/MTNEXT
С
С   ПРОВЕРКА :  НЕ ВНЕШНИЙ-ЛИ АДРЕС
С
 ,ХТА,CHAR
 ,ААХ,В1
 ,UZA,IFLTR
С
С     В АДРЕСЕ - ВНЕШНЕЕ ИМЯ
С
С     В ЭТОМ СЛУЧАЕ  DEF2  СОДЕРЖИТ
С  НОМЕР ПО ТАБЛ. ОПИСАНИЙ ЗАГРУЗЧИКА.
С
 ,CALL,S/PARAM
 14,UТМ,-1
 14,VIM,ERR8
 ,CALL,WRITID.ЗАПИСЬ ИМЕНИ
 14,VIM,SР/IS.=> УЖЕ ЕСТЬ!
 М8,UТМ,-4
 М8,VIM,ERR15.НЕТ ТОЧКИ
 М6,VJM,EXPAR/
 ,WТС,ATEXTERN
 14,VТМ,
 14,UТС,TEXTERN
 ,АТХ,1
 М4,ХТА,TDEF
 14,UТС,TEXTERN
 ,АТХ,
 ,IТА,14
 ,ASN,64+1
 ,АОХ,=:14
 М4,АТХ,TDEF1
 ,ААХ,В20/01
 ,АОХ,DEF2
 ,АТХ,DEF2
 14,UТМ,2
 ,IТА,14
 ,АТХ,ATEXTERN
 ,UJ,WRPC
 SР/IS:,BSS,.ОПИСАНИЕ БЫЛО
 М8,UТМ,-4
 М4,ХТА,TDEF1
 ,ASN,64-4
 ,УТА,
 ,АЕХ,С3
 ,U1А,ERR5A.НЕ ВНЕШН.
 М4,ХТА,TDEF1
 ,ASN,64-1
 ,ATI,М3
 ,ASN,64+1
 ,АОХ,DEF2
 ,АТХ,DEF2
 М3,UТС,TEXTERN
 ,ХТА,1
 ,ААХ,=:7
 ,U1А,ERR5A.COMMON
 М8,VIM,WRPC.=> НЕТ Х-КИ -     - И НЕ НАДО!
 М6,VJM,EXPAR/
 М3,UТС,TEXTERN
 ,ХТА,1
 ,АТХ,W.ЧТО БЫЛО
 ,АЕХ,В45/36
 ,ААХ,В45/36
 ,UZA,IFP*
 ,АЕХ,В45/36
 ,АЕХ,W2.ЧТО ЕСТЬ
 ,ААХ,В45/36
 ,U1А,ERR15.НЕ СОВПАЛО
 IFP*:,ХТА,W
 ,АЕХ,W2
 ,ААХ,В35/28
 ,UZA,WRPC.СОВПАЛО
 ,ХТА,В35/28
 М3,UТС,TEXTERN
 ,АОХ,1
 М3,UТС,TEXTERN
 ,АТХ,1
 ,UJ,WRPC
 IFLTR:,BSS,
С
С   ПРОВЕРКА:  НЕ ЛИТЕРАЛ-ЛИ
С
 ,ХТА,SСН
 ,ААХ,В41
 ,UZA,WRPC.=> НЕ ЛИТЕРАЛ
С      ЗАПИСЬ   ЛИТЕРАЛА
 ,АОХ,DEF2
 ,АТХ,DEF2
 0,UТС,-1
 :,CALL,SYMBOL
 14,VZM,*-1
 ,CALL,LITERAL
 14,VIM,ERROR
 ,ААХ,В20/01
 МС,V1М,CTL
 ,АОХ,В40
 ,АОХ,DEF2
 ,АТХ,DEF2
 ,UJ,WRPC
 CTL:,BSS,
С   ТЕКСТОВЫЙ ЛИТЕРАЛ В КОМАНДАХ. УЖЕ
С   ПРОЧИТАНА СЛЕДУЮЩАЯ КАРТА, ПОЭТОМУ
С   АДРЕС ЛИТЕРАЛА ПО TLT ЗАПИСЫВАЕМ
С   В  DEF1.
С
 ,ASN,64-24
 ,АОХ,DEF1
 ,АТХ,DEF1
 ,UJ,CONTINUE
С
С   ----------------------------
С
 /END:,BSS,
С    КОНЕЦ ПЕРВОГО ПРОХОДА
 13,VJM,NEWWORD
 ,ХТА,=:7
 ,АТХ,CHAR
 ,ХТА,=:5.SOST=10
 ,АОХ,DEF1
 ,АТХ,DEF1
 ,ХТА,W
 ,АТХ,ABDEF.НАЧ. ОПИСАНИЙ
 ,ARX,ACONST.ДЛИНА КОНСТАНТ
 ,АТХ,ABLIT.НАЧ ЛИТЕРАЛОВ
 ,ARX,LLIT.ДЛИНА ЛИТ.
 ,АТХ,NLINE
 ,АТХ,LSUBP
 ,ХТА,ATEXTERN
 ,ASN,64+1
 ,ASN,64-6
 ,АТХ,WDEFLD
 ,ARX,В7
 ,АТХ,ADEFLD
 13,VJM,WRPCR
 ,*70,IW70.НЕ ГЛЯДЯ...
 ,ХТА,
 ,UТС,FRPC
 ,АТХ,
 ,UJ,МКВ/ASS/
С
 COMMENT:,ХТА,STR
  ,АЕХ,=6Н;MOZY=
  ,U1А,WRPC
  ,ХТА,STR+1
  ,ААХ,=6Н@@@@@''
  ,ASN,64+6
  15,АТХ,
  ,ASN,64-3
  15,АОХ,."9"
  ,ARX,STR+1
  ,АРХ,=6Н'17''17''17''17''17'''
  ,ASN,64+28
  ,АТХ,MOZY
С
С====================================
С
 WRPC:,ENTRY,
 WRPC:13,VТМ,NEWCARD
 WRPCR:,BSS,
С
С   ЗАПИСЬ СТРОКИ ПРОМЕЖУТОЧНОГО КОДА
С   НА МБ.
С
 14,VТМ,1-LРС
 LSC:14,ХТА,BEGPC+LРС-1
 ,UZA,ELSC
 14,ХТА,В48+LРС-1
 ,АОХ,SCWORDS
 ,АТХ,SCWORDS
 ELSC:14,VLM,LSC
 ,WТС,S/МВ
 МВ,VТМ,
 ,ХТА,SCWORDS
 15,АТХ,
С.....  I14 = 0 !
 WRWPC:МВ,UТС,PAGE+1023
 ,АТХ,
 ,ХТА,
 14,SТХ,BEGPC-1.ЧИСТКА П/КОДА
 МВ,VLM,ELWR1
 ,ITS,13
 15,АТХ,
 ,*70,IW70
 ,ХТА,IW70
 ,CALL,DRUMTAP*
 ,SТХ,IW70
 ,STI,13
 МВ,VТМ,-1023
 ELWR1:,BSS,
 ,UZA,ELWR
 ,ANX,
 ,ATI,14
 ,УТА,
 14,ASN,64
 14,XTS,BEGPC-1
 ,UJ,WRWPC
 ELWR:,IТА,МВ
 ,АТХ,S/МВ
 13,UJ,
 RETURN:,ENTRY,
 ,WТС,SI13
 ,UJ,
С
С       ЗНАЧЕНИЯ КОДОВ ОШИБОК:
С
С   1 - НЕТ ЗАГОЛОВКА П/П
С   2 - ПОВТОРНОЕ ОПРЕДЕЛЕНИЕ ВХОДА
С   3 - ОШИБКА В КОП
С   4 - ОШИБКА В МЕТКЕ
С   5 - ДВАЖДЫ ОПИСАННЫЙ ИДЕНТИФИКАТОР
С   6 - ОШИБКА В МОДИФИКАТОРЕ
С   7 - НЕВЕРНЫЙ ПОРЯДОК СЛЕДОВАНИЯ
С       ИНСТРУКЦИЙ
С   8 - ОШИБКА В АДРЕСЕ
С   9 - НЕТ МЕТКИ
С  10 - ВХОД НЕ ОПРЕДЕЛЕН
С  11 - НЕРЕАЛИЗОВАННЫЙ ОПЕРАТОР
С  12 - НЕОПИСАННЫЙ ИДЕНТИФИКАТОР
С  13 - ПЕРЕПОЛНЕНИЕ ТАБЛИЦ ТРАНСЛ.
С  14 - АВАРИЯ ТРАНСЛЯТОРА
С  15 - ПЛОХОЙ ИНТЕРФЕЙС ПОДПРОГРАММЫ
С
 ERR1:14,VТМ,1
 ,UJ,ERROR
 ERR2:14,VТМ,2
 ,UJ,ERROR
 ERR3:14,VТМ,3
 ,UJ,ERROR
 ERR4:14,VТМ,4
 ,UJ,ERROR
 ERR5A:М4,ХТА,TDEF
 ERR5:14,VТМ,5
 ,UJ,ERROR
 ERR6:14,VТМ,6
 ,UJ,ERROR
 ERR7:14,VТМ,7
 ,UJ,ERROR
 ERR8:14,VТМ,8
 ,UJ,ERROR
 ERR9:14,VТМ,9
 ,UJ,ERROR
 ERR11:14,VТМ,11
 ,UJ,ERROR
 ERR12:14,VТМ,12
 ,UJ,ERROR
 ERR14:14,VТМ,14
 ,UJ,ERROR
 ERR15:14,VТМ,15
 ,UJ,ERROR
 ERROR:,АТХ,ERRIDENT
 ,IТА,14
 ,АТХ,NERR
 ,АТХ,ERRORS
 ,UJ,WRPC
 ERRSYNT:,BSS,
 14,VТМ,-1
 ,UJ,ERROR
 MI70:,000,NР
 ,010,
 JTEXTERN:,ОСТ,4.LСВ
 ,ОСТ,5.РСВ
 ,ОСТ,2.ССВ
 ,ОСТ,3.РССВ
 М/SOST:,ОСТ,0 2000 2000.2
 ,ОСТ,0 1000 1000.3
 ,ОСТ,0 0400 0400.4
 ,ОСТ,0 0200 0200.5
 ,ОСТ,0 0100 0100.6
 ,ОСТ,0 0040 0040.7
 ,ОСТ,0 0020 0020.8
 ,ОСТ,0 0010 0010.9
 CHARCOM:,ОСТ,4 0430 3310 11
 CHARSPAC:,ОСТ,0 0570 3200 06 1 00 04
 В45/36:,ОСТ,0777 4
 В35/28:,ОСТ,0000 377
 BL:,BSS,LBL
 ,LIST,*
 ,END,МКВ/ASS*

 МКВ/ASS/:1,NАМЕ,***.ВТОРОЙ ПРОХОД
 ,BITS,
 ,DEFLC,
 TABSYM:,SUBP,
 TRAD:,SUBP,
 *ERRORS*:,LС,1
 *TENTRY*:LС,BLOCK,(1),LONGS,TENTRY
 *EXTERN*:LС,BLOCK,(1),TEXTERN
 *TSS*:LС,BLOCK,TSS
 TLT:,LС,.ЛИТЕРАЛЫ
 WСВ:,LС,.РАБ. БЛОК
 BL:I,BLOCK,SOST.СОСТОЯНИЕ
 ,CONT,W       .РАБ. ЯЧ
 ,CONT,ADR     .РАБ. ЯЧ
 ,CONT,LЕХТ    .ДЛИНА БЛОКА
 ,CONT,AENSOST .ОБРАБОТЧИК КОН- ЦА СОСТОЯНИЯ
 ,CONT,BEGLIST
 ,CONT,ENDLIST
 ,CONT,ABEGCOD.АДР.НАЧ.ТЕЛА П/П
 ,CONT,TYPCOD
 ,CONT,NDEF.АДР. ДЛЯ ПЕЧАТИ.
 ,CONT,W2
 ,CONT,NIR
 ,CONT,FBCONST
 LBL:,EQU,17
 LВТ:,EQU,100В
С--------------
  ,ХТА,
  14,VТМ,BL-EZER
  *1:14,UТС,EZER
  ,АТХ,
  14,VLM,*1
 М2,VТМ,
 14,VТМ,RЕАСТ
 ,IТА,14
 ,*50,74В
 ,NTR,7
 ,ХТА,LISTFL
  ,U1А,ВВ."*NО LIST"
 ,CALL,DATAPR*
 ВВ:,BSS,
 ,ХТА,В1
 ,АТХ,SOST
 ,АТХ,F/TWO
 14,VТМ,ENDENTRY
 ,IТА,14
 ,АТХ,AENSOST
С
 NEWS:,BSS,
 ,NTR,7
 ,CALL,IРС
 М2,VZM,CONTINUE
 ,ХТА,ТСОР
 ,АЕХ,=6Н    ,
 М2,UJ,
 CONTINUE:,BSS,
 ,ХТА,
 ,АТХ,NDEF
 ,ХТА,NERR
 ,U1А,ERRPRINT
 ,ХТА,CHAR
 ,U1А,SELECT
 ,ХТА,ТСОР
 ,АЕХ,=6Н    ,
 ,UZA,PRINTL.CONT
 ,UJ,COMMENT.КОММЕНТАРИЙ
 SELECT:,BSS,
 ,ХТА,DEF1
 ,UZA,OLDSOST
 ,ААХ,В24/01
 ,UZA,NEWSOST
С    ЗАПИСЬ УКАЗАНИЯ О РЕЗЕРВИРОВАНИИ
С    ПАМЯТИ.
 ,ASN,64-3
 ,АОХ,=:1400 0000 0000 0005
 ,АТХ,STR
 ,WRITM,STR,В1
 NEWSOST:,ХТА,DEF1
 ,ASN,64-4
 ,УТА,
 ,UZA,OLDSOST
 ,АТХ,SOST
 ,ХТА,AENSOST
 ,UZA,SWBSOST
 ,ATI,14
 14,UJ,
С
   :15,ХТА,
 SWBSOST:,WТС,SOST
 ,UJ,*-1
 :,UJ,BEGENTRY.2
 :,UJ,BEGEXT.3
 :,UJ,BEGMAC.4
 :,UJ,ENDMAC.5
 :,UJ,BEGDEF.6
 :,UJ,BEGDAT.7
 :,UJ,BEGSET.8
 :,UJ,BEGCMD.9
 :,UJ,ENDSUBP.10
 OLDSOST:,BSS,
 ,WТС,SOST
 ,UJ,*
 :,UJ,PUTNAME.1
 :,UJ,PUTENTRY.2
 :,UJ,PUTEXT.3
 :,UJ,PUTMAC.4
 :,033,5.НЕ БЫВАЕТ
 ,SJ,
 :,UJ,PUTDEF.6
 :,UJ,PUTDAT.7
 :,UJ,PUTSET.8
 :,UJ,PUTCMD.9
 BEGMAC:,BSS,
 ENDMAC:,BSS,
 BEGDAT:,BSS,
 BEGSET:,BSS,
 PUTMAC:,BSS,
 PUTDAT:,BSS,
 PUTSET:,BSS,
С       ПУСТЫШКА
 ,033,1
 ,SJ,
 PUTNAME:,BSS,
 ,XTS,ТМТ
 ,АТХ,ERRNAM
 ,ХТА,ТМТ+1
 ,SТХ,ERRNAM+1
 14,VТМ,TENTRY
 ,ITS,14
 ,CALL,ELNAME
 ,NTR,7
 ,ХТА,В2
 ,АТХ,SOST
 ,АТХ,ADR.АДР. ПО TENTRY
 ,UJ,PRINTL
 BEGENTRY:,BSS,
 PUTENTRY:,BSS,
 ,WТС,ADR
 М3,VТМ,
 М3,UТС,TENTRY
 14,VТМ,
 ,ITS,14
 ,CALL,ELEQU
 ,NTR,7
 М3,ХТА,TENTRY+1
 ,ATI,М4.АДР.ПО TDEF
 ,ААХ,=:0777 777
 ,АТХ,W
 М4,ХТА,TDEF1
 ,ARX,
 ,UZA,ERR10
 ,ААХ,В47
 ,UZA,ENIS
 М4,ХТА,TDEF1
 ,CALL,DEFINER
 14,VIM,ERR
 М4,АТХ,TDEF1
 ENIS:,BSS,
 М4,ХТА,TDEF1
 ,АРХ,=:1400 0000 0377 7777
 ,AUX,=:6000 0000 0377 7777
 ,АОХ,W.ТИП ВХОДА
 М3,АТХ,TENTRY+1
 ,АРХ,=:6000 0000 0377 7777
 ,AUX,=:1400 0000 0377 7777
 ,АТХ,NDEF
 М3,UТМ,2
 ,IТА,М3
 ,АТХ,ADR
 ,UJ,PRINTL
 ENDENTRY:,BSS,
С
С     НАЧАЛО ЗАПИСИ ОБ'ЕКТНОГО МОДУЛЯ.
С        ЗАПИСЬ ЗАГОЛОВКА  П/П
 ,ХТА,AVAR
 ,ASN,64-20
 ,АОХ,LSUBP
 ,АТХ,LONGS
 ,ХТА,ATENTRY
 ,ARX,В1
 ,АОХ,В46.ГРУППА 1
 ,UТС,*TENTRY*
 ,АТХ,
 ,ARX,В1
 ,ААХ,С77777
 ,АТХ,W
 ,WRITM,*TENTRY*,W
 ,ХТА,
 ,АТХ,ADR
 ,АТХ,AENSOST
 ,UJ,WRTEX
 BEGEXT:,BSS,
 14,VТМ,ENDEXT
 ,IТА,14
 ,АТХ,AENSOST
 PUTEXT:,BSS,
 ,ХТА,CHAR
 ,АЕХ,В3
 ,ААХ,С77
 ,UZA,SWEQ
 ,UJ,PRINTL
 ENDEXT:,BSS,
 ,ХТА,
 ,АТХ,AENSOST
 ,UJ,WRG4
 WRTEX:,BSS,
С
С     ЗАПИСЬ ТАБЛИЦЫ ВНЕШНИХ ИМЕН.
С
 :,ХТА,ATEXTERN
 ,UZA,WRG4. БЫЛО: SWBSOST
 ,АОХ,=:3
 ,АТХ,*EXTERN*
 ,ARX,В1
 ,ААХ,С77777
 ,АТХ,W
 ,WRITM,*EXTERN*,W
С   БЫЛО:     ,UJ,SWBSOST
 WRG4:,BSS,
С
С      ЗАПИСЫВАЕМ ТЕЛО П/П
С
С   1. З А Г О Л О В О К
С
   ,XTS,ABEGCOD
   ,U1А,SWBSOST-1
  14,VТМ,
       ,CALL,IELADR
 ,NTR,7
 ,АТХ,ABEGCOD
 ,WRITM,0,В1.ПОТОМ ПЕРЕЗАПИШЕМ
 ,ХТА,LCOD
 ,АТХ,LЕХТ
 ,ХТА,
 ,АТХ,LCOD
 ,UJ,SWBSOST
С
С   2. УКАЗАНИЕ ОБ УСТАНОВКЕ НАЧ. АДРЕСА
С      ЗАГРУЗКИ
С
 BEGDEF:,BSS,
 ,ХТА,ABEGCOD
 ,UZA,WRG4
 ,ХТА,=1400 0101 0000 0000
 ,АТХ,STR.ОБЛАСТЬ ПЕРЕМЕН-НЫХ, ТЕГ=1, ADR=0.
 ,WRITM,STR,В1
 ,ХТА,
 ,АТХ,TYPDEF
С
С   3. ЗАПИСЬ ОПИСАНИЙ
С
 PUTDEF:,BSS,
 ,ХТА,CHAR
 ,ААХ,С17
 ,АЕХ,В3.4
 ,UZA,SWEQ
С     ПРИЕМ ПЕРЕМЕННЫХ И КОНСТАНТ
 ,ХТА,CHAR
 ,ASN,64+6
 ,ААХ,С37
 ,ATI,14
 14,UJ,*+1
 :,UJ,/VAR.0
 :,UJ,/CONST.1
 :,UJ,/ISO.2
 :,UJ,/ISOZ.3
 :М4,VТМ,2.4: НЕХ
 ,UJ,/INTHEX
 :,UJ,ERR3.5 РЕЗЕРВ
 :М4,VТМ,1.6: INT
 ,UJ,/INTHEX
 :М4,VТМ,
  ,UJ,/INTH.7
 :М4,VТМ,
  ,UJ,/INTQ.8
 :М4,VТМ,
  ,UJ,/INTB.9
 :,UJ,ERR11.10: REAL
 :,UJ,ERR11.11: DREAL
 :,UJ,PRINTL.12 BSS
 :,UJ,PRINTL.13 BSSZ
 :,UJ,ERR11.DАТА
 :,UJ,ERR11.SЕТ
 :,UJ,ERR11.CMD
 SWEQ:,BSS,
С     ЭКВИВАЛЕНТНОСТИ СРЕДИ ОПИСАНИЙ
 ,ХТА,CHAR
 ,ASN,64+6
 ,ААХ,С17
 ,ATI,14
 14,UJ,*+1
 :,UJ,/SРАСЕ.ПУСТО
 :,UJ,/BLOCK
 :,UJ,/EQU
 :,UJ,/REQ
 /VAR:,BSS,
 ,ХТА,=:1400 0141
 ,АТХ,STR
 ,WRITM,STR,В1
 ,ХТА,
 ,АТХ,TYPDEF
 ,UJ,PRINTL
 /CONST:,BSS,
 ,ХТА,FBCONST
 ,U1А,СС.НЕ ПЕРВЫЙ CONST
 ,ХТА,ABDEF
 ,ASN,64-3
 ,АОХ,=:1400 0002
 WRCN:,АТХ,STR
 ,WRITM,STR,В1
 ,ХТА,В1
 ,АТХ,TYPDEF
 ,UJ,PRINTL
 СС:,ХТА,=:1400 0042
 ,UJ,WRCN
 /ISOZ:,UТС,В48
 /ISO:,ХТА,
 ,АТХ,W
 0,UТС,-1
 :,CALL,SYMBOL
 14,VZM,*-1
 14,UТМ,40В-42В
 14,VIM,ERR8
 14,VТМ,PRINTL
 ,IТА,14
 ,АОХ,W
 ,CALL,FORMTX
 14,VIM,ERR
 ,ASN,64-1
 ,АТХ,W
 ,WRITM,WСВ,W
 ,UJ,CONTINUE
 /INTH:М5,VТМ,
 ,UJ,/INTHEX
 /INTQ:М5,VТМ,1
 ,UJ,/INTHEX
 /INTB:М5,VТМ,2
 /INTHEX:14,VТМ,PRINTL
 ,IТА,14
 ,CALL,FLIST/
 14,VIM,ERR
 ,UJ,CONTINUE
 /SРАСЕ:,BSS,
 ,UJ,PRINTL
 /BLOCK:,BSS,
 ,ХТА,DEF2.НУЖНА
 ,ARX,.ПРОВЕРКА?
 ,U1А,PRINTL.=> НЕТ
С     КОНТРОЛЬ:  НА ПЕРВОМ ПРОХОДЕ В ПОЛЕ
С     МЕТКИ  ИНСТР. BLOCK  СТОЯЛ НЕОПИСАН-
С     НЫЙ ИДЕНТИФИКАТОР.
 ,ATI,14.ЕГО АДР.ПО TDEF
 14,ХТА,TDEF1
 ,ARX,
 ,U1А,PRINTL.=> ОПРЕДЕЛЕН
С      ОН ТАК И НЕ ВСТРЕТИЛСЯ
 14,ХТА,TDEF
 ,UJ,ERR12
 /EQU:,BSS,
 /REQ:,BSS,
 ,ХТА,DEF2
 ,ARX,
 ,U1А,PRINTL
 М6,CALL,TRAD
 14,UJ,*+1
 :,UZA,ERR8.ОШИБКА
 ,UJ,ERR12
 :,UJ,WTD.ВЫЧИСЛЕНО
 :,АТХ,W.НЕВЫЧИСЛИМО
 ,ХТА,STR
 ,АОХ,ADEFLD.FС
 ,АТХ,STR
 ,WRITM,STR,W
 ,ХТА,ADEFLD
 15,АТХ,
 ,ARX,В7
 ,SТХ,ADEFLD
 ,ASN,64+6
 ,АОХ,=:14.ССЫЛКА НА EXTDEF
 WTD:,BSS,
 ,АТХ,NDEF
 ,WТС,DEF2
 14,VТМ,
 14,XTS,TDEF1
 ,ААХ,MTDEF1
 15,АОХ,
 14,АТХ,TDEF1
 ,UJ,PRINTL
С
С
С
 BEGCMD:,BSS,
 ,ХТА,ABEGCOD
 ,UZA,WRG4
С
С     КОНЕЦ   ОПИСАНИЙ
С
С
С    4.  ЗАПИСЬ СПЕЦУКАЗАНИЙ ПО ПЕРЕХОДУ НА
С        ЗАГРУЗКУ КОМАНД.
С
 ,ХТА,=:1400 0004
 ,АТХ,STR.ОБЛ. КОМАНД
 ,ХТА,=1400 0005 0000 0004
 ,АТХ,STR+1.ОДНА ПОМЕЧ. КОМ.
 ,WRITM,STR,В2
С
С     5. ЗАПИСЬ КОМАНД В ОБ'ЕКТНЫЙ МОДУЛЬ
С
 ,ХТА,CHAR
 ,АЕХ,=2003.CMD
 ,ААХ,С7777
 ,UZA,PRINTL
 PUTCMD:,BSS,
С
С     ОЧЕРЕДНОЕ ПОЛУСЛОВО
 ,ХТА,CHAR
 ,ARX,
 ,UZA,PUTCOM/
 ,ААХ,С17
 ,АЕХ,В3.4
 ,U1А,ERR14.АВАРИЯ
 ,ХТА,CHAR
 ,ASN,64+6
 ,ААХ,С17
 ,ATI,14
 14,UJ,*+1
 :,UJ,PRINTL.ПУСТЫШКА
 :,UJ,PRINTL.BLOCK
 :,UJ,/EQU.EQU
 :,UJ,/REQ.REQ
 PUTCOM/:,ХТА,DEF2
 ,UZA,GENCOM.БЕЗ ХИТРОСТЕЙ
 ,ARX,
 ,U1А,IFMTCOM
С
С     ВСТАВЛЯЕМ ПУСТЫШКУ
С
 ,ХТА,=:0000 0011.UТС
 ,АТХ,STR
 ,WRITM,STR,В1
 IFMTCOM:,ХТА,DEF2
 ,ААХ,В45
 ,UZA,GENCOM.НЕ ПОМЕЧЕНО
 ,ХТА,=1400 0005 0000 0004
 ,АТХ,STR.ПОМЕЧ.КОМАНДА
 ,WRITM,STR,В1
 GENCOM:,BSS,
С
С     ФОРМИРОВАНИЕ И ЗАПИСЬ ОДНОЙ КОМАНДЫ.
С
 М6,VJM,GIVICOP
 ,ХТА,CHAR
 ,АТХ,CCHAR.ДУБЛИРУЕМ
 ,ААХ,В1
 ,U1А,/CALL
 ,ХТА,DEF2
 ,ААХ,В41
 ,U1А,LIT/.ЛИТЕРАЛ
 М6,VJM,TRAD.ЗА АДР. ВЫРАЖ.
 М7,VТМ,PRINTL
 14,UJ,*+1
 :,UZA,ERR8.ОШ.СИНТ
 ,UJ,ERR12.НЕОП. ИДЕНТ.
 :,АТХ,NDEF.ЕСТЬ АДРЕС
 ,UJ,ISACOM
 :15,АТХ,.НЕВЫЧИСЛИМО
 ,ХТА,W
 ,АОХ,В1.БУДЕТ УК.-3
 ,АОХ,WDEFLD.РАБ.ЯЧ LOADER'А
 ,АОХ,STR
 ,SТХ,STR
 ,АТХ,W
 ,WRITM,STR,W
 ,UJ,PRINTL
 /CALL:,BSS,
С     В АДРЕСЕ - ВНЕШНЕЕ ИМЯ
 М7,VТМ,PRINTL
 ,ХТА,DEF2
 ,ААХ,В20/01
 ,АОХ,=:14
 ISACOM:,BSS,
 ,АТХ,STR
 ,ААХ,=:14
 ,АЕХ,=:14
 ,U1А,INTACOM
С    ВНЕШНЯЯ  ССЫЛКА
 ,ХТА,STR
 ,ААХ,В20/01
 ,ASN,64-6
 ,АОХ,=:1400 0000 0000 0001
 ,UJ,WRSCOM
 INTACOM:,ХТА,STR.АДРЕС
 ,XTS,CCHAR
 ,ASN,64+20
 ,ААХ,=377
 ,STI,14. К О П
 ,CALL,СНК*ADR
 ,U1А,ERR8. ДЛИННЫЙ АДРЕС
  WRACOM:,ХТА,STR
  ,ААХ,=7777 7400 0377 7777
 WRSCOM:,АОХ,W
 ,АТХ,STR
 ,ХТА,CCHAR
 ,ААХ,В20
 ,UZA,C1STR
С
С    У КОМАНД 2 СТРУКТУРЫ  КОП = 077
С    НАСТОЯЩИЙ КОП НАХОДИТСЯ В 13:20 Р.
С
  ,ХТА,STR
  ,ААХ,=7777 7760 0000 7777
  ,XTS,STR
  ,ASN,64+8
  ,ААХ,=377 0000
  15,АОХ,
  ,АОХ,=37400 0000
  ,АТХ,STR
  C1STR:,BSS,
С
 ,WRITM,STR,В1
 М7,UJ,
 GIVICOP:,BSS,.М6,VJM,GIVICOP
 ,ХТА,TIR
 ,UZA,NOIR
 ,АЕХ,=6Н
 ,UZA,NOIR
С   РАСПОЗНАВАНИЕ ПОЛЯ IR
 ,ХТА,TADS
 ,АТХ,W2
 ,ХТА,TIR
 ,ААХ,=:7777 74.2 БАЙТА
 ,АТХ,TADS
 ,CALL,S/PARAM
 ,XTS,W2
 ,SТХ,TADS
 14,UJ,*+1
 :,UJ,ERR4.ОШ. В IR
 :,UJ,IDIR.ИДЕНТ. В IR
 :,UJ,NUMIR.ЧИСЛО
 NOIR:,ХТА,CHAR.ПУСТО
 ,ААХ,CIRCOP
 READYCOP:,АТХ,W. К О П
 М6,UJ,
 NUMIR:,АТХ,NIR
 ,ATI,М4
 ,ASN,64+4
 ,U1А,ERR6
 М4,UТМ,-16
 ,CALL,ССЫЛКА
 ,ХТА,NIR
 ,ASN,64-28
 ,XTS,CHAR
 ,ААХ,СКОР
 15,АОХ,
 ,UJ,READYCOP
 IDIR:,CALL,CHID
 М4,VZM,ERR12
 М4,ХТА,TDEF1
 ,АОХ,В43
 М4,АТХ,TDEF1
 ,ААХ,=:7400 0377 7777 7777
 ,UJ,NUMIR
 LIT/:,BSS,
С    ЛИТЕРАЛ В АДРЕСНОЙ ЧАСТИ КОМАНДЫ
 ,ХТА,DEF2
 ,ААХ,В40
 ,UZA,LONGLT.ВОЗМОЖНО ПРОД.
С    НЕТЕКСТОВЫЙ ЛИТЕРАЛ ВСЕГДА
С    ЗАНИМАЕТ ОДНУ СТРОКУ
 М7,VТМ,PRINTL
 ,ХТА,DEF2
 ,ААХ,В20/01
 ,ARX,ABLIT
 ,АОХ,В45
 ,АТХ,NDEF
 ,UJ,ISACOM
 LONGLT:,BSS,
С    ЛИТЕРАЛ НА НЕСКОЛЬКИХ СТРОКАХ
 ,ХТА,W
 ,АТХ,COPYW.ПРЯЧЕМ
 ,ХТА,
 ,АТХ,NDEF
 :М2,VJM,PRINTL
 :,UZA,*-1.= ПРОДОЛЖЕНИЕ
 ,ХТА,COPYW
 ,АТХ,W
 ,ХТА,DEF1
 ,ASN,64+24
 ,ААХ,В20/01
 ,ARX,ABLIT
 ,АОХ,В45
 М7,VТМ,CONTINUE
 М2,VТМ,
 ,UJ,ISACOM
 COPYW:,LOG,
 CCHAR:,LOG,
С
С .....
С
 ENDSUBP:,BSS,
 ,ХТА,DEF2
 ,ARX,
 ,U1А,WRLIT/
С
С     ВСТАВЛЯЕМ ПУСТЫШКУ ЗА ПОСЛЕДНЕЙ КОМАНДОЙ
С     ПРОГРАММЫ.
 ,ХТА,=:0000 0011.UТС
 ,АТХ,STR
 ,WRITM,STR,В1
 WRLIT/:,BSS,
С    ЗАПИСЬ ЛИТЕРАЛОВ
 ,ХТА,LLIT
 ,UZA,ENDLIT/.НЕТ ЛИТЕРАЛОВ
 ,ASN,64-1
 ,АТХ,W2
 ,ХТА,ABLIT
 ,ASN,64-3
 ,АОХ,=:1400 0002
 ,АТХ,W
 ,WRITM,W,В1.СПЕЦУКАЗАНИЕ
 ,WRITM,TLT,W2.ЛИТЕРАЛЫ
 ENDLIT/:,BSS,
 ,ХТА,LCOD
 ,АТХ,W2
 ,ХТА,=:77
 ,АТХ,STR
 ,WRITM,STR,В1
 14,VТМ,ABEGCOD
     ,ITS,14
 ,NTR,6
 ,CALL,ELMOVE
 ,NTR,7
 ,ХТА,W2
 ,АОХ,В48
 ,АТХ,STR
 ,ХТА,LCOD
 ,ARX,LЕХТ
 ,АТХ,W2
 ,WRITM,STR,В1
 ,ХТА,W2
 ,АТХ,LCOD
 13,VJM,PRINTLR
 ,NTR,6
  14,VТМ,=I0
  ,ХТА,LISTFL."NО LIST"
  ,UZA,CHFULLL. -НЕТ
  ,ХТА,ERRORS
  ,U1А,IFET. БЫЛИ ОШИБКИ
  ,UJ,ENDTRN
  CHFULLL:,IТА,14
 ,CALL,LREGIM
 ,АЕХ,=I4
 ,U1А,IFET
С
С   В  РЕЖИМЕ   *FULL LIST   ПЕЧАТАЕМ
С   ОБ'ЕКТНЫЙ КОД И ВНУТРЕНИЕ ТАБЛИЦЫ
С
 М6,CALL,REFER
 ,UТС,2
 ,CALL,PRINT8//
 ,XTS,TPRICOD
 ,CALL,LOADANGO
 IFET:,BSS,
 ,UТС,1
 ,CALL,PRINT8//
 ENDTRN:,ХТА,
 ,*50,74В
 ,CALL,ELCL
 ,CALL,CHEXIT
С
С..................................
С
 PRINTL:,BSS,
 13,VТМ,NEWS
 PRINTLR:,BSS,
  ,ХТА,LISTFL
  13,U1А,."*NО LIST"
  PRINTE:,BSS,
 ,ХТА,NUMBER.
 ,U1А,PL01
 ,ХТА,=6Н
 ,АТХ,NUMBER
 PL01:,ХТА,NUMBER+1
 ,ASN,64+32
 ,ASN,64-32
 ,U1А,PL02
 ,ХТА,=6Н   ''''''
 PL02:,АОХ,=6Н'''' '1''1''1'
 ,АТХ,NUMBER+1
 ,ХТА,NLINE
 ,ААХ,С77777
 ,ASN,64+13
 ,ATI,14
 ,УТА,
 ,ASN,64-1
 ,AUX,=6Н'''''17''17''17'''
 15,АТХ,
 ,ARX,=6Н'''''166''166''166'''
 ,ААХ,=6Н'''''160''160''160'''
 ,ASN,64+4
 14,UТС,TW1
 ,АЕХ,
 15,ARX,
 ,АТХ,NUMBER+2
 ,ХТА,NLINE
 ,ASN,64-2
 ,УТА,
 ,ATI,14
 ,UТС,TW1S
 14,ХТА,
 ,АОХ,NUMBER+2
 ,АТХ,NUMBER+2
 ,ХТА,NDEF
 ,U1А,PL04
 ,ХТА,=6Н'1'
 ,UJ,PL05
 PL04:,ASN,64-4
 ,УТА,
 ,ATI,14
 ,ХТА,NDEF
 ,ASN,64+13
 ,УТА,
 ,ASN,64-1
 ,AUX,=6Н'''''17''17''17'''
 15,АТХ,
 ,ARX,=6Н'''''166''166''166'''
 ,ААХ,=6Н'''''160''160''160'''
 ,ASN,64+4
 14,UТС,TW2
 ,АЕХ,
 15,ARX,
 PL05:,АТХ,NUMBER+3
 ,ХТА,ТМТ
 ,U1А,PL06
 ,ХТА,=6Н
 ,АТХ,ТМТ
 ,ХТА,ТМТ+1
 ,U1А,PL06
 ,ХТА,=6Н
 ,АТХ,ТМТ+1
 PL06:,ХТА,TIR
 ,UZA,PL07
 ,ASN,64+24
 ,XTS,ТМТ+1
 ,ААХ,В48/25
 15,АОХ,
 ,АТХ,ТМТ+1
 PL07:,BSS,
 PRNT:,BSS,
 ,IТА,13
 ,ITS,7
 ,ITS,6
 ,ITS,
 14,VТМ,NUMBER
 ,ITS,14
 14,VТМ,TADS+4
 ,ITS,14
 14,VТМ,
 ,ITS,14
 ,CALL,PRINT8//
С    ПРОВЕРКА:  НЕ ТРЕБУЕТСЯ-ЛИ ДОПЕЧАТКА
С    ХВОСТА СТРОКИ.
С
 М6,VТМ,-1
 М7,VТМ,TADS+4
  IFPR:М7,ХТА,
  ,АЕХ,=6Н
  ,ASN,64-16
  14,VТМ,-2
  CHTS:14,UТС,2
  М7,XTS,1
  ,АЕХ,=6Н
  15,АОХ,
  14,VLM,CHTS
  ,UZA,EPR. - НЕ НАДО
  М7,ХТА,
  ,ASN,64-16
 ,ASN,64+16
 ,АОХ,=6Н'1''1'''''''''
 М7,АТХ,
 ,ITS,М7
 М7,UТМ,4
 ,ITS,М7
 14,VТМ,39
 ,ITS,14
 ,CALL,PRINT8//
 М6,VLM,IFPR
 EPR:15,ХТА,
 ,STI,6
 ,STI,7
 ,ATI,13
 13,UJ,
С
С...   ПЕЧАТЬ КОММЕНТАРИЯ ИЛИ ОШ.КАРТЫ
С
  COMMENT:,ХТА,LISTFL
  ,U1А,NEWS."*NО LIST"
  12,VТМ,МСМТ
  ,UJ,CHNUMS
  BADPR:12,VТМ,МСМТ+1
  CHNUMS:,BSS,
 ,ХТА,NUMBER
 ,U1А,СМ2
 ,ХТА,=6Н
 ,АТХ,NUMBER
 СМ1:,ХТА,=6Н  ''''''''
 ,UJ,PRCM
 СМ2:,ХТА,NUMBER+1
 ,UZA,СМ1
 ,ААХ,=:7777 74
 PRCM:12,АОХ,
 ,АТХ,NUMBER+1
 13,VТМ,NEWS
 ,UJ,PRNT
 ERR1:М7,VТМ,1
 ,UJ,ERROR
 ERR2:М7,VТМ,2
 ,UJ,ERROR
 ERR3:М7,VТМ,3
 ,UJ,ERROR
 ERR4:М7,VТМ,4
 ,UJ,ERROR
 ERR5:М7,VТМ,5
 ,АТХ,ERRIDENT
 ,UJ,ERROR
 ERR6:М7,VТМ,6
 ,UJ,ERROR
 ERR7:М7,VТМ,7
 ,UJ,ERROR
 ERR8:М7,VТМ,8
 ,UJ,ERROR
 ERR9:М7,VТМ,9
 ,UJ,ERROR
 ERR10:М7,VТМ,10
 ,UJ,ERROR
 ERR11:М7,VТМ,11
 ,UJ,ERROR
 ERR12:М7,VТМ,12
 ,АТХ,ERRIDENT
 ,UJ,ERROR
 ERR13:М7,VТМ,13
 ,UJ,ERROR
 ERR14:М7,VТМ,14
 ,UJ,ERROR
 ERR:,АТХ,ERRIDENT
 ,IТА,14
 ERRPRINT:,ATI,М7
 ERROR:М7,UТМ,1
 М7,VZM,ERRSYNT
 М7,UТМ,-1
 ERRSYNT:М7,ХТА,В48
 ,АОХ,ERRORS
 ,АТХ,ERRORS
 ,АТХ,*ERRORS*
 ,IТА,М7
 15,АТХ,
 ,ASN,64-2
 15,ARX,
 ,ATI,13
 14,VТМ,-4
 LER:13,UТС,TERRS
 14,ХТА,4
 ,UТС,ERRTX+1
 14,АТХ,4
 14,VLM,LER
 М7,ХТА,В48
 ,ААХ,=:0100 4
 14,VТМ,ERRIDENT
  :,U1А,*+1
  ,АТХ,ERRIDENT
  :,ITS,14
 14,VТМ,ERRTX+6
 ,ITS,14
 ,CALL,TEXISO
 ,ХТА,ERRTX+7
 ,ASN,64+8
 ,АТХ,ERRTX+7
 ,ХТА,ERRTX+6
 ,ASN,64+8
 15,АТХ,
 ,УТА,
 ,АОХ,ERRTX+7
 ,SТХ,ERRTX+7
 ,АОХ,В46
 ,АТХ,ERRTX+6
 15,UТМ,3
 ,ХТА,В8
 ,CALL,PRINT8//
 ,ХТА,ERRNAM
 ,АЕХ,ERRNAM1
 ,UZA,OLDERNAM
 ,ХТА,LISTFL
 ,UZA,OLDERNAM
 14,VТМ,ERRTXNL
 ,ITS,14
 14,UТМ,4
 ,ITS,14
 ,XTS,=2
 ,CALL,PRINT8//
 OLDERNAM:,ХТА,ERRNAM
 ,АТХ,ERRNAM1
 14,VТМ,ERRTX
 ,ITS,14
 14,VТМ,ERRTX+7
 ,ITS,14
 14,VТМ,6
 ,ITS,14
 ,CALL,PRINT8//
  13,VТМ,NEWS
  М7,VIM,PRINTE
  ,UJ,BADPR
 RЕАСТ:,UТС,1
 ,CALL,PRINT8//
 ,*62,
 TPRICOD:,ТЕХТ,7HPRICOD*
 ERRNAM1:,BSS,2
 ERRTXNL:,ISO,18Н..... ПРОГРАММА:
 ERRNAM:,ISO,12Н
 ERRTX:,ISO,6Н*****
 ,BSS,7
 TERRS:0,ISO,30ННЕДОПУСТИМЫЙ ОПЕРАТОР:
 1,CONT,30ННЕТ ЗАГОЛОВКА ПОДПРОГРАММЫ
 2,CONT,30НПОВТОРНОЕ ОПРЕДЕЛЕНИЕ ВХОДА:
 3,CONT,30НОШИБКА В КОП:
 4,CONT,30НОШИБКА В МЕТКЕ:
 5,CONT,30НДВАЖДЫ ОПИСАННЫЙ ИДЕНТИФИКАТОР
 6,CONT,30НОШИБКА В МОДИФИКАТОРЕ:
 7,CONT,30ННАРУШЕН ПОРЯДОК ИНСТРУКЦИЙ:
 8,CONT,30НОШИБКА В АДРЕСЕ:
 9,CONT,30ННЕТ МЕТКИ:
 10,CONT,30НВХОД НЕ ОПРЕДЕЛЕН:
 11,CONT,30НПОКА НЕ РЕАЛИЗОВАНО:
 12,ISO,30Н    НЕОПИСАННЫЙ ИДЕНТИФИКАТОР
 13,ISO,30НПЕРЕПОЛНЕНИЕ ТАБЛИЦ!
 14,ISO,30НАВАРИЯ ТРАНСЛЯТОРА.
 15,ISO,30НПЛОХОЙ ИНТЕРФЕЙС ПОДРОГРАММЫ
 МСМТ:,ISO,6Н'''';'1''1''1'
 ,CONT,6Н'''' '1''1''1'
 TW1:,ISO,6Н'1' 777''.КОМАНДА
 ,CONT,6Н'1'Х777''.КОНСТАНТА
 ,CONT,6Н'1'V777''.ПЕРЕМЕННАЯ
 ,CONT,6Н'1'L777''.СТРОКА
 TW1S:,ISO,6Н''''''''''
 ,ISO,6Н''''''''''>
 ,ISO,6Н'''''''''':
 ,ISO,6Н''''''''''>
 TW2:,ISO,6Н'1'U777 .АБСОЛ.
 ,CONT,6Н'1' 777 .КОМ. ИЛИ КОНСТ.
 ,CONT,6Н'1'V777 .ПЕРЕМЕННАЯ
 ,CONT,6Н'1'W777 .ВНЕШН.
 TW3:,ISO,6Н''''''''''''
 ,CONT,6Н'''''''''''41'
 ,CONT,6Н'''''''''41''41'
 ,CONT,6Н'''''''41''41''41'
 ,CONT,6Н'''''41''41''41''41'
 ,CONT,6Н'''41''41''41''41''41'
 BL:,BSS,LBL
 ВТ:,BSS,LВТ+2
 EZER:,BSS,1
 ,END,МКВ/ASS/

 MNEMO:,NАМЕ,.19/03/86
С*
С*       U S Е R   РЕДАКТОРА.
С*
С*     СОЗДАЕТ ТЕКСТ ТАБЛИЦЫ МНЕМОКОДОВ
С*  И УКАЗАТЕЛИ БЫСТРОГО ПОИСКА  ПО НЕЙ
С*  ДЛЯ ТРАНСЛЯТОРА  МКВ/ASS.
С*
С*
С*     ИСХОДНАЯ ИНФОРМАЦИЯ - В ВИДЕ П/П
С*
С*   MNEMOTXT:,NАМЕ ,
С*   MNEMOCOD:,SKIP ,
С*             ...
С*             ...
С*    ОПИСАНИЯ МНЕМОКОДОВ
С*             ...
С*             ...
С*   MNEMOCOD:,ENDIF,
С*            , END ,
С*
С*     ОПИСАНИЕ МНЕМОКОДА ВЫГЛЯДИТ ТАК:
С*
С*   <МНЕМОКОД><Р><КОП> <ХАРАКТ. СТРОКА>
С*
С*        <Р> - ПРОБЕЛ, ЕСЛИ БЕЗ АВТОМАТИ-
С*              ЧЕСКОЙ ПОМЕЧЕННОСТИ СЛЕДУ-
С*              ЮЩЕЙ КОМАНДЫ, ИНАЧЕ ":".
С*
С*      <КОП> - ВОСЬМЕРИЧНЫЙ КОП. МОЖЕТ БЫТЬ
С*              ПЯТИСИМВОЛЬНЫМ - В ЭТОМ СЛУ-
С*              ЧАЕ ДВА СТАРШИХ СИМВОЛА  ЯВ-
С*              ЛЯЮТСЯ НОМЕРОМ IR ПО УМОЛЧА-
С*              НИЮ (ВОСЬМЕРИЧНЫМ!).
С*
С*      <ХАРАКТ. СТРОКА> СОДЕРЖИТ ХАРАКТЕ-
С*              РИСТИКИ МНЕМОКОДА:
С*      1). СПОСОБ АДРЕСАЦИИ ПАМЯТИ
С*          (Х,Н,Q,В,F,D)
С*      2). ТИП АДРЕСА (I,N,U)
С*      3). IR ОБЯЗАТЕЛЕН (М)
С*      4). КОМАНДА ПЕРЕДАЧИ УПРАВЛЕНИЯ (J)
С*      5). МАГАЗИННАЯ КОМАНДА (S)
С*      6). КОМАНДА РАБОТАЕТ С РАУ (R)
С*      7). ПРИВИЛЕГИРОВАННАЯ КОМАНДА (Р)
С*      8). АДРЕСН.ЧАСТЬ - ВНЕШНЕЕ ИМЯ  (Е)
С*      9). КОМАНДА  2-Й  СТРУКТУРЫ  (С)
С*
С*    ПРИМЕР:
С*
С*     VJM:310 XMJ
С*     EJMP 300 XJE
С*
С*
С*     РЕЗУЛЬТАТ РАБОТЫ - ТЕКСТ BLOCK DАТА
С*  ДЛЯ ПОИСКА МНЕМОКОДА:
С*
С*   *МСН    :  ,NАМЕ ,
С*               ...
С*       УК. БЫСТРОГО ПОИСКА
С*               ...
С*   *MNEMO* :  ,ENTRY,
С*                ...
С*         МНЕМОКОДЫ В ISO
С*                ...
С*   *HARACT*:  ,ENTRY,
С*                ...
С*     ХАРАКТЕРИСТИКИ МНЕМОКОДОВ
С*                ...
С*              , END ,
С*
С*
С*     ХАРАКТЕРИСТИКА МНЕМОКОДА ИМЕЕТ СЛЕДУ-
С*  ЮЩУЮ СТРУКТУРУ (ПО СЛОВУ НА МНЕМОКОД):
С*
С*  48    - ПРИЗНАК КОМАНДЫ
С*
С*   ДАЛЕЕ ДЛЯ КОМАНД:
С*
С*  45    - ПОМЕЧЕННОСТЬ СЛЕД. КОМ.
С*  44    - IR ОБЯЗАТЕЛЕН
С*  43    - КОМАНДА П/У
С*  42:40 - ТИП АДРЕСНОЙ ЧАСТИ:
С*          0 - АДРЕС ПО ПАМЯТИ ИЛИ КОНСТ.
С*          1 - IR
С*          2 - N (NTR И ДР.)
С*          3 - АДРЕСНЫЙ ЛИТЕРАЛ (ARU И ДР.)
С*  39:37 - СПОСОБ АДРЕСАЦИИ К ПАМЯТИ:
С*          0 - БЕЗ ОБРАЩЕНИЯ К ПАМЯТИ
С*          1 - АДРЕС СЛОВА
С*          2 - Н-АДРЕС
С*          3 - Q-АДРЕС
С*          4 - В-АДРЕС
С*          5 - F-АДРЕС
С*  36    - МАГАЗИННАЯ КОМАНДА
С*  35    - ДВОЙНОЕ СЛОВО
С*  34    - РАБОТА С РАУ
С*  33    - ПРИВЕЛИГИРОВАННАЯ КОМАНДА
С*  32:29 - НОМЕР IR ПО УМОЛЧАНИЮ
С*  28:21 - КОП
С*  20    - КОМАНДА 2-Й СТРУКТУРЫ (С)
С*  19:16 - РЕЗЕРВ
С*  15:02 - =0
С*   01   - АДРЕСНАЯ ЧАСТЬ - ВНЕШНЕЕ ИМЯ
С*
С*
С*     ДЛЯ  ПСЕВДОИНСТРУКЦИЙ:
С*
С*  45:34 - ШКАЛА   РАЗРЕШЕННЫХ   СОСТОЯНИЙ
С*          (В45=1, В44=2, ...)
С*  33:22 - ШКАЛА СОСТОЯНИЙ,  ИЗ КОТОРЫХ НУ-
С*          ЖЕН ПЕРЕХОД.
С*  21:16 - НОМЕР СОСТОЯНИЯ,  В КОТОРОЕ СЛЕ-
С*          ДУЕТ ПЕРЕЙТИ.
С*  15    - REQ, RWEQ
С*  14    - BLOCK
С*  13    - ПУСТЫШКА
С*  06:01 - НОМЕР ГРУППЫ ПСЕВДОИНСТРУКЦИЙ
С*  12:07 - НОМЕР ВНУТРИ ГРУППЫ:
С*
С*     Г Р У П П А  1 :
С*
С*       0 - SUBP
С*       1 - LСВ
С*       2 - РСВ
С*       3 - ССВ
С*       4 - РССВ
С*
С*     Г Р У П П А  2:
С*
С*       0 - MACRO
С*       1 - ENDM
С*
С*     Г Р У П П А  3:
С*
С*       0 - VAR
С*       1 - CONST
С*       2 - ISO
С*       3 - ISOZ
С*       4 - НЕХ
С*       5 - ОСТ
С*       6 - INT
С*       7 - INTH
С*       8 - INTQ
С*       9 - INTB
С*      10 - REAL
С*      11 - DREAL
С*      12 - BSS
С*      13 - BSSZ
С*      14 - DАТА
С*      15 - SЕТ
С*      16 - CMD
С*
С*     Г Р У П П А  4 :
С*
С*       0 - ПУСТОЙ КОП
С*       1 - BLOCK
С*       2 - EQU
С*       3 - REQ
С*
С*=========================================
С*
С........ MACRO ............
 XR1:,MACRO,Х
 ,ХТА,Х
 ,ARX,=1
 ,АТХ,Х
 XR1:,ENDM,.
С........ ENDM ..............
С
С     МАКС. КОЛ-ВО МНЕМОКОДОВ
 МАХ:,EQU,450
 BL:I,BLOCK,SERV.АДРЕС СЕРВ. П/П
 ,CONT,IWF
 ,CONT,W(2)
 ,CONT,Х.ЗАГОТ.ХАР.
 ,CONT,N4.К-ВО 4-5 СИМВ. МНЕМОКОДОВ
 ,CONT,STR(14).ИТОГ
 ,CONT,Е/BL(0)
  PRINT8:,SUBP,
  *НТАВ*:,SUBP,
С
С
 В:8,BASE,В.ДЛЯ НАЧАЛА
 ,SТХ,SERV
 ,АОХ,IW50
 ,АТХ,IWF
 ,IТА,7
 7,BASE,В .ДО КОНЦА
 ,ITS,13
 ,ITS,
 ,XR1,NПК
 ,WТС,SWITCH
 ,UJ,*+1
 :,UJ,FIRST.0-ПЕРВОЕ ОБР.
 :13,VТМ,CHBEG.1-ПОИСК НАЧАЛА
 ,UJ,IFIF
 :,UJ,INPUT.2-ВВОД И РАЗБОР
 :,UJ,RЕТ.3-ДОКРУТКА
 FIRST:14,VТМ,NАМЕ
 13,VJM,GIVSTR
 NEWSOST:,XR1,SWITCH
 RЕТ:,BSS,
 ,STI,
 ,STI,13
 ,STI,7
 13,UJ,
 IFIF:,BSS,
 ,ХТА,IWF
 CONT:,*50,12
 14,UТМ,-4
 14,V1М,ER0
 15,АТХ,
 ,УТА,
 ,SТХ,W
 ,АЕХ,=6Н
 ,UZA,NEXTF
 ,АЕХ,=6Н
 ,АЕХ,=6HMNEMOC
 13,U1А,
 ,ХТА,W
 ,АЕХ,=6HOD
 13,UJ,
 CHBEG:,U1А,RЕТ
 ,UJ,NEWSOST
 NEXTF:,ХТА,IW50
 ,UJ,CONT
 INPUT:,BSS,
 13,VJM,IFIF
 ,UZA,WRITE.=КОНЕЦ ВВОДА
 12,VТМ,-5
 ,ХТА,
 ,АТХ,W
 ,UТС,IWF-IW50
 LS1:,ХТА,IW50
 ,АОХ,=100 0000
 ,*50,12
 ,АТХ,W+1
 ,УТА,
 ,ATI,14
 14,UТМ,-40В
 14,VZM,BLANK
 14,UТМ,40В-72В.:
 14,VZM,SDP
 ,ААХ,=177
 ,АОХ,W
 ,ASN,64-8
 ,АТХ,W
 12,VLM,LS1
 ,UJ,ER1
 SDP:,UТС,=:04
 BLANK:,ХТА,
 ,АОХ,=:6
 ,АТХ,Х
 ,ХТА,W
 ,UZA,LS1
 LS2:,АОХ,=40
 ,АТХ,W
 ,ASN,64-8
 12,VLM,LS2
 ,XR1,N
 ,ХТА,W
 ,WТС,N
 ,АТХ,TMNEMO*-1
 ,ХТА,IW50
 ,*50,12.КОП
 14,UТМ,-1
 14,V1М,ER1
 15,АТХ,
 ,АРХ,=17377
 ,ASN,64+16
 ,АОХ,Х
 ,SТХ,Х
 ,АОХ,=17377
 ,АЕХ,=17377
 ,U1А,ER1
 ,ХТА,IW50
 ,*50,12.ХАРАКТ.
 14,UТМ,-4
 14,V1М,ER2
 ,АТХ,W
 ,УТА,
 ,АТХ,W+1
 14,VТМ,W
 ,IТА,14
 ,CALL,LETBI
 ,АРХ,М/Х
 ,АТХ,W
 LХ:,BSS,
 ,ХТА,W
 ,UZA,ELX
 ,ANX,
 ,ATI,14
 ,УТА,
 14,ASN,64
 ,АТХ,W
 14,ХТА,Т/Х-1
 ,АОХ,Х
 ,АТХ,Х
 ,UJ,LХ
 ELX:,ХТА,Х
 ,WТС,N
 ,АТХ,THAR*-1
 ,UJ,RЕТ
 WRITE:,BSS,
 ,IТА,1
 ,ITS,2
 ,ITS,3
 ,ITS,4
 ,ITS,5
 3,VТМ,.СЧ. 4-5 СИМВ.
 ,XTS,N.К-ВО МНЕМОКОДОВ
 ,ATI,1
 ,АЕХ,=77777
 ,ARX,=2
 ,ATI,2
 ,ATI,4
 ,ATI,5
 LСН1:,BSS,
 1,UТС,-1
 2,ХТА,TMNEMO*
 ,АЕХ,=6Н
 ,ASN,64-24
 ,UZA,ELCH1.3-СИМВ.
 3,UТМ,1
 ELCH1:2,VLM,LСН1
 ,IТА,3
 ,АТХ,N4
 LСН2:,BSS,
 2,MTJ,14.УК ДЛЯ 4-СИМВ.
 1,UТС,-1
 4,ХТА,THAR*
 1,UТС,-1
 4,XTS,TMNEMO*
 ,АТХ,W
 ,АЕХ,=6Н
 ,ASN,64-24
 ,U1А,TR
 3,MTJ,14.УК.ДЛЯ 3-СИМВ.
 3,UТМ,1
 0,UТС,-1
 TR:2,UТМ,1
 ,ХТА,W
 14,SТХ,TMNEMO
 14,АТХ,THAR
 4,VLM,LСН2
 14,VТМ,-61
 ,ХТА,
 *1:14,АТХ,МСН+62
 14,VLM,*1
 ,ХТА,N
С         ( , ХТА ,N4 )
 ,АОХ,=I0
 ,АТХ,МСН
С........................................
С
С    СТРУКТУРА МСН:
С
С  СЛОВО
С   0    - К-ВО 4-5 СИМВОЛЬНЫХ МНЕМОКОДОВ
С   1-61 - Т1 ДЛЯ БЫСТРОГО ПОИСКА
С  62-.. - Т2 ДЛЯ БЫСТРОГО ПОИСКА
С
С........................................
С
С
С         ЗАПИСЬ МСН
С
 ,ХТА,=6Н
 ,АТХ,W+1
 1,VТМ,-62
 LWR1:1,ХТА,МСН+62
 13,VJM,GIVLOG
 1,VLM,LWR1
 14,VТМ,ENTRY1
 13,VJM,GIVSTR
 5,MTJ,2
 ,WТС,N
 1,VТМ,.К-ВО МНЕМОКОДОВ
 LWR2:,BSS,
 1,UТС,-1
 5,ХТА,TMNEMO
 13,VJM,GIVISO
 5,VLM,LWR2
 ,ХТА,
 13,VJM,GIVLOG
С
С     ЗАПИСЬ  *HARACT*
С
 14,VТМ,ENTRY2
 13,VJM,GIVSTR
 LWR3:,BSS,
 1,UТС,-1
 2,ХТА,TMNEMO
 ,АТХ,W+1
 1,UТС,-1
 2,ХТА,THAR
 13,VJM,GIVLOG
 2,VLM,LWR3
 14,VТМ,END
 13,VJM,GIVSTR
С
С        СОЗДАНИЕ Н-ТАБЛИЦЫ КОПОВ *НТАВ*
С
  2,VТМ,TMNEMO
  ,CALL,HCLE
  FORMHT:2,ХТА,
  ,UZA,EFHT
  ,АТХ,W
  2,ХТА,THAR-TMNEMO
  ,АТХ,W+1
  14,VТМ,W
  ,IТА,14
  ,CALL,KHPUT
  2,VLM,FORMHT
С---
  HNAME:,ISO,18Н *НТАВ*:,NАМЕ,
  ONAME:,ISO,18Н *MNORD*:,NАМЕ,
  CNAME:,ISO,18Н *MNCHAR*:,NАМЕ,
С---
  EFHT:14,VТМ,HNAME
  13,VJM,GIVSTR
  2,VТМ,*НТАВ*
  ,ХТА,=6Н
  ,АТХ,W+1
  FНТ2:13,VТМ,FНТ3
  2,ХТА,
  ,UZA,GIVLOG
  13,VJM,GIVISO
  FНТ3:2,ХТА,1
  13,VJM,GIVLOG
  2,ХТА,
  2,UТМ,2
  ,АЕХ,=6Н <END>
  ,U1А,FНТ2
  14,VТМ,END
  13,VJM,GIVSTR
С
С        СОЗДАНИЕ "BLOCK DАТА"  MNORD(512)
С          ---"---   ---"---   MNCHAR(512)
С
  ,ХТА,=6Н >*<
  14,VТМ,-511
  :,UТС,MNORD
  14,АТХ,511
  14,VLM,*-1
  1,VТМ,TMNEMO
  2,VТМ,-511
  GMNO:1,ХТА,
  ,UZA,GMNO2
  1,ХТА,THAR-TMNEMO
  ,ARX,
  ,U1А,GMNO1
  ,ASN,64+20
  ,ААХ,=377
  ,ATI,14
  1,ХТА,THAR-TMNEMO
  ,ААХ,=200 0000
  :,UZA,*+1
  14,UТМ,256
  :1,ХТА,
  14,АТХ,MNORD
  1,ХТА,THAR-TMNEMO
  14,АТХ,MNCHAR
  GMNO1:1,VLM,GMNO
  GMNO2:14,VТМ,ONAME
  13,VJM,GIVSTR
  GMNO3:2,ХТА,MNORD+511
  13,VJM,GIVISO
  2,VLM,GMNO3
  2,VТМ,-511
  14,VТМ,END
  13,VJM,GIVSTR
  14,VТМ,CNAME
  13,VJM,GIVSTR
  GMNO4:2,ХТА,MNORD+511
  ,АТХ,W+1
  2,ХТА,MNCHAR+511
  13,VJM,GIVLOG
  2,VLM,GMNO4
  14,VТМ,END
  13,VJM,GIVSTR
С
 14,VТМ,*READ OLD
 13,VJM,GIVSTR
С
  14,VТМ,TMNEMO
  ,ITS,14
  14,VТМ,THAR
  ,ITS,14
  ,CALL,SORCOP
 ,STI,
 ,STI,5
 ,STI,4
 ,STI,3
 ,STI,2
 ,ATI,1
 ,UJ,NEWSOST
 GIVSTR:,BSS,
 12,VJM,CLESTR
 15,MTJ,12
 15,VТМ,STR
 14,ХТА,
 14,XTS,1
 14,XTS,2
 WRSTR:,ITS,
 12,MTJ,15
 14,VТМ,STR
 ,ITS,14
 ,WТС,SERV
 ,UJ,
 GIVLOG:,BSS,
 ,АТХ,W
 12,VJM,CLESTR
 15,MTJ,12
 15,VТМ,STR
 ,ХТА,=6Н ,LOG,
 ,XTS,W
 ,AUX,MSK7
 ,АОХ,=6Н000000
 ,XTS,W
 ,ASN,64-18
 ,АТХ,W
 ,AUX,MSK7
 ,АОХ,=6Н000000
 ,XTS,W
 ,ASN,64-18
 ,AUX,MSK7
 ,АОХ,=6Н0000.
 ,XTS,W+1
 ,UJ,WRSTR
 GIVISO:,BSS,
 ,АТХ,W
 12,VJM,CLESTR
 15,MTJ,12
 15,VТМ,STR
 ,ХТА,=6Н   , I
 ,XTS,=6HSO ,6Н
 ,XTS,W
 ,UJ,WRSTR
 CLESTR:,IТА,12
 ,XTS,=6Н
 12,VТМ,-13
 *2:12,АТХ,STR+13
 12,VLM,*2
 15,WТС,
 ,UJ,
 ER3:,BSS,
 ER2:,BSS,
 ER1:,BSS,
 ER0:,WТС,IWF
  14,VТМ,.ACARD
  14,ХТА,
  ,АЕХ,=6Н;
  ,ASN,64+40
  ,UZA,RЕТ. КОММЕНТАРИЙ
  13,VТМ,RЕТ
  ,ITS,14
  14,UТМ,11
  ,ITS,14
  ,XTS,=2
  ,UJ,PRINT8
С
С====================================
С
С
С    МАСКА СИМВОЛОВ, РАЗРЕШЕННЫХ В
С    ХАРАКТЕРИСТИКЕ
С    (BCDEFHIJMNPQRSUX)
 М/Х:,ОСТ,1756 3364 4
С
С    ТАБЛИЦА ХАРАКТЕРИСТИК
С
 Т/Х:,BSS,
 ,ОСТ,0004.В
 ,LOG,2000000.С
 ,ОСТ,0000 2.D
 ,LOG,1.Е
 ,ОСТ,0005.F
 ,ОСТ,0002.Н
 ,ОСТ,001.I
 ,ОСТ,01.J
 ,ОСТ,02.М
 ,ОСТ,002.N
 ,ОСТ,0000 04.Р
 ,ОСТ,0003.Q
 ,ОСТ,0000 1.R
 ,ОСТ,0000 4.S
 ,ОСТ,003.U
 ,ОСТ,0001.Х
С.....
 MSK7:,GOST,6Н777777
 SWITCH:,LOG,
 NПК:,LOG,
 IW50:,LOG,60 0000
 TMNEMO:,BSS,МАХ
 THAR:,BSS,МАХ
 МСН:,BSS,МАХ
С.....
 NАМЕ:,ISO,18Н *МСН:,NАМЕ,
 ENTRY1:,ISO,18Н *MNEMO*:,ENTRY,
 ENTRY2:,ISO,18Н *HARACT*:,ENTRY,
 END:,ISO,18Н ,END,
 *READOLD:,ISO,18Н*READ OLD
 TMNEMO*:,BSS,
 ,ISO,6ННЕХ
 ,ISO,6HINT
 ,ISO,6HCHAR
 ,ISO,6HINTH
 ,ISO,6HINTQ
 ,ISO,6HINTB
 ,ISO,6HCHARZ
 ,ISO,6HBLOCK
 ,ISO,6HREQU
 ,ISO,6HEQU
 ,ISO,6HSUBP
 ,ISO,6HBSS
 ,ISO,6HBSSZ
 ,ISO,6HCONST
 ,ISO,6HVAR
 ,ISO,6HLCB
 ,ISO,6НРСВ
 ,ISO,6НССВ
 ,ISO,6НРССВ
С ,ISO,6HDATA
С ,ISO,6HSET
  ,ISO,6HCMD
 EFIX:,BSS,МАХ
 THAR*:,BSS,
 ,ОСТ,0 0540 7200 06 0 04 03.НЕХ
 ,ОСТ,0 0540 7200 06 0 06 03.INT
 ,ОСТ,0 0540 7200 06 0 02 03.CHAR
 ,ОСТ,0 0540 7200 06 0 07 03.INTH
 ,ОСТ,0 0540 7200 06 0 10 03.INTQ
 ,ОСТ,0 0540 7200 06 0 11 03.INTB
 ,ОСТ,0 0540 7200 06 0 03 03.CHARZ
 ,ОСТ,0 1770 6000 03 2 01 04.BLOCK
 ,ОСТ,0 1770 6000 03 4 03 04.REQU
 ,ОСТ,0 1770 6000 03 0 02 04.EQU
 ,ОСТ,0 1400 6000 03 0 00 01.SUBP
 ,ОСТ,0 0540 7200 06 0 14 03.BSS
 ,ОСТ,0 0540 7200 06 0 15 03.BSSZ
 ,ОСТ,0 0500 7200 06 0 01 03.CONST
 ,ОСТ,0 0500 7200 06 0 00 03.VAR
 ,ОСТ,0 1400 6000 03 0 01 01.LСВ
 ,ОСТ,0 1400 6000 03 0 02 01.РСВ
 ,ОСТ,0 1400 6000 03 0 03 01.ССВ
 ,ОСТ,0 1400 6000 03 0 04 01.РССВ
С ,ОСТ,0 0500 7600 07 0 16 03.DАТА
С ,ОСТ,0 0440 7340 10 0 17 03.SЕТ
  ,ОСТ,0 0000 7760 11 0 20 03.CMD
 ,BSS,МАХ
С    К-ВО ФИКСИРОВАННЫХ МНЕМОКОДОВ
 N:,Z00,
 ,Z00,EFIX-TMNEMO*
 MNORD:,BSS,512
 MNCHAR:,BSS,512
 LBL:,REQU,Е/BL-BL
 BL:,BSS,24В
 ,END,

 PRICOD*:,NАМЕ,
С*
С*   ГОЛОВНАЯ П/П РАЗДЕЛА ТЕСТОВОЙ ПЕЧАТИ.
С*   ЗАГРУЖАЕТСЯ В РЕЖИМЕ  *FULL LIST
С*   В КОНЦЕ РАБОТЫ МКВ/ASS/.
С*
 PRINT8:,SUBP,
 PRINT8//:,SUBP,
 PRICOD:,SUBP,
 ,BITS,
 ,DEFLC,
 12,BASE,*
 ,IТА,13
 ,АТХ,SI13
 ,ХТА,CPR//
 ,UТС,PRINT8
 ,СТХ,.БУДЕТ БИЛИСТ
 ,ХТА,LCOD
 ,АОХ,=I0
 ,АТХ,W
 ,NTR,6
 14,VТМ,W
 ,ITS,14
 ,CALL,PRICOD.* ФОРТРАН *
 ,UТС,1
 ,CALL,PRINT8//
 ,WТС,SI13
 ,UJ,
 CPR//:,UJ,PRINT8//
 W:,BSS,1
 SI13:,BSS,1
 ,END,

 PRNTS:,NАМЕ,
 PRINT8//:,SUBP,
 ,ATI,14
 14,UТМ,10
 ,ITS,14
 14,VТМ,0                                                              1
 ,ITS,14
 ,UJ,PRINT8//
 ,END,

 PUTHEX:1,NАМЕ,***
С*
С*     М6,CALL,PUTHEX
С*
С*     М6,CALL,PUTINT
С*
С*
С*     ВЫБОРКА  ШЕСТНАДЦАТИРИЧНОГО  ИЛИ
С*  INTEGER- ФРАГМЕНТА  ИЗ АДРЕСНОЙ ЧАСТИ
С*  ОПЕРАТОРА.  СКАНИРОВАНИЕ НАЧИНАЕТСЯ С
С*  ТЕКУЩЕГО СИМВОЛА.
С*
С*     РЕЗУЛЬТАТ:   ЧИСЛО, ПРИЖАТОЕ ВЛЕВО
С*  ДЛЯ  PUTHEX  ИЛИ  ВПРАВО  ДЛЯ  PUTINT,
С*  НАХОДИТСЯ В ЯЧ. LARGE (СТАРШИЕ РАЗРЯДЫ)
С*  И НА СУММАТОРЕ (МЛАДШИЕ РАЗРЯДЫ).
С*
С*  I14.EQ.0 - БЕЗ ОШИБОК, ИНАЧЕ ТАМ НОМЕР
С*             ОШИБКИ.
С*  М8  СОДЕРЖИТ КОД ПОСЛЕДНЕГО СИМВОЛА.
С*
 ,BITS,
 ,DEFLC,
 TABSYM:,SUBP,
 BL:I,BLOCK,W,WW
 ,CONT,SIGN
 LBL:,EQU,3
 МС,VТМ,-7
 МВ,VТМ,LARGE
 LНЕХ:,BSS,
 :,CALL,SYMBOL
 14,VZM,*-1
 ,UТС,TABSYM
 14,ХТА,40В
 ,АТХ,W
 ,ААХ,В39
 ,UZA,IFENDH
 ,ХТА,W
 ,ASN,64+15
 ,ААХ,С17
 ,XTS,WW
 ,ASN,64-4
 15,АОХ,
 ,АТХ,WW
 МС,VLM,LНЕХ
 МВ,VZM,ENDLH
 ,ААХ,В32/01
 МВ,АТХ,
 МВ,VТМ,
 МС,VТМ,-7
 ,UJ,LНЕХ
 IFENDH:14,UТС,
 М8,VТМ,40В
 14,UТМ,40В-54В
 14,VZM,*1
 ,ХТА,W
 ,ААХ,=:0000 14
 ,UZA,ERR8
 *1:,BSS,
 ,IТА,МС
 ,ASN,64-2
 ,ATI,МС
 ,ХТА,WW
 МС,ASN,64-4
 ,ААХ,В32/01
 МВ,VZM,GТ32Н
С    ЧИСЛО ЗАНИМАЕТ ОДНО ПОЛУСЛОВО
 МВ,АТХ,
 ,ХТА,
 GТ32Н:14,VТМ,
 М6,UJ,
 ENDLH:,CALL,SYMBOL
 14,VZM,*-1
 14,UТС,
 М8,VТМ,40В
 14,UТМ,40В-54В
 14,VZM,RЕТ
 М8,UТС,TABSYM
 ,ХТА,
 ,ААХ,=:0000 14
 ,UZA,ERR8
 RЕТ:,ХТА,WW
 ,ААХ,В32/01
 14,VТМ,
 М6,UJ,
 PUTINT:,ENTRY,
 ,ХТА,
 ,АТХ,LARGE
 ,АТХ,WW
 ,АТХ,SIGN
 МС,VТМ,
 LFS:,CALL,SYMBOL
 14,VZM,*-1
 14,UТС,TABSYM
 ,ХТА,40В
 ,АТХ,W
 ,ААХ,В38
 МС,UZA,SWINT
 14,UТС,
 13,VТМ,40В-100В
 13,VZM,SHI.@
 13,UТМ,100В-43В
 13,VZM,SOI.#
 13,UТМ,43В-55В
 13,VZM,SSM.-
С    ФРАГМЕНТ ОКОНЧИЛСЯ, НЕ НАЧАВШИСЬ
 МС,VIM,ERR8.=> БЫЛ @ ИЛИ #
 ,ХТА,SIGN
 ,U1А,ERR8.=> БЫЛ -
 ,UJ,IFENDINT.=> ПУСТО
 SHI:МС,VIM,ERR8
 МС,VТМ,1.@
 ,UJ,LFS
 SOI:МС,VIM,ERR8
 МС,VТМ,2.#
 ,UJ,LFS
 SSM:,ХТА,SIGN
 ,U1А,ERR8
 ,ХТА,В1
 ,АТХ,SIGN
 ,UJ,LFS
 SWINT:,BSS,. Ц Е Л О Е :
 ,UJ,LINTD/.ДЕСЯТИЧНОЕ
 :МС,VТМ,-15
 ,UJ,LINTH/.ШЕСТНАДЦАТИР.
 :МС,VТМ,-19
 ,UJ,LINTO/.ВОСЬМЕРИЧНОЕ
 LINTH:,BSS,
 :,CALL,SYMBOL
 14,VZM,*-1
 LINTH/:14,UТС,TABSYM
 ,ХТА,40В
 ,АТХ,W
 ,ААХ,В39.НЕХ-ЦИФРА
 ,UZA,IFENDINT
  ,ХТА,W
 ,ASN,64+15
 ,ААХ,С17
 ,XTS,LARGE
 ,ASN,64-4
 ,XTS,WW
 ,ASN,64-16
 ,AUX,=:7400 0017 7777 7777
 ,ASN,64-4
 ,АТХ,WW
 ,УТА,
 15,АОХ,.LARGE
 ,SТХ,LARGE
 ,АОХ,WW
 ,АТХ,WW
 МС,VLM,LINTH
 ENDINT:,CALL,SYMBOL
 14,VZM,*-1
 ,UТС,TABSYM
 14,ХТА,40В
 ,АТХ,W
 IFENDINT:,BSS,
 14,UТС,
 М8,VТМ,40В
 14,UТМ,40В-54В
 14,VZM,*3
 ,ХТА,W
 ,ААХ,=:0000 14
 ,UZA,ERR8.=> НЕ РАЗДЕЛИТЕЛЬ
 *3:,BSS,
 ,ХТА,LARGE
 ,ААХ,В32
 ,U1А,ERR8.=> В64=1
 ,ХТА,SIGN
 ,UZA,RЕТ
 ,ХТА,WW
 ,АЕХ,В32/01
 ,ARX,В1
 15,АТХ,
 ,ASN,64+32
 ,XTS,LARGE
 ,АЕХ,В32/01
 15,ARX,
 ,ААХ,В32/01
 ,SТХ,LARGE
 ,ААХ,В32/01
 14,VТМ,
 М6,UJ,
 LINTO:,BSS,
 :,CALL,SYMBOL
 14,VZM,*-1
 LINTO/:14,UТС,TABSYM
 ,ХТА,40В
 ,АТХ,W
 ,ААХ,В41.ОСТ-ЦИФРА
 ,UZA,IFENDINT
 ,IТА,14
 ,ААХ,С7
 ,XTS,LARGE
 ,ASN,64-3
 ,XTS,WW
 ,ASN,64-16
 ,AUX,=:7000 0037 7777 7777
 ,ASN,64-3
 ,АТХ,WW
 ,УТА,
 15,АОХ,.LARGE
 ,SТХ,LARGE
 ,АОХ,WW
 ,АТХ,WW
 МС,VLM,LINTO
 ,UJ,ENDINT
 LINTD:,BSS,
 :,CALL,SYMBOL
 14,VZM,*-1
 LINTD/:,BSS,
 14,UТС,TABSYM
 ,ХТА,40В
 ,АТХ,W
 ,ААХ,В40.DЕС-ЦИФРА
 ,UZA,IFENDINT
 ,IТА,14
 ,ААХ,С17
 ,АТХ,W
 ,ХТА,LARGE.СТ. РАЗРЯДЫ
 ,ASN,64-3.*8
 ,ARX,LARGE.+*
 ,ARX,LARGE.+*
 ,АТХ,LARGE
 ,ХТА,WW.МЛ. РАЗРЯДЫ
 ,ASN,64-3.*8
 ,ARX,WW.+*
 ,ARX,WW.+*
 ,ARX,W.+ЦИФРА
 ,АТХ,WW
 ,ASN,64+32
 ,ARX,LARGE
 ,АТХ,LARGE
 ,ASN,64+31
 ,U1А,ERR8.ПЕРЕПОЛНЕНИЕ
 ,ХТА,WW
 ,ААХ,В32/01
 ,АТХ,WW
 ,UJ,LINTD
 ERR8:14,VТМ,8
 М6,UJ,
 BL:,BSS,LBL
 ,END,

 REFER:1,NАМЕ,***
С*
С*     ПЕЧАТЬ ТАБЛИЦЫ ССЫЛОК.
С*
С*
 ,BITS,
 ,DEFLC,
 *TSS*:LС,BLOCK,TSS
 PRINT8//:,SUBP,
 LPR:,WEQ,PRINT8//
 LPR:I,BLOCK,(2),NSTRING
 BL:I,BLOCK,WSS,WS1
 LBL:,EQU,2
С
С
 13,VJM,BEGTAB. ОТСТУП
 14,VТМ,ТТ
 13,VJM,PR/Т
 14,VТМ,ТТТ
 13,VJM,PR/Т
 13,VJM,SРАСЕ.ОТСТУП
 14,VТМ,TTIR
 13,VJM,PR/Т
 13,VJM,SРАСЕ
С
С     ПЕЧАТЬ ТАБЛИЦЫ ИСПОЛЬЗОВАНИЯ МОДИФИКАТОРОВ
С
 М4,VТМ,-15
 LMOD:,BSS,
 М4,ХТА,TDEF2
 ,UZA,RLMOD
 ,ХТА,=6Н
 ,АТХ,STR
 ,UТС,TNIR
 М4,ХТА,15
 ,АТХ,STR+1
 М7,VJM,Р/REFER
 RLMOD:М4,VLM,LMOD
 13,VJM,SРАСЕ.ОТСТУП
 14,VТМ,TTNAM
 13,VJM,PR/Т
 13,VJM,SРАСЕ
 М4,VТМ,1
 ,NTR,6
 LID:,BSS,
 М4,ХТА,TDEF
 ,UZA,RLID
 М4,UТС,TDEF
 14,VТМ,
 ,ITS,14
 14,VТМ,STR
 ,ITS,14
 ,CALL,TEXISO
 ,ХТА,=3Н'''':
 ,АОХ,STR+1
 ,АТХ,STR+1
  М7,VJM,Р/REFER
 М4,VLM,LID
 RLID:,BSS,
 М6,UJ,
 BEGTAB:,BSS,.13,VJM,BEGTAB
 ,NTR,7
 ,ХТА,NSTRING
 ,Х-А,=12
 ,U1А,SРАСЕ
 ,UJ,PRINT8//+2
 SРАСЕ:15,UТМ,3
 ,ХТА,В7
 ,UJ,PRINT8//
 PR/Т:,ITS,14
 14,UТМ,4
 ,ITS,14
 ,XTS,
 ,UJ,PRINT8//
 Р/REFER:,BSS,
С
С     ПЕЧАТЬ СПИСКА ССЫЛОК
С
 М4,ХТА,TDEF2
 ,ASN,64+24
 ,ААХ,С7777
 ,ATI,14
 ,UТС,TSS
 14,ХТА,
 ,АТХ,WSS
 М5,VТМ,
С
С     ЦИКЛ  ПО  TSS
С
 BLMR:,BSS,
 ,ХТА,
 ,АТХ,STR+2
 М3,VТМ,-9
 LMR:,BSS,
 ,ХТА,WSS
 М5,VIM,JW2
С     ПЕРВЫЙ  ФРАГМЕНТ
 ,ASN,64+30
 ,UJ,ISFR
 JW2:,BSS,
С     ВТОРОЙ  ФРАГМЕНТ
 ,ARX,
 ,U1А,RLMR.КОНЕЦ СПИСКА
 ,ASN,64+15
 ISFR:,ААХ,С77777
 ,АТХ,WS1
 ,ASN,64+13
 ,ATI,14
 ,УТА,
 ,ASN,64-1
 ,AUX,=6Н'''''''17''17''17'
 15,АТХ,
 ,ARX,=6Н'''''''166''166''166'
 ,ААХ,=6Н'''''''160''160''160'
 ,ASN,64+4
 14,UТС,TW1
 ,АЕХ,
 15,ARX,
 М3,АТХ,STR+11
 М5,VZM,ELMR.=> ПЕРВЫЙ ФР.
 ,ХТА,WSS
 ,ATI,14
 14,VIM,PWSS
С     КОНЕЦ СПИСКА
 М3,UТМ,1
 ,UJ,RLMR
 PWSS:14,UТС,TSS
 ,ХТА,
 ,АТХ,WSS
 ELMR:М5,UТМ,40000В
 М3,VLM,LMR
 ,UJ,PRS
 RLMR:,ХТА,=12
 М3,АТХ,STR+11
 М3,VТМ,1
 PRS:,ХТА,STR+2
 М7,UZA,.ВСЕ
 14,VТМ,STR
 ,ITS,14
 14,UТМ,11
 ,ITS,14
 ,XTS,
 ,CALL,PRINT8//
 М7,UТС,
 М3,VIM,.=> КОНЕЦ
 ,ХТА,=6Н
 ,АТХ,STR
 ,АТХ,STR+1
 ,UJ,BLMR.=> ПРОДОЛЖЕНИЕ
С
С
С
 ТТ   :,ISO,30Н ТАБЛИЦА ССЫЛОК
 ТТТ  :,ISO,30Н----------------
 TTIR :,ISO,30НИНДЕКСНЫЕ РЕГИСТРЫ:
 TTNAM:,ISO,30НИДЕНТИФИКАТОРЫ:
 TW1:,ISO,6Н'1'  777
 ,ISO,6Н'1' Х777
 ,ISO,6Н'1' V777
 ,ISO,6Н'1' L777
 TNIR:,ISO,6Н 1:
 ,ISO,6Н 2:
 ,ISO,6Н 3:
 ,ISO,6Н 4:
 ,ISO,6Н 5:
 ,ISO,6Н 6:
 ,ISO,6Н 7:
 ,ISO,6Н 8:
 ,ISO,6Н 9:
 ,ISO,6Н10:
 ,ISO,6Н11:
 ,ISO,6Н12:
 ,ISO,6Н13:
 ,ISO,6Н14:
 ,ISO,6Н15:
 BL:,BSS,LBL
 ,END,

 S/PARAM:1,NАМЕ,***
С*
С*   S/PARAM - ВЫБОРКА ПЕРВОГО ФРАГ-
С*      МЕНТА АДРЕСНОГО ВЫРАЖЕНИЯ
С*
С*   PARAM   - ВЫБОРКА   ОЧЕРЕДНОГО
С*      ФРАГМЕНТА АДРЕСН. ВЫРАЖЕНИЯ
С*
С*  РЕЗУЛЬТАТ:   АСС = ПАРАМЕТР
С*
С*     I14 = ТИП:
С*         0 - ОШИБКА
С*         1 - ИДЕНТИФИКАТОР
С*         2 - ЧИСЛО
С*         3 - ПУСТО
С*
С*     М8 = ТИП РАЗДЕЛИТЕЛЯ:
С*         0 - КОНЕЦ СТРОКИ
С*         1 - ,
С*         2 - +
С*         3 - -
С*         4 - .
С*         5 - (
С*         6 - )
С*         7 - ПРОЧИЕ РАЗДЕЛИТЕЛИ.
С*
С*     СТАВ = КОД РАЗДЕЛИТЕЛЯ
С*
С*
С*      S/SYMBOL - ВЫБОРКА ПЕРВОГО СИМВОЛА
С*         АДРЕСНОГО ВЫРАЖЕНИЯ
С*
С*      SYMBOL   - ВЫБОРКА  ОЧЕРЕДНОГО СИМ-
С*         ВОЛА АДРЕСНОГО ВЫРАЖЕНИЯ.
С*
С*  РЕЗУЛЬТАТ: АСС = СИМВОЛ (КОД ISO)
С*
С*     ВНИМАНИЕ!   РЕГИСТРЫ М9 И МА ИСПОЛЬ-
С*  ЗУЮТСЯ ДЛЯ ХРАНЕНИЯ УКАЗАТЕЛЕЙ, ПЕРЕДА-
С*  ВАЕМЫХ ОТ УСТАНОВОЧНЫХ ВХОДОВ  S/PARAM,
С*  S/SYMBOL   ВХОДАМ   PARAM,  SYMBOL.
С*  ВЫЗЫВАЮЩАЯ ПРОГРАММА НЕ ДОЛЖНА ПОРТИТЬ
С*  ЭТИ РЕГИСТРЫ ДО ОКОНЧАНИЯ ВЫБОРКИ ФРАГ-
С*  МЕНТОВ АДРЕСНОГО ВЫРАЖЕНИЯ.
С*
С*
 ,DEFLC,
 ,BITS,
 TABSYM:,SUBP,
С
С     ВНИМАНИЕ!   НЕКОТОРЫЕ ПРОГРАММЫ
С   ОБРАЩАЮТСЯ К ВХОДУ  S/PARAM   КАК
С   К  PARAM-1  ИЛИ САМОСТОЯТЕЛЬНО УС-
С   ТАНАВЛИВАЮТ  М9 И МА.
С
 М9,VТМ,TADS
 МА,VТМ,
 PARAM:,ENTRY,
С
С    СОСТОЯНИЕ (МВ):
С  0 - НАЧАЛО ФРАГМЕНТА
С  1 - ИДЕНТИФИКАТОР
С  2 - ДЕС. ЧИСЛО
С  3 - ШЕСТН. ЧИСЛО
С  4 - ВОСЬМЕРИЧНОЕ ЧИСЛО
С
 МВ,VТМ,0
 NEXTS:МА,VLM,CONTS
 М9,ХТА,
 ,АТХ,WORD
 М9,UТМ,1
 МА,VТМ,-5
 CONTS:,ХТА,WORD
 ,ASN,64-8
 ,АТХ,WORD
 ,УТА,
 ,ATI,М8
 М8,WТС,TABSYM
 ,UJ,*+2
 :МВ,UJ,SWN.-1 (ЦИФРА)
 :МВ,UJ,SWS.0 (БУКВА)
 :,UJ,OTHER.:
 :М8,VТМ,.;
 ,UJ,TBS
 :,UJ,ERROR.=
 :М8,VТМ,1.,
 ,UJ,TBS
 :М8,VТМ,2.+
 ,UJ,TBS
 :М8,VТМ,3.-
 ,UJ,TBS
 :М8,VТМ,4..
 ,UJ,TBS
 :М8,VТМ,5.(
 ,UJ,TBS
 :М8,VТМ,6.)
 ,UJ,TBS
 :МВ,VZM,BEGX.@
 ,UJ,ERROR
 :,UJ,NEXTS.BLANC
 :М8,VТМ,
 ,UJ,TBS.КОНЕЦ СТРОКИ
 OTHER:,BSS,
 ,IТА,М8
 М8,VТМ,7
 ,UJ,TBS
 ERROR:14,VТМ,
 RЕТ:,ХТА,
 13,UJ,
 SWN:,BSS,.ЦИФРА
 МВ,VТМ,2.НАЧАЛО ЧИСЛА
 ,UJ,BEGDEC
 :,UJ,WRID.ИДЕНТИФИКАТОР
 :,UJ,WRDEC.ДЕС ЧИСЛО
 :,UJ,WRX.ШЕСТН. ЧИСЛО
 :,UJ,IFO.ВОСЬМ. ЧИСЛО
 SWS:,BSS,.БУКВА
 :МВ,VТМ,1.0
 ,UJ,BEGID
 :,UJ,WRID.1
 :,UJ,ERROR.2
 :,UJ,IFX.3
 :,UJ,ERROR.4
 BEGDEC:,ААХ,С17
 ,АТХ,W
 МС,VТМ,-10.10 ЦИФР
 ,UJ,NEXTS
 WRDEC:,ААХ,С17
 ,XTS,W
 ,ASN,64-3.*8
 ,ARX,W.+W
 ,ARX,W.+W
 15,ARX,
 ,АТХ,W
 МС,VLM,NEXTS
 ,UJ,ERROR.МНОГО!
 CHN:,ХТА,W
  ,ASN,64+32
 ,U1А,ERROR
 ,ХТА,W
 14,VТМ,2
 13,UJ,
 BEGX:,BSS,
 ,ХТА,
 ,АТХ,W
 М8,UТМ,-43В.#
 М8,VZM,BEGOCT
 МВ,VТМ,3
 МС,VТМ,-8.8 СИМВОЛОВ
 ,UJ,NEXTS
 BEGOCT:,BSS,
 МС,VТМ,-11.11 СИМВ.
 МВ,VТМ,4
 ,UJ,NEXTS
 WRX:,BSS,
 М8,UТС,TABSYM
 ,ХТА,
 ,ASN,64+15
 ,ААХ,С17
 ,XTS,W
 ,ASN,64-4
 15,АОХ,
 ,АТХ,W
 МС,VLM,NEXTS
 ,UJ,ERROR
 IFX:М8,UТС,TABSYM
 ,ХТА,
 ,ААХ,В39
 ,U1А,WRX
 ,UJ,ERROR
 IFO:,BSS,
 М8,UТС,TABSYM
 ,ХТА,
 ,ААХ,В41
 ,UZA,ERROR
 ,IТА,М8
 ,ААХ,С7
 ,XTS,W
 ,ASN,64-3
 15,АОХ,
 ,АТХ,W
 МС,VLM,NEXTS
 ,UJ,ERROR
 TBS:,BSS,.РАЗДЕЛИТЕЛЬ
 ,АТХ,СТАВ
 МВ,UJ,*+1
 :14,VТМ,3.НАЧАЛО
 ,UJ,RЕТ
 :14,VТМ,1
 ,UJ,ENDID
 :,UJ,CHN.ДЕС. ЧИСЛО
 :,UJ,CHN.16-ЧИСЛО
 :,UJ,CHN.8-ЧИСЛО
 BEGID:,BSS,
 М8,UТС,TABSYM
 ,ХТА,
 ,ASN,64-6
 ,УТА,
 ,АТХ,W
 МС,VТМ,-42
 ,UJ,NEXTS
 WRID:МС,VZM,ERROR
 М8,UТС,TABSYM
 ,ХТА,
 ,ASN,64-6
 ,УТА,
 ,XTS,W
 ,ASN,64-6
 15,АОХ,
 ,АТХ,W
 МС,UТМ,6
 ,UJ,NEXTS
 ENDID:,ХТА,W
 МС,ASN,64
 ,АТХ,W
 13,UJ,
 S/SYMBOL:,ENTRY,
 М9,VТМ,TADS
 МА,VТМ,
 SYMBOL:,ENTRY,
 SYMBOL:,BSS,
 МА,VLM,CONTS2
 М9,ХТА,
 ,АТХ,WORD
 М9,UТМ,1
 МА,VТМ,-5
 CONTS2:,ХТА,WORD
 ,ASN,64-8
 ,АТХ,WORD
 ,УТА,
 ,ATI,14
 14,UТМ,-40В
 13,UJ,
 WORD:,BSS,1
 W:,BSS,1
 ,END,

 SYNTAX:1,NАМЕ,***
С*            20/06/86
С*
С*     СИНТАКСИЧЕСКИЙ АНАЛИЗ ВХОДНОЙ
С*  СТРОКИ ДЛЯ ТРАНСЛЯТОРА  МКВ/ASS.
С*
С*
С*
С*        ФОРМИРУЕТ ПОЛЯ:
С*
С*  ТМТ      - МЕТКА
С*  TIR      - МОДИФИКАТОР
С*  ТСОР     - КОП
С*  TADS     - ПОЛЕ "АДРЕС+КОММЕНТАРИЙ"
С*  LADS     - ДЛИНА ПОЛЯ АДРЕСА В СЛОВАХ
С*
С*       И ШКАЛУ РАБОТ ПО ОБРАБОТКЕ (SСН):
С*
С*  В48 - ОШИБКА
С*  В47 - ПРИНЯТЬ МЕТКУ
С*  В46 - ПРИНЯТЬ АДРЕС В ПОЛЕ МЕТКИ
С*  В45 - ПРИЗНАК "ПОМЕЧЕНО"
С*  В44 - ОБРАБОТАТЬ ПОЛЕ МОДИФИКАТОРА
С*  В43 - ОБРАБОТАТЬ 3-СИМВОЛЬНЫЙ КОП
С*  В42 - ОБРАБОТАТЬ 4-5-СИМВОЛЬНЫЙ КОП
С*  В41 - ОБРАБОТАТЬ ЛИТЕРАЛ
С*  В40 - ОБРАБОТАТЬ АДРЕСНОЕ ВЫРАЖЕНИЕ
С*  В39 - ВСЯ СТРОКА - КОММЕНТАРИЙ
 ,BITS,
 ,DEFLC,
 TABSYM:,SUBP,
 NS:,EQU,М7
 FN:,EQU,М9
 IZ:,EQU,МВ
С
С
С     РАСПРЕДЕЛЕНИЕ РЕГИСТРОВ:
С
С  М3 - УК. СЛОВА ЧТЕНИЯ
С  М4 - СЧЕТЧИК В СЛОВЕ ЧТЕНИЯ
С  М5 - УК. СЛОВА ЗАПИСИ
С  М6 - СЧЕТЧИК В СЛОВЕ ЗАПИСИ
С  М7 - СЧЕТЧИК СИМВОЛОВ ФРАГМЕНТА (NS)
С  М8 - НОМЕР СОСТОЯНИЯ
С  М9 - ПРИЗНАК ЧИСЛА (FN):
С       0 - НЕ ЧИСЛО
С       1 - ПОЛОЖИТЕЛЬНОЕ ЧИСЛО
С       2 - ОТРИЦАТЕЛЬНОЕ ЧИСЛО
С  МА - ТЕКУЩИЙ СИМВОЛ
С  МВ - ПРИЗНАК ШЕСТНАДЦАТИРИЧНОГО
С
С
С
С        С О С Т О Я Н И Я :
С
С  1 - НАЧАЛО ПЕРВОГО ФРАГМЕНТА
С  2 - ПРИЕМ ПЕРВОГО ФРАГМЕНТА
С      (МЕТКА.OR.IR.OR.КОП)
С  3 - НАЧАЛО ВТОРОГО ФРАГМЕНТА
С      (IR.OR.КОП)
С  4 - ПРИЕМ ВТОРОГО ФРАГМЕНТА
С      (IR.OR.КОП)
С  5 - ПРИЕМ ТРЕТЬЕГО ФРАГМЕНТА (КОП)
С  6 - НАЧАЛО ПРИЕМА АДРЕСА
С  7 - ПРИЕМ АДРЕСНОЙ ЧАСТИ И КОММЕНТАРИЯ
С
С
С
С
 ,IТА,13
 ,ITS,М3
 ,ITS,М4
 ,ITS,М5
 ,ITS,М6
 ,ITS,М7
 ,XTS,
С      НАЧАЛЬНЫЕ УСТАНОВКИ
С
 М3,VТМ,BUF
 М4,VТМ,-5
 М5,VТМ,ТСОР
 М6,VТМ,-5
 NS,VТМ,
 М8,VТМ,1
 FN,VТМ,
 IZ,VТМ,
 ,АТХ,LADS
 ,АТХ,LCOMMENT
 ,АТХ,SСН
  ,АТХ,ERRIDENT.КОНЕЦ TADS
 ,ХТА,=6Н
 ,АТХ,TIR
  14,VТМ,-16
  CLM:14,АТХ,ERRIDENT-1.ОТ NUMBER+2
  14,VLM,CLM.ДО TCOMMENT+11
 ,АТХ,=6Н      .В БРЗ
С
С   ПОИСК КОНЦА СТРОКИ
С
 14,VТМ,12
 *1:,BSS,
 14,ХТА,STR-1
 ,АЕХ,=6Н
 ,U1А,*1Е
 14,UТМ,-1
 14,V1М,*1
 ,UJ,RETSYNT.ПУСТАЯ КАРТА
 ERR:,ХТА,В48
 ,АТХ,SСН
 14,VТМ,-11
 LTS:14,UТС,STR+11
 ,ХТА,
 14,АТХ,NUMBER+13
 14,VLM,LTS
 RETSYNT:,SТХ,
 ,STI,М7
 ,STI,М6
 ,STI,М5
 ,STI,М4
 ,STI,М3
 ,ATI,13
 ,ХТА,SСН
 13,UJ,
 *1Е:,BSS,
 ,ХТА,
 ,UТС,BUF
 14,АТХ,
 *2:14,ХТА,STR-1
 ,UТС,BUF-1
 14,АТХ,
 14,UТМ,-1
 14,VIM,*2
 NEWSYM:,BSS,
 М3,ХТА,
 ,ASN,64-8
 М3,АТХ,
 ,УТА,
 ,АТХ,SУМ
 ,ATI,МА
 М4,VLM,SWITCH
 М3,UТМ,1
 М4,VТМ,-5
 SWITCH:,BSS,
 МА,WТС,TABSYM
 ,UJ,*+2
 :М8,UJ,NUMBER*-1.ЦИФРА
 :М8,UJ,SYMBOL-1.СИМВОЛ
 :М8,UJ,SW1-1.:
 :М8,UJ,SW2-1.;
 :М8,UJ,SW3-1.=
 :М8,MTJ,14
 ,UJ,S/4.,
  :М8,UJ,SW5-1. + ИЛИ *
  :М8,UJ,SW6-1. - ИЛИ /
 :,UJ,S/49..
 :М8,UJ,SW8-1.(
 :,UJ,S/49.)
 :М8,UJ,SW10-1.@
 :М8,UJ,SW11-1.ПРОБЕЛ
 :М8,UJ,SW12-1.КОНЕЦ СТРОКИ
 :,UJ,S13.ПРОЧИЕ РАЗДЕЛИТ.
 SW1:,BSS,.ДВОЕТОЧИЕ
 :М8,VТМ,3.S=1
 ,UJ,S1/1С
 :М8,VТМ,3.S=2
 ,UJ,S1/2С
 :,UJ,ERR.3
 :,UJ,ERR.4
 :,UJ,ERR.5
 :,UJ,ERR.6
 :,UJ,WRITSN.7
 SW2:,BSS,.ТОЧКА С ЗАПЯТОЙ
 :М8,VТМ,7.1
 ,UJ,S2/1С
 :М8,VТМ,7.2
 ,UJ,S2/2С
 :М8,VТМ,7.3
 ,UJ,S2/3С
 :М8,VТМ,7.4
 ,UJ,S2/4С
 :М8,VТМ,7.5
 ,UJ,S2/5С
 :М8,VТМ,7.6
 ,UJ,S2/6С
 :,UJ,WRITSN.7
 SW3:,BSS,.=
 ,UJ,ERR.1
 :13,VТМ,S3/6.2
 ,UJ,IFCOP
 :,UJ,ERR.3
 :13,VТМ,S3/6.4
 ,UJ,IFCOP
 :13,VТМ,S3/6.5
 ,UJ,IFCOP
 :,ХТА,В41.6
 ,UJ,PUTADR
 :,UJ,WRITSN.7
 S/4:,BSS,. ,
 14,UТМ,-1
 14,VIM,S/49
С    СТРОКА НАЧИНАЕТСЯ С ЗАПЯТОЙ: ПРОДОЛЖЕНИЕ
 ,ХТА,=6Н    ,
 ,АТХ,ТСОР
 М8,VТМ,6
 М6,VТМ,-5
 NS,VТМ,
 ,UJ,NEWSYM
  SW5:М8,VТМ,5
  ,UJ,WRITSN. 1
  :М8,VТМ,5
  ,UJ,S5/5. 2
  :М8,VТМ,5
  ,UJ,WRITSN. 3
  :,,
  :М8,VТМ,5
  ,UJ,S5/5. 5
  :,ХТА,В40
  ,UJ,PUTADR. 6
  :,UJ,WRITSN. 7
 S5/5:NS,MTJ,14
 14,VZM,WRITSN.ПЕРВЫЙ СИМВ.КОП
 14,UТМ,-1
 14,VZM,WRITSN.ВТОРОЙ СИМВОЛ КОП
 13,VТМ,S6/6
 ,UJ,IFCOP
  S/49:,BSS,. ) , .
 М8,ХТА,BITS
 ,ААХ,=:014.SOST=6,7
 ,UZA,ERR.=> НЕТ
 ,ААХ,В43
 ,U1А,S8/6.6
 ,UJ,WRITSN
 SW6:,BSS,.-
 :FN,VТМ,2.1
 ,UJ,S6/1С
 :,UJ,S5/5.2
 :,UJ,ERR.3
 :,UJ,S5/5.4
 :,UJ,S5/5.5
 S6/6:,ХТА,В40.6
 ,UJ,PUTADR
 :,UJ,WRITSN.7
 SW10:,BSS,.@
 :FN,VТМ,1.1
 ,UJ,S10/1С
 :NS,VIM,ERR.2
 FN,UJ,S10/2С
 :FN,VТМ,1.3
 ,UJ,S10/3С
 :13,VТМ,S10/6.4
 ,UJ,IFCOP
 :13,VJM,IFCOP.5
 S10/6:,ХТА,В40.6
 ,UJ,PUTADR
 :,UJ,WRITSN.7
 SW8:,BSS,. (
 :,UJ,ERR.1
 :13,VТМ,S8/6.2
 ,UJ,IFCOP
 :,UJ,ERR.3
 :13,VТМ,S8/6.4
 ,UJ,IFCOP
 :13,VJM,IFCOP.5
 S8/6:,ХТА,В40.6
 ,UJ,PUTADR
 :,UJ,WRITSN.7
 SW11:,BSS,.BLANK
 :,UJ,NEWSYM.1
 :,UJ,S11/4.2
 :,UJ,NEWSYM.3
 :,UJ,S11/4.4
 S11/5:М8,VТМ,6.5
 13,VJM,IFCOP
 :,UJ,NEWSYM.6
 :,UJ,WRITSN.7
 SW12:,BSS,.КОНЕЦ СТРОКИ
 :,UJ,RETSYNT.1
 :13,VТМ,RETSYNT.2
 ,UJ,IFCOP
 :,ХТА,=6Н      .3
 ,UJ,S12/3С
 :13,VТМ,RETSYNT.4
 ,UJ,IFCOP
 :13,VТМ,RETSYNT.5
 ,UJ,IFCOP
 :,UJ,RETSYNT.6
 :,UJ,S12/7С.7
 S13:,BSS,
 М8,ХТА,BITS
 ,ААХ,=:014.S=6,7
 ,UZA,ERR.S=1...5
 ,ASN,64-6
 ,UZA,S6/6.S=6
 ,UJ,WRITSN.S=7
 NUMBER*:,BSS,.ДЕС. ЦИФРА
 :М8,VТМ,2.1
 FN,VТМ,1
 :,UJ,WRITSN.2
 :М8,VТМ,4.3
 FN,VТМ,1
 :,UJ,WRITSN.4
 :,UJ,WRITSN.5
 :,ХТА,В40.6
 ,UJ,PUTADR
 :,UJ,WRITSN.7
 SYMBOL:,BSS,.БУКВА
 :М8,VТМ,2.1
 ,UJ,WRITSN
 :FN,VIM,IFIR.2
 ,UJ,WRITSN
 :М8,VТМ,4.3
 ,UJ,WRITSN
 :FN,VIM,IFIR.4
 ,UJ,WRITSN
 :,UJ,WRITSN.5
 :,ХТА,В40.6
 ,UJ,PUTADR
 :,UJ,WRITSN.7
С
С..................................
С
 S1/1С:,BSS,
 ,ХТА,В45
 ,АТХ,SСН
 ,ХТА,=6Н  :
 ,АТХ,ТМТ+1
 ,UJ,NEWSYM
 S1/2С:,BSS,
 ,ХТА,В45
 ,АТХ,SСН
 NS,VZM,NEWSYM
 :FN,VZM,*+1.IDENT
  ,UТС,В46-В47.АДРЕС В МЕТКЕ
 :,АОХ,В47
 ,АТХ,SСН
  13,VJM,TRJT
 ,ХТА,ТСОР
 ,АТХ,ТМТ
 ,ХТА,ТСОР+1
 ,ААХ,=:7777 74
 ,АОХ,=6Н'''':
 ,АТХ,ТМТ+1
 ,ХТА,=6Н
 ,АТХ,ТСОР+1
 М5,VТМ,ТСОР
 ,UJ,NEWSYM
 S2/1С:,BSS,
 М5,VТМ,NUMBER+2
 ,ХТА,В39
 ,АТХ,SСН
 ,UJ,NEWSYM
 S2/2С:,BSS,
 S2/4С:,BSS,
 S2/5С:13,VJM,IFCOP
 М5,VТМ,TADS
 ,UJ,NEWSYM
 S2/3С:,ХТА,=6Н
 ,АТХ,ТСОР
 М5,VТМ,TADS
 ,UJ,NEWSYM
 S2/6С:,BSS,
 М5,VТМ,TADS
 ,UJ,WRITSN
 S3/6:,ХТА,В41
 PUTADR:,BSS,
 М8,VТМ,7
 М5,VТМ,TADS
 ,АОХ,SСН
 ,АТХ,SСН
 ,UJ,WRITSN
 S6/1С:,BSS,
 S10/1С:,BSS,
 М8,VТМ,2
 IZ,VТМ,1
 ,UJ,WRITS
 S10/3С:М8,VТМ,4
 IZ,VТМ,1
 ,UJ,WRITSN
  S10/2С:,BSS,
 ,UJ,ERR
 :,UJ,ERR
 :IZ,VТМ,1
 ,UJ,WRITS
 S11/4:,BSS,
 ,IТА,NS
 ,ARX,В1
 ,ASN,64+2
 ,U1А,S11/5.NS.GТ.2
 ,IТА,FN
 ,ASN,64+1
 ,U1А,ERR.ОТРИЦАТ. ЧИСЛО
 13,VJM,TRJT
 ,ХТА,ТСОР
 ,АТХ,TIR
 ,ХТА,В44
 ,АОХ,SСН
 ,АТХ,SСН
 М8,VТМ,5
 ,UJ,NEWSYM
 S12/3С:,АТХ,ТСОР
 ,UJ,RETSYNT
 S12/7С:М6,UТМ,5
 ,IТА,М6
 М6,VZM,WRW
 ,ASN,64-3
 ,ATI,14
 М5,ХТА,
 14,ASN,64-48
  М6,UТС,MBLANKS
  ,АОХ,
  М5,АТХ,
  WRW:,BSS,
 М5,UТМ,-TADS+1
 ,IТА,М5
 ,АТХ,LADS
 ,UJ,RETSYNT
 IFIR:,BSS,
 IZ,V1М,IF16
 FN,MTJ,14
 14,UТМ,-2
 14,VZM,ERR.ОТРИЦ. ИНДЕКС
 ,IТА,NS
 ,ARX,В1
 ,ASN,64+2
 ,U1А,ERR.NS.GТ.2
 13,VJM,TRJT
 ,ХТА,ТСОР
 ,АТХ,TIR
 ,ХТА,В44
 ,АОХ,SСН
 ,АТХ,SСН
 М8,VТМ,5
 ,UJ,WRITSN
 IF16:МА,UТС,TABSYM
 ,ХТА,
 ,ААХ,В39.ШЕСТН. ЦИФРА
 ,UZA,ERR
 WRITSN:,BSS,
 NS,UТМ,1
 WRITS:,BSS,.ЗАПИСЬ СИМВОЛА
 М5,ХТА,
 ,ASN,64-8
 ,АОХ,SУМ
 М5,АТХ,
 М6,VLM,NEWSYM
 М6,VТМ,-5
 М5,UТМ,1
 ,UJ,NEWSYM
 IFCOP:,BSS,.П/П
 FN,VIM,ERR
 NS,ХТА,BITS
 ,АРХ,=:16.3,4,5
 ,UZA,ERR.НЕ 3-5 СИМВ.
 NS,MTJ,14
 14,UТМ,-3
 :14,VZM,*+1
 ,UТС,В42-В43
 :,ХТА,В43
 ,АОХ,SСН
 ,АТХ,SСН
 TRJT:,BSS,
 М6,UТМ,5
 ,IТА,М6
 М6,VZM,WR
 ,ASN,64-3
 ,ATI,14
 М5,ХТА,
 14,ASN,64-48
 WR:М6,UТС,MBLANKS
 ,АОХ,
 М5,АТХ,
 М6,VТМ,-5
С
С    ЧИСТКА ФЛАГОВ ФРАГМЕНТА
С
 NS,VТМ,
 IZ,VТМ,
 FN,VТМ,
 13,UJ,
 MBLANKS:,ISO,6Н
 ,ISO,6Н''
 ,ISO,6Н''''
 ,ISO,6Н''''''
 ,ISO,6Н''''''''
 ,ISO,6Н''''''''''
 SУМ:,BSS,1
 BUF:,BSS,13
 ,END,SYNTAX

 Т/SYNTAX:,NАМЕ,.PROGRAM
С*
С*     ТЕСТ П/П SYNTAX
С*
 ****:,SUBP,
 Д:,РС,0
 SYNTAX:,SUBP,
 *STRING*:,LС,100
 ISYNT:,SUBP,
 TSTSYN:,SUBP,
 ,UJ,TSTSYN
 ,END,

 TABSYM:,NАМЕ,
С*          23/06/86
С*
С*
С*     ТАБЛИЦА СИМВОЛОВ ISO.
С*
С*      ПО СЛОВУ НА СИМВОЛ:
С*
С*  48:43 - КОД В 'ТЕХТ'
С*  42    - БУКВА
С*  41    - ВОСЬМЕРИЧНАЯ ЦИФРА
С*  40    - ДЕСЯТИЧНАЯ ЦИФРА
С*  39    - ШЕСТНАДЦАТИРИЧНАЯ ЦИФРА
С*  38    - РАЗДЕЛИТЕЛЬ
С*  37    - ПРОБЕЛ
С*  36    - СИМВОЛЫ  * /
С*  35    - АПОСТРОФ
С*  34    - КОНЕЦ СТРОКИ
С*  33    - ТОЧКА С ЗАПЯТОЙ
С*  32:25 - КОД СИМВОЛА МКБ
С*  24-20 - РЕЗЕРВ
С*  19-16 - ШЕСТНАДЦАТИРИЧНОЕ ЗНАЧЕНИЕ
С*          (ПРИ В39=1)
С*  15:01 - ТИП ДЛЯ ПЕРЕКЛЮЧАТЕЛЯ:
С*          -1 - ЦИФРА
С*           0 - БУКВА
С*           1 - :
С*           2 - ;
С*           3 - =
С*           4 - ,
С*           5 - + ИЛИ *
С*           6 - - ИЛИ /
С*           7 - .
С*          10 - (
С*          11 - )
С*          12 - @ ИЛИ #
С*          13 - ПРОБЕЛ
С*          14 - КОНЕЦ СТРОКИ ('0', '12')
С*          15 - ПРОЧИЕ РАЗДЕЛИТЕЛИ
С*
 0,LOG,0002 1000 0000 0014
 1,LOG,
 2,LOG,
 3,LOG,
 4,LOG,
 5,LOG,
 6,LOG,
 7,LOG,
 10,LOG,
 11,LOG,
 12,LOG,0002 1000 0000 0014
 13,LOG,
 14,LOG,
 15,LOG,
 16,LOG,
 17,LOG,
 20,LOG,
 21,LOG,
 22,LOG,
 23,LOG,
 24,LOG,
 25,LOG,
 26,LOG,
 27,LOG,
 30,LOG,
 31,LOG,
 32,LOG,
 33,LOG,
 34,LOG,
 35,LOG,
 36,LOG,
 37,LOG,
 40,LOG,0001 0000 0000 0013.BLANK
 41,LOG,0002 0000 0000 0015.!
 42,LOG,0002 2000 0000 0015."
 43,LOG,0002 0000 0000 0012.#
 44,LOG,0002 0000 0000 0015.$
 45,LOG,0002 0000 0000 0015.%
 46,LOG,0002 0000 0000 0015.&
 47,LOG,0002 2000 0000 0015.'
 50,LOG,0002 0000 0000 0010.(
 51,LOG,0002 0000 0000 0011.)
 52,LOG,1202 4000 0000 0005.*
 53,LOG,3602 0000 0000 0005.+
 54,LOG,0002 0000 0000 0004.,
 55,LOG,3502 0000 0000 0006.-
 56,LOG,0002 0000 0000 0007..
 57,LOG,1702 4000 0000 0006./
 60,LOG,2034 0000 0007 7777.0
 61,LOG,2134 0000 0017 7777.1
 62,LOG,2234 0000 0027 7777.2
 63,LOG,2334 0000 0037 7777.3
 64,LOG,2434 0000 0047 7777.4
 65,LOG,2534 0000 0057 7777.5
 66,LOG,2634 0000 0067 7777.6
 67,LOG,2734 0000 0077 7777.7
 70,LOG,3014 0000 0107 7777.8
 71,LOG,3114 0000 0117 7777.9
 72,LOG,0002 0000 0000 0001.:
 73,LOG,0002 0400 0000 0002.;
 74,LOG,0002 0000 0000 0015.<
 75,LOG,0002 0000 0000 0003.=
 76,LOG,0002 0000 0000 0015.>
 77,LOG,0002 0000 0000 0015.?
 100,LOG,0002 0000 0000 0012.@
 101,ОСТ,4144 0000 012.А
 102,ОСТ,4244 0000 013.В
 103,ОСТ,4344 0000 014.С
 104,ОСТ,4444 0000 015.D
 105,ОСТ,4544 0000 016.Е
 106,ОСТ,4644 0000 017.F
 107,ОСТ,474.G
 110,ОСТ,504.Н
 111,ОСТ,514.I
 112,ОСТ,524.J
 113,ОСТ,534.К
 114,ОСТ,544.L
 115,ОСТ,554.М
 116,ОСТ,564.N
 117,ОСТ,574.О
 120,ОСТ,604.Р
 121,ОСТ,614.Q
 122,ОСТ,624.R
 123,ОСТ,634.S
 124,ОСТ,644.Т
 125,ОСТ,654.U
 126,ОСТ,664.V
 127,ОСТ,674.W
 130,ОСТ,704.Х
 131,ОСТ,714.У
 132,ОСТ,724.Z
 133,LOG,0002 0000 0000 0015.[
 134,LOG,0002 0000 0000 0015.\
 135,LOG,0002 0000 0000 0015.]
 136,LOG,0002 0000 0000 0015.^
 137,ОСТ,014._
 140,ОСТ,7740 0236.Ю
 141,ОСТ,4140 0101.А
 142,ОСТ,0240 0201.Б
 143,ОСТ,0340 0226.Ц
 144,ОСТ,0440 0204.Д
 145,ОСТ,4540 0105.Е
 146,ОСТ,0540 0224.Ф
 147,ОСТ,0640 0203.Г
 150,ОСТ,7040 0130.Х
 151,ОСТ,0740 0210.И
 152,ОСТ,1340 0211.Й
 153,ОСТ,5340 0113.К
 154,ОСТ,1440 0213.Л
 155,ОСТ,5540 0115.М
 156,ОСТ,5040 0110.Н
 157,ОСТ,5740 0117.О
 160,ОСТ,3440 0217.П
 161,ОСТ,1540 0237.Я
 162,ОСТ,6040 0120.Р
 163,ОСТ,4340 0103.С
 164,ОСТ,6440 0124.Т
 165,ОСТ,7140 0131.У
 166,ОСТ,1640 0206.Ж
 167,ОСТ,4240 0102.В
 170,ОСТ,3240 0234.Ь
 171,ОСТ,3740 0233.Ы
 172,ОСТ,4040 0207.З
 173,ОСТ,7340 0230.Ш
 174,ОСТ,7440 0235.Э
 175,ОСТ,7540 0231.Щ
 176,ОСТ,7640 0227.Ч
 177,LOG,0002 0000 0000 0015.DEL
 ,END,

 TBLOCK:1,NАМЕ,***
С*
С*    РАЗБОР АДРЕСНОГО СПИСКА ТИПА BLOCK.
С*
С*
С*      ОБРАЩЕНИЕ:
С*
С*   АСС   СОДЕРЖИТ ЗАГОТОВКУ ДЛЯ TDEF1.
С*
С*      ОТВЕТ:
С*
С*   I14 = 0  - СПИСОК РАЗОБРАН.
С*     АСС СОДЕРЖИТ ДЛИНУ БЛОКА В СЛОВАХ.
С*
С*   I14.NЕ.0 - ОШИБКА, I14 СОДЕРЖИТ ЕЕ
С*     НОМЕР.
С*
С*   ПОРТЯТСЯ РЕГИСТРЫ М2,М4,М8-МС.
С*   ВОЗВРАТ ПО I13.
С*
С*   ПРИ УДАЧНОМ ВОЗВРАТЕ (I14=0) СЛЕДУЮЩАЯ
С*   СТРОКА ТЕКСТА П/П УЖЕ ПРОЧИТАНА.
С*
 ,BITS,
 ,DEFLC,
 WRPC:,SUBP,
 BL:I,BLOCK,ADR.АДРЕС ПО БЛОКУ.
 ,CONT,TYPD
 ,CONT,SI13
 LBL:,EQU,3
 ,АТХ,TYPD
 ,IТА,13
 ,АТХ,SI13
 ,ХТА,
 ,АТХ,ADR
 ,CALL,S/PARAM
 14,UJ,SWPD
 SWPD:,UJ,ERROR.ОШ.ПАРАМ
 :,UJ,WRITDEF.ИДЕНТИФИКАТОР
 :,UJ,ERROR.ЧИСЛО
 :0,UТС,-1.ПУСТО
 GIVL:14,VТМ,1
 М8,UJ,*+1
 :,IТА,14
 ,UJ,ENDS.КОНЕЦ СТРОКИ
 :,IТА,14
 ,UJ,ADDL.,
 :,UJ,ERROR.+
 :,UJ,ERROR.-
 :,UJ,ERROR..
 :,UJ,PUTL.(
 :,UJ,ERROR.)
 :,UJ,ERROR.ПРОЧИЕ
 ENDS:,BSS,
С   КОНЕЦ СТРОКИ:  ЗАПИСЬ ЕЕ В ПРОМЕЖУТ. КОД
С   И ВВОД НОВОЙ.
 М2,VТМ,IFCONT
 ,ARX,ADR
 ,АТХ,ADR
 ,UJ,WRPC.ЗАПИСЬ И ВВОД
 IFCONT:,BSS,.ТОЧКА ВОЗВРАТА.
 ,U1А,RETURN
 ,ХТА,NLI
 ,ARX,В1
 ,АТХ,NLI
 ,АТХ,NLINE
 ,UJ,CONT.=> ПРОДОЛЖЕНИЕ
 RETURN:,BSS,
С     КОНЕЦ BLOCK-АДРЕСА.
С     БУДЕМ ВОЗВРАЩАТЬСЯ.
 М2,VТМ,
 ,ХТА,ADR
 14,VТМ,
 RЕТ:,WТС,SI13
 ,UJ,
 PUTL:,BSS,
С   ПРИЕМ ДЛИНЫ МАССИВА.
 М6,CALL,TRAD*.ПОПРОБУЕМ ВЫЧИСЛИТЬ
 14,UJ,*+1
 :,U1А,ERR2.ERROR
 ,UJ,ERROR
 :,UJ,PUTLCH.УДАЧНО
 :,UJ,ERROR.НЕ УДАЛОСЬ
 PUTLCH:М8,UТМ,-6.)
 М8,VIM,ERROR
 ADDL:,ARX,ADR
 ,UZA,ERROR.GЕ.2**32
 ,АТХ,ADR
 ,UТС,1
 CONT:,CALL,S/PARAM
 14,UJ,SWPD
 WRITDEF:,BSS,
 ,CALL,WRITID
 14,VZM,WDEF
С  ПОВТОРНОЕ ОПРЕДЕЛЕНИЕ ИДЕНТИФИКАТОРА
 М4,ХТА,TDEF1
 ,ААХ,=:006.В42*41
 ,UZA,ERR3
 WDEF:,BSS,
 ,ХТА,TYPD
 ,ARX,ADR
 М4,АТХ,TDEF1
 ,UJ,GIVL
 ERR2:14,VТМ,12
 ,UJ,RЕТ
 ERR3:14,VТМ,5
 М4,ХТА,TDEF
 ,UJ,RЕТ
 ERROR:14,VТМ,8
 ,UJ,RЕТ
 BL:,BSS,LBL
 ,END,TBLOCK

 TEXISO:,NАМЕ,.ИЗ LIBRARY-21
С
С******   ПЕРЕВОД ИДЕНТИФИКАТОРА В ISO
С
 С:,STI,14.КУДА
 ,ATI,12.ОТКУДА
С......   РЕГИСТРЫ В МАГАЗИН
 ,IТА,13.
 ,ITS,11.
 ,ITS,10.
 ,ITS,9.
С......
 13,BASE,С.
 12,XTS,.ТЕХТ-СЛОВО
 12,VТМ,1-12.ЦИКЛ : ТЕХТ/12
 11,VТМ,1-6.ЦИКЛ : ISO/6
 L1:,ASN,64-6.
 ,АТХ,R WOR.ОСТАТОК
 ,УТА,.
 15,АТХ,.ТЕХТ-СИМВОЛ
 ,ASN,64+2.
 ,ATI,10.АДРЕС ПО ТАБЛ.
 ,ASN,64-2.
 15,АЕХ,.
 ,ASN,64-3.
 ,ATI,9. СДВИГ
 10,ХТА,TABLE.
 9,ASN,64+16.
 ,ААХ,С377.ISO-СИМВОЛ
 14,XTS,.
 ,ASN,64-8.
 15,АОХ,.
 14,АТХ,.ЗАП.СИМВОЛА
 11,VLM,L2.
 11,VТМ,1-6.
 14,UТМ,1.
 L2:,ХТА,R WOR.
 12,VLM,L1.
С......   ВОССТАНОВЛЕНИЕ ИР
 ,SТХ,.
 ,STI,9.
 ,STI,10.
 ,STI,11.
 ,STI,13.
 13,UJ,.
С
С
  ISOTEX:,ENTRY,
С       ТЕХ=ISOTEX(ISO)
  TABSYM:,SUBP,
  ,ATI,14
  ,IТА,8
  ,ITS,10
  ,ITS,11
  ,ITS,12
  8,BASE,С
  12,VТМ,-1
  11,VТМ,-5
  L8:14,XTS,
     14,UТМ,1
  L6:,ASN,64-8
  15,АТХ,
  ,УТА,
  ,ATI,10
  ,UТС,TABSYM
  10,ХТА,
  ,ASN,64+42
  ,XTS,RWOR
  ,ASN,64-6
  15,АОХ,
  ,SТХ,RWOR
  11,VLM,L6
  11,VТМ,-1
  15,ХТА,
  12,VLM,L8
  ,STI,12
  ,STI,11
  ,STI,10
  ,ATI,8
  ,UТС,RWOR
  ,ХТА,
  13,UJ,
С......
 С377:,LOG,377.
 R WOR:,BSS,1.
 TABLE:,BSS,.С ПОДЧЕРКОМ!
 00,ISO,6НЦБ'137'
 04,ISO,6НИГФД
 10,ISO,6НЙ* .
 14,ISO,6Н/ЖЯЛ
 20,ISO,6Н3210
 24,ISO,6Н7654
 30,ISO,6Н:Ь98
 34,ISO,6НЫ+-П
 40,ISO,6НСВАЗ
 44,ISO,6HGFED
 50,ISO,6HKJIH
 54,ISO,6HONML
 60,ISO,6HSRQP
 64,ISO,6HWVUT
 70,ISO,6HШZYX
 74,ISO,6НЮЧЩЭ
 ,END,.

 TRAD:1,NАМЕ,***
С*
С*     ВЫЧИСЛЕНИЕ АДРЕСНОГО ВЫРАЖЕНИЯ,
С*  НАХОДЯЩЕГОСЯ В АДРЕСНОЙ ЧАСТИ ОПЕРАТОРА
С*
С*
С*     ОТВЕТ  НА СУММАТОРЕ, В I14 И В  STR:
С*
С*  I14 = ТИП РЕЗУЛЬТАТА:
С*    0 - Е R R О R.  НА СУММАТОРЕ - ТИП
С*        ОШИБКИ :  0 - ОШ. В АДРЕСЕ, ИНА-
С*        ЧЕ   АСС   СОДЕРЖИТ  НЕОПИСАННЫЙ
С*        ИДЕНТИФИКАТОР В КОДЕ  ТЕХТ.
С*
С*    1 - ВЫРАЖЕНИЕ ВЫЧИСЛЕНО И ПОЛУЧЕН АД-
С*        РЕС:  ОТНОСИТЕЛЬНЫЙ ЛИБО АБСОЛЮТ-
С*        НЫЙ ЛИБО ВНЕШНИЙ
С*        АСС  СОДЕРЖИТ ТИП АДРЕСА ДЛЯ TDEF
С*        В  РР. 47:45 И ЗНАЧЕНИЕ АДРЕСА  В
С*        РР. 32:01
С*
С*    2 - ВЫРАЖЕНИЕ НЕВЫЧИСЛИМО ПРИ ТРАНСЛЯ-
С*        ЦИИ.  В  STR  НАХОДИТСЯ ПОСЛЕДОВА-
С*        ТЕЛЬНОСТЬ  КОМАНД  ЗАГРУЗЧИКА ДЛЯ
С*        ВЫЧИСЛЕНИЯ АДРЕСА.  НА  АСС - ДЛИ-
С*        НА ЭТОЙ ПОСЛЕДОВАТЕЛЬНОСТИ КОМАНД.
С*        ПЕРВОЕ СЛОВО  STR  - СПЕЦУКАЗАНИЕ
С*        О ВЫЧИСЛЕНИИ ВЫРАЖЕНИЯ.  ПОЛЯ  FW
С*        И  FС   ( ПРИЗНАК WEQ  И  АДР. ПО
С*        TDEF )  НЕ СФОРМИРОВАНЫ.
С*
С*
С*
С*     ИСПОЛЬЗУЮТСЯ БЕЗ ВОССТАНОВЛЕНИЯ
С*  РЕГИСТРЫ М4,М7-МС
С*
С*     ВОЗВРАТ ПО М6!
С*
 RТ:,EQU,М6.РЕГ.ВОЗВРАТА
 ,BITS,
 ,DEFLC,
 BL:I,BLOCK,
 ,CONT,SСТУР.ШКАЛА ТИПОВ ОПЕРАНДОВ
 ,CONT,W.РАБ.ЯЧ
 ,CONT,SIGN.ЗНАК ОПЕРАНДА
 ,CONT,OPER.ОПЕРАЦИЯ
 ,CONT,SADR.КОНЕЧНЫЙ АДР.
 LBL:,EQU,5
 PARAM:,SUBP,
 ССЫЛКА:,SUBP,
 CHID:,SUBP,
С
С   SСТУР:
С  В46 - БЫЛА МЕТКА ПЕРЕМЕННОЙ
С  В45 - БЫЛА МЕТКА КОМАНДЫ ИЛИ КОНСТАНТЫ
С     ОСТАЛЬНЫЕ РАЗРЯДЫ - ГРЯЗЬ
С
 М9,VТМ,TADS
 МА,VТМ,
 TRAD/:,ENTRY,
 М7,VТМ,-7
 ,ХТА,
 ,АТХ,OPER
 ,АТХ,SIGN
 ,АТХ,SСТУР
 ,CALL,PARAM
 14,UJ,*+1
 :,UJ,BADADR.ОШИБКА
 :,UJ,GIVDEF.ИДЕНТ.
 :,UJ,WRN.ЧИСЛО
 :М8,UJ,*+1.ПУСТО: SWITCH
 :14,VТМ,1.КОНЕЦ СТРОКИ
 ,UJ,Z/RЕТ.АБС. НОЛЬ
 :14,VТМ,.,
 ,UJ,Z/RЕТ.=ERROR
 :М7,UТМ,-1
  ,UJ,IFPLUS.+
 :М7,UТМ,-1
  ,UJ,IFMINUS.-
 :,,..
 BADADR:14,VТМ,.(
 ,UJ,Z/RЕТ.=ERROR
 :,UJ,BADADR.)
 :,UJ,BADADR.ПРОЧИЕ
 MLOOP:14,UJ,*+1
 Z/RЕТ:,ХТА,
 RТ,UJ,.ВОЗВРАТ!!!
 :,UJ,GIVDEF.ИДЕНТ
 :,UJ,WRN.ЧИСЛО
 :14,VТМ,.ПУСТО
 ,UJ,Z/RЕТ.ERROR
 WRN:,ARX,
 ,UZA,BADADR.GЕ.2**32
 ,UJ,WRADR
 IFPLUS:,WТС,СТАВ
 14,VТМ,-53В
 14,VZM,PLUS.+
 ,UZA,IF*.* В НАЧАЛЕ
 ,ХТА,В41.УМНОЖЕНИЕ
 ,UJ,NEXTFR
 IFMINUS:,WТС,СТАВ
 14,VТМ,-55В
 14,VZM,MINUS.-
 ,UZA,BADADR./ В НАЧАЛЕ
 ,ХТА,В42.ДЕЛЕНИЕ
 ,UJ,NEXTFR
 OTHER:,BSS,
 ,WТС,СТАВ
 14,VТМ,-46В.&
 14,VZM,/AND
 14,UТМ,46В-74В.<
 14,VZM,IFLSH
 14,UТМ,74В-76В.>
 14,VIM,BADADR.ПЛОХ. РАЗДЕЛИТ.
С     ПРИШЕЛ  ">".  ЗАГЛЯНЕМ ДАЛЬШЕ
 ,CALL,SYMBOL
 14,UТМ,40В-76В.>>
 14,VIM,BADADR
 RSHIFT:,BSS,
 ,ХТА,В43.СДВИГ
 ,UJ,NEXTFR
 IFLSH:,BSS,
С   ПРИШЕЛ  "<"
 ,CALL,SYMBOL
 14,UТМ,40В-74В.<<
 14,V1М,BADADR
 ,ХТА,В48
 ,АТХ,SIGN
 ,UJ,RSHIFT
 /AND:,ХТА,=:012.5 = "AND"
 NEXTFR:,АТХ,OPER
 ,UJ,EMLOOP
 MINUS:,UТС,В48
 PLUS:,ХТА,
 ,АТХ,SIGN
 EMLOOP:13,VТМ,MLOOP
 М7,VLM,PARAM
 14,VТМ,
 ,UJ,Z/RЕТ.МНОГО ОПЕРАНДОВ
 ISMT:М4,ХТА,TDEF1
 ,ААХ,=:6
 ,U1А,DEFMT.НЕДООПРЕДЕЛЕН
 WRMT:М4,ХТА,TDEF1
 ,ААХ,=1400 0377 7777 7777
С      ЗАПИСЬ ОПЕРАНДА
 WRADR:,АОХ,SIGN
 ,АОХ,OPER
 М7,АТХ,STR+8
 ,АОХ,SСТУР
 ,АТХ,SСТУР
 ,ХТА,
 ,АТХ,SIGN
 ,АТХ,OPER
 М8,UJ,*+1.ПО РАЗДЕЛИТЕЛЮ
 :,UJ,ENDSTR.КОНЕЦ СТРОКИ
 :,UJ,ENDSTR.,
 :,ХТА,В1.+ ИЛИ *
 ,UJ,IFPLUS
 :,ХТА,В1
 ,UJ,IFMINUS.- ИЛИ /
 :,UJ,BADADR.ТОЧКА
 :,UJ,BADADR.(
 :,UJ,BADADR.)
 :,UJ,OTHER.ПРОЧИЕ
 GIVDEF:,BSS,
 13,VJM,CHID
 М4,VIM,ISMT
 14,VТМ,
 RТ,UJ,
 IF*:,CALL,PARAM
 14,UТМ,-3
 14,VIM,BADADR.НЕ ПУСТО
С    ЗВЕЗДОЧКА КАК УКАЗАТЕЛЬ СОБСТВЕННОГО
С    АДРЕСА.
 М7,UТМ,1
 ,ХТА,NLINE
 ,ААХ,С77777
 ,ASN,64+13
 ,U1А,BADADR
 ,ХТА,NLINE
 ,ААХ,=1 7777
 ,АОХ,В45.КОМАНДЫ
 ,UJ,WRADR
 DEFMT:М4,ХТА,TDEF1
 ,CALL,DEFINER
 14,VIM,ERRDEF
 М4,XTS,TDEF1
 ,ААХ,MTDEF1
 15,АОХ,
 М4,АТХ,TDEF1
 ,UJ,WRMT
 ERRDEF:14,UТМ,-12
 14,VIM,BADADR
 RТ,UJ,.ВОЗВРАТ !!!
 ENDSTR:,BSS,
 М7,UТМ,8
 М7,MTJ,МВ.N
 ,IТА,М7
 ,АЕХ,С77777
 ,ARX,В2
 ,ATI,М7.1-N
 ,ХТА,SСТУР
 ,ААХ,=:14
 ,АТХ,SСТУР
С
С        ПОПЫТКА ВЫЧИСЛЕНИЯ АДРЕСНОГО
С                 ВЫРАЖЕНИЯ
С
 М7,VIM,TRD
С   В ВЫРАЖЕНИИ ОДИН ОПЕРАНД
 ,ХТА,STR+1
 ,ARX,
 ,UZA,MIN/1.ОТРИЦАТ.
 ,ХТА,STR+1
 RТ1:14,VТМ,1
 RТ,UJ,
 MIN/1:,BSS,
 ,ХТА,STR+1
 ,ААХ,=:14
 ,U1А,ТО LOADER
С    АБСОЛЮТНЫЙ ОТРИЦАТЕЛЬНЫЙ АДРЕС
 ,ХТА,STR+1
 ,АЕХ,В32/01
 ,ARX,В1
 ,ААХ,В32/01
 ,UJ,RТ1
 TRD:,АЕХ,=:14
 ,UZA,TOLOADER
 ,ХТА,
 ,АТХ,SADR
 LD:,BSS,.ЦИКЛ
 МВ,UТС,
 М7,ХТА,STR.НАЧНЕМ С STR+1
 ,АТХ,W
 ,ААХ,=:036.КОП
 ,U1А,LOGIC.НЕ СЛОЖЕНИЕ
С     ОПЕРАЦИЯ  СЛОЖЕНИЯ
 ,ХТА,W
 ,ARX,=:14
 ,АРХ,=:6000 0377 7777 7777
 ,AUX,=:4001 0377 7777 7777
 ,ARX,
 ,UZA,MIN.ВЫЧИТАНИЕ
 ADD:,ARX,SADR
 ,АТХ,SADR
 RLD:М7,VLM,LD
 ,ААХ,=:0077
 ,U1А,IFIS
С     АБСОЛЮТНЫЙ  АДРЕС
 ,ХТА,SADR
 ,ААХ,В32/01
 ,UJ,RТ1
 IFIS:,АЕХ,В37
 ,U1А,TOLOADER
С     ОТНОСИТЕЛЬНЫЙ  АДРЕС
 ,ХТА,SADR
 ,ААХ,В32/01
 ,АОХ,SСТУР
 ,UJ,RТ1
 MIN:,BSS,
 ,АЕХ,=:4077 0377 7777 7777
 ,ARX,=:0001 0000 0000 0001
 ,UJ,ADD
 LOGIC:,BSS,
С
С     ОПЕРАЦИИ    *    /    <<    >>    &
С
 ,ASN,64-8
 ,УТА,
 ,ATI,14
 ,ХТА,SADR
 ,ААХ,=:0077
 ,U1А,TOLOADER.НЕ АБСОЛЮТ
 ,ХТА,W
 ,ААХ,=:14
 ,U1А,TOLOADER.ОПЕРАНД-НЕ АБС.
 ,ХТА,W
 ,ААХ,В32/01
 14,UJ,*
 :,UJ,MULT
 :,UJ,DEV
 :,UJ,BADADR.РЕЗЕРВ
 :,UJ,SHIFT
 :,UJ,AND
 MULT:,NTR,6
 ,АОХ,=I0
 ,А+Х,
 ,XTS,SADR
 ,АОХ,=I0
 ,А+Х,
 15,А*Х,
 MDEV:,NTR,7
 ,А+Х,=I0
 ,ААХ,В32/01
 ,АТХ,SADR
 ,UJ,RLD
 DEV:,NTR,6
 ,АОХ,=I0
 ,А+Х,
 ,XTS,SADR
 ,АОХ,=I0
 ,А+Х,
 15,А/Х,
 ,UJ,MDEV
  SHIFT:,ХТА,W
 ,ARX,
 ,UZA,LSHIFT/
 RSHIFT/:,XTS,SADR
 15,WТС,
 ,ASN,64
 ,ААХ,В32/01
 ,АТХ,SADR
 ,UJ,RLD
 LSHIFT/:,АЕХ,В32/01
 ,ARX,В1
 ,UJ,RSHIFT/
 AND:,ААХ,SADR
 ,АТХ,SADR
 ,UJ,RLD
С   . . . . . . . . . . . . . . . . . . .
 TOLOADER:,BSS,
С    ВЫРАЖЕНИЕ НЕВЫЧИСЛИМО ПРИ ТРАНСЛЯЦИИ
 ,IТА,МВ.N
 ,ААХ,С7
 ,ASN,64-3
 ,АОХ,=1400 0000 0000 0002
 ,АТХ,STR
 ,IТА,МВ.N
 ,ARX,В1
 14,VТМ,2
 RТ,UJ,.ВОЗВРАТ
С------------------------------------------
 BL:,BSS,LBL
 ,END,

 TRAD*:1,NАМЕ,***
С*
С*     ВЫЧИСЛЕНИЕ АДРЕСНОГО ВЫРАЖЕНИЯ
С*     НА ПЕРВОМ ПРОХОДЕ ТРАНСЛЯЦИИ
С*
С*      ДОПУСТИМЫЕ ОПЕРАЦИИ:
С*   +  СЛОЖЕНИЕ
С*   -  ВЫЧИТАНИЕ
С*   *  УМНОЖЕНИЕ
С*   /  ДЕЛЕНИЕ
С*   >> ЛОГ. СДВИГ ВПРАВО
С*   << ЛОГ. СДВИГ ВЛЕВО
С*   &  ЛОГИЧЕСКОЕ "И"
С*
С*
С*     ОТВЕТ  НА СУММАТОРЕ, В I14 И В  М8:
С*
С*  I14 = ТИП РЕЗУЛЬТАТА:
С*    0 - Е R R О R.   НА  СУММАТОРЕ  -  ТИП
С*        ОШИБКИ :  0 - ОШ. В АДРЕСЕ,  ИНАЧЕ
С*        АСС   СОДЕРЖИТ НЕОПИСАННЫЙ ИДЕНТИ-
С*        ФИКАТОР В КОДЕ  ТЕХТ.
С*
С*    1 - ВЫРАЖЕНИЕ ВЫЧИСЛЕНО И ПОЛУЧЕН  АБ-
С*        СОЛЮТНЫЙ АДРЕС.
С*        АСС  СОДЕРЖИТ  ЗНАЧЕНИЕ  АДРЕСА  В
С*        В  РР. 32:01.
С*
С*    2 - ВЫРАЖЕНИЕ  НЕВЫЧИСЛИМО  НА  ПЕРВОМ
С*        ПРОХОДЕ.
С*
С*
С*  М8  = ТИП РАЗДЕЛИТЕЛЯ В КОНЦЕ ВЫРАЖЕНИЯ
С*        (ПОСЛЕ  PARAM).
С*
С*
С*     ИСПОЛЬЗУЮТСЯ БЕЗ ВОССТАНОВЛЕНИЯ
С*     РЕГИСТРЫ М4,М7-МС
С*
С*     ВОЗВРАТ ПО М6 (!)
С*
 RТ:,EQU,М6.РЕГ.ВОЗВРАТА
 ,BITS,
 ,DEFLC,
 CHID:,SUBP,
 TABSYM:,SUBP,
 BL:I,BLOCK,
 ,CONT,SСТУР.ТИПЫ ОПРАНДОВ
 ,CONT,W    .РАБ.ЯЧ
 ,CONT,SIGN .ЗНАК ОПЕРАНДА
 ,CONT,OPER .ОПЕРАЦИЯ
 ,CONT,SADR .КОНЕЧНЫЙ АДР.
 ,CONT,WТУР .СУММАТОР ТИПОВ
 LBL:,EQU,6
 PARAM:,SUBP,
 ССЫЛКА:,SUBP,
 ,ХТА,
 ,АТХ,SСТУР
 ,АТХ,SIGN
 ,АТХ,SADR
  ,АТХ,WТУР
 ,CALL,PARAM
 14,UJ,*+1
 :,UJ,BADADR.ОШИБКА
 :,UJ,GIVDEF.ИДЕНТ.
 :,UJ,WRN.ЧИСЛО
 :М8,UJ,*+1.ПУСТО: SWITCH
 :14,VТМ,1.КОНЕЦ СТРОКИ
 ,UJ,Z/RЕТ.АБС. НОЛЬ
 :14,VТМ,1.,
 ,UJ,Z/RЕТ
 :М7,UТМ,-1
  ,UJ,IFPLUS.+
 :М7,UТМ,-1
  ,UJ,IFMINUS.-
 :,,..
 BADADR:14,VТМ,.(
 ,UJ,Z/RЕТ.=ERROR
 :14,VТМ,1.)
 ,UJ,Z/RЕТ
 :,UJ,BADADR.ПРОЧИЕ
 MLOOP:14,UJ,*+1
 Z/RЕТ:,ХТА,
 RТ,UJ,.ВОЗВРАТ!!!
 :,UJ,GIVDEF.ИДЕНТ
 :,UJ,WRN.ЧИСЛО
 :14,VТМ,.ПУСТО
 ,UJ,Z/RЕТ.ERROR
 WRN:,ARX,
 ,UZA,BADADR
 ,UJ,WRADR
 MINUS:,UТС,В32
 EMLOOP:,ХТА,
 ,АТХ,SIGN
 13,VТМ,MLOOP
 ,UJ,PARAM
 ISMT:М4,ХТА,TDEF1
 ,ААХ,В40
 ,U1А,UNDEFL
 М4,ХТА,TDEF1
 15,АТХ,
 ,ААХ,В32/01
 ,SТХ,W
 ,ARX,
 ,UZA,NОС
 ,ASN,64-4
 ,УТА,
 ,ASN,64-2
 ,ATI,14
 :,UZA,*+1
 14,ХТА,В29
 :,АТХ,WТУР
 ,ААХ,В29+12.БИТ Т=3
 ,U1А,NОС
 ,ХТА,WТУР
 ,ААХ,=420.БИТЫ Т=5,6
 ,UZA,TR/AD
С    ПРОСТАЯ  ЭКВИВАЛЕНТНОСТЬ.
С    ПОМЕЩАЕМ НОМЕР БЛОКА В 29:37 Р. WТУР
 М4,ХТА,TDEF1
 ,ААХ,В32/21
 ,ASN,64-8
 ,АТХ,WТУР
 ,ХТА,W
 ,ААХ,В20/01
 WRADR:,АТХ,W
 TR/AD:,ХТА,SIGN
 ,UIA,MIN
 ,ХТА,SADR
 ,ARX,W
 ,АТХ,SADR
 ,ХТА,SСТУР
 ,ARX,WТУР
 ,АТХ,SСТУР
 SW:,BSS,
 М8,UJ,*+1.ПО РАЗДЕЛИТЕЛЮ
 :,UJ,ENDSTR.КОНЕЦ СТРОКИ
 :,UJ,ENDSTR.,
 :,ХТА,В1.+  *
 ,UJ,IFPLUS
 :,ХТА,В1.-  /
 ,UJ,IFMINUS
 :14,VТМ,.ТОЧКА
 ,UJ,Z/RЕТ.=ERROR
 :14,VТМ,.(
 ,UJ,Z/RЕТ.=ERROR
 :,UJ,ENDSTR.)
 :,UJ,OTHER.ПРОЧИЕ
 GIVDEF:13,VJM,CHID
 М4,VIM,ISMT
 RТ0:,BSS,
 14,VТМ,
 RТ,UJ,
 UNDEFL:М4,ХТА,TDEF
 ,UJ,RТ0
 ENDSTR:,BSS,
 ,ХТА,SСТУР.ТИПЫ ОПЕРАНДОВ
 ,U1А,NОС.НЕСБАЛАНСИРОВАНЫ
С    ВЫРАЖЕНИЕ УДАЛОСЬ ВЫЧИСЛИТЬ
 ,ХТА,SADR
 ,ААХ,В32/01
 14,VТМ,1
 RТ,UJ,
 NОС:14,VТМ,2
 RТ,UJ,
 MIN:,BSS,
С    ВЫЧИТАНИЕ ОПЕРАНДА
 ,ХТА,SADR
 ,А-Х,W
 ,АТХ,SADR
 ,ХТА,WТУР
 ,АЕХ,=0777 7777 7777 7777
 ,ARX,В1
 ,ARX,SСТУР
 ,ААХ,=0777 7777 7777 7777
 ,АТХ,SСТУР
 ,UJ,SW
 IFMINUS:,BSS,
 ,WТС,СТАВ
 14,VТМ,-55В
 14,VZM,MINUS.-
 ,UZA,BADADR./ В НАЧАЛЕ
 14,VJM,IFABS
 ,ХТА,В2.ДЕЛЕНИЕ
 ,UJ,NEXTFR
 IFPLUS:,WТС,СТАВ
 14,VТМ,-53В
 14,VZM,EMLOOP.+
 ,UZA,BADADR.* В НАЧАЛЕ
 14,VJM,IFABS
 ,ХТА,В1.УМНОЖЕНИЕ
 ,UJ,NEXTFR
 OTHER:,BSS,
 ,ХТА,
 ,АТХ,SIGN
 14,VJM,IFABS
 ,WТС,СТАВ
 14,VТМ,-46В.&
 14,VZM,/AND
 14,UТМ,46В-74В.<
 14,VZM,IFLSH
 14,UТМ,74В-76В.>
 14,VIM,BADADR.ПЛОХ.РАЗДЕЛИТ.
С     ПРИШЕЛ СИМВОЛ  ">".
 ,CALL,SYMBOL
 14,UТМ,40В-76В.>>
 14,VIM,BADADR
 RSHIFT:,BSS,
 ,ХТА,В3
 ,UJ,NEXTFR
 IFLSH:,BSS,
С     ПРИШЕЛ СИМВОЛ "<"
 ,CALL,SYMBOL
 14,UТМ,40В-74В.<<
 14,VIM,BADADR
 ,ХТА,В32
 ,АТХ,SIGN
 ,UJ,RSHIFT
 /AND:,ХТА,С5
 NEXTFR:,АТХ,OPER
 ,CALL,PARAM
 14,UJ,*+1
 :,ХТА,.ОШИБКА
 RТ,UJ,.=> ВОЗВРАТ
 :,UJ,GDF.ИДЕНТ
 :,UJ,ABSOP.ЧИСЛО
 :,UJ,BADADR.ПУСТО
 GDF:,BSS,
 13,VJM,CHID
 М4,VZM,BADADR
 М4,ХТА,TDEF1
 ,ААХ,=:74
 ,U1А,BADADR.НЕ АБСОЛЮТ.
 М4,ХТА,TDEF1
 ,ААХ,В32/01
 ABSOP:,WТС,OPER
 ,UJ,*
 :,UJ,MULT.1
 :,UJ,DEV.2
 :,UJ,BADADR.РЕЗЕРВ
 :,UJ,SHIFT.4
 :,UJ,AND.5
 MULT:,NTR,6
 ,АОХ,=I0
 ,А+Х,
 ,XTS,SADR
 ,АОХ,=I0
 ,А+Х,
 15,А*Х,
 MDEV:,NTR,7
 ,А+Х,=I0
 ,ААХ,В32/01
 ,АТХ,SADR
 ,UJ,SW
 DEV:,NTR,6
 ,АОХ,=I0
 ,А+Х,
 ,XTS,SADR
 ,АОХ,=I0
 ,А+Х,
 15,А/Х,
 ,UJ,MDEV
  SHIFT:,XTS,SIGN
  ,UZA,RSHIFT/
  15,ХТА,-1
  ,АЕХ,В32/01
  ,ARX,В1
  15,АТХ,-1
  RSHIFT/:,ХТА,SADR
 15,WТС,
 ,ASN,64
 ,ААХ,В32/01
 ,АТХ,SADR
 ,UJ,SW
 AND:,ААХ,SADR
 ,АТХ,SADR
 ,UJ,SW
 IFABS:,ХТА,SСТУР
 ,U1А,NОС.=> НЕ АБС.
 14,UJ,.=> НОРМА
 BL:,BSS,LBL
 ,END,TRAD*

 TRIDENT:1,NАМЕ,***
С*
С*   ПЕРЕВОД ISO-ТЕХТ С КОНТРОЛЕМ
С*   НА ИДЕНТИФИКАТОР
С*
С*  ОБРАЩЕНИЕ: I14 = АДР. ТЕКСТА (2 СЛОВА)
С*
С*  ОТВЕТ:  АСС = 0 -  ОШИБКА, ИНАЧЕ
С*          АСС = ИДЕНТИФИКАТОР (ТЕХТ)
С*
 TABSYM:,SUBP,
 14,ХТА,
 ,ASN,64-8
 ,АТХ,W
 ,УТА,
 ,ATI,МС
 МС,UТС,TABSYM
 ,ХТА,
 ,ААХ,=:77
 МС,UТС,TABSYM
 ,XTS,
 ,ААХ,=:0040
 ,UZA,ERROR
 ,ХТА,=:0077
 ,АТХ,М
 МВ,VТМ,-4
 L:,ХТА,W
 ,ASN,64-8
 ,АТХ,W
 ,УТА,
 ,ATI,МС
 МС,UТС,TABSYM
 ,ХТА,
 ,AUX,М
 15,АОХ,
 МС,UТС,TABSYM
 ,XTS,
 ,ААХ,=:0050.БУКВА,ЦИФРА
 ,UZA,IFBLANC
 ,ХТА,М
 ,ASN,64+6
 ,АТХ,М
 ,UZA,END
 МВ,VLM,L
 14,ХТА,1
 ,АТХ,W
 МВ,VТМ,-1
 ,UJ,L
 IFBLANC:МС,UТМ,-40В
 МС,VIM,ERROR
 END:15,ХТА,
 13,UJ,
 ERROR:15,UТМ,-1
 13,UJ,
 W:,BSS,1
 М:,BSS,1
 ,END,TRIDENT

 WRITID:1,NАМЕ,***
С*
С*   ЗАПИСЬ НОВОГО ИДЕНТИФИКАТОРА
С*   В ТАБЛИЦУ ОПИСАНИЙ.
С*
С*  ОБРАЩЕНИЕ:  АСС = ID (КОД ТЕХТ)
С*
С*  ОТВЕТ: М4 = НОМЕР ПО TDEF
С*         I14.EQ.0 - НОРМА
С*         I14.NЕ.0 - ПОВТОРНОЕ ОПР.
С*
 ,BITS,
 ,DEFLC,
 *TSS*:LС,BLOCK,TSS
 TDEF3:,LС,
 RETURN:,SUBP,
 J:,EQU,JTD3
 ,АТХ,W
 М4,VТМ,-1022
С...   0-Е СЛОВО TDEF ОСТАВИМ ПУСТЫМ,
С      Т.К. ИНДЕКС ПО TDEF Д.Б.>0 !!!
 L1:М4,ХТА,TDEF+1023
 ,UZA,PL0
 ,АЕХ,W
 ,UZA,PL1
 М4,VLM,L1
 14,VТМ,Т
 ,ITS,14
 14,VТМ,Т+4
 ,ITS,14
 14,VТМ,1
 ,ITS,14
 ,CALL,PRINT8
 ,UJ,RETURN
 PL0:,BSS,
 М4,UТМ,1023
 М4,АТХ,TDEF1
 ,ХТА,W
 М4,АТХ,TDEF.ИМЯ
 FST:,BSS,
 ,ХТА,ATSS
 ,ATI,14
 ,ASN,64+12
 ,УТА,
 15,АТХ,
 ,ASN,64+12
 15,АОХ,
 М4,АТХ,TDEF2
 ,ХТА,NLINE
 ,ASN,64-30
 14,UТС,TSS
 ,АТХ,
 ,ХТА,ATSS
 ,ARX,В1
 ,АТХ,ATSS
 14,VТМ,
 13,UJ,
 PL1:,BSS,.!
 М4,UТМ,1023
 М4,ХТА,TDEF
 ,ААХ,=:77
 ,АЕХ,USPACE
 ,U1А,ССЫЛКА.НЕ ЛОКАЛЬНЫЙ
С     ЛОКАЛЬНЫЙ ИДЕНТИФИКАТОР - ПОВТОРНО
 ,UТС,1
 WRSS:МС,VТМ,
 М4,ХТА,TDEF2
 ,ААХ,С7777
 ,ATI,14.КОНЕЦ СПИСКА
 14,VZM,FIRSTL
 М4,ХТА,TDEF2
 ,ASN,64+12
 ,ASN,64-12
 CNL:,АОХ,J.НОВЫЙ КОНЕЦ
 М4,АТХ,TDEF2
 ,ХТА,J
 ,ASN,64-20
 14,UТС,TDEF3
 ,АОХ,
 14,UТС,TDEF3
 ,АТХ,
 М4,ХТА,TDEF1
 ,ААХ,=:7400 0000 0377 7777
 ,WТС,J
 ,UТС,TDEF3
 ,АТХ,
 ,ХТА,J
 ,ARX,В1
 ,АТХ,J
 МС,VZM,SAVEW
 ,IТА,13
 ,XTS,
 М4,АТХ,TDEF1
 13,VJM,ССЫЛКА
 14,VТМ,
 15,WТС,
 ,UJ,
 FIRSTL:,BSS,
 М4,ХТА,TDEF1
 ,ААХ,В44.ENTRY?
 ,U1А,ССЫЛКА.=> НЕЛЬЗЯ
 ,ХТА,J
 ,ASN,64-12
 М4,АОХ,TDEF2
 ,UJ,CNL
 CHID:,ENTRY,
С*
С*     ПОИСК В ТАБЛИЦЕ ОПИСАНИЙ
С*
 М4,VТМ,-1022
 ,АТХ,W
 L2:М4,ХТА,TDEF+1023
 ,UZA,NOID
 ,АЕХ,W
 ,UZA,ISID
 М4,VLM,L2
 NOID:,ХТА,W
 М4,VТМ,
 13,UJ,
 ISID:М4,UТМ,1023
 ,ХТА,W
 ,ААХ,=:77
 ,АЕХ,USPACE
 ,U1А,ССЫЛКА
С     ИСПОЛЬЗУЕТСЯ ЛОКАЛЬНЫЙ ИДЕНТ.
 ,ХТА,F/TWO.НА ПЕРВОМ ПРОХ.
 ,UZA,NOID.=> НЕЛЬЗЯ
 М4,ХТА,TDEF2
 ,ААХ,В24/01
 ,UZA,ССЫЛКА.ОДНОКРАТНО
 ,ASN,64+12
 ,U1А,WRSS.УПРЯТАТЬ!
 CONTF:М4,WТС,TDEF2
 14,VТМ,
 14,UТС,TDEF3
 ,ХТА,
 15,АТХ,
 ,ААХ,=:7400 0000 0377 7777
 М4,SТХ,TDEF1
 ,ASN,64+20
 ,ААХ,С7777
 М4,XTS,TDEF2
 ,ААХ,В48/25
 15,АОХ,
 М4,АТХ,TDEF2
 ,UJ,ССЫЛКА
 SAVEW:,BSS,
 М4,ХТА,TDEF2.НАЧ. ССЫЛКУ
 ,AUX,=:7777 7777 0000 7777
 М4,АТХ,TDEF2.В В12:01
 ,UJ,CONTF
 ССЫЛКА:,ENTRY,
 ССЫЛКА:,BSS,
 М4,ХТА,TDEF2
 ,ААХ,В48/25
 ,UZA,FST
 ,ASN,64-12
 ,УТА,
 ,ATI,14
 14,UТМ,TSS
 14,ХТА,
 ,ARX,
 ,UZA,NEWW
 ,ХТА,NLINE
 ,ASN,64-15
 ,АОХ,В48
 14,АОХ,
 14,АТХ,
 13,UJ,
 NEWW:,ХТА,ATSS
 14,АОХ,
 14,АТХ,
 ,ATI,14
 ,ASN,64+12
 ,УТА,
 М4,АЕХ,TDEF2
 ,ААХ,В48/37
 М4,АЕХ,TDEF2
 М4,АТХ,TDEF2
 ,ХТА,ATSS
 ,ARX,В1
 ,АТХ,ATSS
 ,ХТА,NLINE
 ,ASN,64-30
 ,UТС,TSS
 14,АТХ,
 13,UJ,
 W:,BSS,1
 Т:,ISO,30НПЕРЕПОЛНЕНА ТАБЛИЦА ОПИСАНИЙ!
 ,END,WRITID

  ZMACRO:,NАМЕ,
С*
С***   17/09/87   А.П.САПОЖНИКОВ
С*
С*  МАКРО-ДОВЕСОК К АССЕМБЛЕРУ А.Л.ДАВЫДОВА
С*  ЗАПОМИНАНИЕ МАКРООПРЕДЕЛЕНИЙ. РАСШИРЕНИЕ
С*  И ЗАПИСЬ ПОЛУЧЕННОГО ТЕКСТА НА 310000.
С*  ВЫЗОВ АССЕМБЛЕРА.   В-ОБЩЕМ, ТАК :
С*      *LIBRA:20,23
С*      *FILE: ... ,30,W  - ДЛЯ АРХИВА
С*      *FILE: ... ,31,W  - РАБОЧИЙ ФАЙЛ
С*      *CALL FICMEMOR
С*      *CALL ZMACRO
С*        < ТЕКСТЫ МАКРООПРЕДЕЛЕНИЙ >
С*        < ТЕКСТЫ ТРАНСЛИРУЕМЫХ ПРОГРАММ >
С*      *[ ПРОДОЛЖЕНИЕ ПАКЕТА УПР.КАРТ ]
С*      *END F
С*  ЕСЛИ ФАЙЛЫ 30 И 31 НЕ ЗАКАЗАНЫ В ПАКЕТЕ,
С*  ТО БУДУТ ЗАКАЗАНЫ SCRATCH-ФАЙЛЫ.
С*  МАКРООПРЕДЕЛЕНИЯ ПЕЧАТАЮТСЯ В ЛИСТИНГЕ,
С*  ПРИ ВЫЗОВЕ:   *CALL ZMACRO:PRINT
С*
С*  ПРИМЕР МАКРООПРЕДЕЛЕНИЯ :
С*   PLUS1:MACRO
С*   / ХТА /     (ВМЕСТО "/" ПОДСТАВЛЯЕТСЯ
С*     А+Х =1     СОДЕРЖИМОЕ СООТВЕТСТВ.
С*   / АТХ /      ПОЛЯ ИЗ МАКРОВЫЗОВА)
С*     ENDM
С*  ВНУТРЕННИЕ ИМЕНА, ЕСЛИ ОНИ ПОЯВЛЯЮТСЯ В
С*  МАКРО, ДОЛЖНЫ ВЫГЛЯДЕТЬ ТАК :  Ь0000ХХХ
С*  ЗДЕСЬ  Ь - МЯГКИЙ ЗНАК, ХХХ - СИМВОЛЫ,
С*  ОТЛИЧНЫЕ ОТ ПРОБЕЛА, ВМЕСТО "0000"
С*  МАКРОГЕНЕРАТОР ДОБАВИТ НОМЕР ВЫЗОВА.
С*
С*  В МАКРООПРЕДЕЛЕНИИ МОЖЕТ БЫТЬ ВНУТРЕННИЙ
С*  КОММЕНТАРИЙ, КОТОРЫЙ НЕ ПЕРЕНОСИТСЯ В
С*  МАКРОРАСШИРЕНИЕ.  ТАКОЙ КОММЕНТАРИЙ
С*  НАЧИНАЕТСЯ ПАРОЙ СИМВОЛОВ   ;/
С*
С*  ИЗ ВХОДНОГО ФАЙЛА АССЕМБЛЕРА ИЗЫМАЕТСЯ
С*  ВСЕ, ЧТО БЫЛО ОБРАМЛЕНО КАРТАМИ "*SKIP".
С*
  *NUMKOP*:,LС,1
  А/I0:,LС,1
  ****:,SUBP,
  *МСН:,SUBP,
  MONCARD*:,LС,
  *STRING*:,LС,70
  MIR:,EQU,*STRING*+23
  CHAR:,EQU,*STRING*+20
  ТСОР:,EQU,*STRING*+30
  *SUBCH*:,LС,26
  SYNTAX:,SUBP,
  TABSYM:,SUBP,
  MACGEN:,SUBP,
  DATAPR*:,SUBP,
  PRINT8//:,SUBP,
С-------------------
  :,ATI,7.<MONCARD>
  7,UТМ,2
 :1,VТМ,-1
  ,UJ,LООР
  ATDS:,,
  ,Z00,TDS
  FDB:,BSS,10
  ТОР:,ISO,12HS,W30(140)
  TDS:,ISO,18НМАЛО РАБ.ДИСКОВ''''''
  RЕР:,ХТА,ATDS
  ,FUN,76В
  ,ХТА,
  ,FUN,211В.ЖДЕМ МД
  LООР:,ХТА,
  14,VТМ,-9
  :14,АТХ,FDB+9
  14,VLM,*
  ,ХТА,=:2
  1,ASN,64
  15,АТХ,
  ,*57,4040В. ЗАКАЗАН
  15,ААХ,
  ,U1А,ELOOP. - ДА
  1,MTJ,14
  14,UТМ,1
  ,IТА,14
  ,ASN,64-8
  ,АОХ,=6HS,W30(
  ,АТХ,ТОР
  14,VТМ,ТОР
  ,IТА,14
  14,VТМ,FDB
  ,ITS,14
  ,CALL,JFOCC
  ,ATI,14
  14,VIM,RЕР
  ELOOP:1,VLM,LООР
  ,ХТА,=:174
  ,UТС,TABSYM+57В
  ,АТХ,. "/" - БУКВА !
  ,ITS,7
  ,UJ,MACGEN
С----
  SYN:,ENTRY,
  ,ITS,13
  ,ITS,1
  ,ITS,2
  ,ITS,3
  15,АТХ,
  1,VТМ,
  2,VТМ,20000В
  3,VТМ,40000В
  ,CALL,SYNTAX
  ,ХТА,ТСОР
  ,АТХ,FDB
  12,VТМ,FDB
  ,IТА,12
  ,CALL,KHGET
  ,АТХ,*NUMKOP*
  ,ХТА,FDB+1
  ,АТХ,CHAR
  ROUND:,ХТА,MIR.ИМЕННО ТАК
  ,ASN,64+8 .ТРЕБУЕТ
  ,АОХ,=6Н ''''''''''
  ,АТХ,MIR.ТОВ.ДАВЫДОВ !
  ,АЕХ,=6Н
  ,UZA,RSYN
  ,ААХ,=6Н'''''''''377'''
  ,UZA,ROUND
  RSYN:15,ХТА,
  ,STI,3
  ,STI,2
  ,STI,1
  ,STI,13
  13,UJ,
С
С
  DATAPR:,ENTRY,
  ,ХТА,
  ,UJ,DATAPR*
С
  PRNT81:,ENTRY,
  ,UJ,PRINT8// + 1
С
  МС:,ENTRY, .К=MONCARD*(I) I=1,2,...
  ,ATI,14
  14,WТС,
  ,ХТА,MONCARD* -1
  13,UJ,
  Ь:,ENTRY,. SUBR(NАМЕ,NМС)
  ,STI,14
  ,STI,12
  12,ХТА,
  ,ASN,64+40
  ,АЕХ,=170
  13,U1А,
  14,ХТА,
  ,ASN,64-36
  ,AUX,=6Н'''7''7''7''7'''
  12,АОХ,
  12,АТХ,
  13,UJ,
  IPAR:,BSS,1
  ,END,

 EXLIB:,NAME,.23-24.07.85
C***********     23/07/85         A.Л.ДABЫДOB
C*
C*                ПAKET  ПPOГPAMM
C*        ДЛЯ PAБOTЫ C БИБЛИOTEKOЙ ДAHHЫX
C*
C*        SUBROUTINE  EXLIB  (LUN)
C*        SUBROUTINE  ELREAD (MAC,LEN)
C*        SUBROUTINE  ELWRIT (MAC,LEN)
C*        SUBROUTINE  ELNAME (NAME)
C*        SUBROUTINE  ELEQU  (NAME)
C*        SUBROUTINE  ELMOVE (NAME)
C*        SUBROUTINE  ELMOVE (MADR)
C*        SUBROUTINE  ELDEL  (NAME)
C*        FUNCTION    IELADR (LIM)
C*        SUBROUTINE  ELCL
C*        SUBROUTINE  ELCLOS
C*        SUBROUTINE  ELPRES
C*        SUBROUTINE  ELPRIN (LPR)
C*
C*    *CALL LIBINIT:<YK>,<ДЛИHA>,<KOMMEHTAPИЙ>
C*    *CALL LCORRECT:<YK>,<ДЛИHA>,<KOMMEHTAPИЙ>
C*    *CALL ELCATALOG:<YK>
C*    *CALL ELPRESS:<YK>
C*
C*
C*     БИБЛИOTEKA ДAHHЫX  ПPEДCTABЛЯET COБOЙ  MHO-
C*  ЖECTBO ИMEHOBAHHЫX PAЗДEЛOB, B KOTOPЫX XPAHЯT-
C*  CЯ ДBOИЧHЫE ДAHHЫE.  ДЛИHA PAЗДEЛA  HE OГPAHИ-
C*  ЧEHA,  HO  BCЯ  БИБЛИOTEKA  COДEЖИT  HE  БOЛEE
C*  1023 K - 2  CЛOB  БЭCM-6.   ЭTO  COOTBETCTBYET
C*  2001 ЗOHE MД(MЛ) БЭCM-6. ДAHHЫE BHYTPИ PAЗДEЛA
C*  AДPECYЮTCЯ C HYЛЯ.
C*
C*     EXLIB  -  ИHИЦИИPYЮЩEE OБPAЩEHИE.  ПAPAMETP
C*  LUN - ЭTO  ШECTИCИMBOЛЬHЫЙ BOCЬMEPИЧHЫЙ YKAЗA-
C*  TEЛЬ ФAЙЛA,  ГДE PAЗMEЩAETCЯ  БИБЛИOTEЧHAЯ OБ-
C*  ЛACTЬ (L-OБЛACTЬ).   ПPИMEP:
C*        CALL EXLIB(640040B)
C*
C*     ELNAME  - ЗAKPЫTИE TEKYЩEГO PAЗДEЛA,  ЗABE-
C*  ДEHИE HOBOГO PAЗДEЛA C ИMEHEM  NAME  И  OБ'ЯB-
C*  ЛEHИE EГO TEKYЩИM.  ECЛИ B БИБЛИOTEKE YЖE ECTЬ
C*  PAЗДEЛ C TAKИM ИMEHEM, TO CTAPЫЙ PAЗДEЛ  YHИЧ-
C*  TOЖAETCЯ  (HO ПAMЯTЬ,  KOTOPYЮ OH ЗAHИMAЛ,  HE
C*  ИCПOЛЬЗYETCЯ).  HOBЫЙ PAЗДEЛ BCEГДA PAЗMEЩAET-
C*  CЯ B XBOCTE  L-OБЛACTИ.
C*
C*     ELEQU  -  OБ'ЯBЛEHИE  ЭKBИBAЛEHTHOГO  ИMEHИ
C*  PAЗДEЛA.  ДOПYCTИMO TOЛЬKO ДЛЯ ПOCЛEДHEГO PAЗ-
C*  ДEЛA L-OБЛACTИ.
C*
C*     ELMOVE  - ПOДBOД YKAЗATEЛЯ ПO L-ПAMЯTИ.
C*  ECЛИ ПAPAMETP - ЦEЛOE (MADR), TO YCTAHOBKA TE-
C*  KYЩEГO AДPECA  MADR  B TEKYЩEM  PAЗДEЛE.  ECЛИ
C*  ПAPAMETP - ИMЯ (NAME),  TO  ПOДBOД YKAЗATEЛЯ K
C*  PAЗДEЛY C ИMEHEM NAME:  ЗAKPЫTИE TEKYЩEГO PAЗ-
C*  ДEЛA, ПOИCK PAЗДEЛA NAME  И OБ'ЯBЛEHИE EГO TE-
C*  KYЩИM.
C*
C*     ELDEL   -  YДAЛEHИE PAЗДEЛA  NAME.  ПAMЯTЬ,
C*  ЗAHЯTAЯ ЭTИM PAЗДEЛOM, HE ИCПOЛЬЗYETCЯ ДO CЛE-
C*  ДYЮЩEГO YПЛOTHEHИЯ.  B  CЛYЧAE OTCYTCTBИЯ PAЗ-
C*  ДEЛA  П/П  ELDEL  OTPAБATЫBAET KAK ПYCTOЙ OПE-
C*  PATOP.
C*
C*     IELADR  -  ФYHKЦИЯ,  BЫДAЮЩAЯ TEKYЩИЙ AДPEC
C*  ПO PAЗДEЛY.  B  LIM  ЗACЫЛAETCЯ KOЛ-BO CЛOB ДO
C*  KOHЦA PAЗДEЛA.  CЛEДYET ИMETЬ B BИДY, ЧTO ECЛИ
C*  PAЗДEЛ  ПOCЛEДHИЙ,  TO  EГO  ДЛИHA - ДO  KOHЦA
C*  L-OБЛACTИ. ECЛИ TEKYЩИЙ PAЗДEЛ HE OБ'ЯBЛEH, TO
C*  OTBET  IELADR : -1 , ЗHAЧEHИE  LIM  HE OПPEДE-
C*  ЛEHO.
C*
C*     ELREAD/ELWRIT  -  ЧTEHИE/ЗAПИCЬ ПEPBЫX  LEN
C*  CЛOB MACCИBA  MAC ,  HAЧИHAЯ C TEKYЩEГO AДPECA
C*  ПO L-ПAMЯTИ  (И ПEPEMEЩEHИE TEKYЩEГO AДPECA HA
C*  LEN CЛOB BПEPEД).
C*
C*     ELCL  -  ЗAKPЫTИE TEKYЩEГO PAЗДEЛA,  ЗAПИCЬ
C*  KATAЛOГA ИMEH И OБMEHHOГO БYФEPA HA HOCИTEЛЬ.
C*
C*     ELCLOS - ЗAKPЫTИE  L-OБЛACTИ.  AHAЛOГ ELCL,
C*  HO ПOCЛE ЭTOЙ OПEPAЦИИ MOЖHO OПPEДEЛЯTЬ HOBYЮ
C*  L-OБЛACTЬ.
C*
C*     ELPRES  -  CЖATИE БИБЛИOTEЧHOЙ OБЛACTИ  ЗA
C*  CЧET ИCПOЛЬЗOBAHИЯ ПAMЯTИ ИЗ-ПOД YHИЧTOЖEHHЫX
C*  PAЗДEЛOB  (YПЛOTHEHИE  L-OБЛACTИ).  ПPИ OБPA-
C*  ЩEHИИ K ELPRES L-OБЛACTЬ ДOЛЖHA БЫTЬ OTKPЫTA.
C*
C*     ELPRIN - ПEЧATЬ KATAЛOГA БИБЛИOTEKИ.   ПA-
C*  PAMETP  LPR  ЗAДAET YCTPOЙCTBO BЫBOДA:
C*  0 - AЦПY, ИHAЧE TEPMИHAЛ.
C*
C*     LIBINIT - ИHИЦИAЛИЗAЦИЯ L-OБЛACTИ.
C*  <YK>          - YKAЗATEЛЬ HAЧAЛA OБЛACTИ;
C*  <ДЛИHA>       - ДЛИHA L-OБЛACTИ B ЗOHAX (BOCЬ-
C*                  MEPИЧHOE ЧИCЛO)
C*  <KOMMEHTAPИЙ> - ПPOИЗBOЛЬHЫЙ TEKCT. BЫДAETCЯ
C*                  ПPИ ПEЧATИ KATAЛOГA.
C*
C*     LCORRECT - ИЗMEHEHИE ДЛИHЫ И KOMMEHTAPИЯ
C*                L-OБЛACTИ.
C*
C*     ELCATALOG - ПEЧATЬ KATAЛOГA БИБЛИOTEKИ.
C*
C*     ELPRESS   - YПЛOTHEHИE БИБЛИOTEKИ.
C*
C*  CTAHДAPTHЫE YMOЛЧAHИЯ ДЛЯ ПPOГPAMM, PAБOTAЮЩИX
C*  ПO Y/K *CALL :
C*
C*     <YK>   = 670000
C*     <ДЛИHA> = 1000
C*
C*
C*     ЗAMEЧAHИЯ:
C*  1). ЗABEДEHИE  HOBOГO PAЗДEЛA  (И  YHИЧTOЖEHИE
C*      CTAPOГO) B KATAЛOГE ПPOИЗBOДИTCЯ  ПPИ ЗAK-
C*      PЫTИИ HOBOГO PAЗДEЛA.
C*
C*  2). CБOЙ ЭBM  ПPИ OПEPAЦИИ  ELPRES  БEЗOПACEH.
C*      ПPИ HOBOM BЫЗOBE ELPRES PAБOTA  БYДET ПPO-
C*      ДOЛЖEHA C ПPEPBAHHOГO MECTA.  ПOCЛE  ЭTOГO
C*      L-ПAMЯTЬ BOCCTAHOBИT PAБOTOCПOCOБHOCTЬ.
C*
C*  3). L-ПAMЯTЬ ИCПOЛЬЗYET ПAKET ПPOГPAMM  PAБOTЫ
C*      C  BHEШHEЙ ПAMЯTЬЮ  (EXMEM).  ПOЭTOMY  ДЛЯ
C*      CБPOCA БYФEPA  HA  BHEШHИЙ HOCИTEЛЬ  MOЖHO
C*      ИCПOЛЬЗOBATЬ  П/П  EXSAVE ИЗ ЭTOГO ПAKETA.
C*
C*  4). HEЛЬЗЯ ПOЛЬЗOBATЬCЯ OДHOBPEMEHHO E-ПAMЯTЬЮ
C*      И L-ПAMЯTЬЮ. OДHOBPEMEHHO C L-ПAMЯTЬЮ MOЖ-
C*      HO ИCПOЛЬЗOBATЬ V-ПAMЯTЬ.
C*
C*  5). ДЛЯ  OБMEHOB  И  XPAHEHИЯ KATAЛOГA ИCПOЛЬ-
C*      ЗYETCЯ 2 ЛИCTA  MOЗY (БEЗ ELPRES).  ELPRES
C*      ИCПOЛЬЗYET EЩE OДИH ЛИCT. ДЛЯ ЛYЧШEГO PAC-
C*      ПPEДEЛEHИЯ ПAMЯTИ ПPИ ИCПOЛЬЗOBAHИИ ELPRES
C*      ЦEЛECOOБPAЗHO OПИCATЬ EE ЗAPAHEE  B ГOЛOB-
C*      HOЙ ПPOГPAMME OПEPATOPOM
C*        EXTERNAL  ELPRES
C*
C*
C*           ДИAГHOCTИKA OБ OШИБKAX.
C*
C*     ПPИ PAБOTE ПPOГPAMM BOЗMOЖHA  BЫДAЧA  ДИAГ-
C*  HOCTИKИ ПO PAБOTE C E-ПAMЯTЬЮ (CM. П/П EXMEM).
C*  KPOME TOГO,  BЫДAETCЯ CЛEДYЮЩAЯ ДИAГHOCTИKA:
C*
C*  HET P.<ИMЯ>  - OTCYTCTBYET PAЗДEЛ  <ИMЯ>   ПPИ
C*    003          OПEPAЦИИ  ELMOVE (NAME).
C*
C*  OШ. L-AДPEC  - CЛИШKOM БOЛЬШOЙ TEKYЩИЙ AДPEC B
C*    004          PAЗДEЛE (BЫXOД ЗA ГPAHИЦY  PAЗ-
C*                 ДEЛA).
C*
C*  MAЛO L-ПAM.  - BCЯ L-OБЛACTЬ ЗAПOЛHEHA.
C*    007
C*  HET L-ПAMЯTИ - OTCYTCTBYET ИHИЦИИPYЮЩEE  OБPA-
C*                 ЩEHИE K  EXLIB.
C*
C*  HEOБX.YПЛOT. - БЫЛ CБOЙ  ПPИ OПEPAЦИИ  ELPRES.
C*    006          HEOБXOДИMO ПOBTOPИTЬ EE.
C*
C*  PAЗДEЛ!      - HE OTKPЫT PAЗДEЛ ПPИ OПEPAЦИЯX
C*    002          ELMOVE (MADR), ELREAD, ELWRIT.
C*
C*  ПOЛOH KATAЛ. - CЛИШKOM MHOГO PAЗДEЛOB.  KOЛИ-
C*    001          ЧECTBO PAЗДEЛOB (BKЛЮЧAЯ YHИЧ-
C*                 TOЖEHHЫE,  HO  HE YПЛOTHEHHЫE)
C*                 HE ДOЛЖHO ПPEBЫШATЬ 500.
C*  HET KATAЛOГA - KATAЛOГ HE ЗABEДEH ЛИБO ИCПOP-
C*    005          ЧEH.
C*
C*     ЛЮБAЯ ДИAГHOCTИKA ЯBЛЯETCЯ ФATAЛЬHOЙ. ПOC-
C*  ЛE EE BЫДAЧИ ПPOИЗBOДИTCЯ CБPOC  БYФEPA BHEШ-
C*  HEЙ ПAMЯTИ.   KATAЛOГ  L-ПAMЯTИ   HE  CБPACЫ-
C*  BAETCЯ (!).
C*     ИMEETCЯ BOЗMOЖHOCTЬ ПEPEXBATA  ДИAГHOCTИKИ
C*  OБ OШИБKAX ПPИ PAБOTE C L-ПAMЯTЬЮ.  ДЛЯ ЭTOГO
C*  CЛYЖИT  COMMON-БЛOK  ELERR:
C*
C*     COMMON /ELERR/ IREACT,IERR,IERRTX(2)
C*
C*     ДЛЯ ПEPEXBATA OШИБKИ HAДO ЗACЛATЬ 1  B ПE-
C*  PEMEHHYЮ IREACT.  ПOCЛE ЭTOГO  B  CЛYЧAE BOЗ-
C*  HИKHOBEHИЯ OШИБKИ EE HOMEP  БYДET  ПOMEЩEH  B
C*  IERR, A TEKCT ДИAГHOCTИKИ  -  B IERRTX. HOME-
C*  PA ДИAГHOCTИK ПPИBEДEHЫ BЫШE  (ПOД ИX TEKCTA-
C*  MИ).
C*     ДЛЯ CHЯTИЯ ПEPEXBATA ДOCTATOЧHO  ЗACЛATЬ 0
C*  B IREACT.
C*     B  ЦEЛЯX  OБЛEГЧEHИЯ  ПPOГPAMMИPOBAHИЯ  HA
C*  ACCEMБЛEPE ЗHAЧEHИE IERR ДYБЛИPYETCЯ B И.P.14
C*
C*================================================
C*
C
C           CTPYKTYPA KATAЛOГA
C
C     0  - CПEЦKOД 1
C     1  - ДЛИHA OБЛACTИ B ЗOHAX  (LZ)
C     2  - MAKCИMAЛЬHЫЙ E-AДPEC (EMAX)
C     3  - KOЛИЧECTBO PAЗДEЛOB  (KR)
C     4  - YK.HA HAЧAЛO ИMEH PAЗДEЛOB (YK)
C     5  - KOЛ-BO PAЗДEЛOB ДЛЯ ,VLM,  (VL)
C          (VL=1-KR)
C     6  - PAБ. ЯЧEЙKA (KOПИЯ LUN)
C   7-21 - KOMMEHTAPИЙ L-OБЛACTИ.
C  22-23 - PEЗEPB
C    24  - DATES:
C          48:25 - ДATA YПЛOTHEHИЯ
C          24:01 - ДATA ЗABEДEHИЯ L-ПAMЯTИ
C    25  - HOMEP BEPCИИ ПOCЛEДHEГO CЖATИЯ
C          (VERS).
C    26-1011 - NAMES:  ИMEHA PAЗДEЛOB.
C  1012-1775 - NADRS:
C          48    - PAЗДEЛ YHИЧTOЖEH
C          47    - ЭKBИBAЛEHTHOE ИMЯ
C          46    - PEЗEPB
C          45:25 - ДATA ЗABEДEHИЯ PAЗДEЛA
C          24:01 - E-AДPEC HAЧAЛA PAЗДEЛA
C  1776  - E-AДPEC ДЛЯ PAЗMEЩEHИЯ HOBOГO
C          PAЗДEЛA ('CBOБOДHO')    (ENEW)
C  1777  - CПEЦKOД 2
C.........................................
C
 /EXPAGE/:PC,BLOCK,EXPG(1024),ELCAT(1024)
 *ELERR*:LC,BLOCK,REACT,IERR(3)
 NP:,P/P,(ELCAT)16
 A/I0:,EQU,=I0
 BL:I,BLOCK,LUN
 ,CONT,NAME.ИMЯ TEK. PAЗДEЛA
 ,CONT,N.HOMEP TEK. PAЗДEЛA
 ,CONT,LBEG.HAЧAЛO PAЗДEЛA
 ,CONT,LEND.KOHEЦ PAЗДEЛA
 ,CONT,LMAX.MAKC. AДPEC B PAЗДEЛE
 ,CONT,LADR.TEK. AДPEC B PAЗДEЛE
 ,CONT,FLCAT.ФЛAГ:KATAЛOГ MEHЯЛCЯ
 ,CONT,W.PAБ. ЯЧEЙKA
 ,CONT,FLMOVE.ФЛAГ:HYЖEH EXMOVE
 ,CONT,SI15
 LBL:,EQU,11.ДЛИHA BL.
 *8:,BLOCK,BS,P,C,IR,JM(2),T
 EX,BLOCK,EXMEM,EXREAD,EXWRIT,IEXADR
 ,CONT,EXSAVE,EXCLOS,EXMOVE
 ,CONT,ERREXMEM
 *0:,BLOCK,SPC1,LZ,EMAX,KR,YK
 ,CONT,VL,COPYLUN,COMMENT(11)
 ,CONT,(2),DATES,VERS
 ,CONT,NAMES(500),NADRS(500)
 ,CONT,ENEW,SPC2
 B:,BSS,
 BS,BASE,B
 C,VTM,ELCAT
 ,ATI,P
 ,ITA,15
 ,ATX,SI15
 P,XTA,
 ,ATX,LUN
 ,ARX,=1
 ,ATX,W
 ,XTA,LUN
 ,AOX,PW
 ,AOX,=:0010
 ,*70,
 C,XTA,SPC1
 ,AEX,SPEC
 ,U1A,ERR5.HET KATAЛOГA
 C,XTA,SPC2
 ,AEX,SPEC
 ,UZA,OKEY
 C,XTA,SPC2
 ,AEX,SPEC2
 ,U1A,ERR5
 OKEY:,XTA,LUN
 C,ATX,COPYLUN
 ,XTA,A/I0
 ,UTC,IERR
 ,ATX,
 T,VTM,W
 ,ITA,T
 ,UJ,EXMEM
 ELREAD:,ENTRY,
 JM,VTM,ELREAD
 ,UJ,SACC
 ELWRIT:,ENTRY,
 JM,VTM,ELWRIT
 ,UJ,SACC
 ELNAME:,ENTRY,
 JM,VTM,ELNAME
 ,UJ,PAR
 ELEQU:,ENTRY,
 JM,VTM,ELEQU
 ,UJ,PAR
 ELMOVE:,ENTRY,
 JM,VTM,ELMOVE
 ,UJ,PAR
 ELDEL:,ENTRY,
 JM,VTM,ELDEL
 ,UJ,PAR
 ELCL:,ENTRY,
 ,UTC,1
 ELCLOS:,ENTRY,
 IR,VTM,
 JM,VTM,ELCL
 CHB:,BSS,
 15,MTJ,T
 CHB*:,BSS,
 ,UTC,LUN
 ,XTA,
 ,UZA,ERR0.HET ИHИЦ.
 BASE:BS,BASE,B
 ,ITA,T
 ,ATX,SI15
 ,XTA,A/I0
 ,UTC,IERR
 ,ATX,
 C,VTM,ELCAT
 C,XTA,SPC2
 ,AEX,SPEC2
 ,UZA,ERR6
 ,NTR,7
 JM,UJ,
 PAR:,STI,P
 ,UJ,CHB
 IELADR:,ENTRY,
 JM,VTM,IELADR
 SACC:,ATI,P
 15,MTJ,T
 T,UTM,-2
 ,UJ,CHB
 ELNAME:,BSS,
 C,XTA,EMAX
 C,A-X,ENEW
 ,U1A,ERR7
 C,XTA,YK
 ,ATI,IR
 IR,VZM,ERR1.ПOЛOH KAT.
 IR,UTM,-1
 ,ITA,IR
 C,ATX,YK
 P,XTA,.ИMЯ
 ,ATX,NAME
 C,XTA,KR
 ,ARX,=1
 C,ATX,KR
 ,ATX,N
 ,AEX,=77777
 ,ARX,=2.1-KR
 C,ATX,VL.ДЛЯ ,ATI, (!)
 ,*50,67B.DATE*
 ,APX,=:7707 7774
 ,ASN,64+3
 C,AOX,ENEW
 C,UTC,
 IR,ATX,NADRS
 C,XTA,ENEW
 ,ATX,LBEG
 ,ATX,LMAX
 C,XTA,EMAX
 ,ATX,LEND
 ,ATX,FLCAT
 JM,VJM,CHNAM
 ,XTS,NAME
 C,UTC,
 IR,STX,NAMES
 ,U1A,MOVE0
C
C   PAЗДEЛ C TAKИM ИMEHEM YЖE ECTЬ.
C   ПPOCTABЛЯEM CTAPOMY PAЗДEЛY B48
C   B NADRS - ПPИЗHAK  'MOЖHO ЛИKBИ-
C   ДИPOBATЬ'.
C
 T,UTC,
 P,XTA,NADRS-1
 ,AOX,=:4
 T,UTC,
 P,ATX,NADRS-1
 ,UJ,MOVE0
 ELEQU:,BSS,
 C,XTA,KR
 ,AEX,N
 ,U1A,ERR2
 ,XTA,NAME
 ,UZA,ERR2
 ,ATX,W
 P,XTA,
 ,ATX,NAME
 JM,VJM,CHNAM
 ,U1A,ELEQ2
 T,UTC,
 P,XTA,NADRS-1
 ,AOX,=:4
 T,UTC,
 P,ATX,NADRS-1
 ELEQ2:C,XTA,YK
 ,ATI,IR
 IR,VZM,ERR1.ПOЛOH KATAЛ.
 IR,UTM,-1
 ,ITA,IR
 C,ATX,YK
 C,MTJ,T
 IR,J+M,T
 T,XTA,NAMES+1
 T,ATX,NAMES
 T,XTA,NADRS+1
 T,ATX,NADRS
 ,AAX,=7777 7777
 ,AOX,=:2
 T,ATX,NADRS+1
 ,XTA,NAME
 T,ATX,NAMES+1
 C,XTA,KR
 ,ARX,=1
 C,ATX,KR
 ,ATX,N
 ,AEX,=77777
 ,ARX,=2
 C,ATX,VL
 ,XTA,W
 ,ATX,NAME
 ,UJ,RET
 ELDEL:,BSS,
 ,XTA,NAME
 ,ATX,W
 P,XTA,
 ,ATX,NAME
 JM,VJM,CHNAM
 ,XTS,W
 ,STX,NAME
 ,U1A,RET.HET TAKOГO
 T,UTC,
 P,XTA,NADRS-1
 ,AOX,=:4
 T,UTC,
 P,ATX,NADRS-1
 ,UJ,RET
 ELMOVE:,BSS,
 P,XTA,
 ,ASN,64-8
 ,YTA,
 ,UZA,SHIFT.OCTAL
 P,XTA,
 ,AEX,=I0
 ,ASN,64+24
 ,UZA,SHIFT.INTEGER
C
C        CALL  ELMOVE (NAME)
C
C      ПOДBOД K HOBOMY PAЗДEЛY.
C
 P,XTA,
 ,ATX,NAME.ИMЯ PAЗДEЛA
 JM,VTM,
 CHNAM:C,XTA,KR.KOЛ-BO PAЗДEЛOB
 ,ATI,P
 C,J+M,P
 C,WTC,YK
 P,UTM,
 C,XTA,VL
 ,ATI,T
 L1:,BSS,
 T,UTC,.1-KR
 P,XTA,NAMES-1.ELCAT+NAMES+KR-1+YK
 ,AEX,NAME
 ,UZA,CHG
 EL1:T,VLM,L1
 JM,VZM,BADNAM
 JM,UJ,
 CHG:JM,UTC,
 JM,VIM,
 T,UTC,
 P,XTA,NADRS-1
 ,ARX,
 ,UZA,EL1.YДAЛEH
 LCHG:T,UTC,
 P,XTA,NADRS-1.TPEБYETCЯ
 ,AAX,=:2.ГOЛOBHOE
 ,UZA,IS.ИMЯ PAЗДEЛA
 T,UTM,-1
 ,UJ,LCHG
C      HET PAЗДEЛA
 BADNAM:,BSS,
 ,XTA,FLCAT
 ,UZA,*1
 ,XTA,LUN
 ,AOX,PW
 ,*70,.ЗAП.KAT.
 *1:,XTA,NAME
 ,ATX,T3+1
 ,UJ,ERR3.HET PAЗДEЛA
C
C
 IS:,BSS,
 T,UTM,-2
 ,ITA,T
 ,AEX,=77777
 ,ATX,N
 T,UTC,2
 P,XTA,NADRS-1.AДP. PAЗДEЛA
 ,AAX,=7777 7777
 ,ATX,LBEG
 ,ATX,LMAX
 ,XTA,N
 C,AEX,KR
 ,UZA,LAST
 T,UTC,2
 P,XTA,NADRS-2.CЛEД.AДPEC
 ,AAX,=7777 7777
 *2:,ATX,LEND.ГPAHИЦA
 MOVE0:P,VTM,
 ,UJ,SHIFT
 LAST:,BSS,
 C,XTA,ENEW
 ,ATX,LMAX
 C,XTA,EMAX
 ,UJ,*2
 SHIFT:,BSS,
 ,XTA,NAME
 ,UZA,ERR2
 P,XTA,
 ,ARX,LBEG
 ,AAX,=7777 7777
 ,ATX,LADR
 ,NTR,7
 ,X-A,LEND
 ,U1A,ERR4.OШ. L-AДPEC
 ,XTA,=1
 ,ATX,FLMOVE
 RET:,BSS,
 14,VTM,
 ,NTR,6
 13,UJ,
 IELADR:,BSS,
 ,XTA,NAME
 ,UZA,NONAM
 ,NTR,7
 ,XTA,LEND
 ,A-X,LADR
 ,AOX,A/I0
 P,ATX,
 ,XTA,LADR
 ,A-X,LBEG
 ,AOX,A/I0
 ,UJ,RET
 NONAM:,BSS,
 ,XTA,=I-1
 ,UJ,RET
 ELCL:,BSS,
 ,XTA,
 ,ATX,NAME
 ,UTC,REACT
 ,ATX,
 ,XTA,FLCAT
 ,UZA,NOWR
 ,XTA,LUN
 ,AOX,PW
 ,*70,.ЗAП.KAT
 NO WR:IR,V1M,EXS
 T,VTM,1-LBL
 ,XTA,
 LCL:,BSS,
 T,ATX,BL+LBL-1
 T,VLM,LCL
 C,ATX,SPC1
 ,UJ,EXCLOS
 EXS:,ITS,
 ,UJ,EXSAVE
 ELREAD:,BSS,
 ,UTC,EXREAD-EXWRIT
 ELWRIT:,BSS,
 IR,VTM,EXWRIT
 ,XTA,NAME
 ,UZA,ERR2
 ,XTA,LADR
 ,ATX,W
 P,ARX,.ДЛИHA MACCИBA
 ,AAX,=7777 7777
 ,NTR,7
 ,ATX,LADR
 ,X-A,LEND
 ,U1A,ERR4.ПEPEЙДEM ГPAHИЦY
 C,XTA,KR
 ,AEX,N
 ,U1A,IFMOVE
 ,XTA,LADR
 ,X-A,LMAX
 ,UZA,IFMOVE
 ,XTA,LADR
 ,ATX,LMAX
 C,ATX,ENEW
 ,ATX,FLCAT
 IFMOVE:,XTA,FLMOVE
 ,U1A,TOMOVE
 ,ITA,P
 IR,UJ,
 TOMOVE:,BSS,
 ,ITA,P
 ,ITS,13
 ,ITS,IR
 ,XTS,
 14,VTM,W
 ,ITS,14
 ,CALL,EXMOVE
 ,NTR,7
 ,STX,
 ,STI,IR
 ,STI,13
 IR,UJ,
 ERR0:T,VTM,
 ,UJ,ERRPR
 ERR1:T,VTM,1
 ,UJ,E
 ERR2:T,VTM,2
 ,UJ,E
 ERR3:T,VTM,3
 ,UJ,E
 ERR4:T,VTM,4
 ,UJ,E
 ERR5:T,VTM,5
 ,UJ,E
 ERR6:T,VTM,6
 ,UJ,E
 ERR7:T,VTM,7
 E:,BSS,
 ,UTC,REACT
 ,XTA,
 ,ASN,64-7
 ,UZA,ERRPR
 T,MTJ,JM
 ,ITA,T
 ,AOX,A/I0
 15,VTM,IERR
 JM,J+M,JM
 JM,UTM,T0
 JM,XTS,
 JM,XTS,1
 ,XTS,SI15
 ,ATI,15
 ,XTA,=I-1
 ,NTR,6
 13,UJ,.*****
 ERRPR:T,J+M,T
 T,UTM,T0
 ,ITA,13
 ,XTS,
 ,UJ,ERREXMEM
 T0:,ISO,12HHET L-ПAMЯTИ
 T1:,ISO,12HПOЛOH KATAЛ.
 T2:,ISO,12H  PAЗДEЛ!
 T3:,ISO,12HHET P.
 T4:,ISO,12HOШ. L-AДPEC
 T5:,ISO,12HHET KATAЛOГA
 T6:,ISO,12HHEOБX.YПЛOT.
 T7:,ISO,12HMAЛO L-ПAM.
 PW:,,NP
 ,,
 SPEC:,ISO,6HL-CAT.
 SPEC2:,ISO,6HPRESS
 BL:,BSS,LBL
 ,END,

 EXMEM:,NAME,15.07.85
C**********          15.07.85      A.Л.ДABЫДOB
C*
C*               ПAKET ПPOГPAMM
C*        ДЛЯ PAБOTЫ C BHEШHEЙ ПAMЯTЬЮ
C*
C*
C*          SUBROUTINE  EXMEM  (LUN)
C*          SUBROUTINE  EXWRIT (MAC,LEN)
C*          SUBROUTINE  EXREAD (MAC,LEN)
C*          SUBROUTINE  EXMOVE (EADR)
C*          SUBROUTINE  EXSAVE (TIM)
C*          FUNCTION    IEXADR (0)
C*          SUBROUTINE  EXCLOS
C*
C*
C*     BHEШHЯЯ ПAMЯTЬ ПPEДCTABЛЯET COБOЙ MHOЖECTBO
C*  CЛOB,  AДPECYEMЫX   ЦEЛOЧИCЛEHHOЙ   ПEPEMEHHOЙ
C*  EADR, ПPИHИMAЮЩEЙ ЗHAЧEHИЯ:  0,1,...,1047551.
C*  TAKИM  OБPAЗOM,  OБ'EM  BHEШHEЙ  ПAMЯTИ  PABEH
C*  1023K - 2  CЛOB.  B OДHOЙ ЗOHE MЛ ИЛИ MД ПOME-
C*  ЩAETCЯ  1023  CЛOBA  BHEШHEЙ ПAMЯTИ.  CЛEДOBA-
C*  TEЛЬHO,  MAKCИMAЛЬHO  BHEШHЯЯ ПAMЯTЬ  ЗAHИMAET
C*  2000B  ЗOH  MЛ (MД).
C*
C*     EXMEM - ИHИЦИИPYЮЩEE OБPAЩEHИE.  LUN - YKA-
C*  ЗATEЛЬ ФAЙЛA, ГДE БYДET PAЗMEЩATЬCЯ E-ПAMЯTЬ,
C*  HAПPИMEP:
C*       CALL EXMEM (410215B)
C*       CALL EXMEM (650000B)
C*
C*     EXWRIT, EXREAD - ЗAПИCЬ/ЧTEHИE ПEPBЫX  LEN
C*  CЛOB MACCИBA  MAC,  HAЧИHAЯ C TEKYЩEГO AДPECA
C*  ПO E-ПAMЯTИ (И ПEPEMEЩEHИE TEKYЩEГO AДPECA HA
C*  LEN  CЛOB BПEPEД).
C*
C*     EXMOVE - YCTAHOBKA TEKYЩEГO AДPECA  EADR
C*  ПO E-ПAMЯTИ.
C*
C*     EXSAVE - CБPOC БYФEPA BHEШHEЙ ПAMЯTИ HA HO-
C*  CИTEЛЬ.  ECЛИ TIM.NE.0,  TO ЧEPEЗ KAЖДЫE  TIM
C*  MИHYT KOMMEPЧECKOГO BPEMEHИ  ПPИ  OБPAЩEHИИ K
C*  ЛЮБOЙ ИЗ ПOДПPOГPAMM PAБOTЫ C E-ПAMЯTЬЮ, KPOME
C*  IEXADR, БYДET ПPOИЗBOДИTЬCЯ CБPOC БYФEPA.
C*
C*     IEXADR - ФYHKЦИЯ C OДHИM ФИKTИBHЫM ПAPAMET-
C*  POM,  BЫДAЮЩAЯ  ЗHAЧEHИE  TEKYЩEГO  AДPECA  ПO
C*  BHEШHEЙ ПAMЯTИ.
C*
C*     EXCLOS - CБPOC БYФEPOB  И  KOHEЦ  PAБOTЫ  C
C*  E-ПAMЯTЬЮ.  ПOCЛE ЭTOЙ OПEPAЦИИ  MOЖHO OПPEДE-
C*  ЛИTЬ HOBYЮ OБЛACTЬ ДЛЯ PAБOTЫ C E-ПAMЯTЬЮ
C*  ( CALL  EXMEM (LUN) ).
C*
C*
C*      BHEШHЯЯ ПAMЯTЬ ИMEET MHOГO OБЩEГO C BИPTY-
C*  AЛЬHOЙ ПAMЯTЬЮ (CM. П/П VMEMOR, LIB23), OДHAKO
C*  PACCЧИTAHA HA ИCПOЛЬЗOBAHИE B ПPOГPAMMAX, ПPИ-
C*  MEHЯЮЩИX  B OCHOBHOM  ПOCЛEДOBATEЛЬHЫЙ  ДOCTYП
C*  K ДAHHЫM (ЭTO HAИXYДШИЙ BAPИAHT ДЛЯ VMEMOR).
C*  БЫCTPOДEЙCTBИE E-ПAMЯTИ PACTET C YMEHЬШEHИEM
C*  KOЛИЧECTBA CMEH TEKYЩEГO AДPECA (EXMOVE) И C
C*  YBEЛИЧEHИEM PAЗMEPOB ПEPEДABAEMЫX MACCИBOB
C*  (ПAPAMETP  LEN  B  EXREAD/EXWRIT).
C*
C*
C*      BOЗMOЖHAЯ ДИAГHOCTИKA:
C*
C*  OШ. ПAPAMETP  - OШИБOЧHЫЙ ПAPAMETP LUN ПPИ
C*                  OБPAЩEHИИ K EXMEM.
C*  HET E-ПAMЯTИ  - OTCYTCTBYET ИHИЦИИPYЮЩEE OБ-
C*                  PAЩEHИE K EXMEM.
C*  ДBA ПOДBOДA   - ДBA OБPAЩEHИЯ ПOДPЯД K EXMOVE.
C*
C*  OШ. E-AДPEC   - CЛИШKOM БOЛЬШOЙ TEKYЩИЙ AДPEC.
C*
C*  ЧYЖAЯ ЗOHA    - ИCПOPЧEHA OБЛACTЬ ДЛЯ XPAHEHИЯ
C*                  BHEШHEЙ ПAMЯTИ  ИЛИ  BЫXOД  ЗA
C*                  ГPAHИЦY ЗAПИCAHHOГO ПPИ ЧTEHИИ.
C*
C*    BMECTE C ДИAГHOCTИKOЙ BЫДAETCЯ AДPEC BOЗBPA-
C*  TA B ПPOГPAMMY ПOЛЬЗOBATEЛЯ, BЫЗBABШYЮ OШИБKY,
C*  ЛИБO HOMEP ЧYЖOЙ ЗOHЫ. ПOCЛE BЫДAЧИ ДИAГHOCTИ-
C*  KИ ПPOИЗBOДИTCЯ  CБPOC  БYФEPA  И PAБOTA ПPOГ-
C*  PAMMЫ ПPEKPAЩAETCЯ.
C*
C*  ПOPTЯTCЯ PEГИCTPЫ 8-14. HA BЫXOДE - ,NTR,6.
C*
C*
 BL:I,BLOCK,LUN.YK. HAЧAЛA E-OБЛACTИ
 2,CONT,NZ.HOMEP ЗOHЫ B БYФEPE.
 3,CONT,NW.CCЫЛKA HA CЛOBO B ЗOHE.
 4,CONT,EXADR.TEKYЩИЙ E-AДPEC.
 5,CONT,CTIM*.BPEMЯ CБPOCA
 6,CONT,TIM.ИHTEPBAЛ CБPOCA.
 7,CONT,FLMOVE.ФЛAГ:БЫЛ MOVE(=0)
 8,CONT,FLRD.ФЛAГ: ЗOHA B БYФEPE
 9,CONT,FLWR.ФЛAГ: HYЖEH CБPOC
 10,CONT,W.PAБ.ЯЧEЙKA
 LBL:,EQU,10
 A/I0:,EQU,=I0
 /EXPAGE/:PC,BLOCK,*EXPAGE*(1024)
 NP:,P/P,(*EXPAGE*)16
 *8:,BLOCK,BS,P1,P2,IR,JM(2),T
 ACTIM:,EQU,70100B+110B.KOMM.BPEMЯ
 B:,BSS,
 JM,VTM,EXMEM
 ,STI,P1
 ,UJ,BAS2
 EXWRIT:,ENTRY,
 JM,VTM,EXWRIT
 ,UJ,TWO
 EXREAD:,ENTRY,
 JM,VTM,EXREAD
 ,UJ,TWO
 EXMOVE:,ENTRY,
 JM,VTM,EXMOVE
 ,UJ,ONE
 EXSAVE:,ENTRY,
 JM,VTM,EXSAVE
 ,UJ,ONE
 EXCLOS:,ENTRY,
 JM,VTM,EXCLOS
 ,UJ,BAS2
 IEXADR:,ENTRY,
 ,UTC,EXADR
 ,XTA,
 ,UTC,A/I0
 ,AOX,
 13,UJ,
 ONE:,STI,P1
 ,UJ,BAS
 TWO:,STI,P2
 ,STI,P1
 BAS:,UTC,LUN
 ,XTA,
 ,UZA,ERR0.HET ИHИЦИAЛ.
 BAS2:BS,BASE,B
 ,ITA,13
 15,ATX,
 JM,UJ,
 EXMEM:P1,XTA,
 ,AAX,=77 7777
 ,UZA,ERRLUN
 ,ATX,LUN
 ,ATX,FLMOVE
 ,UJ,RET2
 INITMOV:JM,VTM,INI
 13,VTM,CONT
 ,UJ,BAS2
 EXMOVE:,BSS,
 ,XTA,FLMOVE
 ,UZA,ERR1.ДBA MOVE ПOДPЯД
 ,XTA,
 ,ATX,FLMOVE
 P1,XTA,
 ,AAX,=7777 7777
 ,ATX,EXADR
 INI:,XTA,EXADR
 JM,VJM,GIVADREM
 ,ITS,14
 ,STX,NW
 ,ATX,W.N ЗOHЫ
 ,AEX,NZ
 ,AAX,=7777
 ,U1A,RNZ
 ,XTA,FLRD
 ,U1A,RET.ЗOHA B ПAMЯTИ
 RNZ:JM,VJM,WRZ
 ,XTA,W
 JM,VTM,RET2
 ,UJ,RDZ
 EXREAD:,UTC,LLR-LLW
 EXWRIT:IR,VTM,LLW
 ,ATX,FLMOVE
 ,XTA,FLRD
 ,UZA,INITMOV
 CONT:,BSS,
 P2,XTA,
 ,ATI,P2.KOЛИЧ. CЛOB
 P2,UTM,1
 ,AAX,=77777
 ,ARX,EXADR.БYДYЩИЙ EXADR
 ,ATX,EXADR
 ,XTA,NW
 ,ATI,14
 ,AEX,=77777
 ,ARX,=1
 ,ATI,JM
 JM,J+M,P1
 JM,VTM,
 IR,UJ,
 LLW:,XTA,SPEC
 ,UTC,*EXPAGE*+1777B
 ,ATX,
 LLW2:P2,UTM,-1
 P2,VZM,EW
 P1,UTC,
 14,XTA,
 ,UTC,*EXPAGE*+1776B
 14,ATX,
 14,VLM,LLW2
 ,XTA,LUN
 ,ATX,FLWR
 NEWR:JM,VJM,WRZ
 ,XTA,NZ
 ,ARX,=1
 JM,VJM,RDZ
 14,VTM,-1776B
 P1,UTM,1777B
 IR,UJ,
 EW:,BSS,
 ,XTA,LUN
 ,ATX,FLWR
 ERW:,BSS,
 ,ITA,14
 ,ATX,NW
 JM,VZM,RET.CБPOCA HE БЫЛO
 ,UJ,RET2.БЫЛ CБPOC
 LLR:,UTC,*EXPAGE*+1777B
 ,XTA,
 ,AEX,SPEC
 ,ASN,64+18
 ,U1A,ERR3
 LLR2:P2,UTM,-1
 P2,VZM,ERW
 ,UTC,*EXPAGE*+1776B
 14,XTA,
 P1,UTC,
 14,ATX,
 14,VLM,LLR2
 ,UJ,NEWR
 EXSAVE:,BSS,
 P1,XTA,
 ,ASN,64-7
 ,UZA,NTIM.ЗAДAH TIM=0
 P1,XTA,
 ,NTR,6
 ,A*X,=R60.0
 NTIM:,ATX,TIM
 JM,VTM,RET2
 SAVE:,BSS,
 JM,MTJ,IR
 JM,VJM,WRZ
 IR,MTJ,JM
 INTERV:,XTA,TIM
 JM,UZA,
 ,*50,ACTIM
 ,NTR,6
 ,A+X,TIM
 ,ATX,CTIM*
 JM,UJ,
 RDZ:,BSS,
 15,ATX,
 ,ASN,64+12
 ,U1A,ERR2.Z.GE.7777B
 15,XTA,
 ,ATX,NZ
 ,ARX,LUN
 ,ATX,FLRD
 ,AEX,LUN
 ,ASN,64+12
 ,U1A,ERR3
 ,XTA,FLRD
 ,AOX,=:0010
 ,AOX,WP
 ,*70,
 JM,UJ,
 WRZ:,BSS,
 ,XTA,FLWR
 JM,UZA,
 ,XTA,NZ
 ,ARX,LUN
 ,AOX,WP
 ,*70,
 ,XTA,
 ,ATX,FLWR
 JM,UJ,
 ERREXMEM:,ENTRY,
 BS,VTM,B
 ,UZA,ERRORA
 ,UJ,ERRORZ
 ERR0:BS,VTM,B
 T,VTM,T0
 ERRORA:,UTC,TEA-TEZ
 ERRORZ:,XTA,TEZ
 ,ATX,TADR-1
 T,XTA,
 ,ATX,TB
 T,XTA,1
 ,STX,TB+1
 ,ASN,64+15
 ,YTA,
 ,AUX,=6H'7''7''7''7''7'''
 ,AOX,=6H00000B
 ,ATX,TADR
 14,VTM,TBEG
 ,ITS,14
 14,VTM,TEND
 ,ITS,14
 14,VTM,1
 ,ITS,14
 ,CALL,PRINT8
 STOP*:,SUBP,
 JM,VTM,STOP*
 ,UJ,WRZ
 ERR1:T,VTM,T1
 ,UJ,ERRORA
 ERR2:T,VTM,T2
 ,STX,
 ,UJ,ERRORA
 ERR3:T,VTM,T3
 ,XTA,NZ
 15,ATX,-1
 ,UJ,ERRORZ
 ERRLUN:T,VTM,TLUN
 ,UJ,ERRORA
 EXCLOS:JM,VJM,SAVE
 ,XTA,
 14,VTM,1-LBL
 LCL:14,ATX,BL+LBL-1
 14,VLM,LCL
 ,UJ,RET2
 RET:,BSS,
 ,XTA,TIM
 ,UZA,RET2
 ,*50,ACTIM
 ,NTR,7
 ,A-X,CTIM*
 ,U1A,RET2
 JM,VJM,SAVE
 RET2:,NTR,6
 14,VTM,
 15,WTC,
 ,UJ,
 RDEMEM:,ENTRY,
C      BXOД ДЛЯ ПOCЛOBHOГO ЧTEHИЯ
C   ИЗ E-ПAMЯTИ. ФYHKЦИЯ C OДHИM
C   ФИKTИBHЫM ПAPAMETPOM, OTBET -
C   OЧEPEДHOE CЛOBO.
C      PAБOTAET TOЛЬKO ПOCЛE ПPEД-
C   BAPИTEЛЬHOГO ЧTEHИЯ XOTЯ-БЫ
C   OДHOГO CЛOBA ПOДPOГPAMMOЙ
C   EXREAD.
 RDEXMEM:,ENTRY,
 BS,BASE,B
 ,XTA,EXADR
 ,ARX,=1
 ,ATX,EXADR
 ,XTA,NW
 ,ATI,14
 ,ARX,=1
 ,ATX,NW
 ,UTC,*EXPAGE*+1776B
 14,XTA,
 13,UTC,
 14,VIM,.BOЗBPAT
C     ЧTEHИE CЛEД. ЗOHЫ
 ,XTS,NZ
 ,ARX,=1
 JM,VJM,RDZ
 15,XTA,
 13,UJ,
C ========================================
C
C     ПOЛYЧEHИE HOMEPA ЗOHЫ И AДPECA ПO
C  HEЙ ИЗ E-AДPECA.  ПPИ OБPAЩEHИИ ACC =
C  E-AДPEC;  OTBET HA  ACC  (N ЗOHЫ)  И
C  B  I.R.14 (AДPEC B ЗOHE - 1776B)
C
C     BOЗBPAT ПO JM.
C
 GIVADREM:,ENTRY,
 GIVADREM:,BSS,
 15,ATX,
 ,ASN,64+10
 15,ARX,-1
 ,ASN,64+10
 15,ARX,
 *1:15,ATX,
 ,UTC,=1777
 ,AAX,
 ,ATI,14
 ,UTC,=1777
 ,AEX,
 ,U1A,*2
 15,XTA,
 ,UTC,=1
 ,ARX,
 ,UJ,*1
 *2:14,UTM,-1776B
 15,XTA,
 ,ASN,64+10
 JM,UJ,
 TBEG:,ISO,30H ***** OШИБKA BHEШHEЙ ПAMЯTИ:
 TB:,ISO,12H
 ,CONT,6H   (A=
 TADR:,BSS,
 TEND:,LOG,
 T0:,ISO,12HHET E-ПAMЯTИ
 T1:,ISO,12HДBA ПOДBOДA
 T2:,ISO,12HOШ. E-AДPEC
 T3:,ISO,12HЧYЖAЯ ЗOHA
 TLUN:,ISO,12HOШ. ПAPAMETP
 TEA:,ISO,6H    A=
 TEZ:,ISO,6H    Z=
 SPEC:,TEXT,8HEXMEM
 WP:,,NP
 ,,
 BL:,BSS,LBL
 ,END,

  LREGIM:,NAME,
C*
C*********  29.10.82    A.П.CAПOЖHИKOB
C*
C*       FUNCTION LREGIM(N)
C*   CMEHA PEЖИMA ПEЧATИ ЛИCTИHГA
C*
C*   ПPИ N=1 YCTAHABЛИBAETCЯ PEЖИM *NO LIST
C*   ПPИ N=2   ---"---   *STANDARD LIST
C*   ПPИ N=4   ---"---   *FULL LIST
C*   ПPИ N=0 PEЖИM HE MEHЯETCЯ, A KOД PEЖИMA
C*           (1,2 ИЛИ 4) BЫДAETCЯ B KAЧECTBE
C*           ЗHAЧEHИЯ ФYHKЦИИ LREGIM.
C*
C*   ПOPTИTCЯ TOЛЬKO 14 PEГИCTP.
C*
  NOLISFL*:,LC,1
  FULISFL*:,LC,1
  FLAGS   :,LC,1
  A/I0    :,LC,1
  B:,ATI,14
  14,XTA,
  14,BASE,B
  ,AEX,A/I0
  ,UZA,GET ONLY
  ,AEX,FLAGS
  ,AAX,=7
  ,AEX,FLAGS
  ,ATX,FLAGS
  ,AAX,=1
  ,UTC,NOLISFL*
  ,ATX,
  ,XTA,FLAGS
  ,ASN,64+2
  ,AAX,=1
  ,UTC,FULISFL*
  ,ATX,
  GET ONLY:,XTA,FLAGS
  ,AAX,=7
  :,U1A,*+1
  ,XTA,=2
  :,AOX,A/I0
  13,UJ,
  ,END,

  IGETS:,NAME,
C*
C*********** 7.11.78. A.p.CApOvHiKOB
C*
C*   FUNCTION IGETS (AR,NB)
C*   FUNCTION IGETSL(NB)
C*   SUBROUTINE IPUTS (SYM,AR,NB)
C*   SUBROUTINE IPUTSL(SYM,NB)
C*
C*   PAbOTA C bAjTAMi, plOTHO YpAKOBAHHyMi
C*   B MACCiBE AR ( IGETS,IPUTS ) ili HA
C*   ObMEHHOM bYfEPE ( IGETSL,IPUTSL ). :
C*
C*               bAjTy
C*   ClOBO I----I----I----I----I----I----I
C*     0   :  0 :  1 :  2 :  3 :  4 :  5 :
C*         I----I----I----I----I----I----I
C*     1   :  6 :  7 :  8 :  9 : 10 : 11 :
C*         I----I----I----I----I----I----I
C*    ...    ..   ..   ..   ..   ..   ..
C*
C*    NB - HOMEP bAjTA : 0,1,2,...
C*
C*   IGETS (IGETSL) BydAET HA CYMMATOPE
C*   B BidE cElOgO ~iClA NB-j bAjT
C*   MACCiBA AR (ObMEHHOgO bYfEPA).
C*
C*   IPUTS (IPUTSL) YpAKOByBAET cElOE SYM
C*   B NB-j bAjT MACCiBA AR (ObM.bYfEPA).
C*
C*   pOPTqTCq PEgiCTPy 10-14
C*   HA ByXOdE : ,NTR,2
C*
C*   pPi iCpOlxzOBAHii ObMEHHOgO bYfEPA
C*   HE zAbyBAjTE O CiHXPOHizAcii C
C*   ObMEHHyMi pPOcEdYPAMi (CM. GIVPAG)
C*
C***************************************
C*
  14,VTM,
  ,UJ,B
  IPUTS:,ENTRY,
  14,VTM,1
  B:,ATI,12
  12,XTA,
  12,BASE,B
  ,AAX,=777777
  15,ATX,
  ,NTR,7
  ,A*X,C1/6
  ,ATX,NWORD
  ,ASN,64-2
  ,ARX,NWORD
  ,ARX,NWORD
  15,X-A,
  ,ASN,64-3
  ,AAX,=77
  14,VIM,P
  ,X-A,=50
  P:,STI,11
  ,ARX,NWORD
  ,ATI,10
  ,NTR,2
  14,VIM,PP
  10,XTA,
  11,ASN,64
  ,AAX,=377
  ,AOX,=I0
  13,UJ,
  PP:15,WTC,
  ,XTA,
  11,ASN,64-40
  10,AEX,
  ,XTS,=377
  11,ASN,64-40
  15,AAX,
  10,AEX,
  10,STX,
  13,UJ,
  IGETSL:,ENTRY,
  14,VTM,
  ,UJ,BB
  IPUTSL:,ENTRY,
  14,VTM,1
  BB:,ATI,12
  ,XTA,43B
  ,ITS,12
  ,UJ,B
  C1/6:,LOG,4002 5252 5252 5253
  NWORD:,BSS,1
  ,END,

  SCANER:,NAME,
C*
C****** 08/07/81   A.П.CAПOЖHИKOB
C*
C*   ФOPTPAHHЫЙ ИHTEPФEЙC ЭKCTPAKOДA
C*      ФOPMATHЫX ПPEOБPAЗOBAHИЙ
C*
C*  SCANER(STRI,FRAG,IREG,ITYP,KOD,IPOZ)
C*  PUTH(STRI,TEXT,INOR,N,M,IPOZ)
C*  PUTO(STRI,NUMB,INOR,N,M,IPOZ)
C*  PUTG(STRI,NUMB,INOR,N,M,IPOZ)
C*   BCE BXOДЫ TИПA "SUBROUTINE".
C*
C*   SCANER - BЫБOPKA CИHTAKCИЧECKOГO
C*     ФPAГMEHTA ИЗ TEKCTOBOЙ CTPOKИ.
C*   STRI - AДPEC HAЧAЛA CTPOKИ. ECЛИ
C*     STRI=0, BЫБOPKA HAЧИHAETCЯ C TOГO
C*     CИMBOЛA CTPOKИ, ГДE OCTAHOBИЛИCЬ
C*     ПOCЛE ПPEДЫДYЩEГO BXOДA.
C*   FRAG - MACCИB, KYДA ПOПAДAET BЫБPAH-
C*     HЫЙ ФPAГMEHT
C*   IREG - PEЖИM PAБOTЫ SCANER-A :
C*     БИT 1 : 0-ПPOБEЛ ECTЬ PAЗДEЛИTEЛЬ
C*             1-ПPOБEЛЫ ИГHOPИPYЮTCЯ
C*     БИT 2 : 0-CTAHДAPTHЫЙ AЛФABИT
C*             1-AЛФABИT ABTOKOДA MADLEN
C*     БИT 3 : 0-BOCЬMEPИЧHЫE ЧИCЛA ЗAДA-
C*               ЮTCЯ C CYФФИKCOM "B".
C*             1-BCE ЦEЛЫE ЧИCЛA CЧИTAЮT-
C*               CЯ BOCЬMEPИЧHЫMИ.
C*     БИT 4 : 0-CKAHИPOBAHИE ФPAГMEHTOB.
C*             1-ПOCИMBOЛЬHOE CKAHИPOBAHИE
C*     ПPOЧИE БИTЫ HE CYЩECTBEHHЫ.
C*   ITYP - TИП ПOЛYЧEHHOГO ФPAГMEHTA :
C*     0 - OШИБKA, ФPAГMEHT HE PACПOЗHAH
C*     1 - BOCЬMEPИЧHOE ЧИCЛO
C*     2 - ЦEЛOE
C*     3 - BEЩECTBEHHOE
C*     4 - TEKCTOBAЯ KOHCTAHTA
C*     5 - ПYCTOЙ ФPAГMEHT
C*   KOD - KOД PAЗДEЛИTEЛЯ (ЦEЛOE)
C*
C*   ПPИ ПOCИMBOЛЬHOM CKAHИPOBAHИИ ЗA OДHO
C*   OБPAЩEHИE K SCANER-Y BЫБИPAETCЯ OДИH
C*   OЧEPEДHOЙ CИMBOЛ CTPOKИ. OH ПOMEЩAETCЯ
C*   BO FRAG B BИДE  1H<CИMBOЛ> И B KOD B
C*   BИДE ЦEЛOГO ЧИCЛA.
C*   ITYP COДEPЖИT TИП BЫБPAHHOГO CИMBOЛA:
C*     0 - KOHEЦ CTPOKИ
C*     1 - ЦИФPA
C*     2 - БYKBA
C*     3 - PAЗДEЛИTEЛЬ
C*
C*   PUTH, PUTO, PUTG - OПEPAЦИИ TИПA
C*   "ENCODE" ПO CПEЦИФИKAЦИЯM "H","O","G"
C*
C*   ЗДECЬ STRI - AДPEC BЫXOДHOГO TEKCTA
C*   ИЛИ 0, ECЛИ OЧEPEДHOЙ TEKCTOBЫЙ ФPAГ-
C*   MEHT ЗACЫЛAETCЯ BCTЫK K ПPEДЫДYЩEMY
C*
C*   N,M - ЦEЛЫE.  N<32 , M<16
C*
C*   PUTH - ПEPECЫЛKA B BЫXOДHYЮ CTPOKY
C*     N CИMBOЛOB MACCИBA TEXT.
C*     INOR И M - HECYЩECTBEHHЫ
C*   PUTO - ПPEOБPAЗOBAHИE CЛOBA NUMB
C*     ПO ФOPMATY  O<N>  CO BCTABKOЙ
C*     OДHOГO ПPOБEЛA ПOCЛE KAЖДЫX  M
C*     BOCЬMEPИЧHЫX ЦИФP.
C*   PUTG - ПPEOБPAЗOBAHИE CЛOBA NUMB
C*     ПO ФOPMATY  G<N>.<M>  ИЛИ  I<N>
C*     ECЛИ M=0
C*   INOR - PEЖИM BЫPABHИBAHИЯ TEKCTA
C*     BHYTPИ CBOEГO ПOЛЯ ИЗ N ПOЗИЦИЙ :
C*     0 - ПO ЛEBOMY KPAЮ, 1 - ПO ПPABOMY
C*
C*   B  IPOZ  BO BCEX CЛYЧAЯX ПOMEЩAETCЯ
C*   HOMEP TEKYЩEЙ ПOЗИЦИИ ПO BXOДHOЙ
C*   (BЫXOДHOЙ) CTPOKE: 1,2,3,...
C*
  A/I0:,LC,1
  ,UTC,-1
  PUTH:,ENTRY,
  ,UTC,-1
  PUTO:,ENTRY,
  ,UTC,-1
  PUTG:,ENTRY,
  9,VTM,3
  8,BASE,*
  ,STX,IPOZ
  ,STX,M
  ,STX,N
  ,STI,11
  ,STI,10
  ,ATI,12
  12,XTA,
  ,AEX,A/I0
  :,U1A,*+1
  12,VTM,
  :11,XTA,
  ,AAX,=17
  ,ASN,64-15
  9,VZM,MACRO
  ,AAX,=10 0000
  ,ASN,64-4
  ,WTC,N
  ,XTS,
  ,AAX,=37
  ,ASN,64-24
  ,WTC,M
  ,XTS,
  ,AAX,=17
  15,AOX,
  ,ASN,64-15
  15,AOX,
  MACRO:,AOX,IW50
  9,FUN,12
  9,VIM,EXIT
  14,UJ,SW
  SW:,UJ,FIN.OШИБKA
  :10,ATX,.OCTAL
  ,UJ,FIN
  :10,ATX,.INTEGER
  ,UJ,FIN
  :10,ATX,.REAL
  ,UJ,FIN
  :,,.IDENT
  :,,.LONG
  ,UJ,ID
  :14,VTM,5.EMPTY
  ,UJ,FIN
  LONG:11,XTA,
  ,AAX,=17
  ,ASN,64-15
  ,FUN,12
  14,VZM,FIN
  10,UTM,2
  ID:10,ATX,
  ,YTA,
  10,ATX,1
  14,UTM,-5
  14,VZM,LONG
  ,FUN,70100B+136B.OГP.
  15,ATX,
  ,FUN,70100B+135B.ПOЗ.
  ,ASN,64+24
  ,ASN,64-9
  15,AOX,
  14,VTM,4
  ,AEX,
  FIN:,YTA,
  15,ATX,
  ,ITS,14
  ,AOX,A/I0
  ,WTC,N
  ,STX,
  ,AAX,=377
  ,AOX,A/I0
  ,WTC,M
  ,STX,
  ,ASN,64+24
  ,AAX,=377
  EXIT:,AOX,A/I0
  ,WTC,IPOZ
  ,STX,
  13,UJ,
  IW50:10,Z00,
  12,Z00,
  IPOZ:,BSS,1
  N   :,BSS,1
  M   :,BSS,1
  ,END,

  SHSORT:,NAME,
C*
C**********  2.11.80.  A.П.CAПOЖHИKOB
C*
C*   SUBROUTINE SHSORT(M,N,L,MORD)
C*   COPTИPOBKA ЗAПИCEЙ, ПOCЛEДOBATEЛЬHO
C*   PACПOЛOЖEHHЫX B MACCИBE  M :
C*    M[1], ... ,M[L]        - 1-Я ЗAПИCЬ
C*    M[L+1], ... ,M[2L]     - 2-Я ЗAПИCЬ
C*       .................
C*    M[NL-L+1], ... ,M[NL]  - N-Я ЗAПИCЬ
C*   N - KOЛИЧECTBO ЗAПИCEЙ
C*   L - ДЛИHA OДHOЙ ЗAПИCИ
C*
C*   ПOCЛE BЫXOДA ИЗ ПOДПPOГPAMMЫ
C*   MACCИB M COДEPЖИT TE ЖE N ЗAПИCEЙ,
C*   HO OTCOPTИPOBAHHЫX COГЛACHO БИHAPHOMY
C*   OTHOШEHИЮ  " < " , OПPEДEЛEHHOMY
C*   ФYHKЦИEЙ ЮЗEPA  MORD  ИЛИ CTAHДAPTHO
C*   B CЛYЧAE, ECЛИ ЗHAЧEHИE MORD =0,1,2,3.
C*
C*   FUNCTION MORD(Z1,Z2) MOЖET БЫTЬ
C*   HAПИCAHA HA ФOPTPAHE. ЗДECЬ Z1,Z2 -
C*   - AДPECA HAЧAЛ ДBYX ЗAПИCEЙ.
C*   ФYHKЦИЯ ДOЛЖHA CPABHИTЬ Z1 И Z2,
C*   ПOЛЬЗYЯCЬ CBOИM COБCTBEHHЫM KPИTEPИEM
C*   (HAПPИMEP, ПO COДEPЖИMOMY KAKOГO-TO
C*   KЛЮЧEBOГO ПOЛЯ), И BЫДATЬ B KAЧECTBE
C*   PEЗYЛЬTATA :
C*     1, ECЛИ  Z1 < Z2  ,
C*     0, ECЛИ  Z1 = Z2  ,
C*    -1, ECЛИ  Z1 > Z2  .
C*
C*   ECЛИ KЛЮЧ PACПOЛAГAETCЯ B ПEPBOM CЛOBE
C*   KAЖДOЙ ЗAПИCИ И ЗAHИMAET POBHO OДHO
C*   CЛOBO, TO MOЖHO ПOЛЬЗOBATЬCЯ OДHИM ИЗ
C*   TPEX CTAHДAPTHЫX KPИTEPИEB YПOPЯДOЧEHИЯ
C*   ЗAДABAЯ B KAЧECTBE ПAPAMETPA  MORD
C*   ЦEЛOE ЧИCЛO  1,2 ИЛИ 3 :
C*    1 - YПOPЯДOЧEHИE ПO AЛФABИTY
C*        (KЛЮЧ - TEKCTOBAЯ KOHCTAHTA)
C*    2 - YПOPЯДOЧEHИE ПO BOЗPACTAHИЮ
C*        (KЛЮЧ - ЦEЛOE ИЛИ BEЩECTB.)
C*    3 - YПOPЯДOЧEHИE ПO YБЫBAHИЮ
C*   ECЛИ ЖE ЗAДATЬ  MORD = 0,  TO BCЯ
C*   ЗAПИCЬ PACCMATPИBAETCЯ KAK CИMBOЛЬ-
C*   HЫЙ KЛЮЧ ДЛИHOЙ L CЛOB, И YПOPЯДO-
C*   ЧEHИE ПPOИCXOДИT ПO AЛФABИTY.
C*
C*   ЗAMEЧAHИE: ПOД AЛФABИTHЫM YПOPЯДOЧEHИEM
C*     MЫ ЗДECЬ ПOHИMAEM YПOPЯДOЧEHИE
C*     ЛATИHCKИX ИMEH ДЛЯ KOДA ISO
C*     ИЛИ PYCCKИX ИMEH ДЛЯ KOДA GOST.
C*     ДЛЯ COPTИPOBKИ PYCCKИX ИMEH B KOДE
C*     ISO CЛEДYET ПOЛЬЗOBATЬCЯ ПPOГPAMMOЙ
C*     RUSORT.
C*
C*   ИCПOЛЬЗYETCЯ METOД ШEЛЛA C YБЫBAЮЩИM
C*   ШAГOM  S(I+1)=(S(I)-1)/3
C*   CM. KHYT "ИCKYCCTBO ПPOГPAMMИPOBAHИЯ"
C*
C*   BЫXOДHAЯ ПOCЛEДOBATEЛЬHOCTЬ ЗAПИCEЙ
C*   YДOBЛETBOPЯET COOTHOШEHИЮ
C*    Z1 <= Z2 <=  ...  <= ZN
C*   OTHOCИTEЛЬHOE PACПOЛOЖEHИE ЗAПИCEЙ
C*   C PABHЫMИ KЛЮЧAMИ  HE  COXPAHЯETCЯ.
C*
  A/I0:,LC,1
  B:8,BASE,B
  ,STX,MORD
  ,STI,11.L
  ,STI,10.N
  ,AOX,A/I0
  ,ATX,M
  ,ITA,13
  ,ATX,RET
  ,NTR,3
C
C---   ПPOБA MORD HA 1,2,3  И
C      ПOДCTAHOBKA AДPECA
C      CTAHДAPTHOЙ ПPOЦEДYPЫ
C      CPABHEHИЯ KЛЮЧEЙ.
C
  ,WTC,MORD
  ,XTA,
  ,ATI,9
  ,AEX,A/I0
  ,ASN,64+2
  ,U1A,BEGIN
  9,XTA,ADR
  ,ATX,MORD
C
C
C      SUBROUTINE SHSORT(M,N,L,MORD)
C      DIMENSION M(2)
C      IF(L.LT.1.OR.N.LT.2) RETURN
C
C
  BEGIN:10,XTA,
  ,ATX,N
  ,A-X,=I2
  ,U1A,EXIT
  11,XTA,
  ,ATX,L
  ,A-X,=I1
  ,U1A,EXIT
C
C
C      S = 1
C   1  IF(9*S+4.GE.N) GOTO 2
C      S = 3*S+1
C      GOTO 1
C
C
  :12,VTM,1
  14,VTM,13
  MSTEP:,ITA,14
  ,AOX,A/I0
  ,A-X,N
  ,UZA,STEP
  12,MTJ,13
  12,J+M,12
  13,J+M,12
  12,UTM,1
  14,MTJ,13
  14,J+M,14
  13,J+M,14
  14,VLM,MSTEP
C
C
C    2  D = L*S
C       JJ = (S-1)*L
C       DO 3 J= S+1, N
C
C
  STEP:,ITA,12
  ,AOX,A/I0
  ,ATX,S
  ,ATX,J
  ,E+N,64-40
  ,A*X,L
  ,YTA,64
  ,ATX,D
  SSORT:,ATX,JJ
  ,XTA,J
  ,A+X,=I1
  ,ATX,J
  ,X-A,N
  ,U1A,ESTEP
C
C
C      II = JJ
C    5 IF(MORD(M(II+1),M(II+D+1)).NE.-1) GOTO 3
C
C
  ,XTA,JJ
  SIMPLE:,BSS,
  ,ATX,II
  ,A+X,M
  15,ATX,
  ,A-X,D
  ,WTC,MORD
  13,VJM,
  CHORD:,BSS,
  ,NTR,3
  8,BASE,B
  ,ATI,14
  14,UTM,-1
  14,VIM,ORD
C
C
C      DO 4 K = 1,L
C      KK = M(II+K)
C      M(II+D+K) = M(II+K)
C   4  M(II+K) = KK
C
C
  ,XTA,II
  ,A+X,M
  ,ATI,10
  ,A-X,D
  ,ATI,11
 ,WTC,L
  14,VTM,
  TRAN:10,XTA,
  11,XTS,
  10,STX,
  11,ATX,
  10,UTM,1
  11,UTM,1
  14,UTM,-1
  14,VIM,TRAN
C
C
C      II = II-D
C      IF(II.GE.0) GOTO 5
C
C
  ,XTA,II
  ,A-X,D
  ,A-X,D
  ,U1A,ORD
  ,A+X,D
  ,UJ,SIMPLE
C
C
C    3 JJ = JJ+L
C
C
  ORD:,XTA,JJ
  ,A+X,L
  ,UJ,SSORT
C
C
C      IF(S.EQ.1) RETURN
C      S = (S-1)/3
C      GOTO 2
C      END
C
C
  ESTEP:,WTC,S
  12,VTM,-1
  12,VZM,EXIT
  ,ITA,12
  ,A*X,C1/3
  ,ATI,12
  ,UJ,STEP
C---
  EXIT:,XTA,RET
  ,STI,13
  ,NTR,6
  13,UJ,
C---
  ADR:,,
  ,Z00,CHEK0
  ,,
  ,Z00,CHEK1
  :,,
  ,Z00,CHEK2
  :,,
  ,Z00,CHEK3
C
C---   CTAHД.ПPOЦEДYPЫ
C      CPABHEHИЯ KЛЮЧEЙ :
C
  CHEK0:,STI,10
  ,ATI,9
  ,WTC,L
  14,VTM,
  :14,UTM,-1
  13,VJM,CHEK1/1
  ,U1A,CHORD
  14,VZM,CHORD
  9,UTM,1
  10,VLM,*-2
  CHEK1:,STI,10
  ,ATI,9
  CHEK1/1:,BSS,
  9,XTA,
  ,ASN,64+15
  10,XTS,
  ,ASN,64+15
  15,X-A,
  ,U1A,P1
  ,AEX,
  ,U1A,MM1
  9,XTA,
  ,AAX,=77777
  10,XTS,
  ,AAX,=77777
  SUB:15,X-A,
  ,U1A,P1
  ,AEX,
  13,UZA,
  MM1:,XTA,=77777
  13,UJ,
  P1:,XTA,=I1
  13,UJ,
  CHEK3:,STI,10
  ,ATI,9
  ,UJ,CHEK23
  CHEK2:,STI,9
  ,ATI,10
  CHEK23:,NTR,6
  10,XTA,
  9,XTS,
  ,UJ,SUB
C---
  C1/3:,LOG,4005 2525 2525 2526
  MORD:,BSS,1
  RET :,BSS,1
  II  :,BSS,1
  JJ  :,BSS,1
  M   :,BSS,1
  N   :,BSS,1
  L   :,BSS,1
  S   :,BSS,1
  J   :,BSS,1
  D   :,BSS,1
  ,END,

 LIBINIT:,NAME,.30/07/85
C*
C*    ПOДПPOГPAMMЫ ИЗ ПAKETA ПPOГPAMM
C*    PAБOTЫ C БИБЛИOTEKOЙ ДAHHЫX
C*
C*  *CALL LIBINIT:<YK>,<ДЛИHA>,<COMMENT>
C*  *CALL LCORRECT:<YK>,<ДЛИHA>,<COMMENT>
C*  *CALL ELCATALOG:<YK>
C*  *CALL ELPRESS:<YK>
C*
C*
C*
C*
 A/I0:,LC,1
 *MONCRD*:LC,BLOCK,MC(12)
 /EXPAGE/:PC,BLOCK,EXRG(1024),ELCAT(1024)
 /ELPRES/:PC,BLOCK,PG2(1024)
 NPC:,P/P,(ELCAT)16
 ELPRES:,SUBP,
 ELCAT:EQ,BLOCK,SPC1,LZ,EMAX,KR,YK
 ,CONT,VL,COPYLUN,COMMENT(11),(2)
 ,CONT,DATES,VERS,NAMES(500)
 ,CONT,NADRS(500),ENEW,SPC2
 8,VTM,
 ,UJ,COM
 LCORRECT:,ENTRY,
 8,VTM,CORR
 ,UJ,COM
 ELCATALO:,ENTRY,
 8,VTM,PRINT
 ,UJ,COM
 ELPRESS:,ENTRY,
 8,VTM,PRESS
 COM:,BSS,
 ,ITA,13
 ,ATX,SI13
 ,ITA,7
 ,ATX,SI7
 8,MTJ,7
 10,VTM,
 ,CALL,PARAMT*
 11,VZM,ESTR
 9,VTM,LUN
 10,VTM,4
 ,CALL,PARAMT*
 11,VZM,ESTR
 9,VTM,LONG
 10,VTM,5
 ,CALL,PARAMT*
 11,VZM,ESTR
 ,XTA,=6H
 14,VTM,-11
 CLMC:14,ATX,MC+11
 14,VLM,CLMC
 10,VTM,1
 ,CALL,PARAMT*
 14,VTM,-10
 TRMC:14,XTA,MC+10
 14,ATX,COMM+10
 14,VLM,TRMC
 ESTR:,BSS,
 7,VZM,INIT
 14,VTM,LUN
 ,ITS,14
 ,CALL,EXLIB
 7,UJ,
 INIT:,BSS,
 ,XTA,=6HL-CAT.
 ,ATX,SPC1
 ,ATX,SPC2
 ,XTA,
 ,ATX,KR
 ,ATX,ENEW
 14,VTM,500
 ,ITA,14
 ,ATX,YK
 14,VTM,1
 ,ITA,14
 ,ATX,VL
 ,*50,67B
 ,APX,=:7707 7774
 ,ASN,64+27
 ,ATX,DATES
 ,XTA,=1
 ,ATX,VERS
 CORR:,BSS,
C
C   OБЩAЯ ЧACTЬ LIBINIT И LCORRECT
C
 ,XTA,LONG
 ,UZA,L
 ,ARX,=1777
 ,ASN,64+11
 ,UZA,LG
 L:,XTA,=1000
 ,ATX,LONG
 LG:,XTA,LONG
 ,ATX,LZ
 ,AOX,A/I0
 ,NTR,6
 ,A*X,=R1023
 ,NTR,7
 ,A-X,=I1
 ,AAX,=7777 7777
 ,ATX,EMAX
 14,VTM,-10
 LCOMM:14,XTA,COMM+10
 14,ATX,COMMENT+10
 14,VLM,LCOMM
 ,NTR,6
 ,XTA,LUN
 ,AOX,WPC
 ,*70,.ЗAП.KAT. ***
 RET:,WTC,SI7
 7,VTM,
 ,WTC,SI13
 ,UJ,
 PRINT:,BSS,
 ,ITS,
 ,CALL,ELPRIN
 ,UJ,RET
 PRESS:13,VTM,RET
 ,UJ,ELPRES
 LUN:,LOG,670000
 LONG:,LOG,1000
 SI13:,BSS,1
 SI7:,BSS,1
 COMM:,BSS,11
 WPC:,Z00,NPC
 ,,
 ,END,

 ELPRES:,NAME,.24-26/07/85
C*********      24/07/85      A.Л.ДABЫДOB
C*
C*     YПЛOTHEHИE БИБЛИOTEKИ ДAHHЫX
C*          (CБOPKA MYCOPA)
C*
C*     П/П  ИЗ ПAKETA ПPOГPAMM  PAБOTЫ C
C*  БИБЛИOTEKOЙ ДAHHЫX  (CM. П/П EXLIB).
C*
C*.......................................
C
 /EXPAGE/:PC,BLOCK,EXPG(1024),ELCAT(1024)
 /ELPRES/:PC,BLOCK,PG2(1024)
 ERREXMEM:,SUBP,
 GIVADREM:,SUBP,
 GIVADR:,EQU,GIVADREM
 NPC:,P/P,(ELCAT)16
 NP0:,P/P,(EXPG)16
 NP2:,P/P,(PG2)16
 A/I0:,LC,1
 *5:,BLOCK,L,MW,MR
 ,CONT,BS,P,C,IR,JM(2),T
 *0:,BLOCK,SPC1,LZ,EMAX,KR,YK
 ,CONT,VL,COPYLUN,COMMENT(11)
 ,CONT,(2),DATES,VERS
 ,CONT,NAMES(500),NADRS(500)
 ,CONT,ENEW,SPC2
 BL:I,BLOCK,FLC.ФЛAГ-ПPOДOЛЖEHИE
 ,CONT,AR
 ,CONT,AW
 ,CONT,LONG
 ,CONT,NZR
 ,CONT,IWW
 ,CONT,NZP2
 ,CONT,W
 LBL:,EQU,8
 B:,BSS,
 ,ITA,13
 ,ITS,
 ,ITS,
 ,CALL,EXSAVE
 15,XTA,
 ,ATI,13
 BS,BASE,B
 C,VTM,ELCAT
 ,ITA,7
 ,ITS,6
 ,ITS,5
 ,NTR,7
 C,XTS,SPC2
 ,AEX,SPEC2
 ,ATX,FLC
 ,UZA,**
 C,XTA,VERS
 VRS:,ARX,=1
 ,AAX,=77 7777
 ,UZA,VRS
 C, ATX,VERS
 ,*50,67B.DATE*
 ,APX,=:7707 7774
 ,ASN,64+3
 C,XTS,DATES
 ,AAX,=7777 7777
 15,AOX,
 C,ATX,DATES
 C,XTA,SPC1
 ,AEX,SPEC
 ,U1A,ERR0
 C,XTA,KR
 ,UZA,ERR0
 ,XTA,SPEC2.ПPИЗHAK:
 C,ATX,SPC2.ИДET YПЛOTHEHИE
 JM,VJM,WRC
 **:,BSS,
 ,XTA,=:4
 ,ATX,NZP2
C................................
C
C      OCHOBHOЙ ЦИKЛ CЖATИЯ
C
 ,XTA,
 ,ATX,AW
 ,ATX,LONG
 ,ATX,IWW
 C,WTC,YK
 P,VTM,ELCAT
 C,WTC,KR
 IR,VTM,-1
 MW,VTM,-1776B.YK. ЗAП.
 ,XTA,FLC
 ,UZA,REPRESS.ПOBTOPHO
 MLC*:,XTA,=:4
 ,ATX,FLC
 MLCH:,BSS,
 IR,UTC,
 P,XTA,NADRS
 ,AAX,=:2
 ,U1A,KILL
 IR,UTC,
 P,XTA,NADRS
 ,AAX,=7777 7777
 ,ATX,AR.E-AДPEC ЧTEHИЯ
 IR,VZM,LASTR
 IR,UTC,
 P,XTA,NADRS-1.CЛEД.PAЗДEЛ
 GLONG:,AAX,=7777 7777
 ,A-X,AR
 ,ATX,LONG.ДЛИHA PAЗДEЛA
 IR,UTC,
 P,XTA,NADRS
 ,ARX,
 ,U1A,SAVER.COXP. PAЗДEЛ
 IR,MTJ,T
 P,J+M,T
 LCR:T,UTM,1
 T,XTA,NADRS.ПPEД. PAЗДEЛ
 ,AAX,=:2
 ,UZA,KILL.HE ENTRY
 T,XTA,NADRS
 ,ARX,.BXOД
 ,UZA,LCR.TOЖE YДAЛEH
C     ECЛИ B YДAЛЯEMOM PAЗДEЛE ECTЬ
C     HEYДAЛЯEMЫЙ BXOД, TO OH CTAHO-
C     BИTCЯ ИMEHEM PAЗДEЛA, A YДAЛE-
C     HИE БЛOKИPYETCЯ.
 IR,UTC,
 P,XTA,NADRS
 ,AEX,=:4
 IR,UTC,
 P,ATX,NADRS
 T,XTA,NADRS
 ,AOX,=:4
 T,ATX,NADRS
 T,XTA,NAMES
 IR,UTC,
 P,ATX,NAMES
 SAVER:,BSS,
 ,XTA,AR
 ,AEX,AW
 ,UZA,TR/CH
 ,XTA,IWW
 ,UZA,GIVADRW
 RGW:,BSS,
 IR,UTC,
 P,XTA,NADRS
 ,AAX,=:7777 7777
 ,AOX,AW
 IR,UTC,
 P,ATX,NADRS
 IR,MTJ,T
 P,J+M,T
 LL:T,XTA,NADRS+1
 ,AAX,=:2
 ,UZA,RLONG
 T,XTA,NADRS+1
 ,AAX,=:7777 7777
 ,AOX,AW
 T,ATX,NADRS+1
 T,VLM,LL
 RLONG:,BSS,
 ,XTA,LONG
 ,ASN,64+15
 ,ATX,W
 ,XTA,LONG
 ,AEX,=77777
 ,ARX,=2
 ,ATI,L
 ,XTA,FLC
 ,ARX,
 ,U1A,CHPRESS.ДOЖATИE
 GADRR:,XTA,AR
 JM,VJM,GIVADR
 T,MTJ,MR
 ,ATX,NZR
 ,AEX,NZP2
 ,UZA,ZIS
 ,XTA,NZR
 JM,VJM,RDZ
 ZIS:,BSS,
C
C     OCHOBHOЙ ЦИKЛ ПEPEПИCИ
C
 MLTR:,BSS,
 ,UTC,PG2
 MR,XTA,1776B
 MR,VLM,WRW
 ,XTS,NZR
 ,ARX,=1
 ,ATX,NZR
 JM,VJM,RDZ
 MR,VTM,-1776B
  15,XTA,
 WRW:,UTC,EXPG
 MW,ATX,1776B
 MW,VLM,NEWW
 ,*70,IWW
 MW,VTM,-1776B
 ,XTA,IWW
 ,ARX,=1
 ,ATX,IWW
 NEWW:L,VLM,MLTR
 ,XTA,W.ПPOДOЛЖEHИE  L
 ,UZA,TR/CH.PAЗДEЛ ПEPEПИCAH
 ,ARX,=77777
 ,AAX,=77777
 ,ATX,W
 L,VTM,1
 ,UJ,MLTR
C.....................................
 TR/CH:,BSS,
 ,XTA,AW
 ,ARX,LONG
 ,ATX,AW
 KILL:,BSS,
 IR,VZM,RETWR.,VLM,
 IR,UTM,-1.B ДPYГYЮ
 ,UJ,MLCH.CTOPOHY
C
C          CЖATИE ЗABEPШEHO.
C
C     ИCKЛЮЧAEM ИЗ KATAЛOГA YДAЛEHHЫE
C     PAЗДEЛЫ.
C
 RETWR:,*70,IWW
 RETWRC:C,WTC,VL
 IR,VTM,
 C,UTC,
 MR,VTM,499
 MR,MTJ,MW
 LCAT:,BSS,
 MR,XTA,NADRS
 ,ARX,
 ,UZA,DELR.YДAЛEH
 MW,ATX,NADRS
 MR,XTA,NAMES
 MW,ATX,NAMES
 MW,UTM,-1
 KCT:MR,UTM,-1
 IR,VLM,LCAT
C
C   BHOCИM ИЗMEHEHИЯ B YKAЗATEЛИ KATAЛOГA
C
 ,ITA,MW
 ,ITS,MR
 15,X-A,.ACC=MW-MR
 ,ATX,LONG.YДAЛEHO PAЗД.
 C,ARX,VL
 C,ATX,VL
 C,XTA,KR
 ,A-X,LONG
 C,ATX,KR
 C,XTA,YK
 ,ARX,LONG
 C,ATX,YK
 ,XTA,AW
 C,ATX,ENEW
 ,XTA,SPEC
 C,ATX,SPC2
 JM,VJM,WRC
 ,STX,
 ,STI,5
 ,STI,6
 ,ATI,7
 13,UJ,
 DELR:,XTA,
 MR,ATX,NAMES
 ,UJ,KCT
 LASTR:C,XTA,ENEW
 ,UJ,GLONG
 GIVADRW:,XTA,AW
 JM,VJM,GIVADR
 T,MTJ,MW
 C,ARX,COPYLUN
 ,ARX,=1
 ,AOX,WP0
 ,ATX,IWW
 ,AOX,=:0010
 ,*70,
 ,XTA,SPECEX
 C,AOX,VERS
 ,UTC,EXPG
 ,ATX,1777B
 ,UJ,RGW
 RDZ:,BSS,
 ,ATX,NZP2
 ,AOX,WP2
 ,AOX,=:0010
 C,ARX,COPYLUN
 ,ARX,=1.CMEЩ.B L-OБЛACTИ
 ,*70,
 ,UTC,PG2
 ,XTA,1777B
 ,AEX,SPECEX
 ,ASN,64+18
 ,U1A,ERRZ
 JM,UJ,
 WRC:,BSS,
 ,XTA,WPC
 C,AOX,COPYLUN
 ,*70,
 JM,UJ,
C
C-------------------------------------
C
C     БЛOK, OБPAБATЫBAЮЩИЙ ПPOДOЛЖEHИE
C     YПЛOTHEHИЯ ПOCЛE CБOЯ.
C
C    ПPИЗHAK ГPAHИЦЫ YПЛOTHEHHOЙ OБЛACTИ -
C  HECOBПAДEHИE BEPCИЙ B CПEЦKOДE ЗOHЫ.
C
 REPRESS:,BSS,
 C,XTA,ENEW
 JM,VJM,GIVADR
 ,AEX,=77777
 ,ARX,=1
 ,ATI,L
 ,XTA,
C   B HAЧAЛE E-OБЛACTИ M.Б. KYCOK, KOTOPЫЙ
C   HE YПЛOTHЯЛCЯ, HE ПEPEПИCЫBAЛCЯ И ИMEET
C   CTAPYЮ BEPCИЮ B CПEЦKOДE.
 LCB:,BSS,
 ,ATX,NZR
 JM,VJM,RDZ
 ,UTC,PG2
 ,XTA,1777B
 C,AEX,VERS
 ,AAX,=77 7777
 ,UZA,LCG.ПOШЛO HOBOE
 ,XTA,NZR
 ,ARX,=1
 L,VLM,LCB
C
C  BCE CTAPOE:  ПEPEПИCЫBAEM BCE.
C
 ,UJ,MLC*
 LCG:,BSS,
 ,XTA,NZR
 ,ARX,=1
 ,ATX,NZR
 JM,VJM,RDZ
 ,UTC,PG2
 ,XTA,1777B
 C,AEX,VERS
 ,AAX,=77 7777
 ,U1A,GRAN.CTAPOE
  L,VLM,LCG
C
C     BCE YЖE YПЛOTHEHO
C
 C,XTA,ENEW
 ,ATX,AW
 ,UJ,RETWRC
 GRAN:,BSS,
 ,XTA,NZR.HOMEP ЗOHЫ ЗAП.
 MW,VTM,-1776B
 C,ARX,COPYLUN
 ,ARX,=1
 ,AOX,WP0
 ,ATX,IWW
 ,XTA,NZR
 ,NTR,6
 ,AOX,A/I0
 ,A*X,=R1023.0
 ,A+X,=R0.5
 ,NTR,7
 ,A+X,=I0
 ,AAX,=7777 7777
 ,ATX,FLC
 ,UJ,MLCH
C...................................
C
 CHPRESS:,BSS,
C
C   HE ПOPA ЛИ ПEPEПИCЫBATЬ
 ,XTA,AW.A.HAЧ.ЗП
 ,ARX,LONG.ДЛИHA PAЗДEЛA
 ,X-A,FLC.A.HAЧ.HECЖATOГO(WR)
 ,UZA,TR/CH.PAЗДEЛ CЖAT
C
C    B ЭTOM PAЗДEЛE - ГPAHИЦA HECЖATOГO.
C  OПPEДEЛЯEM AДPEC ЧTEHИЯ, COOTBETCTBYЮЩИЙ
C  ГPAHИЦE.
C
 ,A+X,LONG.LPAЗДEЛA-LHECЖATOГO
C        ACC = KOЛ-BY ПPOПYCKAEMЫX CЛOB.
 15,ATX,
 ,ARX,AR.A.HAЧ.ЧT
 ,STX,AR.A.HAЧ.HECЖATOГO(RD)
 ,X-A,LONG
 15,ATX,
 ,ASN,64+15
 ,STX,W
 ,AEX,=77777
 ,ARX,=2
 ,ATI,L
 ,XTA,=:4
 ,ATX,FLC
 ,XTA,SPECEX
 C,AOX,VERS
 ,UTC,EXPG
 ,ATX,1777B
 ,UJ,GADRR
 ERR0:T,VTM,T0
 ,ITA,13
 ,XTS,
 ,UJ,ERREXMEM
 ERRZ:T,VTM,TZ
 ,XTA,NZP2
 15,ATX,
 ,UJ,ERREXMEM
 T0:,ISO,12HHET L-ПAMЯTИ
 TZ:,ISO,12HЧYЖAЯ ЗOHA
 SPEC:,ISO,6HL-CAT.
 SPEC2:,ISO,6HPRESS
 SPECEX:,TEXT,8HEXMEM
 WPC:,,NPC
 ,,
 WP0:,,NP0
 ,,
 WP2:,,NP2
 ,,
 BL:,BSS,LBL
 ,END,ELPRES
 ELPRIN:,NAME,.29-30/07/85
C*             29/07/85      ДABЫДOB A.Л.
C*
C*     ПEЧATЬ KATAЛOГA БИБЛИOTEKИ ДAHHЫX.
C*
C*     П/П ИЗ ПAKETA ПPOГPAMM PAБOTЫ C БИБ-
C*  ЛИOTEKOЙ ДAHHЫX  (CM. П/П EXLIB).
C*
 /EXPAGE/:PC,BLOCK,EXPG(1024),ELCAT(1024)
 ERREXMEM:,SUBP,
 PRINT8:,SUBP,
 NPC:,P/P,(ELCAT)16
 A/I0:,LC,1
 *5:,BLOCK,FT,C,JM
 *8:,BLOCK,BS,P,IR,M,(2),T
 *0:,BLOCK,SPC1,LZ,EMAX,KR,YK
 ,CONT,VL,COPYLUN
 ,CONT,COMMENT(11),(2),DATES,VERS
 ,CONT,NAMES(500),NADRS(500)
 ,CONT,ENEW,SPC2
 BL:I,BLOCK,LPG.ДЛИHA ЛИCTA ПEЧATИ
 ,CONT,SI15
 ,CONT,L.TEK.CTPOKA
 ,CONT,LCF.TEK.ФPAГMEHT
 ,CONT,LSE.ПOCЛ.CTPOKA ЛИCTA
 ,CONT,W(2)
 LBL:,EQU,7
 B:BS,BASE,B
 ,STI,P
 ,ITA,13
 ,ITS,7
 ,ITS,6
 ,ITS,5
 C,VTM,ELCAT
 C,XTS,SPC1
 ,AEX,SPEC
 ,U1A,ERR0.HET ИHИЦ.
 ,ATX,L
 ,NTR,7
 P,XTA,
 ,ASN,64-7
 ,UZA,*1
 ,XTA,=1
 *1:,ATI,FT.ФЛAГ:TEPMИHAЛ
 FT,XTA,LFIRST
 ,ATX,LPG
 FT,V1M,*2.TEPMИHAЛ
 ,XTA,
 ,CALL,DATAPR*
 13,VJM,NEWSTR
 *2:,BSS,
 ,ITA,15
 ,ATX,SI15
 JM,VJM,CLEARS
 15,VTM,STRP
 ,XTA,=6HEXTERN
 ,XTS,=6HAL LIB
 ,XTS,=6HRARY
 C,XTS,COPYLUN
 ,APX,=77 7777
 ,AUX,=6H'7''7''7''7''7''7'
 ,AOX,=6H000000
 C,XTS,LZ
 ,APX,=7777
 ,AUX,=6H'''''7''7''7''7'
 ,AOX,=6H (0000
 ,XTS,=6H)  OT
 C,XTS,DATES
 ,ASN,64+21
 ,YTA,
 ,AUX,=6H'3''17''''1''17'''
 ,AOX,=6H00/00/
 C,XTS,DATES
 ,ASN,64+21-11
 ,YTA,
 ,AUX,=6H'17''17'''''''''
 ,AOX,=6H00  (Y
 ,XTS,=6HПЛOTH.
 C,XTS,DATES
 ,ASN,64-3
 ,AUX,=6H'3''17''''1''17'''
 ,AOX,=6H00/00/
 C,XTS,DATES
 ,ASN,64-3-11
 ,AUX,=6H'17''17'''''''''
 ,AOX,=6H00).
 15,ATX,
 13,VJM,OUTPUT
 JM,VJM,CLEARS
 15,VTM,STRP
 T,VTM,-9
 C,XTA,COMMENT
 *4:T,UTC,9
 C,XTS,COMMENT+1
 FT,VZM,*4E
 ,AEX,=6H
 ,UZA,*4/
 ,AEX,=6H
 *4E:T,VLM,*4
 *4/:,XTS,
 13,VJM,OUTPUT
C   ПEPBЫE ДBE CTPOKИ HAПEЧATAHЫ
 FT,V1M,LOOP1
C   ПPOПYCK OДHOЙ CTPOKИ AЦПY
 ,WTC,SI15
 15,VTM,
 13,VJM,NEWSTR
 LOOP1:,BSS,
C   ПEЧATЬ ЗAГOЛOBKA CTPAHИЦЫ
 JM,VJM,CLEARS
 15,VTM,STRP
 ,NTR,7
 ,XTA,L
 ,ARX,LPG
 ,ATX,LSE
 ,XTS,L
 ,ARX,=1
 ,STX,L
 JM,VTM,
 C,A-X,KR
 ,UZA,L1B.OДHA CTPAHИЦA
 JM,VTM,-1
 L1B:,BSS,
 T,VTM,-5
 ,XTA,ЗAГ
 L2B:T,XTS,ЗAГ+6
 T,VLM,L2B
 15,ATX,
 FT,V1M,EL1B
 15,UTM,4
 EL1B:JM,VLM,L1B
 FT,VZM,OT
 15,XTA,
 ,AEX,=6H''
 15,ATX,
 OT:13,VJM,OUTPUT
 LOOP2:,BSS,
 JM,VJM,CLEARS
 P,VTM,STRP
 ,XTA,L
 ,ATX,LCF
 13,VJM,GFRAG
 ,UZA,ENDCAT
 ,XTA,L
 ,ARX,LPG
 ,ATX,LCF
 P,VTM,STRP+7
 FT,V1M,NFRG
 P,UTM,4
 NFRG:13,VJM,GFRAG
 ,ATX,W.ФЛAГ:ПOCЛ.CTPAH.
 FT,VZM,ELOOP2
 ,XTA,L
 ,AEX,LSE
 ,U1A,LF
 P,XTA,6
 ,ASN,64+40
 ,ASN,64-8
 ,AOX,=22
 ,ASN,64-32
 P,ATX,6
 ,UJ,ELOOP2
 LF:P,XTA,6
 ,ASN,64+40
 ,ASN,64-40
 P,ATX,6
 ELOOP2:13,VJM,OUTPUT
 ,XTA,L
 ,AEX,LSE
 ,UZA,ELOOP1
 ,XTA,L
 ,ARX,=1
 ,ATX,L
 ,UJ,LOOP2
 ELOOP1:,BSS,
 ,XTA,L
 ,ARX,LPG
 ,ATX,L
 FT,XTA,LONG
 ,ATX,LPG
 ,XTA,W
 ,U1A,LOOP1
C   ПEЧATЬ OKOHЧEHA
 ENDCAT:,STX,
 ,STI,5
 ,STI,6
 ,STI,7
 ,ATI,13
 ,NTR,6
 13,UJ,
 GFRAG:,BSS,
C   П/П  ПOЛYЧEHИЯ ФPAГMEHTA
C   ДЛЯ ПEЧATИ CTPOKИ LCF KATAЛOГA.
C   OTBET - HA CYMMATOPE ДЛИHA
C   ФPAГMEHTA B CИMBOЛAX.
 ,NTR,7
 C,XTA,KR
 13,UZA,
 C,XTA,KR
 ,A-X,LCF
 ,UZA,OUR
 ,XTA,
 P,VTM,STRP
 13,UJ,
 OUR:,XTA,LCF
 ,ATI,IR
 C,WTC,YK
 IR,UTM,ELCAT-1
 M,VTM,=1H
 IR,XTA,NADRS
 ,ARX,
 ,U1A,F1
 M,VTM,=1H*
 F1:,XTA,ZF1
 ,*50,13.*
 IR,XTA,NAMES
 ,ATX,W
 ,XTA,=1H
 ,ATX,W+1
 ,XTA,ZF2
 ,*50,13.NAME
 IR,XTA,NADRS
 ,ASN,64-3
 ,AUX,=6H'3''17''''1''17'''
 ,AOX,=6H00/00/
 ,ATX,W
 IR,XTA,NADRS
 ,ASN,64-3-11
 ,AUX,=6H'17''17'''''''''
 ,AOX,=6H00
 ,ATX,W+1
 ,XTA,ZF3
 ,*50,13.DATE
 IR,XTA,NADRS
 ,AAX,=7777 7777
 ,AOX,A/I0
 ,ATX,W
 ,ATX,W+1
 ,XTA,ZF4
 ,*50,15.E-ADR.
 ,XTA,LCF
 ,AEX,=1
 ,UZA,FRG1
 IR,XTA,NADRS-1
 GL:,AAX,=7777 7777
 ,AOX,A/I0
 ,A-X,W
 ,ATX,W
 ,XTA,ZF4
 ,*50,15.LONG
 ,XTA,W+1.E-AДPEC
 12,CALL,GIVADREM
 ,ARX,=1
 ,ASN,64+12
 ,YTA,
 ,AUX,=6H'''7''7''7''7'''
 ,AOX,=6H 0000
 ,ATX,W
 ,XTA,ZF6
 ,*50,13
 13,UJ,
 FRG1:C,XTA,ENEW
 ,UJ,GL
 OUTPUT:,BSS,
 ,WTC,SI15
 15,VTM,
 FT,V1M,TTOUT
 14,VTM,STRP
 ,ITS,14
 14,UTM,20
 ,ITS,14
 ,ITS,
 ,UJ,PRINT8
 TTOUT:,BSS,
 ,*71,IWOUT
 13,UJ,
 NEWSTR:,BSS,
 14,VTM,=6H
 ,ITS,14
 ,ITS,14
 ,ITS,
 ,UJ,PRINT8
 CLEARS:,BSS,
 T,VTM,-21
 ,XTA,
 FT,V1M,LCL
 ,XTA,=6H
 LCL:T,ATX,STRP+21
 T,VLM,LCL
 JM,UJ,
 ERR0:,BSS,
 T,VTM,T0
 ,STI,
 ,STI,5
 ,STI,6
 ,STI,7
 ,XTA,
 ,UJ,ERREXMEM
C
C---------------------------------
C
 T0:,ISO,12HHET L-ПAMЯTИ
 ЗAГ:,ISO,H/  ИMЯ     ДATA/
 ,CONT,H/   E-AДPEC   ДЛИHA/
 ,CONT,H/ ЗOHA/
 LFIRST:,LOG,74.60
 ,LOG,15.13
 LONG:,LOG,101.65
 ,LOG,17.15
 ZF1:M,Z01,
 P,,
 ZF2:,Z07,W
 ,,
 ZF3:,Z10,W
 ,,
 ZF4:,Z10,W
 ,Z20,
 ZF6:,Z05,W
 ,,
 IWOUT:,Z04,STRP
 ,,
 SPEC:,ISO,6HL-CAT.
 BL:,BSS,LBL
 STRP:,BSS,23
 ,END,ELPRIN

*edit
*rо
*w:1
 MNEMOTXT:,NАМЕ,
 LST:,LIST,FС
 MNEMOCOD:,SKIP,
;
;         С Т А Р Ы Й   Р Е Ж И М   Б Э С М - 6
;
;             1. ЭКСТРАКОДЫ
;
 *50:050 U
 *51:051 U
 *52:052 U
 *53:053 U
 *54:054 U
 *55:055 U
 *56:056 U
 *57:057 U
 *60 060 U
 *61:061 U
 *62:062 U
 *63:063 U
 *64:064 U
 *65:065 U
 *66:066 U
 *67:067 U
 *70:070 U
 *71:071 U
 *72:072 U
 *73:073 U
 *74:074 U
 *75:075 U
 *76:076 U
 *77:077 U
;
;      2.  КОМАНДЫ ПЕРЕСЫЛОК:
;
АТХ 000 Х
SТХ 001 XS
XTS 003 XS
ХТА 010 Х
;
;      3.  АРИФМЕТИКА С ПЛАВ.ЗАПЯТОЙ
;
А+Х 345 Х  ; БЫЛО 004
А-Х 346 Х  ; БЫЛО 005
Х-А 347 Х  ; БЫЛО 006
АМХ 350 Х  ; 007
AVX 351 Х  ; 014
А/Х 016 Х
А*Х 352 Х  ; 017
Е+Х 024 Х
Е-Х 025 Х
Е+N 034 NС  ; БЫЛО 034 N
Е-N 035 NС  ;      035 N
;
;      4.  ЛОГИЧЕСКИЕ КОМАНДЫ:
;
ААХ 011 Х
АЕХ 012 Х
ARX 013 Х
АОХ 015 Х
АРХ 020 Х
AUX 021 Х
АСХ 022 Х
ANX 023 Х
ASX 026 Х
ASN 026 NС  ; БЫЛО 036 N
;
;         5. ИНДЕКСНЫЕ КОМАНДЫ:
;
ATI 040 IС  ; БЫЛО 040 I
STI 041 ISC ;      041 IS
IТА 200 IС  ;      042 I
ITS 042 ISC ;      043 IS
MTJ 044 IМС ;      044 IМ
J+М 045 IМС ;      045 IМ
UТС 220 U
WТС 073 Х   ;      230 Х
VТМ 074 UМ  ;      240 UМ
UТМ 075 UМ  ;      250 UМ
;
;         6. КОМАНДЫ ПЕРЕДАЧИ УПРАВЛЕНИЯ:
;
UZA 353 XJ  ; БЫЛО 260 XJ
U1А 354 XJ  ;      270 XJ
UIA 354 XJ  ;      270 XJ
JМР 030 XJ  ;      300 XJ
VJM:031 XJM ;      310 XJM
JMEQ 040 XJM ;     340 XJM
JMNE 041 XJM ;     350 XJM
VLM 033 XJM ;      370 XJM
;
;         7. РАБОТА С РЕГИСТРОМ РЕЖИМОВ:
;
XTR 333 XR   ; БЫЛО 027 XR
NTR 202 NRC  ;      037 NR
RТЕ 203 NRC  ;      030 NR
;
;
;           Н О В Ы Й    Р Е Ж И М :
;
;        1. НЕПОСРЕДСТВЕННЫЙ ОПЕРАНД:
;
А+U 201 U
А-U 202 U
U-А 203 U
;     ИЗЬЯТО:       AMU 204 U
UТА 110 U
AAU 111 U
AEU 112 U
;     ИЗЬЯТО:       ARU 113 U
;     ИЗЬЯТО:       AVU 205 U
AOU 115 U
А/U 206 U
А*U 207 U
;     ИЗЬЯТО:       APU 120 U
;     ИЗЬЯТО:       AUU 121 U
ACU 122 U
ANU 123 U
UTS 103 US      ; БЫЛО 211 US
UТУ 210 U
;
;         2. РАБОТА С   Р М Р :
;
УТА 201 NС   ; БЫЛО 031 N
ХТУ 224 Х
УАХ 225 Х
УЕХ 226 Х
УОХ 227 Х
УТХ 230 Х
ААУ 204 UС
АЕУ 205 UС
АОУ 206 UС
;
;         3. ИНДЕКСНЫЕ КОМАНДЫ:
;
MCJ 220 CIM
MSN 221 CNM
J-М 047 CIM
;      ИЗЬЯТО:      М-J 223 CIM
VТМН 240 UМ
VTMQ 241 UМ
VТМВ 242 UМ
VTMF 243 UМ
UTCS 244 U
МРАС 224 CI
MUNP 225 CI
А+I 242 CI
А-I 243 CI
I-А 244 CI
;      ИЗЬЯТО:      AMI 245 CI
;      ИЗЬЯТО:      AVI 246 CI
А/I 247 CI
А*I 250 CI
AAI 251 CI
AEI 252 CI
AOI 253 CI
;
;        4. ПЕРЕДАЧИ УПРАВЛЕНИЯ:
;
JMGE 042 XJM
JMLT 043 XJM
JMLE 044 XJM
JMGT 045 XJM
VRM 251 XJM
UZAS 252 XJS
U1AS 253 XJS
JOVR 254 XJ
JCRY 255 XJ
JAGT 256 XJ
JAGE 257 XJ
JALT 260 XJ
JALE 261 XJ
JAEQ 262 XJ
JANE 263 XJ
JMPS:265 XJM
RETS 207 UС
;
;         5. ЦЕЛАЯ 64-РАЗРЯДНАЯ АРИФМЕТИКА
;
ASNA 226 CN
А+L 231 Х
А-L 232 Х
L-А 233 Х
;       ИЗЬЯТО:     AML 234 Х
;       ИЗЬЯТО:     AVL 235 Х
А/L 236 Х
А*L 237 Х
;
;          6. ОПЕРАЦИИ С ПОЛУСЛОВАМИ:
;
АТН 266 Н
SТН 267 HS
НТА 270 Н
HTS 271 HS
А+Н 272 Н
А-Н 273 Н
Н-А 274 Н
;        ИЗЬЯТО:   АМН 275 Н
;        ИЗЬЯТО:   AVH 276 Н
А/Н 277 Н
А*Н 300 Н
ААН 301 Н
АЕН 302 Н
АОН 303 Н
;
;          7. ОПЕРАЦИИ НАД ЧЕТВЕРТИНКАМИ:
;
ATQ 304 Q
STQ 305 QS
QТА 306 Q
QTS 307 QS
AEQ 310 Q
AAQ 311 Q
AOQ 312 Q
;
;         8. ОПЕРАЦИИ НАД БАЙТАМИ:
;
АТВ 313 В
SТВ 314 BS
ВТА 315 В
BTS 316 BS
АЕВ 317 В
ААВ 320 В
АОВ 321 В
;
;         9. ОПЕРАЦИИ НАД БИТАМИ:
;
ATF 322 F
STF 323 FS
FТА 324 F
FTS 325 FS
AEF 326 F
AAF 327 F
AOF 330 F
SETF 331 F
CLRF 332 F
EXTF 241 CN
NТА 260 CN
NTS 261 CNS
AEN 262 CN
AAN 263 CN
AON 264 CN
;
;         10. АРИФМЕТИКА С ДВОЙНОЙ ТОЧНОСТЬЮ:
;
ATD 334 D
DТА 335 D
А+D 336 D
А-D 337 D
D-А 340 D
;       ИЗЬЯТО:      AMD 341 D
;       ИЗЬЯТО:      AVD 342 D
А/D 343 D
А*D 344 D
;
;         11. ПРИВИЛЕГИРОВАННЫЕ КОМАНДЫ:
;
RMOD 360 Р
WMOD 361 Р
IJP 375 РС
SETR 362 PUX
CLRR 363 PUX
SMON 370 PUX
CMON 371 PUX
SEMR 364 РХ
SEMW 367 РХ
ТТХ 365 ХР
ТТА 366 ХР
RЕТ 376 СР
HLT 377 СР
REF 377 Р      ; ДЛЯ ОРГАНИЗАЦИИ ССЫЛОК
;
;         12. ЭКСТРАКОДЫ :
;
FUN 200 U      ; ДЛЯ МКБ
CFUN 000 CU    ; ДЛЯ ЭЛЬБРУС-Б
;
;       И ЕЩЕ ОСТАЛИСЬ НЕОПИСАННЫМИ КОМАНДЫ
;       ЭЛЬБРУС-Б,   ПРИДУМАННЫЕ М.Г.ЧАЙКОВСКИМ
;
 MNEMOCOD:,ENDIF,
 ,LIST,*
 ,END,MNEMOTXT
*ее

*edit
*r:1
*w:2
*u:mnemo
*ее

*edit
*r:2
*lp
*ее

*assem
*read:2

*to perso:50
*end file
``````
еконец
