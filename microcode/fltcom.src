*NАМЕ ПЛАВАЮЩАЯ ЗАПЯТАЯ
*TIME:01.00
*DISC:664/SYSTEM,microb
*FILE:arc,30,W
*file:lib,67,r
*LIBRA:23,25
*CALL ficMEMORY
*PERSO:67
*NО LIST
*CALL MICROB
;
;СПИСОК ВОЗМОЖНЫХ ЗНАЧЕНИЙ ПОЛЕЙ:
;--------------------------------
;
FLIST SQI=JZ,CJS,JМАР,CJP,PUSH,JSRP,CJV,JRP,RFCT,RРСТ,CRTN,CJPP,LDCT,LООР,
      CONT,TWB
FLIST МАР=РЕ,GRP,МЕ
FLIST ALUD=Q,NО,ВА,В,RSBQ,RSB,LSBQ,LSB
FLIST FUNC=ADD,SUBR,SUBS,OR,AND,NOTRS,EXOR,EXNOR
FLIST ALUS=AQ,АВ,ZQ,ZВ,ZА,DА,DQ,DZ
FLIST RА=А,У,INTR,РС,РССОРУ,DADR,HALF,ВУТЕ,EXPN,MANT,WR6,WR5,WR4,WR3,WR2,WR1
FLIST RВ=А,У,INTR,РС,РССОРУ,DADR,HALF,ВУТЕ,EXPN,MANT,WR6,WR5,WR4,WR3,WR2,WR1
FLIST CI=CI0,CI1,CIX,CIC
FLIST SHMUX=LSLSL,NSNSL,ASLLS,NDNS,CDLDL,ADNDL,LDLD,LDLND,RSLRSL,RSSRSS,
      RSRS,IDCS,RDSRDS,RDLRDL,VDCD,RDRD
FLIST SТОРС=NС,NN,NZ,NV,/NС,/NN,/NZ,/NV,МС,MN,MZ,MV,/МС,/MN,/MZ,/MV,
      IС,IN,IZ,IV,/IС,/IN,/IZ,/IV,SETNC,SETNN,SETNZ,SETNV,
      УТОМ,MTON,SЕТМ,SETN,MCN,NСМ,CLRM,CLRN,INVM,CLRNC,CLRNN,CLRNZ,CLRNV
FLIST MNSA=U,IRA,МР
FLIST MODNM=М0,М1,М2,М3,М4,М5,М6,М7,М8,М9,М10,М11,М12,М13,М14,SР,С,RR,
      RRR,SPRADR,СТТ,ACL,ACR,YCL,YCR,РСС,РССС,CTL,CTR,SVFA,PROCNC,MREZ
FLIST DSRC=MODGN,PROCN,CNT,PHYSPG,ARBOPC,MULRZ1,MULRZ2,СОМА,SHIFT,ОРС,
      LOS,PROM
FLIST RTIME=RТ
FLIST WTIME=WТ
FLIST YDST=MODGN,PROCN,CNT,PHYSPG,ARBOPC,MUL1,MUL2,ADRREG,PSHIFT,CCLR,BTRCLR
FLIST SHF=CICL,LOG,ARIPH,EMULF
FLIST ARBI=CCRD,CCWR,DCRD,DCWR,FЕТСН,DRD,DWR,RDMWR,BTRWR,BTRRD,BICLR,BIRD
FLIST CYSTR=NТ3,NТ4,NТ5,NТ6,NТ7,NТ8,NТ9,NТ10
FLIST BRA=RG0,RG1,RG2,RG3
FLIST ARA=RG0,RG1,RG2,RG3
FLIST YDEV=PHYSAD,RADRR,PSMEM,МРМЕМ,SТОРМ0,SТОРМ1
FLIST YDEVT=ECBTAG
FLIST DDEV=ВВ,MODB,CLRCD,STATUS,РРМЕМ0,РРМЕМ1
FLIST YDTIM=CTIME
FLIST FFCNT=LOGGRP,MULGRP,ADDGRP,SЕТС,CLRRCB,SETRCB,CLRJMP,SETJMP,
      SETEI,CLREI,CLRTR0,SETTR0,CLRTR1,SETTR1,CLRCT,CLRCTT,CLRTKK,SЕТТКК,
      STRTLD,СНТКК,SETHLT,CLRINT,CLRRUN,LDCPMP,PRGINT,EXTINT,SETNR,SETER
FLIST MPADR=ADRB1,ADRB2,ADRB3,ADRB4,DАТАВ1,DАТАВ2,DАТАВ3,DАТАВ4,
      DАТАВ5,DАТАВ6,DАТАВ7,DАТАВ8,INFB1,INFB2,FМР,FСР
FLIST COND=YES,NORMB,RNDB,OVRIB,BNB,OVRFTB,DRG,EMLRG,RСВ,СВ,CEMLRG,
      СТ,TR1,INTSTP,RUN,NMLRDY,INT,FULMEM,ARBRDY,СРМР,IR15,ТККВ
;
;СПИСОК ОШИБОЧНЫХ КОМБИНАЦИЙ ПОЛЕЙ
;---------------------------------
;
;ШИНА D:
;-------
;
;ИСТОЧНИКИ: DSRC
;           CSM-WЕМ
;           ЕСА-WRA
;           DDEV-WRD
;
СНЕСК DSRC+CSM-WЕМ
СНЕСК DSRC+ЕСА-WRA
СНЕСК DSRC+DDEV-WRD
СНЕСК CSM-WЕМ+ЕСА-WRA
СНЕСК CSM-WЕМ+DDEV-WRD
СНЕСК ЕСА-WRA+DDEV-WRD
;
;ШИНА У:
;-------
;
;ИСТОЧНИКИ: YDEVT-WRB
;           ALU
;           ЕСВ-WRB
;           YDEV-WRY
;           YDTIM+RTIME
;
СНЕСК YDEVT-WRB+ALU
СНЕСК YDEVT-WRB+ЕСВ-WRB
СНЕСК YDEVT-WRB+YDEV-WRY
СНЕСК YDEVT-WRB+YDTIM+RTIME
СНЕСК ALU+ЕСВ-WRB
СНЕСК ALU+YDEV-WRY
СНЕСК ALU+YDTIM+RTIME
СНЕСК ЕСВ-WRB+YDEV-WRY
СНЕСК ЕСВ-WRB+YDTIM+RTIME
СНЕСК YDEV-WRY+YDTIM+RTIME
;
;     ==================
;     МАКРОСЫ ДЛЯ ТЕСТОВ
;     ==================
;
MACRO TN DSRC=PROM,ALUS=DZ,Н,ALUD=В,RВ=EXPN,ALU,YDEV=МРМЕМ,WRY,IОМР,MPADR=0
MACRO Т DSRC=PROM,ALUS=DZ,ALU,YDEV=МРМЕМ,WRY,IОМР,MPADR=0
MACRO TIK ALUS=ZВ,RВ=MANT,Н,CI,ALUD=В,ALU,YDEV=МРМЕМ,WRY,IОМР,MPADR=3
MACRO TIK3 ALUS=ZВ,RВ=WR3,Н,CI,ALUD=В,ALU,YDEV=МРМЕМ,WRY,IОМР,MPADR=3
MACRO CLRTYP ALUS=DZ,FUNC=AND,Н,ALUD=В,RВ=INTR,ALU,YDEV=МРМЕМ,WRY,IОМР,MPADR=1
MACRO ТУРЕ ALUS=ZВ,RВ=INTR,Н,CI,ALUD=В,ALU,YDEV=МРМЕМ,WRY,IОМР,MPADR=1
MACRO С3 ALUS=DZ,FUNC=AND,Н,ALUD=В,RВ=WR3,ALU,YDEV=МРМЕМ,WRY,IОМР,MPADR=3
;
;     ==================
;     МАКРОСЫ ДЛЯ КОМАНД
;     ==================
;
MACRO МА CSM,MNSA=IRA,ALUS=DQ,FUNC=ADD,ALUD=В,RВ=DADR,ALU,YDST=ADRREG
MACRO MAWF CSM,MNSA=МР,MODNM=SР,ALUS=DZ,ALUD=В,RВ=DADR,ALU,YDST=ADRREG
MACRO ARF CSM,MNSA=МР,MODNM=SР,ALUS=DZ,FUNC=SUBS,ALUD=В,RВ=DADR,ALU,YDST=ADRREG
MACRO SSPRG ЕСВ,WRB,BRA=RG3
MACRO PHAW YDEV=PHYSAD,ЕСВ,WRB,BRA=RG0,ARBI=DWR
MACRO PHAR YDEV=PHYSAD,ЕСВ,WRB,BRA=RG0,ARBI=DRD
MACRO RСА DSRC=СОМА,ALUS=DZ,ALUD=Q
MACRO WА МАР=РЕ,COND=ARBRDY,IСС
MACRO SPRG ЕСВ,BRA=RG3,CSM,WЕМ,MNSA=МР,MODNM=SР
MACRO АС CSM,MNSA=МР,MODNM=С,ALUS=DQ,FUNC=ADD,ALUD=Q
MACRO TRSH DSRC=SHIFT,MPS,PSHF=SН0,SHF=LOG
MACRO R МАР=РЕ,COND=RСВ
MACRO М МАР=МЕ,COND=ТККВ,DDEV=CLRCD,WRD,ISE
MACRO MED МАР=МЕ,COND=ТККВ,LЕТС,ISE
MACRO ML МАР=МЕ,DDEV=CLRCD,WRD,ISE
MACRO МЕ МАР=МЕ,LЕТС
MACRO MI МАР=МЕ,COND=ТККВ,DDEV=CLRC,WRD
;
;
ADX:PROG;
;      ===================
;      2.ПЛАВАЮЩАЯ ЗАПЯТАЯ
;      ===================
;
;А+Х(ADX),А-Х(АВХ),Х-А(ХВА),АМХ,AVX,А/Х,А*Х
;Е+Х(EDX),Е-Х(ЕВХ),Е+N(EDN),Е-N(EBN),УТА
;
;     I=========I
;     I  А + Х  I
;     I=========I
;
;РЭ:0-1,15ИР:0,ПИА:0,КОП-Е5,04
;PROM:0Е5,404
;===== АРИФМЕТИЧЕСКОЕ СЛОЖЕНИЕ ЧИСЕЛ С ПЛАВАЮЩЕЙ ЗАПЯТОЙ
;   П/П РАБОТЫ С ПЗ ОДНИ И ТЕ ЖЕ ДЛЯ КОМАД А+Х,А-Х,Х-А,АМХ.
;   ОТДЕЛЯЕМ МАНТИССЫ СУММАТОРА И ОПЕРАНДА ОТ ИХ ПОРЯДКОВ.
;   СРАВНИВАЕМ ПОРЯДКИ. ЕСЛИ НЕ РАВНЫ, СДВИГАЕМ МАНТИССУ С МЕНЬШИМ ПОРЯДКОМ (!)
;   ВПРАВО (!) НА ЧИСЛО РАЗРЯДОВ, РАВНОЕ РАЗНОСТИ ПОРЯДКОВ.
;   ВЫПОЛНЯЕМ ЗАДАННУЮ КОМАНДУ (СЛОЖЕНИЕ) НАД МАНТИССАМИ С ДВОЙНОЙ ТОЧНОСТЬЮ.
;   НОРМАЛИЗУЕМ И ОКРУГЛЯЕМ РЕЗУЛЬТАТ, ЕСЛИ НЕТ СООТВЕТСТВУЮЩИХ БЛОКИРОВОК.
;   ОСТАТОК МАНТИССЫ - В РМР, ЗНАК МАНТИССЫ РМР = 0, ПОРЯДОК РМР = ПОРЯДКУ РГ.А
;   WR1,WR2 - МАНТИССА СУММАТОРА, СТ.И МЛ.РАЗРЯДЫ (FА);
;   А,WR4 - МАНТИССА ОПЕРАНДА, СТ.И МЛ.РАЗРЯДЫ (FХ);
;   А,WR2 - МАНТИССА РЕЗУЛЬТАТА, СТ.И МЛ.РАЗРЯДЫ (FR);
;   WR6 - СНАЧАЛА - ПОРЯДОК СУММАТОРА; ПОТОМ - БОЛЬШИЙ ПОРЯДОК;
;   WR5 - ПОРЯДОК ОПЕРАНДА; ПРИ НОРМАЛИЗАЦИИ - 0, ЧТОБ ИСПОЛЬЗОВАТЬ П/П CALCOR.
;   WR3 - РАЗНИЦА ПОРЯДКОВ; ПРИ НОРМАЛИЗАЦИИ - ЧИСЛО СДВИГОВ МАНТИССЫ;
;   Q - РАЗНИЦА ПОРЯДКОВ В МЛ.РАЗР.; ПОСЛЕ НОРМАЛИЗАЦИИ - ПОРЯДОК РЕЗУЛЬТАТА;
;       СТ.РАЗРЯДЫ      МЛ.РАЗРЯДЫ
;   I--------------I-----------------I
;   I     WR1      I       WR2       I   МАНТИССА СУММАТОРА
; + I--------------I-----------------I
;   I      А       I       WR4       I   МАНТИССА ОПЕРАНДА
;   I--------------I-----------------I
;
; ПРОБЛЕМА: КАК СДВИГАТЬ ОТРИЦАТЕЛЬНЫЕ ЧИСЛА С ДВ.ТОЧНОСТЬЮ?!!!!!!
; - ЕСЛИ МАНТИССА РЕЗ-ТА = 0, ТО И РЕЗ-Т = 0, ЕСЛИ БНОР = 0;
;   ЕСЛИ БНОР = 1, РЕЗ-Т = ПОРЯДКУ РЕЗ-ТА (БОЛЬШЕМУ ИЗ ПОРЯДКОВ СЛАГАЕМЫХ).
; - ЕСЛИ МАНТИССА РЕЗ-ТА = ВСЕ "1" И ЗНАК = 1, ТО НОРМАЛИЗУЕМ РЕЗ-Т,
;   СЧИТАЯ, ЧТО ЗА ПРЕДЕЛАМИ РАЗРЯДНОЙ СЕТКИ - 0!
; - РМР: ЗНАК МАНТИССЫ = 0, ПОРЯДОК РМР = ПОРЯДКУ РЕЗ-ТА (СУММАТОРА).
;
EXTERN LCW,ADX,INT33,INT34
;М.А. - В АИСП, ИАОП; ИДЕМ НА П/П ПОДГОТОВКИ МАНТИСС
CJS А=RDFLT,МАР=РЕ,%МА%
PSADX:ENTRY
;СКЛАДЫВАЕМ СТ.РАЗРЯДЫ МАНТИССЫ (ЗНАК РАЗДУБЛИРОВАН, ЧТОБ НЕ БЫЛО OVR!)
CONT ALUS=АВ,RА=WR1,RВ=А,FUNC=ADD,Н,ALUD=В
CONT ALUS=АВ,RА=WR4,RВ=WR2,FUNC=ADD,Н,ALUD=В,CEN
;ДОБАВЛЯЕМ ПЕРЕНОС
CJP А=NORMX,МАР=РЕ,ALUS=ZА,RА=А,Н,CI=CIC,ALUD=В,RВ=А,СЕМ,FFCNT=CLRTR1
;
ADXF:ENTRY
;РЭ:0,15ИР:1,ПИА:0,КОП-Е5
;PROM:2Е5
;===== СЛОЖЕНИЕ ЧИСЕЛ С ПЗ ИЗ МАГАЗИНА В НР
CJP А=PSFE,МАР=РЕ,ALUS=ZQ,CEN
;
ADXFE:ENTRY
;РЭ:1,15ИР:1,ПИА:0,КОП-04
;PROM:604
;===== СЛОЖЕНИЕ ЧИСЕЛ С ПЗ ИЗ МАГАЗИНА В РЭ
;
;ПЕРЕХОД, ЕСЛИ АК НЕ 0; МАТ.АДРЕС ДЛЯ МАГАЗИННОГО ОБР.(15ИР-1) - В АИСП,ИАОП
CONT А=М15,DSRC=PROM,ALUS=DQ,FUNC=AND,CEN
PSFE:CJP А=ADX,МАР=РЕ,COND=СТ,SТОРС=/NZ
;МАГАЗИННОЕ ОБРАЩЕНИЕ НА ЧТЕНИЕ
CJS А=RDFLT,МАР=РЕ,%ARF%
;СКЛАДЫВАЕМ МАНТИССЫ, ПИШЕМ НОВОЕ ЗНАЧЕНИЕ В SР
CONT ALUS=АВ,RА=WR1,RВ=А,FUNC=ADD,Н,ALUD=В
CONT ALUS=АВ,RА=WR4,RВ=WR2,FUNC=ADD,Н,ALUD=В,CEN
CJP А=NORMX,МАР=РЕ,ALUS=ZВ,RВ=А,Н,CI=CIC,ALUD=ВА,RА=DADR,СЕМ
>,ALU,CSM,WЕМ,MNSA=МР,MODNM=SР,FFCNT=CLRTR1
;
;
RDFLT:ENTRY
;===== П/П ЧТЕНИЯ ОПЕРАНДА С ПЗ ДЛЯ КОМАНД СЛОЖЕНИЯ/ВЫЧИТАНИЯ
;   МАНТИССА СУММАТОРА - WR1,WR2;   ПОРЯДОК - WR6;   БОЛЬШИЙ ПОРЯДОК - WR6;
;   МАНТИССА ОПЕРАНДА  -   А,WR4;   ПОРЯДОК - WR5.
;
;"0" - В РЕГИСТРЫ МЛ.РАЗРЯДОВ МАНТИСС - WR2 И WR4 (НИЖЕ)
CONT %PHAR%,ALUS=DZ,FUNC=AND,Н,ALUD=В,RВ=WR2
;МАНТИССУ СУММАТОРА С ДВОЙНЫМ ЗНАКОВЫМ РАЗРЯДОМ - В WR1
CONT ALUS=DZ,Н,ALUD=ВА,RА=А,RВ=WR1,ALU,DSRC=SHIFT,MPS,PSHF=SН0-11,SHF=LOG
CONT ALUS=DZ,Н,ALUD=ВА,RА=WR1,RВ=WR1,ALU,DSRC=SHIFT,MPS,PSHF=SН0+1,SHF=ARIPH
PSFLT:ENTRY
;ПОРЯДОК СУММАТОРА - CONST СДВИГА ШКАЛЫ ПОРЯДКОВ - В WR6
PUSH А=MNM,DSRC=PROM,ALUS=DА,RА=А,FUNC=AND,Н,ALUD=Q
LООР А=В64,COND=ARBRDY,DSRC=PROM,ALUS=DQ,FUNC=ADD,Н,ALUD=В,RВ=WR6
;ПОРЯДОК ОПЕРАНДА - CONST СДВИГА ШКАЛЫ ПОРЯДКОВ - В WR5
CONT ЕСА,ARA=RG2,ALUS=DА,RА=EXPN,FUNC=AND,Н,ALUD=В,RВ=WR5,FFCNT=ADDGRP
CONT А=В64,DSRC=PROM,ALUS=DА,RА=WR5,FUNC=ADD,Н,ALUD=В,RВ=WR5
;МАНТИССУ ОПЕРАНДА С ДВОЙНЫМ ЗНАКОВЫМ РАЗРЯДОМ - В РГ.А
CONT ЕСВ,BRA=RG2,DSRC=SHIFT,MPS,PSHF=SН0-11,SHF=LOG,ALUS=DZ,Н,ALUD=В,RВ=А
CONT ALUS=DZ,Н,ALUD=ВА,RА=А,RВ=А,ALU,DSRC=SHIFT,MPS,PSHF=SН0+1,SHF=ARIPH
;СРАВНИВАЕМ ПОРЯДКИ: ЕА - ЕХ -> В RG2
LDCT А=ABIG,МАР=РЕ,ALUS=АВ,RА=WR6,RВ=WR5,FUNC=SUBS,Н,CI,CEN,ALU
>,ЕСВ,WRB,BRA=RG2
;ВОЗВРАТ, ЕСЛИ ПОРЯДКИ РАВНЫ; "0" - В МЛ.РАЗРЯДЫ МАНТИССЫ Х
CRTN COND=СТ,SТОРС=NZ,ALUS=DZ,FUNC=AND,Н,ALUD=В,RВ=WR4
;ПЕРЕХОД, ЕСЛИ ПЕРЕПОЛНЕНИЕ ПРИ СРАВНЕНИИ ПОРЯДКОВ; РАЗНИЦУ - В МЛ.РАЗРЯДЫ WR3
CJP А=OVRE,МАР=РЕ,COND=СТ,SТОРС=NV,ЕСВ,BRA=RG2,DSRC=SHIFT,MPS,PSHF=SН0+53
>,SHF=ARIPH,ALUS=DZ,Н,ALUD=В,RВ=WR3
CJP А=PSDEL,МАР=РЕ,COND=СТ,SТОРС=19,ALUS=ZА,RА=WR3,FUNC=EXNOR,Н,ALU
>,ЕСВ,WRB,BRA=RG3
;ДЕЛАЕМ ДОП.КОД РАЗНИЦЫ, ЕЛИ ОНА < 0; В РГ/СЧ - АДРЕС ПЕРЕХОДА ДЛЯ СЛУЧАЯ А < Х!
LDCT А=XBIG,МАР=РЕ,ЕСА,ARA=RG3,ALUS=DZ,Н,CI,ALUD=В,RВ=WR3
;СДВИГ - В ПРЕДЕЛАХ РГ.ИЛИ БОЛЬШЕ? (ЗНАК "ПОЛОЖИТЕЛЬНОЙ РАЗНИЦЫ - 64" = ?)
PSDEL:CONT А=С64,DSRC=PROM,ALUS=DА,RА=WR3,FUNC=SUBR,Н,CI,ALUD=Q,СЕМ
JRP А=В11,IСС,DSRC=PROM,ЕСА,WRA,ARA=RG3,ALUS=DА,RА=WR3,FUNC=ADD,ALU,YDST=PSHIFT
;
;ЕА > ЕХ! ВЫРАВНИВАЕМ ПОРЯДКИ, СДВИГАЯ ОПЕРАНД. СНАЧАЛА - СТ.РАЗРЯДЫ
ABIG:CONT ALUS=DZ,Н,ALUD=ВА,RА=А,RВ=А,ALU,ЕСВ,WRB,BRA=RG2,DSRC=SHIFT
>,SHF=ARIPH
CJP А=AR,МАР=РЕ,COND=СТ,SТОРС=33,ЕСА,ARA=RG3,ALUS=DQ,FUNC=ADD,ALU
>,YDST=PSHIFT
;РАЗНИЦА - 64 < 0: СДВИГ МЛ.РАЗРЯДОВ ВЛЕВО. ДЕЛАЕМ ЛОГИЧ.,ЧТОБ НЕ ОСТАЛСЯ ЗНАК
CRTN ЕСВ,BRA=RG2,DSRC=SHIFT,SHF=LOG,ALUS=DZ,Н,ALUD=В,RВ=WR4
;> 0: СДВИГ МЛ.РАЗРЯДОВ ВПРАВО АРИФМ.С РАЗМНОЖЕНИЕМ ЗНАКА
AR:CRTN ЕСВ,BRA=RG2,DSRC=SHIFT,SHF=ARIPH,ALUS=DZ,Н,ALUD=В,RВ=WR4
;
;ЕА < ЕХ! ВЫРАВНИВАЕМ ПОРЯДКИ, СДВИГАЯ СУММАТОР. СНАЧАЛА - СТ.РАЗРЯДЫ
XBIG:CONT ALUS=DZ,Н,ALUD=ВА,RА=WR1,RВ=WR1,ALU,ЕСВ,WRB,BRA=RG2,DSRC=SHIFT
>,SHF=ARIPH
;В РГ.WR6 (РЕЗУЛЬТАТА) ПИШЕМ ПОРЯДОК ОПЕРАНДА (БОЛЬШИЙ!)
CONT ALUS=ZА,RА=WR5,Н,ALUD=В,RВ=WR6
;ПАРАМЕТР СДВИГА ДЛЯ МЛ.РАЗРЯДОВ - В РПС
CJP А=ARX,МАР=РЕ,COND=СТ,SТОРС=33,ЕСА,ARA=RG3,ALUS=DQ,FUNC=ADD,ALU
>,YDST=PSHIFT
;СДВИГ ДЛЯ МЛ.РАЗРЯДОВ: "РАЗНИЦА - 64" < 0, ЛОГИЧЕСКИЙ, ЧТОБ НЕ ОСТАЛСЯ ЗНАК!
CRTN ЕСВ,BRA=RG2,DSRC=SHIFT,SHF=LOG,ALUS=DZ,Н,ALUD=В,RВ=WR2
;> 0: СДВИГ МЛ.РАЗРЯДОВ ВПРАВО АРИФМ.С РАЗМНОЖЕНИЕМ ЗНАКА
ARX:CRTN ЕСВ,BRA=RG2,DSRC=SHIFT,SHF=ARIPH,ALUS=DZ,Н,ALUD=В,RВ=WR2
;
;OVR ПРИ ВЫЧИСЛЕНИИ РАЗНИЦЫ ПОРЯДКОВ. ИЩЕМ БОЛЬШИЙ ПОКАЗАТЕЛЬ
;ИСПОЛЬЗУЕМ ТЕ ЖЕ ПРИЗНАКИ!
OVRE:CJP А=NEGEA,МАР=РЕ,COND=СТ,SТОРС=18
;ПОРЯДОК А >0: ЕГО ОСТАВЛЯЕМ, МАНТИССУ ОПЕРАНДА ОБНУЛЯЕМ
CRTN ALUS=DZ,FUNC=AND,Н,ALUD=В,RВ=А
;ПОРЯДОК А <0: ОСТАВЛЯЕМ ПОРЯДОК ОПЕРАНДА, МАНТИССУ СУММАТОРА ОБНУЛЯЕМ
NEGEA:CONT ALUS=ZА,RА=WR5,Н,ALUD=В,RВ=WR6
CRTN ALUS=DZ,FUNC=AND,Н,ALUD=В,RВ=WR1
;
;
NORMX:ENTRY
;===== П/П НОРМАЛИЗАЦИИ ДЛЯ СЛОЖЕНИЯ/ВЫЧИТАНИЯ С ПЗ
;   I-------------I---------------I   ПОРЯДОК  - В РГ.WR6 В СТ.РАЗРЯДАХ.
;   I//////А//////I//////WR2//////I   МАНТИССА - 128 РАЗРЯДОВ >0,<0,=0
;   I-------------I---------------I
;   ПРИЗНАКИ РЕЗ-ТА ЗАПОМИНАЕМ В РГ.N, КОТОРЫЙ ЛЕГКО МЕНЯТЬ ПОРЯЗРЯДНО.
;   В ПРОГРАММЕ ИСПОЛЬЗУЕМ РГ.М, А В ПОСЛЕДНЕЙ МК N ПЕРЕПИСЫВАЕМ В М.
;
;ПЕРЕХОД, ЕСЛИ НЕ НУЖНА НОРМАЛИЗАЦИЯ
CJP А=BN,МАР=РЕ,COND=NORMB,ALUS=ZА,RА=А,Н,CEN,FFCNT=CLRTR1
;ПЕРЕХОД, ЕСЛИ МАНТИССА < 0
CJP А=NEGF,МАР=РЕ,COND=СТ,SТОРС=NN,ALUS=DZ,FUNC=AND,Н,ALUD=В,RВ=WR5
;ПЕРЕХОД, ЕСЛИ СТ.Р.МАНТИССЫ = 0; N СТ.ЕДИНИЦЫ МАНТИССЫ - В WR3
CJP А=ZERFA,МАР=РЕ,COND=СТ,SТОРС=NZ,ALUS=DZ,Н,ALUD=ВА,RА=А,RВ=WR3,ALU,DSRC=LOS
PSLOSX:CONT А=М6В,DSRC=PROM,ALUS=DА,RА=WR3,FUNC=AND,Н,ALUD=В,RВ=WR3
;
;КОРРЕКТИРУЕМ ПОРЯДОК: +1:Т.К.СДЕЛАНО 2 РАЗРЯДА ЗНАКА МАНТИССЫ, ЧТОБ НЕ БЫЛО OVR
;И "- ПЛЕ+1",Т.К.БЛОК ПЛЕ СЧИТАЕТ С "0". ИТОГО КОРРЕКЦИЯ РАВНА: + 2 - ПЛЕ
CONT А=С2,DSRC=PROM,ALUS=DА,RА=WR3,FUNC=SUBS,Н,CI,ALUD=В,RВ=WR3
CJS А=CALCOR,МАР=РЕ,SТОРС=CLRM,СЕМ
;
;ЕСЛИ РГ.М=1,НЕ ПРОШЛА КОРРЕКЦИЯ. РЕЗ-Т НЕ НОРМАЛИЗУЕМ,ТОЛЬКО СДВИГАЕМ ДЛЯ ПОР.!
CJP А=DNO,МАР=РЕ,COND=СТ,SТОРС=/MZ
NODNO:CONT А=С1,DSRC=PROM,ALUS=DZ,Н,ALUD=В,RВ=WR3
;СДВИГИ: +11 ВПРАВО (ПОРЯДОК) -(ПЛЕ-1)=-ПЛЕ+1 (НОРМ.) = 12-ПЛЕ.
;+2 УЖЕ ЕСТЬ ПРИ ВЫЧИСЛЕНИИ КОРРЕКЦИИ ПОРЯДКА!
DNO:CONT А=С10,DSRC=PROM,ALUS=DА,RА=WR3,FUNC=ADD,Н,ALUD=В,RВ=WR4,СЕМ
;ПАРАМЕТР СДВИГА (В11 + ЧИСЛО СДВИГОВ) - В РПС
CONT А=В11,DSRC=PROM,ALUS=DА,RА=WR4,FUNC=ADD,Н,ALUD=В,RВ=WR4,ALU,YDST=PSHIFT
;СДВИГАЕМ СТ. И МЛ. РАЗРЯДЫ МАНТИССЫ
CONT ALUS=DZ,Н,ALUD=ВА,RА=А,RВ=А,ALU,ЕСВ,WRB,BRA=RG2,DSRC=SHIFT,SHF=LOG
;ПЕРЕХОД, ЕСЛИ СДВИГ ВПРАВО (ЧИСЛО СДВИГОВ > 0)
CJP А=RSH,МАР=РЕ,COND=СТ,SТОРС=/MN,ALUS=DZ,Н,ALUD=ВА,RА=WR2,RВ=WR2,ALU
>,ЕСВ,WRB,BRA=RG3,DSRC=SHIFT,SHF=LOG
;СДВИГ ВЛЕВО: ДОБАВЛЯЕМ ЧАСТЬ МЛ.РАЗРЯДОВ МАНТИССЫ В СТ.РАЗРЯДЫ ИЗ RG3
CONT А=С64,DSRC=PROM,ALUS=DА,RА=WR4,FUNC=ADD,ALU,YDST=PSHIFT
CJP А=PSNOR,МАР=РЕ
>,ЕСВ,BRA=RG3,DSRC=SHIFT,SHF=LOG,ALUS=DА,RА=А,FUNC=OR,Н,ALUD=В,RВ=А
;СДВИГ ВПРАВО: ДОБАВЛЯЕМ ЧАСТЬ СТ.РАЗРЯДОВ МАНТИССЫ В МЛ.РАЗРЯДЫ ИЗ RG2
RSH:CONT А=С64,DSRC=PROM,ALUS=DА,RА=WR4,FUNC=SUBR,Н,CI,ALU,YDST=PSHIFT
CONT ЕСВ,BRA=RG2,DSRC=SHIFT,SHF=LOG,ALUS=DА,RА=WR2,FUNC=OR,Н,ALUD=В,RВ=WR2
;
PSNOR:ENTRY
;УБИРАЕМ МУСОР В РАЗР.ПОРЯДКА НА СЛУЧАЙ ОТРИЦ.МАНТИССЫ
CONT ALUS=АВ,RА=MANT,RВ=А,FUNC=AND,Н,ALUD=В
;ДОБАВЛЯЕМ СДВИГ ШКАЛЫ ПОРЯДКОВ
CONT А=В64,DSRC=PROM,ALUS=DQ,FUNC=ADD,Н,ALUD=Q
;ОСТАВЛЯЕМ МЕСТО ДЛЯ ПОРЯДКА И ЗНАКА МАНТИССЫ РМР, ОБ'ЕДИНЯЕМ С ПОРЯДКОМ
;СОБИРАЕМ РГ.А,У; ЕСЛИ РЭ, ИДЕМ НА П/П КОПИРОВАТЬ МЛ.12Р.РГ.А В СТ.Р.МАНТИССЫ У
CJS А=EMRNOR,МАР=РЕ,COND=EMLRG,ALUS=AQ,RА=А,FUNC=OR,Н,ALUD=В,RВ=А,SТОРС=7,CEN
;РМР СДВИГАЕМ ВПРАВО НА 12: ОСТАВЛЯЕМ МЕСТО ДЛЯ ЗНАКА МАНТИССЫ!
OUTZ:CJP А=BER,МАР=РЕ,COND=EMLRG,ALUS=DQ,FUNC=OR,Н,ALUD=ВА,RА=WR2,RВ=У,ALU
>,DSRC=SHIFT,MPS,PSHF=SН0+12,SHF=LOG
;ОБХОД, ЕСЛИ УСТАНОВЛЕНА БОКР
CJP А=NORND,МАР=РЕ,COND=RNDB,ALUS=АВ,RА=MANT,RВ=У,FUNC=AND,Н,СЕМ
;НЕ МЕНЯЕМ РГ.А, ЕСЛИ РМР = 0
CJP А=NORND,МАР=РЕ,COND=СТ,SТОРС=MZ
;ОКРУГЛЕНИЕ: НАКЛАДЫВАЕМ "1" НА МЛ.РАЗРЯД РГ.А
CONT А=В1,DSRC=PROM,ALUS=DА,RА=А,FUNC=OR,Н,ALUD=В,RВ=А
NORND:ENTRY
LDCT А=LCW,МАР=РЕ,FFCNT=СНТКК
;ИДЕМ НА СЛЕД.КОМАНДУ
JRP %М%,%RСА%,SТОРС=MCN,СЕМ
;
;
CALCOR:ENTRY
;===== ВЫЧИСЛЯЕМ И КОРРЕКТ.ПОРЯДОК. ПОСЛЕ CALCOR РГ.N НЕ ИСПОЛЬЗОВАТЬ! ТАМ OVR!
;ЕСТЬ ЛИ ОТРИЦАТЕЛЬНЫЙ ПОРЯДОК?
LDCT А=LCW,МАР=РЕ,ALUS=АВ,RА=WR6,RВ=WR5,FUNC=OR,Н,CEN
CJP А=MBSN,МАР=РЕ,COND=СТ,SТОРС=NN,ALUS=ZА,RА=WR3,Н,ALU,DSRC=SHIFT,MPS
>,PSHF=SН0-53,SHF=LOG,ЕСА,WRA,ARA=RG3
;ПОРЯДКИ ТОЛЬКО > 0. М.Б.OVR ПРИ СЛОЖЕНИИ, КОТ.МОЖЕТ ПРОПАСТЬ ПРИ КОРРЕКЦИИ!
;ПОЭТОМУ СНАЧАЛА ДЕЛАЕМ КОРРЕКЦИЮ ОДНОГО ИЗ ПОРЯДКОВ, А ПОТОМ УЖЕ СЛОЖЕНИЕ
CONT ЕСА,ARA=RG3,ALUS=DА,RА=WR6,FUNC=ADD,Н,ALUD=Q
CONT ALUS=AQ,RА=WR5,FUNC=ADD,Н,ALUD=Q,CEN
CRTN COND=СТ,SТОРС=/NV
;ЕСТЬ OVR! ПРОДОЛЖАЕМ, ЕСЛИ БПП=1. ПРИЗНАК OVR ЗАПОМИНАЕТСЯ В РГ.N.
CRTN COND=OVRIB
CJPP А=INT33,МАР=РЕ
;ЕСТЬ ОТРИЦАТЕЛЬНЫЙ ПОРЯДОК! OVR М.Б.ТОЛЬКО СНИЗУ
MBSN:CONT ALUS=АВ,RА=WR6,RВ=WR5,FUNC=ADD,Н,ALUD=Q,CEN
CJP А=PSCOR,МАР=РЕ,COND=СТ,SТОРС=/NV
;OVR ПРИ СЛОЖЕНИИ/ВЫЧИТАНИИ ПОРЯДКОВ (ЕЩЕ ДО КОРРЕКЦИИ!). РЕЗ-Т=0, Т.К.БНОР=0
CONT А=В64,DSRC=PROM,ALUS=DZ,Н,ALUD=Q
CONT ALUS=DZ,FUNC=AND,Н,ALUD=В,RВ=А
CJPP А=PSNOR,МАР=РЕ,ALUS=DZ,FUNC=AND,Н,ALUD=В,RВ=WR2,SТОРС=CLRN,CEN
;КОРРЕКТИРУЕМ ПОРЯДОК ДЛЯ НОРМАЛИЗАЦИИ. ВЫХОД, ЕСЛИ НЕТ OVR.
PSCOR:CONT ЕСА,ARA=RG3,ALUS=DQ,FUNC=ADD,Н,ALUD=Q,CEN
CRTN COND=СТ,SТОРС=/NV
;OVR СНИЗУ ПРИ КОРРЕКЦИИ: ПИШЕМ НЕСКОРР.ПОРЯДОК И НЕНОРМ.ЧИСЛО; ИНВЕРТИРУЕМ М!
CRTN ALUS=АВ,RА=WR6,RВ=WR5,FUNC=ADD,Н,ALUD=Q,CEN,SТОРС=INVM,СЕМ
;
;===== СТ.Р.МАНТИССЫ В РГ.А = 0; ПРОВЕРЯЕМ МЛ.РАЗРЯДЫ В РГ.WR2
ZERFA:CONT ALUS=ZА,RА=WR2,Н,CEN
CJP А=PSZERF,МАР=РЕ,COND=СТ,SТОРС=/NZ,ALUS=DZ,Н,ALUD=ВА,RА=WR2,RВ=WR3,ALU
>,DSRC=LOS
;ВСЕ РАЗРЯДЫ МАНТИССЫ = 0. ОБНУЛАЕМ РЕЗ-Т, Т.К. НЕТ БНОР
CJP А=OUTZ,МАР=РЕ,ALUS=DZ,FUNC=AND,Н,ALUD=Q
;НАШЛИ СТАРШУЮ "1" В МЛ.РАЗРЯДАХ МАНТИССЫ
PSZERF:CONT А=М6В,DSRC=PROM,ALUS=DА,RА=WR3,FUNC=AND,Н,ALUD=В,RВ=WR4
;КОРРЕКТИРУЕМ ПОРЯДОК: (+2-ПЛЕ)-64, Т.К.СТ.Р.МАНТИССЫ=0. ИТОГО: -62-ПЛЕ
CONT А=MN62,DSRC=PROM,ALUS=DZ,Н,ALUD=В,RВ=WR3
CJS А=CALCOR,МАР=РЕ,SТОРС=CLRM,СЕМ
;
;ЕСЛИ НЕ ПРОШЛА КОРРЕКЦИЯ, ТОЛЬКО ОСТАВЛЯЕМ МЕСТО ДЛЯ ПОРЯДКА!
CJP А=NODNO,МАР=РЕ,COND=СТ,SТОРС=MZ
;ЧИСЛО СДВИГОВ: +11 ВПРАВО (ПОРЯДОК) -(ПЛЕ-1)=-ПЛЕ+1 (НОРМАЛИЗАЦИЯ) = 12-ПЛЕ
CONT А=С12,DSRC=PROM,ALUS=DА,RА=WR4,FUNC=SUBS,Н,CI,ALUD=В,RВ=WR4,СЕМ
;ПАРАМЕТР СДВИГА (В11 + ЧИСЛО СДВИГОВ) - В РПС
CONT А=В11,DSRC=PROM,ALUS=DА,RА=WR4,FUNC=ADD,Н,ALUD=В,RВ=WR4,ALU,YDST=PSHIFT
;МЛ.Р.МАНТИССЫ - В СУММАТОР, ВПРАВЛЯЕМ ЗНАК МАНТИССЫ,Т.К.ЕГО В МЛ.Р.М.НЕ БЫТЬ!
CONT А=В64,DSRC=PROM,ALUS=DА,RА=А,FUNC=AND,Н,ALUD=В,RВ=А
CONT ALUS=ZА,RА=А,Н,ALU,DSRC=SHIFT,MPS,PSHF=SН0+11,SHF=LOG,ЕСА,WRA,ARA=RG3
CONT ALUS=DZ,Н,ALUD=ВА,RА=WR2,RВ=А,ALU,DSRC=SHIFT,SHF=LOG
CONT А=ММ12,DSRC=PROM,ALUS=DА,RА=А,FUNC=AND,Н,ALUD=В,RВ=А
CONT ЕСА,ARA=RG3,ALUS=DА,RА=А,FUNC=OR,Н,ALUD=В,RВ=А
;УМЕНЬШАЕМ ЧИСЛО СДВИГОВ НА 64 = ЧИСЛУ СДВИГОВ ОСТАТКА МАНТИССЫ ДЛЯ РМР
CONT А=С64,DSRC=PROM,ALUS=DА,RА=WR4,FUNC=SUBR,CI,ALU,YDST=PSHIFT
CJP А=PSNOR,МАР=РЕ,ALUS=DZ,Н,ALUD=ВА,RА=WR2,RВ=WR2,ALU,DSRC=SHIFT,SHF=LOG
;
;===== НОРМАЛИЗАЦИЯ ДЛЯ ОТРИЦАТЕЛЬНОЙ МАНТИССЫ: ИНВЕРТИРУЕМ, ИЩЕМ N СТ.ЕДИНИЦЫ
NEGF:CONT ALUS=ZА,RА=А,FUNC=EXNOR,Н,ALUD=В,RВ=WR4,СЕМ
;ПЕРЕХОД, ЕСЛИ НАШЛИ ЗНАЧАЩИЙ РАЗРЯД (0). N СТ.ЕДИНИЦЫ МАНТИССЫ - В WR3
CJP А=PSLOSX,МАР=РЕ,COND=СТ,SТОРС=/MZ,ALUS=DZ,Н,ALUD=ВА,RА=WR4,RВ=WR3,ALU
>,DSRC=LOS
;СТ.Р.МАНТИССЫ В РГ.А = ЗНАКУ (FFF); ПРОВЕРЯЕМ МЛ.РАЗРЯДЫ В РГ.WR2
CONT ALUS=ZА,RА=WR2,FUNC=EXNOR,Н,ALUD=В,RВ=WR4,СЕМ
;ПЕРЕХОД, ЕСЛИ НАШЛИ, ЧТО НОРМАЛИЗОВАТЬ. N СТ.ЕДИНИЦЫ (0) - В WR3
CJP А=PSZERF,МАР=РЕ,COND=СТ,SТОРС=/MZ,ALUS=DZ,Н,ALUD=ВА,RА=WR4,RВ=WR3
>,ALU,DSRC=LOS
;
;ВСЕ РАЗРЯДЫ МАНТИССЫ = ЗНАКУ < 0
;КОРРЕКТИРУЕМ ПОРЯДОК: УМЕНЬШАЕМ НА 127, Т.К.ЗНАЧАЩИХ ЦИФР НЕ НАШЛИ
CONT А=H7F,DSRC=PROM,ALUS=DZ,FUNC=SUBS,Н,CI,ALUD=В,RВ=WR3
CJS А=CALCOR,МАР=РЕ,SТОРС=CLRM,СЕМ
CJP А=NODNO,МАР=РЕ,COND=СТ,SТОРС=MZ
;ЕСЛИ СКОРРЕКТИРОВАЛИ, ПИШЕМ МАХ ОТР.ЧИСЛО В СТ.РАЗРЯДЫ МАНТИССЫ, 0 - В МЛ.
CONT А=В53,DSRC=PROM,ALUS=DZ,Н,ALUD=В,RВ=А
CJP А=PSNOR,МАР=РЕ,ALUS=DZ,FUNC=AND,Н,ALUD=В,RВ=WR2
;
;===== УСТАНОВЛЕНА БЛОКИРОВКА НОРМАЛИЗАЦИИ
;Т.К.МАНТИССЫ БЫЛИ СДВИНУТЫ НА 1Р.ВПРАВО,ЧТОБ НЕ БЫЛО OVR ПРИ СЛОЖЕНИИ/ВЫЧИТАНИИ
;МАНТИСС, ПРОВЕРЯЕМ, НУЖНА ЛИ НОРМАЛИЗАЦИЯ ВПРАВО: 64Р.=63Р?
BN:CONT ALUS=ZА,RА=WR2,Н,ALUD=Q
CONT ALUS=ZА,RА=А,Н,ALUD=LSBQ,RВ=А,SHMUX=LDLD
CJP А=NMAN,МАР=РЕ,COND=СТ,SТОРС=NN,ALUS=ZА,RА=А,Н,СЕМ
;МАНТИССА > 0: НЕ ПОТЕРЯЛИ ЛИ ЗНАК?
CJP А=OKBN,МАР=РЕ,COND=СТ,SТОРС=/MN,ALUS=ZQ,Н,ALUD=В,RВ=WR2
;СДВИГАЕМ ОБРАТНО (ВПРАВО)
CONT ALUS=ZА,RА=А,Н,ALUD=RSBQ,RВ=А,SHMUX=LDLD
;УВЕЛИЧИВАЕМ ПОРЯДОК НА 1
ADDE:CONT А=В54,DSRC=PROM,ALUS=DА,RА=WR6,FUNC=ADD,Н,ALUD=В,RВ=WR6,CEN
CJP А=OKBN,МАР=РЕ,COND=СТ,SТОРС=/NV,ALUS=ZQ,Н,ALUD=В,RВ=WR2
CJP А=INT33,МАР=РЕ,COND=OVRIB,IСС
CJP А=OKBN,МАР=РЕ
;МАНТИССА < 0: НЕ ПОТЕРЯЛИ ЗНАК?
NMAN:CJP А=OKBN,МАР=РЕ,COND=СТ,SТОРС=MN,ALUS=ZQ,Н,ALUD=В,RВ=WR2
;СДВИГАЕМ ОБРАТНО (ВПРАВО) С ЗАГРУЗКОЙ 1!
CJP А=ADDE,МАР=РЕ,ALUS=ZА,RА=А,Н,ALUD=RSBQ,RВ=А,SHMUX=NDNS
;
;СДВИГАЕМ РГ.А,У: ГОТОВИМ МЕСТО ДЛЯ ПОРЯДКА
OKBN:CONT ALUS=DZ,Н,ALUD=ВА,RА=А,RВ=А,ALU,ЕСВ,WRB,BRA=RG2,DSRC=SHIFT,MPS
>,PSHF=SН0+11,SHF=LOG
CONT ALUS=DZ,Н,ALUD=ВА,RА=WR2,RВ=WR2,ALU,DSRC=SHIFT,MPS,PSHF=SН0+11
>,SHF=LOG
CONT ЕСВ,BRA=RG2,DSRC=SHIFT,MPS,PSHF=SН0-53,SHF=LOG,ALUS=DА
>,RА=WR2,FUNC=OR,Н,ALUD=В,RВ=WR2
;ПОРЯДОК РЕЗ-ТА = МАХ ПОРЯДКУ (РГ.WR6)
CJP А=PSNOR,МАР=РЕ,ALUS=ZА,RА=WR6,Н,ALUD=Q
;
;===== П/П ДЛЯ РЭ
EMRNOR:ENTRY
;СДВИГ МЛ.12 РАЗРЯДОВ МАНТИССЫ СУММАТОРА В СТ.РАЗРЯДЫ РМР, КОТОРЫЕ В WR2
CONT ALUS=DZ,Н,ALUD=ВА,RА=А,RВ=WR4,ALU,DSRC=SHIFT,MPS,PSHF=SН0-52
>,SHF=LOG
CONT ALUS=DZ,Н,ALUD=ВА,RА=WR2,RВ=WR2,ALU,DSRC=SHIFT,MPS,PSHF=SН0+12,SHF=LOG
CRTN ALUS=АВ,RА=WR4,RВ=WR2,FUNC=OR,Н,ALUD=В
;
BER:ENTRY
;В А/Х (TR1=1) ОКРУГЛЕНИЕ НЕЗАВИСИМО ОТ БОКР! ДЛЯ ОКРУГЛЕНИЯ ПРОВЕРЯЕМ ВЕСЬ РМР
CJP А=PSBER,МАР=РЕ,COND=TR1,IСС,FFCNT=CLRJMP
CJP А=PSPSB,МАР=РЕ,ALUS=АВ,RА=MANT,RВ=У,FUNC=AND,Н,СЕМ
;ОБХОД, ЕСЛИ УСТАНОВЛЕНА БОКР
PSBER:CJP А=BERA,МАР=РЕ,COND=RNDB,ALUS=DZ,Н,ALUD=ВА,RА=У,RВ=WR4,СЕМ,ALU
>,DSRC=SHIFT,MPS,PSHF=SН0-23,SHF=LOG
PSPSB:CJP А=BMAN,МАР=РЕ,COND=BNB,IСС
;ББЧ=1: ПОПЫТАЕМСЯ СДЕЛАТЬ ТОЧНОСТЬ, КАК В Б-6:
;В СУММАТОРЕ ОСТАВЛЯЕМ ТОЛЬКО 48 РАЗРЯДОВ,
;ПРОВЕРЯЕМ НА "0" ВСЮ МАНТИССУ РМР (С "ХВОСТОМ" СУММАТОРА)
CONT ALUS=АВ,RА=MANT,RВ=У,FUNC=AND,Н,СЕМ
CJP А=BERA,МАР=РЕ,COND=СТ,SТОРС=MZ
;НАКЛАДЫВАЕМ "1" НА МЛ.Р.РГ.А В РЭ - 13-ЫЙ! ОБНУЛЯЕМ МЛ.12 РАЗРЯДОВ
CONT А=В13,DSRC=PROM,ALUS=DА,RА=А,FUNC=OR,Н,ALUD=В,RВ=А
CONT А=МРР,DSRC=PROM,ALUS=DА,RА=А,FUNC=NOTRS,Н,ALUD=В,RВ=А
CJP А=DBER,МАР=РЕ
;ПРОВЕРЯЕМ НА "0" ОСТАТОК МАНТИССЫ РМР (ТО, ЧЕГО НЕТ В СУММАТОРЕ!)
BMAN:CJP А=BERA,МАР=РЕ,COND=СТ,SТОРС=MZ
;ОКРУГЛЕНИЕ: НАКЛАДЫВАЕМ "1" НА МЛ.РАЗРЯД РГ.А (В РЭ - БОЛЬШУЮ ТОЧНОСТЬ!)
CONT А=В1,DSRC=PROM,ALUS=DА,RА=А,FUNC=OR,Н,ALUD=В,RВ=А
BERA:ENTRY
;ЕСЛИ BLOCK BIG NUMBERS =1, ОБНУЛЯЕМ МЛ.12 РАЗРЯДОВ РГ.А
CJP А=DBER,МАР=РЕ,COND=BNB,IСС
;ЕСЛИ РАЗРЕШЕНО ОКРУГЛЕНИЕ И МЛ.12Р.НЕ 0, НАКЛАДЫВАЕМ "1" НА 13-Й РАЗРЯД
CONT А=МРР,DSRC=PROM,ЕСА,WRA,ARA=RG3,ALUS=DА,RА=А,FUNC=AND,СЕМ
CJP А=DBER,МАР=РЕ,COND=СТ,SТОРС=MZ,ЕСА,ARA=RG3,ALUS=DА,RА=А,FUNC=NOTRS
>,Н,ALUD=В,RВ=А
CJP А=DBER,МАР=РЕ,COND=RNDB
CONT А=В13,DSRC=PROM,ALUS=DА,RА=А,FUNC=OR,Н,ALUD=В,RВ=А
DBER:ENTRY
;ПРОВЕРКА ВЫХОДА ПОРЯДКА ЧИСЛА ЗА ДИАПАЗОН Б-6
CJP А=KEMR,МАР=РЕ,COND=BNB,IСС,ALUS=ZА,RА=А,Н,СЕМ
CJP А=BIGREM,МАР=РЕ,COND=СТ,SТОРС=MN
;64Р.= 0, ЧИСЛА - МАЛЕНЬКИЕ, ДЫРА Д.БЫТЬ = 1111!
CONT А=MD,DSRC=PROM,ALUS=DА,RА=А,FUNC=AND,Н,ALUD=В,RВ=WR3
CONT А=MD,DSRC=PROM,ALUS=DА,RА=WR3,FUNC=EXOR,Н,СЕМ
CJP А=KEMR,МАР=РЕ,COND=СТ,SТОРС=MZ
CONT А=MD,DSRC=PROM,ALUS=DZ,Н,ALUD=В,RВ=А,CEN
CJP А=KEMR,МАР=РЕ,ALUS=ZА,RА=А,Н,ALUD=В,RВ=У
;64Р.= 1, ПОРЯДОК > 0, ЧИСЛА БОЛЬШИЕ! "ДЫРА" Д.Б.= ИНВЕРТИР.ЗНАКУ, Т.Е.0
BIGREM:CONT А=MD,DSRC=PROM,ALUS=DА,RА=А,FUNC=AND,Н,СЕМ
CJP А=INT34,МАР=РЕ,COND=СТ,SТОРС=/MZ,FFCNT=CLRTR1
KEMR:LDCT А=LCW,МАР=РЕ,FFCNT=СНТКК
;ПИШЕМ ПРИЗНАКИ В РГ.М СУСС, ИДЕМ НА СЛЕД.КОМАНДУ
JRP %М%,%RСА%,SТОРС=MCN,СЕМ,FFCNT=CLRTR1
END
АВХ:PROG;
;     I=========I
;     I  А - Х  I
;     I=========I
;
;РЭ:0-1,15ИР:0,ПИА:0,КОП-Е6,05
;PROM:0Е6,405
;===== АРИФМЕТИЧЕСКОЕ ВЫЧИТАНИЕ ЧИСЕЛ С ПЛАВАЮЩЕЙ ЗАПЯТОЙ
;   ОПИСАНИЕ ПРОГРАММ ВЫДЕЛЕНИЯ МАНТИССЫ И ПОРЯДКА И НОРМАЛИЗАЦИИ СМ.У А+Х!
;   ВАРИАНТ:ПРИ ВЫЧИТАНИИ В КАЧЕСТВЕ ЗАЕМА ИСПОЛЬЗОВАТЬ ИНВЕРТИРОВАННЫЙ ПЕРЕНОС?
;       СТ.РАЗРЯДЫ      МЛ.РАЗРЯДЫ
;   I--------------I-----------------I
;   I     WR1      I       WR2       I   МАНТИССА СУММАТОРА
; - I--------------I-----------------I
;   I      А       I       WR4       I   МАНТИССА ОПЕРАНДА
;   I--------------I-----------------I
;
;
EXTERN RDFLT,PSADX,АВХ
;М.А. - В АИСП, ИАОП; ИДЕМ НА П/П ПОДГОТОВКИ МАНТИСС
CJS А=RDFLT,МАР=РЕ,%МА%
;ДЕЛАЕМ ДОП.КОД СТ.И МЛ.РАЗРЯДОВ МАНТИССЫ ОПЕРАНДА, А НА СЛОЖЕНИЕ!
CONT ALUS=ZВ,RВ=А,FUNC=EXNOR,Н,ALUD=В
PSABX:CONT ALUS=ZВ,RВ=WR4,FUNC=EXNOR,Н,ALUD=В
CONT ALUS=ZВ,RВ=WR4,Н,CI,ALUD=В,CEN
;ДОБАВЛЯЕМ ПЕРЕНОС
CJP А=PSADX,МАР=РЕ,ALUS=ZВ,RВ=А,Н,CI=CIC,ALUD=В
;
ABXF:ENTRY
;РЭ:0,15ИР:1,ПИА:0,КОП-Е6
;PROM:2Е6
;===== ВЫЧИТАНИЕ ЧИСЕЛ С ПЗ ИЗ МАГАЗИНА В НР
CJP А=PSFE,МАР=РЕ,ALUS=ZQ,CEN
;
ABXFE:ENTRY
;РЭ:1,15ИР:1,ПИА:0,КОП-05
;PROM:605
;===== ВЫЧИТАНИЕ ЧИСЕЛ С ПЗ ИЗ МАГАЗИНА В РЭ
;
CONT А=М15,DSRC=PROM,ALUS=DQ,FUNC=AND,CEN
PSFE:CJP А=АВХ,МАР=РЕ,COND=СТ,SТОРС=/NZ
;МАГАЗИННОЕ ОБРАЩЕНИЕ НА ЧТЕНИЕ
CJS А=RDFLT,МАР=РЕ,%ARF%
CJP А=PSABX,МАР=РЕ,ALUS=ZВ,RВ=А,FUNC=EXNOR,Н,ALUD=ВА,RА=DADR,ALU
>,CSM,WЕМ,MNSA=МР,MODNM=SР
END
ХВА:PROG;
;     I=========I
;     I  Х - А  I
;     I=========I
;
;РЭ:0-1,15ИР:0,ПИА:0,КОП-Е7,06
;PROM:0Е7,406
;===== ОБРАТНОЕ ВЫЧИТАНИЕ ЧИСЕЛ С ПЛАВАЮЩЕЙ ЗАПЯТОЙ
;   ОПИСАНИЕ ПРОГРАММ ВЫДЕЛЕНИЯ МАНТИССЫ И ПОРЯДКА И НОРМАЛИЗАЦИИ СМ.У А+Х!
;       СТ.РАЗРЯДЫ      МЛ.РАЗРЯДЫ
;   I--------------I-----------------I
;   I      А       I       WR4       I   МАНТИССА ОПЕРАНДА
; - I--------------I-----------------I
;   I     WR1      I       WR2       I   МАНТИССА СУММАТОРА
;   I--------------I-----------------I
;
;
EXTERN RDFLT,PSADX,ХВА
;М.А. - В АИСП, ИАОП; ИДЕМ НА П/П ПОДГОТОВКИ МАНТИСС
CJS А=RDFLT,МАР=РЕ,%МА%
;ДЕЛАЕМ ДОП.КОД МАНТИССЫ СУММАТОРА - И НА СЛОЖЕНИЕ!
CONT ALUS=ZВ,RВ=WR1,FUNC=EXNOR,Н,ALUD=В
PSXBA:CONT ALUS=ZВ,RВ=WR2,FUNC=EXNOR,Н,ALUD=В
CONT ALUS=ZВ,RВ=WR2,Н,CI,ALUD=В,CEN
;ДОБАВЛЯЕМ ПЕРЕНОС
CJP А=PSADX,МАР=РЕ,ALUS=ZВ,RВ=WR1,Н,CI=CIC,ALUD=В,СЕМ
;
XBAF:ENTRY
;РЭ:0,15ИР:1,ПИА:0,КОП-Е7
;PROM:2Е7
;===== ОБРАТНОЕ ВЫЧИТАНИЕ ЧИСЕЛ С ПЗ ИЗ МАГАЗИНА В НР
CJP А=PSFE,МАР=РЕ,ALUS=ZQ,CEN
;
XBAFE:ENTRY
;РЭ:1,15ИР:1,ПИА:0,КОП-06
;PROM:606
;===== ОБРАТНОЕ ВЫЧИТАНИЕ ЧИСЕЛ С ПЗ ИЗ МАГАЗИНА В РЭ
;
CONT А=М15,DSRC=PROM,ALUS=DQ,FUNC=AND,CEN
PSFE:CJP А=ХВА,МАР=РЕ,COND=СТ,SТОРС=/NZ
;МАГАЗИННОЕ ОБРАЩЕНИЕ НА ЧТЕНИЕ
CJS А=RDFLT,МАР=РЕ,%ARF%
CJP А=PSXBA,МАР=РЕ,ALUS=ZВ,RВ=WR1,FUNC=EXNOR,Н,ALUD=ВА,RА=DADR,ALU
>,CSM,WЕМ,MNSA=МР,MODNM=SР
END
АМХ:PROG;
;     I=========I
;     I  А М Х  I
;     I=========I
;
;РЭ:0-1,15ИР:0,ПИА:0,КОП-Е8,07
;PROM:0Е8,407
;===== ВЫЧИТАНИЕ МОДУЛЕЙ ЧИСЕЛ С ПЛАВАЮЩЕЙ ЗАПЯТОЙ
;       СТ.РАЗРЯДЫ      МЛ.РАЗРЯДЫ
;   I--------------I-----------------I
;   I     WR1      I       WR2       I   МОДУЛЬ МАНТИССЫ СУММАТОРА
; - I--------------I-----------------I
;   I      А       I       WR4       I   МОДУЛЬ МАНТИССЫ ОПЕРАНДА
;   I--------------I-----------------I
;
EXTERN PSFLT,PSADX,АМХ
;М.А. - В АИСП, ИАОП
CONT %МА%
;"0" - В РЕГИСТР МЛ.РАЗРЯДОВ МАНТИССЫ - WR2
CONT %PHAR%,ALUS=DZ,FUNC=AND,Н,ALUD=В,RВ=WR2
;МАНТИССУ СУММАТОРА С ДВОЙНЫМ ЗНАКОВЫМ РАЗРЯДОМ - В WR1
CONT ALUS=DZ,Н,ALUD=ВА,RА=А,RВ=WR1,CEN,ALU,DSRC=SHIFT,MPS,PSHF=SН0-11,SHF=LOG
CJP А=PRA,МАР=РЕ,COND=СТ,SТОРС=/NN
>,ALUS=DZ,Н,ALUD=ВА,RА=WR1,RВ=WR1,ALU,DSRC=SHIFT,MPS,PSHF=SН0+1,SHF=ARIPH
;МАНТИССА < 0: ДЕЛАЕМ ДОП.КОД (ДОСТАТОЧНО ТОЛЬКО В РГ.WR1, Т.К.WR2 = 0)
CONT ALUS=ZВ,RВ=WR1,FUNC=EXNOR,Н,ALUD=В
CONT ALUS=ZВ,RВ=WR1,Н,CI,ALUD=В
PRA:CJS А=PSFLT,МАР=РЕ
;ПРОВЕРЯЕМ, ЧЕМУ РАВЕН ЗНАК МАНТИССЫ ОПЕРАНДА.
CONT ALUS=ZА,RА=А,Н,CEN
;ИДЕМ НА СЛОЖЕНИЕ, ЕСЛИ МАНТИССА < 0!
PSAMX:CJP А=PSADX,МАР=РЕ,COND=СТ,SТОРС=NN,ALUS=ZА,RА=А,FUNC=EXNOR,Н,ALUD=В
>,RВ=WR3
CONT ALUS=ZВ,RВ=WR4,FUNC=EXNOR,Н,ALUD=В
CONT ALUS=ZВ,RВ=WR4,Н,CI,ALUD=В,CEN
CJP А=PSADX,МАР=РЕ,ALUS=ZА,RА=WR3,Н,CI=CIC,ALUD=В,RВ=А
;
AMXF:ENTRY
;РЭ:0,15ИР:1,ПИА:0,КОП-Е8
;PROM:2Е8
;===== ВЫЧИТАНИЕ МОДУЛЕЙ ЧИСЕЛ С ПЗ ИЗ МАГАЗИНА В НР
CJP А=PSFE,МАР=РЕ,ALUS=ZQ,CEN
;
AMXFE:ENTRY
;РЭ:1,15ИР:1,ПИА:0,КОП-07
;PROM:607
;===== ВЫЧИТАНИЕ МОДУЛЕЙ ЧИСЕЛ С ПЗ ИЗ МАГАЗИНА В РЭ
;
;ПЕРЕХОД, ЕСЛИ АК НЕ 0; МАТ.АДРЕС ДЛЯ МАГАЗИННОГО ОБР.(15ИР-1) - В АИСП,ИАОП
CONT А=М15,DSRC=PROM,ALUS=DQ,FUNC=AND,CEN
PSFE:CJP А=АМХ,МАР=РЕ,COND=СТ,SТОРС=/NZ,%ARF%
;МАГАЗИННОЕ ОБРАЩЕНИЕ НА ЧТЕНИЕ
;"0" - В РЕГИСТР МЛ.РАЗРЯДОВ МАНТИССЫ - WR2
CONT %PHAR%,ALUS=DZ,FUNC=AND,Н,ALUD=В,RВ=WR2
;МАНТИССУ СУММАТОРА С ДВОЙНЫМ ЗНАКОВЫМ РАЗРЯДОМ - В WR1
CONT ALUS=DZ,Н,ALUD=ВА,RА=А,RВ=WR1,CEN,ALU,DSRC=SHIFT,MPS,PSHF=SН0-11,SHF=LOG
CJP А=PRAF,МАР=РЕ,COND=СТ,SТОРС=/NN
>,ALUS=DZ,Н,ALUD=ВА,RА=WR1,RВ=WR1,ALU,DSRC=SHIFT,MPS,PSHF=SН0+1,SHF=ARIPH
;МАНТИССА < 0: ДЕЛАЕМ ДОП.КОД
CONT ALUS=ZА,RА=WR1,FUNC=EXNOR,Н,ALUD=В,RВ=WR1
CONT ALUS=ZВ,RВ=WR1,Н,CI,ALUD=В
PRAF:CJS А=PSFLT,МАР=РЕ
CJP А=PSAMX,МАР=РЕ,ALUS=ZВ,RВ=А,Н,CEN,ALUD=ВА,RА=DADR,ALU,CSM,WЕМ,MNSA=МР
>,MODNM=SР
END
AVX:PROG;
;     I=========I
;     I  А V Х  I
;     I=========I
;
;РЭ:0-1,15ИР:0,ПИА:0,КОП-Е9,0С
;PROM:0Е9,40С
;===== ИЗМЕНЕНИЕ ЗНАКА ЧИСЛА НА СУММАТОРЕ С ПЛАВАЮЩЕЙ ЗАПЯТОЙ
;   WR1 - МАНТИССА СУММАТОРА (В МЛ.РАЗРЯДАХ!); WR6 - ПОРЯДОК СУММАТОРА
;
EXTERN AVX,LCW,CALCOR,BERA,INT33
;М.А. - В АИСП, ИАОП
CJS А=SUBAVX,МАР=РЕ,%МА%
CJP А=NORMA,МАР=РЕ,FFCNT=CLRTR1
;
AVXF:ENTRY
;РЭ:0,15ИР:1,ПИА:0,КОП-Е9
;PROM:2Е9
;===== ИЗМЕНЕНИЕ ЗНАКА ЧИСЛА НА СУММАТОРЕ С ПЗ МАГАЗИННОЕ В НР
CJP А=PSFE,МАР=РЕ,ALUS=ZQ,CEN
;
AVXFE:ENTRY
;РЭ:1,15ИР:1,ПИА:0,КОП-0С
;PROM:60С
;===== ИЗМЕНЕНИЕ ЗНАКА ЧИСЛА НА СУММАТОРЕ С ПЗ МАГАЗИННОЕ В РЭ
;
CONT А=М15,DSRC=PROM,ALUS=DQ,FUNC=AND,CEN
PSFE:CJP А=AVX,МАР=РЕ,COND=СТ,SТОРС=/NZ
;МАГАЗИННОЕ ОБРАЩЕНИЕ НА ЧТЕНИЕ
CJS А=SUBAVX,МАР=РЕ,%ARF%
CJP А=NORMA,МАР=РЕ,ALUS=ZА,RА=DADR,ALU,CSM,WЕМ,MNSA=МР,MODNM=SР,FFCNT=CLRTR1
;
;
SUBAVX:ENTRY
;МАНТИССУ СУММАТОРА - В WR1 (В МЛ.РАЗРЯДАХ!)
CONT А=ММ,%PHAR%,DSRC=PROM,ALUS=DА,RА=А,FUNC=AND,Н,ALUD=В,RВ=WR1
;ЗАПОМИНАЕМ ЗНАК МАНТИССЫ В MN!
CONT ALUS=DZ,Н,ALUD=ВА,RА=WR1,RВ=WR3,ALU,DSRC=SHIFT,MPS,PSHF=SН0-11,SHF=LOG,СЕМ
;ИНВЕРТИРОВАННУЮ МАНТИССУ - В WR3
CONT А=ММ,DSRC=PROM,ALUS=DА,RА=WR1,FUNC=EXOR,Н,ALUD=В,RВ=WR3
;ПОРЯДОК РГ.А - В WR6 ( - СДВИГ ШКАЛЫ ПОРЯДКОВ!); LCW - В РГ/СЧ ДЛЯ OVR И ДР.
PUSH А=LCW,МАР=РЕ,ALUS=АВ,RА=EXPN,RВ=А,FUNC=AND,Н,ALUD=Q
LООР А=В64,COND=ARBRDY,DSRC=PROM,ALUS=DQ,FUNC=ADD,Н,ALUD=В,RВ=WR6
;ПРОВЕРЯЕМ ЗНАК ОПЕРАНДА
CONT ЕСВ,BRA=RG2,DSRC=SHIFT,MPS,PSHF=SН0-11,SHF=LOG,ALUS=DZ,Н,CEN
;ВОЗВРАТ, ЕСЛИ > 0
CRTN COND=СТ,SТОРС=/NN,ALUS=DZ,FUNC=AND,Н,ALUD=В,RВ=WR5,FFCNT=ADDGRP
;<0: ДОП.КОД МАНТИССЫ - В WR1, ИНВЕРТИРУЕМ ЗНАК В MN, ЕСЛИ МАНТИССА НЕ "0"!
CONT ALUS=ZА,RА=WR3,Н,CI,ALUD=В,RВ=WR1
CRTN COND=СТ,SТОРС=MZ,ALUS=АВ,RА=MANT,RВ=WR1,FUNC=AND,Н,ALUD=В
CRTN SТОРС=INVM,СЕМ
;
;
NORMA:ENTRY
;===== П/П НОРМАЛИЗАЦИИ РАСПАКОВАННОГО СУММАТОРА СО ЗНАКОМ, НО БЕЗ ОКРУГЛЕНИЯ!
;   I----------------------I
;   I....I/////////////////I    WR6 - ПОРЯДОК СУММАТОРА, WR5 - ОПЕРАНДА
;   I----------------------I
;   WR1 - МАНТИССА РЕЗ-ТА (СУММАТОРА), 53 ИЛИ 54 РАЗРЯДА! ЗНАК МАНТИССЫ - В MN!
;   ЕСЛИ TR1 = 1, РМР СДВИГАЕТСЯ, КАК РГ.А, И ДОБАВЛЯЕТСЯ ПОРЯДОК (ДЛЯ А/Х);
;   ЕСЛИ TR1 = 0, РМР НЕ МЕНЯЕТСЯ (AVX, А+N, А-N, А+Х, А-Х)
;
;ПЕРЕХОД, ЕСЛИ НЕ НУЖНА НОРМАЛИЗАЦИЯ
CJP А=BNNE,МАР=РЕ,COND=NORMB,ALUS=ZА,RА=WR1,Н,CEN
;ЕСЛИ МАНТИССА < 0
CJP А=NEGE,МАР=РЕ,COND=СТ,SТОРС=MN
;ПЕРЕХОД, ЕСЛИ МАНТИССА = 0; N ЛЕВОЙ ЕДИНИЦЫ - В WR3
CJP А=ZRRZAE,МАР=РЕ,COND=СТ,SТОРС=NZ,ALUS=DZ,Н,ALUD=ВА,RА=WR1,RВ=WR3,ALU
>,DSRC=LOS
PSLOSE:CONT А=М6В,DSRC=PROM,ALUS=DА,RА=WR3,FUNC=AND,Н,ALUD=В,RВ=WR3
;ПРИ НОРМАЛИЗАЦИИ СДВИГАЕМ ТОЛЬКО В ПРЕРЕЛАХ ОДНОГО РЕГИСТРА! М.Б.И ВПРАВО НА 1Р
;ЧИСЛО СДВИГОВ = "12-ПЛЕ" <0, Т.К.СТ.ЕДИНИЦА Д.Б.НА 52 МЕСТЕ ДЛЯ МАНТ.>0
CONT А=С12,DSRC=PROM,ALUS=DА,RА=WR3,FUNC=SUBS,Н,CI,ALUD=В,RВ=WR3
;ИДЕМ ВЫЧИСЛЯТЬ И КОРРЕКТИРОВАТЬ ПОРЯДОК
CJS А=CALCOR,МАР=РЕ,SТОРС=CLRM,СЕМ
;НЕ СДВИГАЕМ МАНТИССУ, ЕСЛИ НЕ ПРОШЛА КОРРЕКЦИЯ
CJP А=PSNORA,МАР=РЕ,COND=СТ,SТОРС=MZ
;ПАРАМЕТР СДВИГА (В11 + ЧИСЛО СДВИГОВ <0 ) - В РПС; СДВИГАЕМ МАНТИССУ
CONT А=В11,DSRC=PROM,ALUS=DА,RА=WR3,FUNC=ADD,Н,ALU,YDST=PSHIFT
CJP А=PSNORA,МАР=РЕ,COND=TR1,IСС,ALUS=DZ,Н,ALUD=ВА,RА=WR1,RВ=WR1,ALU,DSRC=SHIFT
>,SHF=LOG
CONT ALUS=DZ,Н,ALUD=ВА,RА=У,RВ=У,ALU,DSRC=SHIFT,SHF=LOG
;ДОБАВЛЯЕМ СДВИГ ШКАЛЫ ПОРЯДКОВ
PSNORA:CONT А=В64,DSRC=PROM,ALUS=DQ,FUNC=ADD,Н,ALUD=Q
;ТО ЖЕ ДЕЛАЕМ С РМР, ЕСЛИ TR1=1; УБИРАЕМ МУСОР НА СЛУЧАЙ ОТР.МАНТ.
CJP А=OUTE,МАР=РЕ,COND=TR1,IСС,ALUS=АВ,RА=MANT,RВ=WR1,FUNC=AND,Н,ALUD=В
CONT ALUS=АВ,RА=MANT,RВ=У,FUNC=AND,Н,ALUD=В
CONT ALUS=AQ,RА=У,FUNC=OR,Н,ALUD=В,RВ=У
;СОБИРАЕМ РГ.А
OUTE:CJP А=BERA,МАР=РЕ,COND=EMLRG,ALUS=AQ,RА=WR1,FUNC=OR,Н,ALUD=В,RВ=А,SТОРС=7
>,CEN
LDCT А=LCW,МАР=РЕ,FFCNT=СНТКК
JRP %М%,%RСА%,SТОРС=MCN,СЕМ,FFCNT=CLRTR1
;
;МАНТИССА <0: ИНВЕРТИРУЕМ ЕЕ, ИЩЕМ N СТ.ЕДИНИЦЫ
NEGE:CONT А=ММ,DSRC=PROM,ALUS=DА,RА=WR1,FUNC=AND,Н,ALUD=В,RВ=WR3
;УБРАЛИ СТ.РАЗРЯДЫ, Т.К. ЗНАК М.Б. В 54 РАЗРЯДЕ!
CONT ALUS=АВ,RА=MANT,RВ=WR3,FUNC=EXOR,Н,CEN,ALU,ЕСВ,WRB,BRA=RG3
;ПЕРЕХОД, ЕСЛИ НАШЛИ, ЧТО НОРМАЛИЗОВАТЬ В РГ.А
CJP А=PSLOSE,МАР=РЕ,COND=СТ,SТОРС=/NZ,ЕСВ,BRA=RG3,DSRC=LOS,ALUS=DZ,Н,ALUD=В
>,RВ=WR3
;
;ВСЕ 52 РАЗРЯДА МАНТИССЫ = ЗНАКУ <0. КОРРЕКТИРУЕМ НА СТОЛЬКО ПОРЯДОК (WR3=-52)
CONT А=С52,DSRC=PROM,ALUS=DZ,FUNC=SUBR,Н,CI,ALUD=В,RВ=WR3
CJS А=CALCOR,МАР=РЕ,SТОРС=CLRM,СЕМ
CJP А=PSNORA,МАР=РЕ,COND=СТ,SТОРС=MZ
;В СТ.РАЗРЯДЫ МАНТИССЫ ПИШЕМ САМОЕ БОЛЬШОЕ ОТР.ЧИСЛО
CONT А=В53,DSRC=PROM,ALUS=DZ,Н,ALUD=В,RВ=WR1
CJP А=PSNORA,МАР=РЕ
;
;УСТАНОВЛЕНА БНОР
BNNE:CJP А=NEGBN,МАР=РЕ,COND=СТ,SТОРС=MN,ALUS=DZ,FUNC=AND,Н,ALUD=В,RВ=WR3
;МАНТИССА > 0: НУЖНА ЛИ НОРМАЛИЗАЦИЯ ВПРАВО? 53Р.= 1?
CONT А=В53,DSRC=PROM,ALUS=DА,RА=WR1,FUNC=AND,Н,CEN
CJP А=CORBN,МАР=РЕ,COND=СТ,SТОРС=NZ
;СДВИГАЕМ МАНТИССУ ВПРАВО НА 1Р., КОРРЕКТИРУЕМ СООТВЕТСТВЕННО ПОРЯДОК
SHBN:CONT А=С1,DSRC=PROM,ALUS=DZ,Н,ALUD=В,RВ=WR3
CJP А=CORBN,МАР=РЕ,COND=TR1,IСС,ALUS=ZА,RА=WR1,Н,ALUD=RSB,RВ=WR1,SHMUX=LSLSL
CJP А=CORBN,МАР=РЕ,ALUS=ZА,RА=У,Н,ALUD=RSB,RВ=У,SHMUX=LSLSL
;МАНТИССА < 0: НУЖНА ЛИ НОРМАЛИЗАЦИЯ ВПРАВО? 53Р.= 0?
NEGBN:CONT А=В53,DSRC=PROM,ALUS=DА,RА=WR1,FUNC=AND,Н,CEN
CJP А=SHBN,МАР=РЕ,COND=СТ,SТОРС=NZ
CORBN:CJS А=CALCOR,МАР=РЕ,SТОРС=CLRM,СЕМ
CJP А=PSNORA,МАР=РЕ,COND=СТ,SТОРС=/MZ
;ПЕРЕПОЛНЕНИЕ СНИЗУ В CALCOR: 0 - В РЕЗУЛЬТАТ
CONT ALUS=DZ,FUNC=AND,Н,ALUD=В,RВ=WR1
;МАНТИССА = 0 И НЕТ БНОР, ПИШЕМ "0" В РГ.А
ZRRZAE:CJP А=OUTE,МАР=РЕ,ALUS=DZ,FUNC=AND,Н,ALUD=Q
END
А*Х:PROG;
;     I=========I
;     I  А * Х  I
;     I=========I
;
;РЭ:0-1,15ИР:0,ПИА:0,КОП-ЕА,0F
;PROM:0ЕА,40F
;===== АРИФМЕТИЧЕСКОЕ УМНОЖЕНИЕ ЧИСЕЛ С ПЛАВАЮЩЕЙ ЗАПЯТОЙ
;
EXTERN LCW,А*Х,EMRNOR,BER,CALCOR,PSNOR,DBER
;М.А. - В АИСП, ИАОП; ИДЕМ НА П/П ПОДГОТОВКИ МАНТИСС И ПОРЯДКОВ А,Х
CJS А=MDXSUB,МАР=РЕ,%МА%
;
;ПРОВЕРЯЕМ МАНТИССУ СУММАТОРА И ОПЕРАНДА НА 0
MULX:CJP А=ZERMA,МАР=РЕ,COND=СТ,SТОРС=NZ,FFCNT=CLRJMP
CJP А=PSMUL,МАР=РЕ,COND=СТ,SТОРС=/MZ,FFCNT=CLRTR1
ZERMA:ENTRY
;FА ИЛИ FХ = 0: ЕСЛИ НЕТ БНОР, А=У=0
CJP А=OUTZ,МАР=РЕ,COND=NORMB,IСС,ALUS=DZ,FUNC=AND,Н,ALUD=В,RВ=У
;ЕСЛИ БНОР = 1: F=0, ОСТАВЛЯЕМ ТОЛЬКО ПОРЯДОК РЕЗУЛЬТАТА, ЕСЛИ НЕ БЫЛО OVR
CONT ALUS=АВ,RА=WR5,RВ=WR6,FUNC=ADD,Н,ALUD=Q,CEN
CONT А=В64,DSRC=PROM,ALUS=DQ,FUNC=ADD,Н,ALUD=В,RВ=У
CJP А=OUTZ,МАР=РЕ,COND=СТ,SТОРС=/NV
;БЫЛО OVR: ОСТАВЛЯЕМ ПОРЯДОК СУММАТОРА
CONT А=MNM,DSRC=PROM,ALUS=DА,RА=А,FUNC=AND,Н,ALUD=В,RВ=У
OUTZ:CJP А=DBER,МАР=РЕ,COND=EMLRG,ALUS=ZА,RА=У,Н,ALUD=В,RВ=А,СЕМ
LDCT А=LCW,МАР=РЕ,FFCNT=СНТКК
JRP %М%,FFCNT=MULGRP,%RСА%
;           А                  Q
;   I----------------I-----------------I
;   I    I///////////I    I////////////I   ------> ДВОЙНОЙ СДВИГ ВПРАВО
;   I----------------I-----------------I
; +        WR1
;   I----------------I                     WR1 - МНОЖИМОЕ
;   I    I///////////I                     Q   - МНОЖИТЕЛЬ
;   I----------------I                     А,Q - ЧАСТ.СУММА, РЕЗУЛЬТАТ
;            А                Q
;   I----------------I-----------------I
;   I    I///////////I////////////I    I
;   I----------------I-----------------I
;СОБСТВЕННО УМНОЖЕНИЕ
PSMUL:PUSH А=53,МАР=РЕ,SТОРС=CLRN,CEN,ALUS=DZ,FUNC=AND,Н,ALUD=В,RВ=А
;СДВИГ А,Q ВПРАВО; ПЕРЕХОД, ЕСЛИ COUT(Q) =1 (NС=1)
CJP А=ADDM,МАР=РЕ,COND=СТ,SТОРС=NС,ALUS=ZА,RА=А,Н,ALUD=RSBQ,RВ=А
>,SHMUX=LDLND,СЕМ
TWB А=NORMMD,МАР=РЕ,IСС,SТОРС=MCN,CEN,СЕМ
ADDM:TWB А=NORMMD,МАР=РЕ,IСС,ALUS=АВ,RА=WR1,RВ=А,FUNC=ADD,Н,ALUD=В,SТОРС=MCN
>,CEN,СЕМ
;
;
А*XF:ENTRY
;РЭ:0,15ИР:1,ПИА:0,КОП-0F
;PROM:2ЕА
;===== УМНОЖЕНИЕ ЧИСЕЛ С ПЗ ИЗ МАГАЗИНА В НР: ПРОВЕРЯЕМ АК НА 0
CJP А=А*XFAN,МАР=РЕ,ALUS=ZQ,CEN
;
А*XFE:ENTRY
;РЭ:1,15ИР:1,ПИА:0,КОП-0F
;PROM:60F
;===== УМНОЖЕНИЕ ЧИСЕЛ С ПЗ ИЗ МАГАЗИНА В РЭ: ПРОВЕРЯЕМ МЛ.15 РАЗР.АК НА 0
;
CONT А=М15,DSRC=PROM,ALUS=DQ,FUNC=AND,CEN
А*XFAN:CJP А=А*Х,МАР=РЕ,COND=СТ,SТОРС=/NZ
;МАГАЗИННОЕ ОБРАЩЕНИЕ НА ЧТЕНИЕ
CJS А=MDXSUB,МАР=РЕ,%ARF%
CJP А=MULX,МАР=РЕ,ALUS=ZА,RА=DADR,ALU,CSM,WЕМ,MNSA=МР,MODNM=SР
;
;
MDXSUB:ENTRY
;===== П/П ЧТЕНИЯ ОПЕРАНДА С ПЗ ДЛЯ КОМАНД УМНОЖЕНИЯ И ДЕЛЕНИЯ
;   WR1 - МАНТИССА А;  WR6 - ПОРЯДОК А;  WR3 - ПРИЗНАК ЗНАКА
;   Q   - МАНТИССА Х;  WR5 - ПОРЯДОК Х
;МАНТИССУ СУММАТОРА - В WR1
CONT А=ММ,%PHAR%,DSRC=PROM,ALUS=DА,RА=А,FUNC=AND,Н,ALUD=В,RВ=WR1,CEN
;ПРОВЕРЯЕМ ЗНАК
CONT А=В53,DSRC=PROM,ALUS=DА,RА=А,FUNC=AND,Н,СЕМ,FFCNT=CLRTR1
;ДЕЛАЕМ ДОП.КОД МАНТИССЫ, ЕСЛИ ОНА < 0; "0" - В РГ.ПРИЗНАКОВ
CJS А=DОРА,МАР=РЕ,COND=СТ,SТОРС=/MZ,ALUS=DZ,FUNC=AND,Н,ALUD=В,RВ=WR3
;ПОРЯДОК СУММАТОРА БЕЗ СДВИГА ШКАЛЫ - В WR6
PUSH А=MNM,DSRC=PROM,ALUS=DА,RА=А,FUNC=AND,Н,ALUD=Q
LООР А=В64,COND=ARBRDY,DSRC=PROM,ALUS=DQ,FUNC=ADD,Н,ALUD=В,RВ=WR6
;ПОРЯДОК ОПЕРАНДА - CONST СДВИГА ШКАЛЫ ПОРЯДКОВ - В WR5
CONT ЕСА,ARA=RG2,ALUS=DА,RА=EXPN,FUNC=AND,Н,ALUD=В,RВ=WR5,FFCNT=MULGRP
CONT А=В64,DSRC=PROM,ALUS=DА,RА=WR5,FUNC=SUBR,Н,CI,ALUD=В,RВ=WR5
;ПРОВЕРЯЕМ ЗНАК МАНТИССЫ; ДЕЛАЕМ ДОП.КОД, ЕСЛИ ОНА < 0
LDCT А=LCW,МАР=РЕ,ЕСВ,BRA=RG2,DSRC=SHIFT,MPS,PSHF=SН0-11,SHF=LOG,ALUS=DZ,Н,СЕМ
;МАНТИССУ ОПЕРАНДА - В РГ.Q
CJS А=DОРХ,МАР=РЕ,COND=СТ,SТОРС=MN,ЕСА,ARA=RG2,ALUS=DА,RА=MANT,FUNC=AND,Н
>,ALUD=Q
;ЗАПОМИНАЕМ ПРИЗНАКИ МАНТИССЫ ОПЕРАНДА (РАНЬШЕ - СУММАТОРА) ДЛЯ ПРОВЕРКИ НА 0
CRTN ALUS=ZQ,Н,СЕМ
;
;МАНТИССА РГ.А < 0: ВЫЧИСЛЯЕМ ДОП.КОД, ЗАПОМИНАЕМ ЗНАК
DОРА:CONT ALUS=АВ,RА=MANT,RВ=WR1,FUNC=EXOR,Н,ALUD=В
CONT ALUS=ZВ,RВ=WR1,Н,CI,ALUD=В
PSDOP:CRTN А=В9,DSRC=PROM,ALUS=DА,RА=WR3,FUNC=EXOR,Н,ALUD=В,RВ=WR3
;
;МАНТИССА ОПЕРАНДА < 0: ВЫЧИСЛЯЕМ ДОП.КОД, ЗАПОМИНАЕМ ЗНАК
DОРХ:CONT ALUS=AQ,RА=MANT,FUNC=EXOR,Н,ALUD=Q
CJP А=PSDOP,МАР=РЕ,ALUS=ZQ,Н,CI,ALUD=Q
;
;
NORMMD:ENTRY
;===== П/П НОРМАЛИЗАЦИИ ДЛЯ УМНОЖЕНИЯ С ПЗ
;        А              WR2                               WR6
; I--------------I---------------I               ЕА  I////I...........I >0,<0,=0
; I   I//////////I///////////I   I     OVR СНИЗУ И   +    WR5
; I--------------I---------------I     СВЕРХУ    ЕХ  I////I...........I >0,<0,=0
; WR3 - ПР.ЗНАКА МАНТ.ПРИ ВХОДЕ В П/П; OVR ТОЛЬКО    +    WR3
; ПРИ НОРМ - ЧИСЛО СДВИГОВ И КОРР.ПОР. СНИЗУ   Е HOPMI////I...........I <0,=0
; WR4 - РАБОЧИЙ                                      =    Q
; OVR - В РГ.N ПОСЛЕ П/П CALCOR                Е РЕЗ I////I...........I
;ЧИТАЕМ ПРИЗНАКИ РЕЗ-ТА В РГ.М СУСС
CONT ALUS=ZА,RА=WR3,ALU,DDEV=STATUS,WRD,SТОРС=0,СЕМ,FFCNT=CLRTR1
;ПЕРЕХОД, ЕСЛИ МАНТИССА РЕЗ-ТА Д.Б. ОТРИЦАТЕЛЬНОЙ
CJP А=NEGRZ,МАР=РЕ,COND=СТ,SТОРС=MN,ALUS=ZQ,Н,ALUD=В,RВ=WR2
;ПЕРЕХОД, ЕСЛИ НЕ НУЖНА НОРМАЛИЗАЦИЯ
CJP А=BNN,МАР=РЕ,COND=NORMB,ALUS=DZ,Н,ALUD=ВА,RА=А,RВ=WR4,CEN,ALU,DSRC=SHIFT
>,MPS,PSHF=SН0-12,SHF=LOG
;ПЕРЕХОД, ЕСЛИ РЕЗ-Т УЖЕ НОРМАЛИЗОВАН: 52Р.А = 1 (ПОСЛЕ СДВИГА НА 12 ЗНАК = 1)
CJP А=BNN,МАР=РЕ,COND=СТ,SТОРС=NN
;ПЕРЕХОД,ЕСЛИ РГ.А = 0; N ЛЕВОЙ ЕДИНИЦЫ - В WR3
CJP А=ZRRZA,МАР=РЕ,COND=СТ,SТОРС=NZ,ALUS=DZ,Н,ALUD=ВА,RА=А,RВ=WR3,ALU,DSRC=LOS
;
PSLOS:CONT А=М6В,DSRC=PROM,ALUS=DА,RА=WR3,FUNC=AND,Н,ALUD=В,RВ=WR3
;ЧИСЛО СДВИГОВ ВЛЕВО = КОРРЕКЦИИ ПОРЯДКА = "12-ПЛЕ" <0,
;Т.К.СТ.ЕДИНИЦА Д.БЫТЬ НА 52 МЕСТЕ ДЛЯ ПОЛОЖИТЕЛЬНОЙ МАНТИССЫ
CONT А=С12,DSRC=PROM,ALUS=DА,RА=WR3,FUNC=SUBS,Н,CI,ALUD=В,RВ=WR3,СЕМ
;ИДЕМ ВЫЧИСЛЯТЬ И КОРРЕКТИРОВАТЬ ПОРЯДОК
CJS А=CALCOR,МАР=РЕ,SТОРС=CLRM,СЕМ
CJP А=PSNOR,МАР=РЕ,COND=СТ,SТОРС=MZ
;ПАРАМЕТР СДВИГА (В11 + ЧИСЛО СДВИГОВ <0 ) - В РПС
CONT А=В11,DSRC=PROM,ALUS=DА,RА=WR3,FUNC=ADD,Н,ALUD=В,RВ=WR4,ALU
>,YDST=PSHIFT
;===== НОРМАЛИЗАЦИЯ: СДВИГАЕМ СТ.И МЛ. РАЗРЯДЫ МАНТИССЫ. СДВИГ - ТОЛЬКО ВЛЕВО!
CONT ALUS=DZ,Н,ALUD=ВА,RА=А,RВ=А,ALU,ЕСВ,WRB,BRA=RG3,DSRC=SHIFT,SHF=LOG
CONT ALUS=DZ,Н,ALUD=ВА,RА=WR2,RВ=WR2,ALU,ЕСВ,WRB,BRA=RG3,DSRC=SHIFT,SHF=LOG
;ДОБАВЛЯЕМ ЧАСТЬ МЛ.РАЗРЯДОВ МАНТИССЫ В СТ.РАЗРЯДЫ
CONT А=С64,DSRC=PROM,ALUS=DА,RА=WR4,FUNC=ADD,ALU,YDST=PSHIFT
CJP А=PSNOR,МАР=РЕ,ЕСВ,BRA=RG3,DSRC=SHIFT,SHF=LOG,ALUS=DА,RА=А,FUNC=OR,Н
>,ALUD=В,RВ=А
;
;УСТАНОВЛЕНА БНОР ИЛИ РЕЗ-Т НОРМАЛИЗОВАН. СКЛАДЫВАЕМ ПОРЯДКИ, И НА ВЫХОД.
BNN:CJS А=CALCOR,МАР=РЕ,ALUS=DZ,FUNC=AND,Н,ALUD=В,RВ=WR3,SТОРС=CLRM,СЕМ
CJP А=PSNOR,МАР=РЕ,COND=СТ,SТОРС=/MZ
;ПЕРЕПОЛНЕНИЕ СНИЗУ ПРИ СЛОЖЕНИИ ПОРЯДКОВ: 0 - В РЕЗУЛЬТАТ
CONT А=В64,DSRC=PROM,ALUS=DZ,Н,ALUD=Q
CONT ALUS=DZ,FUNC=AND,Н,ALUD=В,RВ=А
CJP А=PSNOR,МАР=РЕ,ALUS=DZ,FUNC=AND,Н,ALUD=В,RВ=WR2
;
;МАНТИССА РГ.А=0; ИЩЕМ 1-Ю "1" В МЛ.РАЗРЯДАХ РЕЗУЛЬТАТА (РГ.WR2)
ZRRZA:LDCT А=ZERMA,МАР=РЕ,ALUS=ZА,RА=WR2,Н,CEN
JRP А=ONSIN,МАР=РЕ,COND=СТ,SТОРС=/NZ,ALUS=DZ,Н,ALUD=ВА,RА=WR2,RВ=WR3,ALU
>,DSRC=LOS
;
;МАНТИССА Д.Б.ОТРИЦАТЕЛЬНОЙ! ДЕЛАЕМ ДОП.КОД А,WR2; НОРМАЛИЗУЕМ
NEGRZ:CONT А=M53L,DSRC=PROM,ALUS=DА,RА=WR2,FUNC=EXOR,Н,ALUD=В,RВ=WR2
CONT ALUS=АВ,RА=MANT,RВ=А,FUNC=EXOR,Н,ALUD=В
CONT А=В12,DSRC=PROM,ALUS=DА,RА=WR2,FUNC=ADD,Н,ALUD=В,RВ=WR2,CEN
CJP А=BNN,МАР=РЕ,COND=NORMB,ALUS=ZА,RА=А,FUNC=ADD,Н,CI=CIC,ALUD=В,RВ=А
;ПЕРЕХОД, ЕСЛИ РЕЗ-Т УЖЕ НОРМАЛИЗОВАН: 52Р.А = 0
CONT А=В52,DSRC=PROM,ALUS=DА,RА=А,FUNC=AND,Н,CEN
CJP А=BNN,МАР=РЕ,COND=СТ,SТОРС=NZ,ALUS=АВ,RА=MANT,RВ=А,FUNC=EXOR,Н
>,CEN,ALU,ЕСВ,WRB,BRA=RG3
;ПЕРЕХОД, ЕСЛИ НАШЛИ, ЧТО НОРМАЛИЗОВАТЬ В РГ.А
CJP А=PSLOS,МАР=РЕ,COND=СТ,SТОРС=/NZ,ЕСВ,BRA=RG3,DSRC=LOS,ALUS=DZ,Н,ALUD=В
>,RВ=WR3
;
;МАНТИССА РГ.А=-1; ИЩЕМ 1-Й "0" В МЛ.РАЗРЯДАХ РЕЗУЛЬТАТА (РГ.WR2)
CONT А=M53L,ALUS=DА,RА=WR2,FUNC=EXOR,Н,CEN,ALU,ЕСВ,WRB,BRA=RG3
;ПЕРЕХОД, ЕСЛИ И WR2 = ЗНАКУ (-1)
CJP А=DBLNEG,МАР=РЕ,COND=СТ,SТОРС=NZ,ЕСВ,BRA=RG3,DSRC=LOS,ALUS=DZ,Н,ALUD=В
>,RВ=WR3
;===== НОРМАЛИЗАЦИЯ НА СЛУЧАЙ, ЕСЛИ СТ.РАЗРЯДЫ (РГ.А) = ЗНАКУ, МЛ.Р.- НЕТ
ONSIN:CONT А=М6В,DSRC=PROM,ALUS=DА,RА=WR3,FUNC=AND,Н,ALUD=В,RВ=WR3
;ЧИСЛО СДВИГОВ = "12-ПЛЕ", Т.К.СТАРШИЙ ЗНАЧАЩИЙ РАЗРЯД Д.Б.НА 52 МЕСТЕ
CONT А=С12,DSRC=PROM,ALUS=DА,RА=WR3,FUNC=SUBS,Н,CI,ALUD=В,RВ=WR1
;И "-ПЛЕ-52" ДЛЯ КОРРЕКЦИИ ПОРЯДКА, Т.К.СТ.РАЗРЯДЫ МАНТИССЫ = ЗНАКУ
CONT А=С52,DSRC=PROM,ALUS=DZ,FUNC=SUBR,Н,CI,ALUD=В,RВ=WR4
;КОРРЕКТИРУЕМ ПОРЯДОК:
CJS А=CALCOR,МАР=РЕ,ALUS=АВ,RА=WR4,RВ=WR3,FUNC=SUBS,Н,CI,ALUD=В,SТОРС=CLRM,СЕМ
CJP А=PSNOR,МАР=РЕ,COND=СТ,SТОРС=MZ
;ПАРАМЕТР СДВИГА = В11 + ЧИСЛО СДВИГОВ - В РПС
CONT А=В11,DSRC=PROM,ALUS=DА,RА=WR1,FUNC=ADD,ALU,YDST=PSHIFT
;СОХРАНЯЕМ ЗНАК, Т.К.ОН М.Б.ТОЛЬКО В СТ.Р.МАНТИССЫ
CONT А=В53,DSRC=PROM,ALUS=DА,RА=А,FUNC=AND,Н,ALU,ЕСВ,WRB,BRA=RG3
;СДВИГАЕМ МЛ. РАЗРЯДЫ МАНТИССЫ - ПИШЕМ В РГ.А
CONT ALUS=DZ,Н,ALUD=ВА,RА=WR2,RВ=А,ALU,DSRC=SHIFT,SHF=LOG
CONT А=ММ12,DSRC=PROM,ALUS=DА,RА=А,FUNC=AND,Н,ALUD=В,RВ=А
;ДОБАВЛЯЕМ ЗНАК
CONT ЕСА,ARA=RG3,ALUS=DА,RА=А,FUNC=OR,Н,ALUD=В,RВ=А
;ПИШЕМ "0" В МЛ.РАЗРЯДЫ, Т.К.ИХ ВСЕ ПЕРЕНЕСЛИ В СТАРШИЕ. ПЕРЕХОД
CJP А=PSNOR,МАР=РЕ,ALUS=DZ,FUNC=AND,Н,ALUD=В,RВ=WR2
;
;ВСЕ 52+52=104 РАЗРЯДА МАНТИССЫ = ЗНАКУ <0. СЧИТАЕМ, ЧТО 1-Й "0" - ЗА ПОСЛЕДНИМ
;ЗНАЧАЩИМ РАЗРЯДОМ! КОРРЕКТИРУЕМ ПОРЯДОК НА -104.
DBLNEG:CONT А=MN104,DSRC=PROM,ALUS=DZ,Н,ALUD=В,RВ=WR3
CJS А=CALCOR,МАР=РЕ,SТОРС=CLRM,СЕМ
CJP А=PSNOR,МАР=РЕ,COND=СТ,SТОРС=MZ
;В СТ.РАЗРЯДЫ МАНТИССЫ ПИШЕМ САМОЕ БОЛЬШОЕ ОТР.ЧИСЛО; В МЛ.РАЗРЯДЫ - 0
CONT А=В53,DSRC=PROM,ALUS=DZ,Н,ALUD=В,RВ=А
CJP А=PSNOR,МАР=РЕ,ALUS=DZ,FUNC=AND,Н,ALUD=В,RВ=WR2
END
А/Х:PROG;
;     I=========I
;     I  А / Х  I
;     I=========I
;
;РЭ:0-1,15ИР:0,ПИА:0,КОП-0Е
;PROM:00Е,40Е
;===== АРИФМЕТИЧЕСКОЕ ДЕЛЕНИЕ ЧИСЕЛ С ПЛАВАЮЩЕЙ ЗАПЯТОЙ
;   ПОСЛЕ ЧТЕНИЯ ОПЕРАНДА:
;   WR1 - МАНТИССА А;  WR6 - ПОРЯДОК А;  WR3 - ПРИЗНАК ЗНАКА;
;   Q   - МАНТИССА Х;  WR5 - ПОРЯДОК Х.
;
EXTERN А/Х,MDXSUB,NORMA,INT33,ZERMA,NORND
;М.А. - В АИСП, ИАОП; ИДЕМ НА П/П ПОДГОТОВКИ МАНТИСС И ПОРЯДКОВ А,Х
CJS А=MDXSUB,МАР=РЕ,%МА%
;
;МАНТ.= 0? ДЕЛАЕМ ДОП.КОД ПОРЯДКА ДЕЛИТЕЛЯ, ЧТОБ СВЕСТИ К СЛОЖЕНИЮ ПОРЯДКОВ
;ДЛЯ ДЕЛЕНИЯ ВЫРАВНИВАЮТСЯ СТ.ЕДИНИЦЫ ДЕЛИМОГО И ДЕЛИТЕЛЯ,Т.Е.РЕЗ-Т=1.ХХХ,
DIVX:CONT А=В54,DSRC=PROM,ЕСА,WRA,ARA=RG3,ALUS=АВ,RА=EXPN,RВ=WR5,FUNC=EXOR
>,Н,ALUD=В
CJP А=ZERMA,МАР=РЕ,COND=СТ,SТОРС=NZ,ЕСА,ARA=RG3,ALUS=DА,RА=WR5,FUNC=ADD,Н
>,ALUD=В,RВ=WR5
CJP А=PSDIV,МАР=РЕ,COND=СТ,SТОРС=/MZ,ALUS=ZQ,Н,ALUD=В,RВ=А
;ДЕЛИТЕЛЬ = 0. ЕСЛИ НЕТ БПП - INT33, ИНАЧЕ - НА ВЫХОД!
CJP А=INT33,МАР=РЕ,COND=OVRIB,IСС
CJP А=NORND,МАР=РЕ,SТОРС=SETNV,CEN
;          "ДЕЛЕНИЕ СО СДВИГОМ ОСТАТКА"
;  (ДЛЯ ПОЛОЖИТЕЛЬНЫХ ЧИСЕЛ С ПЛАВАЮЩЕЙ ЗАПЯТОЙ)
;       У - ДЕЛИМОЕ      Q - РЕЗ-Т
;   I----------------I-----------------I  ДВОЙНОЙ СДВИГ ВЛЕВО У+Q 53 РАЗА
;   I   I////////////I   I/////////////I  <------ С ЗАГРУЗКОЙ В РГ.Q 0 ИЛИ 1
; - I----------------I-----------------I
;   I   I////////////I
;   I----------------I
;      А - ДЕЛИТЕЛЬ
;
;СОБСТВЕННО ДЕЛЕНИЕ
PSDIV:CONT А=М6В,DSRC=PROM,ALUS=DZ,Н,ALUD=Q
;ЛЕВАЯ "1" ДЕЛИМОГО - В WR4, ЛЕВАЯ "1" ДЕЛИТЕЛЯ - В WR2
CONT ALUS=DQ,FUNC=AND,Н,ALUD=ВА,RА=А,RВ=WR2,ALU,DSRC=LOS
CONT ALUS=DQ,FUNC=AND,Н,ALUD=ВА,RА=WR1,RВ=WR4,ALU,DSRC=LOS
;НА СКОЛЬКО ДВОИЧНЫХ ПОРЯДКОВ ДЕЛИМОЕ > ДЕЛИТЕЛЯ: WR4 - WR2 -> В WR4
CONT ALUS=АВ,RА=WR2,RВ=WR4,FUNC=SUBR,Н,CI,ALUD=В,ALU,CEN
CJP А=PDT,МАР=РЕ,COND=СТ,SТОРС=/NN,ALUS=ZА,RА=WR1,Н,ALUD=В,RВ=У
;WR4 - WR2 < 0: СДВИГАЕМ ДЕЛИТЕЛЬ, ЧТОБ ВЫРОВНЯТЬ СТ.ЕДИНИЦЫ ДЛЯ ДЕЛЕНИЯ.
CONT А=В11,DSRC=PROM,ALUS=DА,RА=WR4,FUNC=ADD,Н,ALU,YDST=PSHIFT
CONT ALUS=DZ,Н,ALUD=ВА,RА=А,RВ=А,ALU,DSRC=SHIFT,SHF=LOG
;КОРРЕКТИРУЕМ ПОРЯДОК ДЕЛИТЕЛЯ!
CONT ALUS=DZ,Н,ALUD=ВА,RА=WR4,RВ=WR4,ALU,DSRC=SHIFT,MPS,PSHF=SН0-53,SHF=ARIPH
CONT ALUS=АВ,RА=WR4,RВ=WR5,FUNC=SUBR,Н,CI,ALUD=В,CEN
CJP А=ZERMA,МАР=РЕ,COND=СТ,SТОРС=NV
;
;ЛЕВАЯ "1" ДЕЛИТЕЛЯ (СДВИНУТОГО, ЕСЛИ НАДО!) - В WR2
PDT:CONT ALUS=DQ,FUNC=AND,Н,ALUD=ВА,RА=А,RВ=WR2,ALU,DSRC=LOS
;ЧИСЛО ЦИКЛОВ ДЕЛЕНИЯ = 64 - ПЛЕ ДЕЛИТЕЛЯ -> В РГ/СЧ; ДЛЯ НОРМ.ЧИСЛА = 64-11=53.
CONT А=С64,DSRC=PROM,ALUS=DА,RА=WR2,FUNC=SUBS,Н,CI,ALU,RLD
;Т.К.ДЛЯ ПОЛУЧЕНИЯ МАХ ЧИСЛА ТОЧНЫХ ЦИФР В АЛГОРИТМЕ ДЕЛЕНИЯ ВЫРАВНИВАЕМ
;СТ.ЕДИНИЦЫ ДЕЛИМОГО И ДЕЛИТЕЛЯ, РЕЗ-Т Д.БЫТЬ = "1" (ИЛИ 1/2*2**2 ДЛЯ ПЗ).
;А У НАС РЕЗ-Т = ЧИСЛУ ЦИФР ДЕЛИТЕЛЯ, И "1" ОСТАЕТСЯ НА МЕСТЕ СТ.ЕГО СТ.ЕДИНИЦЫ.
;ПОЭТОМУ КОРРЕКТИРУЕМ ПОРЯДОК, НА СКОЛЬКО СТ.ЕДИНИЦА ДЕЛИТЕЛЯ (ГДЕ РЕЗ-Т),
;ОТЛИЧАЕТСЯ ОТ 1/2*2**1.
;НА СКОЛЬКО МАНТИССА МЕНЬШЕ, НА СТОЛЬКО ПОРЯДОК БОЛЬШЕ!
;
;        ПЛЕ ДЕЛИТЕЛЯ:  0     10   11 12                 51   52    63
;                    --------------------------------------------------
;                    I 64     54 I 53 52                 13 I 12    1 I
;                    --------------------------------------------------
;  ЧИСЛО ЦИКЛОВ ДЕЛЕНИЯ = 64-ПЛЕ:  53 52                 13   12    1
;КОРРЕКЦИЯ ПОРЯДКА ДЛЯ НЕНОРМАЛИЗОВАННОГО
;ДЕЛИТЕЛЯ И РЕЗУЛЬТАТА = ПЛЕ - 11:  0  1                 40   41    52
;
CONT А=С11,DSRC=PROM,ALUS=DА,RА=WR2,FUNC=SUBR,Н,CI,ALUD=В,RВ=WR2
CONT ALUS=DZ,Н,ALUD=ВА,RА=WR2,RВ=WR2,ALU,DSRC=SHIFT,MPS,PSHF=SН0-53,SHF=ARIPH
CONT ALUS=АВ,RА=WR2,RВ=WR5,FUNC=ADD,Н,ALUD=В
;
;ДЕЛИМ
PUSH IСС,ALUS=DZ,FUNC=AND,Н,ALUD=Q
;У - А -> У
CONT ALUS=АВ,RА=А,RВ=У,FUNC=SUBR,Н,CI,ALUD=В,CEN
;ПЕРЕХОД, ЕСЛИ РАЗНИЦА < 0
CJP А=NEG,МАР=РЕ,COND=СТ,SТОРС=NN
;>,=0: СДВИГАЕМ ВЛЕВО У+Q С ЗАГРУЗКОЙ В Q "1"
TWB А=ENDCKL,МАР=РЕ,IСС,ALUS=ZА,RА=У,Н,ALUD=LSBQ,RВ=У,SHMUX=LDLND
;<0: ВОССТАНАВЛИВАЕМ У, СДВИГАЕМ ВЛЕВО У+Q С ЗАГРУЗКОЙ В Q "0"
NEG:TWB А=ENDCKL,МАР=РЕ,IСС,ALUS=АВ,RА=А,RВ=У,FUNC=ADD,Н,ALUD=LSBQ
>,SHMUX=ASLLS
;
;СДВИГАЕМ ДЕЛИМОЕ+РЕЗУЛЬТАТ 1 РАЗ ОБРАТНО
ENDCKL:CONT ALUS=ZА,RА=У,Н,ALUD=RSBQ,RВ=У,SHMUX=LDLD,CEN
;МАНТИССУ РЕЗ-ТА - В РГ.WR1 ДЛЯ П/П НОРМАЛИЗАЦИИ; В РМР ОСТАЕТСЯ ОСТАТОК
CONT ALUS=ZQ,Н,ALUD=ВА,RА=WR3,RВ=WR1,ALU,DDEV=STATUS,WRD,SТОРС=0,СЕМ
CJP А=NORMA,МАР=РЕ,COND=СТ,SТОРС=/MN,FFCNT=SETTR1
;ДЕЛАЕМ ДОП.КОД РЕЗУЛЬТАТА (54 РАЗРЯДА!) И ОСТАТКА
CONT А=M54R,DSRC=PROM,ALUS=DА,RА=WR1,FUNC=EXOR,Н,ALUD=В,RВ=WR1
CONT ALUS=ZВ,RВ=WR1,Н,CI,ALUD=В
CONT А=M54R,DSRC=PROM,ALUS=DА,RА=У,FUNC=EXOR,Н,ALUD=В,RВ=У
CJP А=NORMA,МАР=РЕ,ALUS=ZВ,RВ=У,Н,CI,ALUD=В
;
;
А/XF:ENTRY
;РЭ:0,15ИР:1,ПИА:0,КОП-0Е
;PROM:20Е
;===== ДЕЛЕНИЕ ЧИСЕЛ С ПЗ ИЗ МАГАЗИНА В НР
CJP А=PSFE,МАР=РЕ,ALUS=ZQ,CEN
;
А/XFE:ENTRY
;РЭ:1,15ИР:1,ПИА:0,КОП-0Е
;PROM:60Е
;===== ДЕЛЕНИЕ ЧИСЕЛ С ПЗ ИЗ МАГАЗИНА В РЭ
;
;ПЕРЕХОД, ЕСЛИ АК НЕ 0; МАТ.АДРЕС ДЛЯ МАГАЗИННОГО ОБР.(15ИР-1) - В АИСП,ИАОП
CONT А=М15,DSRC=PROM,ALUS=DQ,FUNC=AND,CEN
PSFE:CJP А=А/Х,МАР=РЕ,COND=СТ,SТОРС=/NZ
;МАГАЗИННОЕ ОБРАЩЕНИЕ НА ЧТЕНИЕ
CJS А=MDXSUB,МАР=РЕ,%ARF%
CJP А=DIVX,МАР=РЕ,ALUS=ZА,RА=DADR,ALU,CSM,WЕМ,MNSA=МР,MODNM=SР
END
EDX:PROG;
;     I=========I
;     I  Е + Х  I
;     I=========I
;
;РЭ:0-1,15ИР:0,ПИА:0,КОП-14
;PROM:014,414
;===== СЛОЖЕНИЕ ПОРЯДКА С КОДОМ ПОРЯДКА ОПЕРАНДА
;   WR1 - МАНТИССА СУММАТОРА; WR6 = ПОРЯДОК А; WR5 - ПОРЯДОК ОПЕРАНДА
;
EXTERN EDX,LCW,NORMA
CJS А=SUBEDX,МАР=РЕ,%МА%
CJP А=NORMA,МАР=РЕ,FFCNT=CLRTR1
;
SUBEDX:ENTRY
;МАНТИССУ СУММАТОРА - В WR1
CONT А=ММ,%PHAR%,DSRC=PROM,ALUS=DА,RА=А,FUNC=AND,Н,ALUD=В,RВ=WR1,CEN
>,FFCNT=CLRTR1
;ЗНАК МАНТИССЫ - В РГ.М
CONT ALUS=DZ,Н,ALUD=ВА,RА=WR1,RВ=WR3,ALU,DSRC=SHIFT,MPS,PSHF=SН0-11,SHF=LOG,СЕМ
;ПОРЯДОК РГ.А - В WR6 ( - СДВИГ ШКАЛЫ ПОРЯДКОВ!)
PUSH А=LCW,МАР=РЕ,ALUS=АВ,RА=EXPN,RВ=А,FUNC=AND,Н,ALUD=Q
LООР А=В64,COND=ARBRDY,DSRC=PROM,ALUS=DQ,FUNC=ADD,Н,ALUD=В,RВ=WR6
;ПОРЯДОК ОПЕРАНДА - В WR5 ( - СДВИГ ШКАЛЫ)
CONT ЕСА,ARA=RG2,ALUS=DА,RА=EXPN,FUNC=AND,Н,ALUD=В,RВ=WR5
CRTN А=В64,DSRC=PROM,ALUS=DА,RА=WR5,FUNC=ADD,Н,ALUD=В,RВ=WR5,FFCNT=MULGRP
;
;
EDXF:ENTRY
;РЭ:0,15ИР:1,ПИА:0,КОП-14
;PROM:214
;===== СЛОЖЕНИЕ ПОРЯДКА С КОДОМ  ИЗ МАГАЗИНА В НР
CJP А=PSFE,МАР=РЕ,ALUS=ZQ,CEN
;
EDXFE:ENTRY
;РЭ:1,15ИР:1,ПИА:0,КОП-14
;PROM:614
;===== СЛОЖЕНИЕ ПОРЯДКА С КОДОМ  ИЗ МАГАЗИНА В РЭ
;
;ПЕРЕХОД, ЕСЛИ АК НЕ 0; МАТ.АДРЕС ДЛЯ МАГАЗИННОГО ОБР.(15ИР-1) - В АИСП,ИАОП
CONT А=М15,DSRC=PROM,ALUS=DQ,FUNC=AND,CEN
PSFE:CJP А=EDX,МАР=РЕ,COND=СТ,SТОРС=/NZ
;МАГАЗИННОЕ ОБРАЩЕНИЕ НА ЧТЕНИЕ
CJS А=SUBEDX,МАР=РЕ,%ARF%
CJP А=NORMA,МАР=РЕ,ALUS=ZА,RА=DADR,ALU,CSM,WЕМ,MNSA=МР,MODNM=SР,FFCNT=CLRTR1
END
ЕВХ:PROG;
;     I=========I
;     I  Е - Х  I
;     I=========I
;
;РЭ:0-1,15ИР:0,ПИА:0,КОП-15
;PROM:015,415
;===== ВЫЧИТАНИЕ ИЗ ПОРЯДКА КОДА
;   WR1 - МАНТИССА СУММАТОРА; WR2 = 0 (МЛ.Р.МАНТИССЫ); WR6 - ПОРЯДОК СУММАТОРА
;
EXTERN ЕВХ,SUBEDX,NORMA
;М.А. - В АИСП, ИАОП
CJS А=SUBEDX,МАР=РЕ,%МА%
CJP А=NORMA,МАР=РЕ,ALUS=ZВ,RВ=WR5,FUNC=SUBS,Н,CI,ALUD=В,FFCNT=CLRTR1
;
EBXF:ENTRY
;РЭ:0,15ИР:1,ПИА:0,КОП-15
;PROM:215
;===== ВЫЧИТАНИЕ ИЗ ПОРЯДКА КОДА МАГАЗИННОЕ В НР
CJP А=PSFE,МАР=РЕ,ALUS=ZQ,CEN
;
EBXFE:ENTRY
;РЭ:1,15ИР:1,ПИА:0,КОП-15
;PROM:615
;===== ВЫЧИТАНИЕ ИЗ ПОРЯДКА КОДА МАГАЗИННОЕ В НР
;
;ПЕРЕХОД, ЕСЛИ АК НЕ 0; МАТ.АДРЕС ДЛЯ МАГАЗИННОГО ОБР.(15ИР-1) - В АИСП,ИАОП
CONT А=М15,DSRC=PROM,ALUS=DQ,FUNC=AND,CEN
PSFE:CJP А=ЕВХ,МАР=РЕ,COND=СТ,SТОРС=/NZ
;МАГАЗИННОЕ ОБРАЩЕНИЕ НА ЧТЕНИЕ
CJS А=SUBEDX,МАР=РЕ,%ARF%
CJP А=NORMA,МАР=РЕ,ALUS=ZВ,RВ=WR5,FUNC=SUBS,Н,CI,ALUD=ВА,RА=DADR,ALU,CSM,WЕМ
>,MNSA=МР,MODNM=SР,FFCNT=CLRTR1
END
EDN:PROG;
;     I=========I
;     I  Е + N  I
;     I=========I
;
;РЭ:0-1,15ИР:0-1,ПИА:0,КОП-1С,ОСН.И ДОП.ФОРМАТ
;PROM:81С,А1С,41С,61С
;===== СЛОЖЕНИЕ ПОРЯДКА С АДРЕСОМ
;   WR1 - МАНТИССА СУММАТОРА; WR6 - ПОРЯДОК СУММАТОРА; WR5 - СДВИНУТЫЙ АДРЕС
;
EXTERN NORMA,LCW
;М.А. - В АИСП, ИАОП
CJS А=SUBEDN,МАР=РЕ,CSM,MNSA=IRA,ALUS=DQ,FUNC=ADD,ALUD=В,RВ=DADR,ALU,ЕСВ
>,WRB,BRA=RG3
CJP А=NORMA,МАР=РЕ,FFCNT=CLRTR1
;
SUBEDN:ENTRY
;МАНТИССУ СУММАТОРА - В WR1
CONT А=ММ,DSRC=PROM,ALUS=DА,RА=А,FUNC=AND,Н,ALUD=В,RВ=WR1,CEN,FFCNT=CLRTR1
;ЗНАК МАНТИССЫ - В РГ.М
CONT ALUS=DZ,Н,ALUD=ВА,RА=WR1,RВ=WR3,ALU,DSRC=SHIFT,MPS,PSHF=SН0-11,SHF=LOG,СЕМ
;ПОРЯДОК РГ.А - В WR6 ( - СДВИГ ШКАЛЫ ПОРЯДКОВ!)
LDCT А=LCW,МАР=РЕ,ALUS=АВ,RА=EXPN,RВ=А,FUNC=AND,Н,ALUD=Q
PSEDN:ENTRY
CONT А=В64,DSRC=PROM,ALUS=DQ,FUNC=ADD,Н,ALUD=В,RВ=WR6
;СДВИНУТЫЙ АДРЕС - В WR5 ( - СДВИГ ШКАЛЫ)
CJP А=SUBEML,МАР=РЕ,COND=EMLRG,ЕСВ,BRA=RG3,DSRC=SHIFT,MPS,PSHF=SН0-53,SHF=LOG
>,ALUS=DZ,Н,ALUD=В,RВ=WR5,FFCNT=MULGRP
RETSUB:CRTN А=В64,DSRC=PROM,ALUS=DА,RА=WR5,FUNC=ADD,Н,ALUD=В,RВ=WR5
>,FFCNT=MULGRP
;РЭ: СДВИГАЕМ 7-Й РАЗР. В 64-Й
SUBEML:CONT ЕСВ,BRA=RG3,DSRC=SHIFT,MPS,PSHF=SН0-57,SHF=LOG,ALUS=DZ,Н
>,ALUD=В,RВ=WR5
;РАСХЛАПЫВАЕМ
CJP А=RETSUB,МАР=РЕ,ALUS=DZ,Н,ALUD=ВА,RА=WR5,RВ=WR5,ALU,DSRC=SHIFT,MPS
>,PSHF=SН0+4,SHF=EMULF
END
EBN:PROG;
;     I=========I
;     I  Е - N  I
;     I=========I
;
;РЭ:0-1,15ИР:0-1,ПИА:0,КОП-1D,ОСН.И ДОП.ФОРМАТ
;PROM:81D,A1D,41D,61D
;===== ВЫЧИТАНИЕ ИЗ ПОРЯДКА АДРЕСА
;   WR1 - МАНТИССА СУММАТОРА; WR6 - ПОРЯДОК СУММАТОРА; WR5 - СДВИНУТЫЙ АДРЕС
;
EXTERN SUBEDN,NORMA
;М.А. - В АИСП, ИАОП
CJS А=SUBEDN,МАР=РЕ,CSM,MNSA=IRA,ALUS=DQ,FUNC=ADD,ALUD=В,RВ=DADR,ALU,ЕСВ
>,WRB,BRA=RG3
;ДЕЛАЕМ ДОП.КОД АДРЕСА
CJP А=NORMA,МАР=РЕ,ALUS=ZВ,RВ=WR5,FUNC=SUBS,Н,CI,ALUD=В,FFCNT=CLRTR1
END
УТА:PROG;
;     I=========I
;     I  У Т А  I
;     I=========I
;
;РЭ:0,15ИР:0-1,ПИА:0,КОП:19;81, ДОП.ФОРМАТ
;PROM:881,А81,419,619
;===== ВЫДАЧА РМР В ЗАВИСИМОСТИ ОТ ПРИЗНАКОВ ГРУПП
;   СЛ.1 = 1     СЛ.0 = 1
;
EXTERNAL LCW,PSEDN,NORMA,YTALOG
;УСТАНАВЛИВАЕМ СЛ.0,1 = 1; ВЫЧИСЛЯЕМ АИСП.
CONT FFCNT=SETTR1,CSM,MNSA=IRA,ALUS=DQ,FUNC=ADD,ALUD=В,RВ=DADR
>,ALU,ЕСВ,WRB,BRA=RG3
LDCT А=LCW,МАР=РЕ,FFCNT=SETTR0
;ПЕРЕХОДИМ ПО ПНА ГРУПП
CJP МАР=GRP
;
;===== ПРОДОЛЖЕНИЕ ПОСЛЕ ПЕРЕХОДА ПО ПНА
YTAD:ENTRY
;ГРУППА СЛОЖЕНИЯ/УМНОЖЕНИЯ
;WR1 - МАНТИССА РЕЗ-ТА; WR6 - ПОРЯДОК СУММАТОРА; WR5 - СДВИНУТЫЙ АДРЕС
;МАНТИССУ СУММАТОРА - В WR1
CONT А=ММ12,DSRC=PROM,ALUS=DА,RА=У,FUNC=AND,Н,ALUD=В,RВ=WR1,SТОРС=CLRM,СЕМ
;ДАЛЬШЕ - КАК В КОМАНДЕ Е+N
CJS А=PSEDN,МАР=РЕ,ALUS=АВ,RА=EXPN,RВ=А,FUNC=AND,Н,ALUD=Q
CJP А=NORMA,МАР=РЕ,FFCNT=CLRTR1
;
YTAL:ENTRY
;
;ВРЕМЕННО!!! ОТДЕЛЯЕМ СЛУЧАЙ, КОГДА ВСЕ ПРИЗНАКИ ГРУПП = 0!!!
CONT DSRC=CNT,ALUS=DZ,ALUD=В,RВ=WR4
CONT А=Н1С,DSRC=PROM,ALUS=DА,RА=WR4,FUNC=AND,CEN
;ЕСЛИ ВСЕ ПР.ГРУПП = 0 - ГРУППА СЛОЖЕНИЯ/УМНОЖЕНИЯ!
CJP А=YTAD,МАР=РЕ,COND=СТ,SТОРС=NZ
;ИНАЧЕ - ДЕЙСТВИТЕЛЬНО ЛОГИЧЕСКАЯ ГРУППА!
CONT ALUS=ZА,RА=У,Н,ALUD=В,RВ=А,FFCNT=СНТКК
;
;ЛОГИЧЕСКАЯ ГРУППА
JRP %М%,%RСА%,FFCNT=CLRJMP
END
YTAPNA:PROG;
;===== ПНА-ЗАВИСИМАЯ ЧАСТЬ КОМАНДЫ УТА
;   СИГНАЛЫ, УПРАВЛЯЮЩИЕ ВЫБОРКОЙ ИЗ ПНА ГРУПП:
;   СЛ1, СЛ0, ГС, ГУ, ГЛ
;    1    1    Х   Х   Х  -  ДЛЯ КОМАНДЫ УТА
;   ЕСЛИ ОДНОВРЕМЕННО УСТАНОВЛЕНЫ НЕСКОЛЬКО ПРИЗНАКОВ ГРУПП, ТО ПРИОРИТЕТ
;   ИМЕЕТ ГРУППА СЛОЖЕНИЯ (ГС); ЕСЛИ ЕЕ НЕТ - ГРУППА УМНОЖЕНИЯ (ГУ)
;
EXTERNAL YTAD,YTAL
;
YTAADD:ENTRY
;===== ГРУППА СЛОЖЕНИЯ (ГС) ДЛЯ КОМАНДЫ УТА
;   ВОЗМОЖНЫЕ КОМБИНАЦИИ ВХОДНЫХ СИГНАЛОВ:
;   1 1 1 0 0  =  1С Н
;   1 1 1 0 1  =  1D Н
;   1 1 1 1 0  =  1Е Н
;   1 1 1 1 1  =  1F Н
;PROM:181С,181D,181Е,181F
;
YTAZER:ENTRY
;===== ВСЕ ПРИЗНАКИ ГРУПП = 0 ДЛЯ КОМАНДЫ УТА - КАК АРИФМЕТИЧЕСКАЯ ГРУППА!!
;   1 1 0 0 0  =  18 Н
;PROM:1818
;
YTAMUL:ENTRY
;===== ГРУППА УМНОЖЕНИЯ (ГУ) ДЛЯ КОМАНДЫ УТА
;   ВОЗМОЖНЫЕ КОМБИНАЦИИ ВХОДНЫХ СИГНАЛОВ:
;   1 1 0 1 0  =  1А Н
;   1 1 0 1 1  =  1В Н
;PROM:181А,181В
;ПРИ ГРУППЕ СЛОЖЕНИЯ И УМНОЖЕНИЯ ПЕРЕДАЮТСЯ 1-52Р.РГ.У, 52-Й ГАСИТСЯ. И НА Е+N!
CJP А=YTAD,МАР=РЕ
;
YTALOG:ENTRY
;===== ЛОГИЧЕСКАЯ ГРУППА (ГЛ) ДЛЯ КОМАНДЫ УТА
;   ВОЗМОЖНЫЕ КОМБИНАЦИИ ВХОДНЫХ СИГНАЛОВ:
;   1 1 0 0 1  =  19 Н
;PROM:1819
;ПРОИСХОДИТ ПЕРЕСЫЛКА РМР В РГ.А ПОЛНОСТЬЮ!
;
;ВРЕМЕННО ЗАМЕНЯЕМ!!!
;CJP А=YTAL,МАР=РЕ,ALUS=ZА,RА=У,Н,ALUD=В,RВ=А,FFCNT=СНТКК
CJP А=YTAL,МАР=РЕ
END
*END F
*END FILE
