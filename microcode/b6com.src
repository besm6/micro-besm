*NАМЕ КОМАНДЫ Б-6+ПРИВИЛЕГИРОВАННЫЕ
*TIME:01.00
*DISC:664/SYSTEM,microb
*FILE:arc,30,W
*file:lib,67,r
*LIBRA:23,25
*CALL ficMEMORY
*PERSO:67
*NО LIST
*CALL MICROB
;
;СПИСОК ВОЗМОЖНЫХ ЗНАЧЕНИЙ ПОЛЕЙ:
;--------------------------------
;
FLIST SQI=JZ,CJS,JМАР,CJP,PUSH,JSRP,CJV,JRP,RFCT,RРСТ,CRTN,CJPP,LDCT,LООР,
      CONT,TWB
FLIST МАР=РЕ,GRP,МЕ
FLIST ALUD=Q,NО,ВА,В,RSBQ,RSB,LSBQ,LSB
FLIST FUNC=ADD,SUBR,SUBS,OR,AND,NOTRS,EXOR,EXNOR
FLIST ALUS=AQ,АВ,ZQ,ZВ,ZА,DА,DQ,DZ
FLIST RА=А,У,INTR,РС,РССОРУ,DADR,HALF,ВУТЕ,EXPN,MANT,WR6,WR5,WR4,WR3,WR2,WR1
FLIST RВ=А,У,INTR,РС,РССОРУ,DADR,HALF,ВУТЕ,EXPN,MANT,WR6,WR5,WR4,WR3,WR2,WR1
FLIST CI=CI0,CI1,CIX,CIC
FLIST SHMUX=LSLSL,NSNSL,ASLLS,NDNS,CDLDL,ADNDL,LDLD,LDLND,RSLRSL,RSSRSS,
      RSRS,IDCS,RDSRDS,RDLRDL,VDCD,RDRD
FLIST SТОРС=NС,NN,NZ,NV,/NС,/NN,/NZ,/NV,МС,MN,MZ,MV,/МС,/MN,/MZ,/MV,
      IС,IN,IZ,IV,/IС,/IN,/IZ,/IV,SETNC,SETNN,SETNZ,SETNV,
      УТОМ,MTON,SЕТМ,SETN,MCN,NСМ,CLRM,CLRN,INVM,CLRNC,CLRNN,CLRNZ,CLRNV
FLIST MNSA=U,IRA,МР
FLIST MODNM=М0,М1,М2,М3,М4,М5,М6,М7,М8,М9,М10,М11,М12,М13,М14,SР,С,RR,
      RRR,SPRADR,СТТ,ACL,ACR,YCL,YCR,РСС,РССС,CTL,CTR,SVFA,PROCNC,MREZ
FLIST DSRC=MODGN,PROCN,CNT,PHYSPG,ARBOPC,MULRZ1,MULRZ2,СОМА,SHIFT,ОРС,
      LOS,PROM
FLIST RTIME=RТ
FLIST WTIME=WТ
FLIST YDST=MODGN,PROCN,CNT,PHYSPG,ARBOPC,MUL1,MUL2,ADRREG,PSHIFT,CCLR,BTRCLR
FLIST SHF=CICL,LOG,ARIPH,EMULF
FLIST ARBI=CCRD,CCWR,DCRD,DCWR,FЕТСН,DRD,DWR,RDMWR,BTRWR,BTRRD,BICLR,BIRD
FLIST CYSTR=NТ3,NТ4,NТ5,NТ6,NТ7,NТ8,NТ9,NТ10
FLIST BRA=RG0,RG1,RG2,RG3
FLIST ARA=RG0,RG1,RG2,RG3
FLIST YDEV=PHYSAD,RADRR,PSMEM,МРМЕМ,SТОРМ0,SТОРМ1
FLIST YDEVT=ECBTAG
FLIST DDEV=ВВ,MODB,CLRCD,STATUS,РРМЕМ0,РРМЕМ1
FLIST YDTIM=CTIME
FLIST FFCNT=LOGGRP,MULGRP,ADDGRP,SЕТС,CLRRCB,SETRCB,CLRJMP,SETJMP,
      SETEI,CLREI,CLRTR0,SETTR0,CLRTR1,SETTR1,CLRCT,CLRCTT,CLRTKK,SЕТТКК,
      STRTLD,СНТКК,SETHLT,CLRINT,CLRRUN,LDCPMP,PRGINT,EXTINT,SETNR,SETER
FLIST MPADR=ADRB1,ADRB2,ADRB3,ADRB4,DАТАВ1,DАТАВ2,DАТАВ3,DАТАВ4,
      DАТАВ5,DАТАВ6,DАТАВ7,DАТАВ8,INFB1,INFB2,FМР,FСР
FLIST COND=YES,NORMB,RNDB,OVRIB,BNB,OVRFTB,DRG,EMLRG,RСВ,СВ,CEMLRG,
      СТ,TR1,INTSTP,RUN,NMLRDY,INT,FULMEM,ARBRDY,СРМР,IR15,ТККВ
;
;СПИСОК ОШИБОЧНЫХ КОМБИНАЦИЙ ПОЛЕЙ
;---------------------------------
;
;ШИНА D:
;-------
;
;ИСТОЧНИКИ: DSRC
;           CSM-WЕМ
;           ЕСА-WRA
;           DDEV-WRD
;
СНЕСК DSRC+CSM-WЕМ
СНЕСК DSRC+ЕСА-WRA
СНЕСК DSRC+DDEV-WRD
СНЕСК CSM-WЕМ+ЕСА-WRA
СНЕСК CSM-WЕМ+DDEV-WRD
СНЕСК ЕСА-WRA+DDEV-WRD
;
;ШИНА У:
;-------
;
;ИСТОЧНИКИ: YDEVT-WRB
;           ALU
;           ЕСВ-WRB
;           YDEV-WRY
;           YDTIM+RTIME
;
СНЕСК YDEVT-WRB+ALU
СНЕСК YDEVT-WRB+ЕСВ-WRB
СНЕСК YDEVT-WRB+YDEV-WRY
СНЕСК YDEVT-WRB+YDTIM+RTIME
СНЕСК ALU+ЕСВ-WRB
СНЕСК ALU+YDEV-WRY
СНЕСК ALU+YDTIM+RTIME
СНЕСК ЕСВ-WRB+YDEV-WRY
СНЕСК ЕСВ-WRB+YDTIM+RTIME
СНЕСК YDEV-WRY+YDTIM+RTIME
;
;     ==================
;     МАКРОСЫ ДЛЯ ТЕСТОВ
;     ==================
;
MACRO TN DSRC=PROM,ALUS=DZ,Н,ALUD=В,RВ=EXPN,ALU,YDEV=МРМЕМ,WRY,IОМР,MPADR=0
MACRO Т DSRC=PROM,ALUS=DZ,ALU,YDEV=МРМЕМ,WRY,IОМР,MPADR=0
MACRO TIK ALUS=ZВ,RВ=MANT,Н,CI,ALUD=В,ALU,YDEV=МРМЕМ,WRY,IОМР,MPADR=3
MACRO TIK3 ALUS=ZВ,RВ=WR3,Н,CI,ALUD=В,ALU,YDEV=МРМЕМ,WRY,IОМР,MPADR=3
MACRO CLRTYP ALUS=DZ,FUNC=AND,Н,ALUD=В,RВ=INTR,ALU,YDEV=МРМЕМ,WRY,IОМР,MPADR=1
MACRO ТУРЕ ALUS=ZВ,RВ=INTR,Н,CI,ALUD=В,ALU,YDEV=МРМЕМ,WRY,IОМР,MPADR=1
MACRO С3 ALUS=DZ,FUNC=AND,Н,ALUD=В,RВ=WR3,ALU,YDEV=МРМЕМ,WRY,IОМР,MPADR=3
;
;     ==================
;     МАКРОСЫ ДЛЯ КОМАНД
;     ==================
;
MACRO МА CSM,MNSA=IRA,ALUS=DQ,FUNC=ADD,ALUD=В,RВ=DADR,ALU,YDST=ADRREG
MACRO MAWF CSM,MNSA=МР,MODNM=SР,ALUS=DZ,ALUD=В,RВ=DADR,ALU,YDST=ADRREG
MACRO ARF CSM,MNSA=МР,MODNM=SР,ALUS=DZ,FUNC=SUBS,ALUD=В,RВ=DADR,ALU,YDST=ADRREG
MACRO SSPRG ЕСВ,WRB,BRA=RG3
MACRO PHAW YDEV=PHYSAD,ЕСВ,WRB,BRA=RG0,ARBI=DWR
MACRO PHAR YDEV=PHYSAD,ЕСВ,WRB,BRA=RG0,ARBI=DRD
MACRO RСА DSRC=СОМА,ALUS=DZ,ALUD=Q
MACRO WА МАР=РЕ,COND=ARBRDY,IСС
MACRO SPRG ЕСВ,BRA=RG3,CSM,WЕМ,MNSA=МР,MODNM=SР
MACRO АС CSM,MNSA=МР,MODNM=С,ALUS=DQ,FUNC=ADD,ALUD=Q
MACRO TRSH DSRC=SHIFT,MPS,PSHF=SН0,SHF=LOG
MACRO R МАР=РЕ,COND=RСВ
MACRO М МАР=МЕ,COND=ТККВ,DDEV=CLRCD,WRD,ISE
MACRO MED МАР=МЕ,COND=ТККВ,LЕТС,ISE
MACRO ML МАР=МЕ,DDEV=CLRCD,WRD,ISE
MACRO МЕ МАР=МЕ,LЕТС
MACRO MI МАР=МЕ,COND=ТККВ,DDEV=CLRC,WRD
;
;
АТХ:PROG;
;      ===================
;      1.КОМАНДЫ ПЕРЕСЫЛОК
;      ===================
;
;АТХ,SТХ,XTS,ХТА
;
;     I=========I
;     I  А Т Х  I
;     I=========I
;
;РЭ:0-1,15ИР:0,ПИА:0,КОП-00
;PROM:000,400
;===== ЗАПИСЬ СУММАТОРА В ПАМЯТЬ ПО ИСП. АДРЕСУ; НЕЗАВИСИМО ОТ РЭ!
;
EXTERN LCW,АТХ
;МАТ.АДРЕС (М + Q) - В АИСП, ИАОП; ФИЗ.АДРЕС - В RG0
CONT %МА%
LDCT А=LCW,МАР=РЕ,%PHAW%,FFCNT=СНТКК
;А - В БОИ: С КОНЦА Ф.А. ДО ТОГО, КАК ПОНАДОБЯТСЯ ДАННЫЕ В RG3 - 300 НСЕК:
;100 - ЗАХВАТ ШИНЫ; 200 - ПЕРЕДАЧА АДРЕСА. ДОЛЖНЫ УСПЕТЬ!!!
WА:CJP А=WА,%WА%,ALUS=ZВ,RВ=WR6,Н,ALUD=ВА,RА=А,ALU,ЕСВ,WRB,BRA=RG3
;ПЕРЕХОД ПО КОП, ПМИ, ТКК КОПИРУЕТСЯ В ППК ПО ПМИ
JRP %М%,FFCNT=CLRJMP,%RСА%
;
;
ATXF:ENTRY
;РЭ:0,15ИР:1,ПИА:0,КОП-00
;PROM:200
;
;ЕСЛИ ДАЖЕ МАГАЗИННОЕ ОРБАЩЕНИЕ, МАТ.АДРЕС ВЫЧИСЛЯЕТСЯ ТАК ЖЕ!
CONT %МА%
;Ф.А.; ПРОВЕРЯЕМ АК
LDCT А=LCW,МАР=РЕ,%PHAW%,FFCNT=СНТКК,ALUS=ZQ,CEN
PSATXF:ENTRY
;СУММАТОР - В БОИ, ПЕРЕХОД, ЕСЛИ НЕ МАГАЗИН
CJP А=WА,МАР=РЕ,COND=СТ,SТОРС=/NZ,ALUS=ZВ,RВ=WR6,Н,ALUD=ВА,RА=А,ALU
>,ЕСВ,WRB,BRA=RG3
;МАГАЗИННОЕ ОБРАЩЕНИЕ: ЖДЕМ ГОТ. АРБИТРА, ЧИТАЕМ АК В Q
WAF:CJP А=WAF,%WА%,%RСА%
;ПЕРЕХОД ПО КОП, 15+1 - В 15 РЕГИСТР, СБРОС ПИА, ПМИ, ТКК КОПИРУЕТСЯ В ППК
JRP %М%,ALUS=ZА,RА=DADR,FUNC=ADD,CI,ALU,CSM,WЕМ,MNSA=МР,MODNM=SР
>,FFCNT=CLRJMP
END
ATXFE:PROG;
;РЭ:1,15ИР:1,ПИА:0,КОП-00
;PROM:600
EXTERNAL LCW,PSATXF
LDCT А=LCW,МАР=РЕ,%МА%
;Ф.А.; ПРОВЕРЯЕМ АК
CONT А=М15,%PHAW%,DSRC=PROM,ALUS=DQ,FUNC=AND,CEN,FFCNT=СНТКК
CJP А=PSATXF,МАР=РЕ,ALUS=ZВ,RВ=WR6,Н,ALUD=ВА,RА=А,ALU,ЕСВ,WRB,BRA=RG3
END
SТХ:PROG;
;     I=========I
;     I  S Т Х  I
;     I=========I
;
;РЭ:0-1,15ИР:0-1,ПИА:0,КОП-01
;PROM:001,201,401,601
;===== АТХ  -  ЗАПИСЬ СУММАТОРА В ПАМЯТЬ ПО ИСПОЛНИТЕЛЬНОМУ АДРЕСУ
;   15 ХТА  -  ЧТЕНИЕ МАГАЗИННОЕ
;   ВЫПОЛНЯЕТСЯ НЕЗАВИСИМО ОТ ПРИЗНАКА РЭ!
;   ЕСЛИ ИР=15, ПРОИЗВОДИТСЯ НЕМАГАЗИННАЯ(!) ЗАПИСЬ СУММАТОРА ПО АДРЕСУ 15ИР,
;   ЗАТЕМ ОБЫЧНОЕ МАГАЗИННОЕ ЧТЕНИЕ ИЗ МАГАЗИННОЙ ПАМЯТИ.
;
EXTERN LCW
;МАТ.АДРЕС (М + Q) - В АИСП, ИАОП
CONT %МА%
;ФИЗ.АДРЕС - В RG0
CONT %PHAW%
;СУММАТОР - В РГ.РЕЗУЛЬТАТА
PUSH А=LCW,МАР=РЕ,ALUS=ZВ,RВ=WR6,Н,ALUD=ВА,RА=А,ALU,ЕСВ,WRB,BRA=RG3
;МАТ.АДРЕС (15ИР-1) - В АИСП, WR1 (В ИАОП - ПОЗЖЕ!); ЖДЕМ ГОТ.АРБИТРА
LООР COND=ARBRDY,МАР=РЕ,CSM,MNSA=МР,MODNM=SР,ALUS=DZ,FUNC=SUBS,ALUD=В,RВ=WR1
>,ALU,YDST=ADRREG
CJS А=RDOPER1,МАР=РЕ,SCI,ICI,%PHAR%,ALUS=ZА,RА=WR1,ALUD=В,RВ=DADR,FFCNT=СНТКК
;ПЕРЕХОД ПО КОП; ОПЕРАНД - В СУММАТОР; ПЕРЕПИСЫВАЕМ НОВОЕ ЗНАЧЕНИЕ SР В ОЗУ МОД.
JRP %М%,ЕСА,ARA=RG2,ALUS=DZ,Н,ALUD=ВА,RА=DADR,RВ=А,СЕМ,FFCNT=LOGGRP
>,ALU,CSM,WЕМ,MNSA=МР,MODNM=SР
;
RDOPER:ENTRY
;ФИЗ.АДРЕС - В RG0, СМЕНА ТКК, ЧИТАЕМ АК В Q, ЖДЕМ ГОТ.АРБИТРА, ВОЗВРАТ

LDCT А=LCW,МАР=РЕ,SCI,ICI,%PHAR%,FFCNT=СНТКК
RDOPER1:ENTRY
CRTN COND=ARBRDY,SCI,%RСА%,FFCNT=CLRJMP
;ДОБАВЛЯЕМ ЕЩЕ 1 МК НА СЛУЧАЙ ПРЕРЫВАНИЯ!
CRTN COND=ARBRDY,SCI
; И ещё парочку
CRTN COND=ARBRDY,SCI
CRTN COND=ARBRDY,SCI
END
XTS:PROG;
;     I=========I
;     I  Х Т S  I
;     I=========I
;
;РЭ:0-1,15ИР:0-1,ПИА:0,КОП-03
;PROM:003,203,403,603
;
;===== 15 АТХ  -  МАГАЗИННАЯ ЗАПИСЬ
;      ХТА  -  ЧТЕНИЕ ПО ИСПОЛНИТ.АДРЕСУ
;
EXTERN LCW,RDOPER,RDOPER1
;ПИШЕМ М.А.НА ЗАПИСЬ
CONT %MAWF%
;ФИЗ. АДРЕС - В RG0; ПЕРЕПИСЫВАЕМ НОВОЕ ЗНАЧЕНИЕ SР - В WR5; ПЕРЕХОД, ЕСЛИ ИР15
CJP А=XTSF,МАР=РЕ,COND=IR15,%PHAW%,ALUS=ZА,RА=DADR,CI,ALUD=В,RВ=WR5
;СУММАТОР - В БОИ
PUSH А=LCW,МАР=РЕ,ALUS=ZВ,RВ=WR6,Н,ALUD=ВА,RА=А,ALU,ЕСВ,WRB,BRA=RG3
;МАТ.АДРЕС 2 - НА ЧТЕНИЕ (М + Q) - В АИСП, ИАОП; ЖДЕМ ГОТ.АРБИТРА
LООР COND=ARBRDY,CSM,MNSA=IRA,ALUS=DQ,FUNC=ADD,ALUD=В,RВ=WR1,ALU,YDST=ADRREG
CJS А=RDOPER1,МАР=РЕ,SCI,ICI,%PHAR%,ALUS=ZА,RА=WR1,ALUD=В,RВ=DADR,FFCNT=СНТКК
;ПЕРЕХОД, ПМИ, ЧИТАЕМ ОПЕРАНД В СУММАТОР, ПЕРЕПИСЫВАЕМ НОВЫЙ SР
JRP %М%,ЕСА,ARA=RG2,ALUS=DZ,Н,ALUD=ВА,RА=WR5,RВ=А,СЕМ,FFCNT=LOGGRP
>,ALU,CSM,WЕМ,MNSA=МР,MODNM=SР
;
;
;ИР15: ПРИ 2-ОМ ЧТЕНИИ ПО ИСПОЛНИТ.АДРЕСУ ИСПОЛЬЗУЕТСЯ НОВОЕ ЗНАЧЕНИЕ SР!
;ЗДЕСЬ НЕ ТОРОПИМСЯ (М.А. В АИСП. ПИШЕМ ПОЗЖЕ), ВАРИАНТ КОМАНДЫ РЕДКИЙ!
XTSF:CJP А=XTSF,%WА%,ALUS=ZВ,RВ=WR6,Н,ALUD=ВА,RА=А,ALU,ЕСВ,WRB,BRA=RG3
;МАТ.АДРЕС 2 НА ЧТЕНИЕ: (НОВОЕ ЗНАЧЕНИЕ SР! + Q) - В АИСП, ИАОП
CJS А=RDOPER,МАР=РЕ,ALUS=AQ,RА=WR5,ALUD=В,RВ=DADR,ALU,YDST=ADRREG
;ПЕРЕХОД, ПМИ, ЧИТАЕМ ОПЕРАНД В СУММАТОР, ПЕРЕПИСЫВАЕМ НОВЫЙ SР
JRP %М%,ЕСА,ARA=RG2,ALUS=DZ,Н,ALUD=ВА,RА=WR5,RВ=А,СЕМ,FFCNT=LOGGRP
>,ALU,CSM,WЕМ,MNSA=МР,MODNM=SР
END
ХТА:PROG;
;     I=========I
;     I  Х Т А  I
;     I=========I
;
;РЭ:0-1,15ИР:0,ПИА:0,КОП-08
;PROM:008,408
;===== ЧТЕНИЕ ЧИСЛА ПО ИСПОЛНИТЕЛЬНОМУ АДРЕСУ В СУММАТОР; НЕЗАВИСИМО ОТ РЭ!
;
EXTERN RDOPER,LCW,ХТА,RDOPER1
;М.А. - В АИСП, ИАОП
CJS А=RDOPER,МАР=РЕ,%МА%
;ПЕРЕХОД ПО КОП, ПМИ, ЧИТАЕМ В СУММАТОР
JRP %М%,ЕСА,ARA=RG2,ALUS=DZ,Н,ALUD=В,RВ=А,СЕМ,FFCNT=LOGGRP
;
;
XTAF:ENTRY
;РЭ:0,15ИР:1,ПИА:0,КОП-08
;PROM:208
;===== ЧТЕНИЕ ЧИСЛА ИЗ МАГАЗИНА: 15-1 -> 15; ЧТЕНИЕ ПО 15ИР.
;
;ПЕРЕХОД, ЕСЛИ АК НЕ 0; МАТ.АДРЕС ДЛЯ МАГАЗИННОГО ОБР.(15ИР-1) - В АИСП,ИАОП
LDCT А=LCW,МАР=РЕ,ALUS=ZQ,CEN
PSXTAF:ENTRY
CJP А=ХТА,МАР=РЕ,COND=СТ,SТОРС=/NZ,%ARF%
;МАГАЗИННОЕ ОБРАЩЕНИЕ НА ЧТЕНИЕ; ФИЗ.АДРЕС + МЕНЯЕМ ТКК
CJS А=RDOPER1,МАР=РЕ,SCI,ICI,%PHAR%,FFCNT=СНТКК
;ПЕРЕХОД НА СЛЕД.КОМАНДУ, НОВЫЙ SР НА МЕСТО, ЧИТАЕМ В СУММАТОР, СБРОС ПИА, ПМИ
JRP %М%,ЕСА,ARA=RG2,ALUS=DZ,Н,ALUD=ВА,RА=DADR,RВ=А,СЕМ,FFCNT=LOGGRP
>,ALU,CSM,WЕМ,MNSA=МР,MODNM=SР
END
XTAFE:PROG;
;РЭ:1,15ИР:1,ПИА:0,КОП-08
;PROM:608
EXTERNAL LCW,PSXTAF
LDCT А=LCW,МАР=РЕ,ALUS=ZQ,ALUD=В,RВ=WR6
CJP А=PSXTAF,МАР=РЕ,ALUS=DZ,Н,ALUD=ВА,RА=WR6,RВ=WR6,CEN,ALU,DSRC=SHIFT
>,MPS,PSHF=SН0-49,SHF=LOG
END
ААХ:PROG;
;      ====================
;      3.ЛОГИЧЕСКИЕ КОМАНДЫ
;      ====================
;
;ААХ,АЕХ,ARX,АОХ,АРХ,AUX,АСХ,ANX,ASX,ASN
;
;     I=========I
;     I  А А Х  I
;     I=========I
;
;РЭ:0,15ИР:0,ПИА:0,КОП:09
;PROM:009
;===== ЛОГИЧЕСКОЕ УМНОЖЕНИЕ СУММАТОРА НА ОПЕРАНД
;
EXTERNAL RDOPER,LCW,ААХ,RDOPER1
;М.А. - В АИСП, ИАОП
CJS А=RDOPER,МАР=РЕ,%МА%
;ПЕРЕХОД ПО КОП, ПМИ, ЛОГИЧЕСКОЕ УМНОЖЕНИЕ СУММАТОРА НА ОПЕРАНД
JRP %М%,ЕСА,ARA=RG2,ALUS=DА,RА=А,FUNC=AND,Н,ALUD=В,RВ=А,СЕМ,FFCNT=LOGGRP
;
;
AAXF:ENTRY
;РЭ:0,15ИР:1,ПИА:0,КОП:09
;PROM:209
;
;ПЕРЕХОД, ЕСЛИ АК НЕ 0; ПИШЕМ МАТ.АДРЕС ДЛЯ МАГАЗИННОГО ОБРАЩЕНИЯ
LDCT А=LCW,МАР=РЕ,ALUS=ZQ,CEN
CJP А=ААХ,МАР=РЕ,COND=СТ,SТОРС=/NZ,%ARF%,%SSPRG%
;ЧИТАЕМ ОПЕРАНД ИЗ МАГАЗИНА: ФИЗ.АДРЕС
CJS А=RDOPER1,МАР=РЕ,SCI,ICI,%PHAR%,FFCNT=СНТКК
;ПЕРЕХОД ПО КОП;НОВЫЙ SР ИЗ RG3 НА МЕСТО В ОЗУ МОД.;ОПЕРАНД УМНОЖАЕМ НА СУММАТОР
JRP %М%,ЕСА,ARA=RG2,ALUS=DА,RА=А,FUNC=AND,Н,ALUD=В,RВ=А,СЕМ,FFCNT=LOGGRP
>,ЕСВ,BRA=RG3,CSM,WЕМ,MNSA=МР,MODNM=SР
END
ААХЕ:PROG;
;РЭ:1,15ИР:0,ПИА:0,КОП:09
;PROM:409
;===== ЛОГИЧЕСКОЕ УМНОЖЕНИЕ СУММАТОРА НА ОПЕРАНД В РЭ
;   В П/П LOGER: ПРОВЕРКА СУММАТОРА И ОПЕРАНДА ДЛЯ РЭ;
;   РЕЗУЛЬТАТ: СДВИНУТЫЕ СУММАТОР (WR1) И ОПЕРАНД (WR2), МЛ.16 РАЗРЯДОВ = 0;
;   ДЕЛАЕТСЯ УСТ.ЛОГ.ГРУППЫ, СН ТКК, СБРОС ППУ, ЗАГРУЗКА LCW В РГ/СЧ, АК - В Q
;
EXTERNAL ААХЕ,LOGER
;М.А. - В АИСП, ИАОП
CJS А=LOGER,МАР=РЕ,%МА%,FFCNT=CLRTR1
;ЛОГ.УМНОЖАЕМ СДВИНУТЫЕ СУММАТОР И ОПЕРАНД
CONT ALUS=АВ,RА=WR1,RВ=WR2,FUNC=AND,Н,ALUD=В
PSAAXE:ENTRY
;===== РАСДВИГАЕМ РЕЗУЛЬТАТ ДЛЯ ФОРМАТА РЭ - В РГ.А, ПЕРЕХОД НА СЛЕД.КОМАНДУ
JRP %М%,ALUS=DZ,Н,ALUD=ВА,RА=WR2,RВ=А,СЕМ,ALU,DSRC=SHIFT
>,MPS,PSHF=SН0+4,SHF=EMULF
;
;
AAXFE:ENTRY
;РЭ:1,15ИР:1,ПИА:0,КОП:09
;PROM:609
;
;ПЕРЕХОД, ЕСЛИ АК НЕ 0
CONT А=М15,DSRC=PROM,ALUS=DQ,FUNC=AND,CEN
CJP А=ААХЕ,МАР=РЕ,COND=СТ,SТОРС=/NZ
;МАГАЗИН: 15ИР-1 - В АИСП, В ИАОП, В RG3; П/У НА АНАЛИЗ СУММАТОРА И ОПЕРАНДА
CJS А=LOGER,МАР=РЕ,%ARF%,%SSPRG%,FFCNT=CLRTR1
;ЛОГ.УМНОЖАЕМ СДВИНУТЫЕ СУММАТОР И ОПЕРАНД, ПИШЕМ 15ИР
CJP А=PSAAXE,МАР=РЕ,ALUS=АВ,RА=WR1,RВ=WR2,FUNC=AND,Н,ALUD=В
>,ЕСВ,BRA=RG3,CSM,WЕМ,MNSA=МР,MODNM=SР
END
LOGERG:PROG;
;===== П/П АНАЛИЗА СУММАТОРА И ОПЕРАНДА В ЛОГИЧЕСКИХ КОМАНДАХ ДЛЯ РЭ
;   ЕСЛИ 20-Й РАЗРЯД РР=1, СУММАТОР И ОПЕРАНД НЕ ПРОВЕРЯЕМ;
;   ЕСЛИ =0, ПРОВЕРЯЕМ "ДЫРУ" В ПОРЯДКЕ;
;   РЕЗУЛЬТАТ: СДВИНУТЫЕ СУММАТОР - В РГ.WR1; ОПЕРАНД - В РГ.WR2
;   МЛАДШИЕ 16 РАЗРЯДОВ РАВНЫ 0!
;   ДЕЛАЕТСЯ УСТАНОВКА LOGGRP, СМЕНА ТКК, ЗАГРУЗКА LCW В РГ/СЧ, ЧТЕНИЕ АК В Q
;   TR1 = 1 - ПРОВЕРЯЕТСЯ ТОЛЬКО СУММАТОР!
;   НАЧАЛО ПРОРАММЫ - LOGER!
;
EXTERNAL LCW,INT37
;===== 64Р.= 1; "ДЫРА" = ВСЕ "0"?
АВ1:CJP А=WО,МАР=РЕ,COND=СТ,SТОРС=MZ,ALUS=DZ,Н,ALUD=ВА,RА=WR1,RВ=WR1,ALU
>,DSRC=SHIFT,MPS,PSHF=SН0-4,SHF=ARIPH
;===== 64 Р.=1, "ДЫРА" = НЕ ВСЕ "0"! ПРЕРЫВАНИЕ: "НЕЗАКОННЫЙ СУММАТОР"
WAI:CJP А=WAI,%WА%
;ОБНУЛЯЕМ ИАОП, П/У НА ПРЕРЫВАНИЕ
CJP А=INT37,МАР=РЕ,ALUS=DZ,FUNC=AND,Н,ALUD=В,RВ=DADR
;
;===== 64Р.= 1, "ДЫРА" Д.Б. РАВНА 0
ОВ1:CRTN COND=СТ,SТОРС=MZ,ALUS=DZ,Н,ALUD=ВА,RА=WR2,RВ=WR2,ALU,DSRC=SHIFT
>,MPS,PSHF=SН0-4,SHF=ARIPH
;===== 64Р.=1, "ДЫРА" НЕ = "0" - П/У НА ПРЕРЫВАНИЕ "НЕЗАКОННЫЙ ОПЕРАНД"
CJP А=INT37,МАР=РЕ
;
;ОБНУЛЯЕМ МЛАДШИЕ 16 РАЗРЯДОВ WR2, ВОЗВРАТ (БЕЗ АНАЛИЗА!)
PSNOAN:CRTN A=MNQ,DSRC=PROM,ALUS=DA,RA=WR2,FUNC=AND,H,ALUD=B,RB=WR2
;
LOGER:ENTRY
;ФИЗ.АДРЕС - В RG0, ОБНУЛЯЕМ МЛ.12Р.СУММАТОРА, ПРОВЕРЯЕМ ЗНАК
CONT А=МРР,%PHAR%,DSRC=PROM,ALUS=DА,RА=А,FUNC=NOTRS,Н,ALUD=В,RВ=WR1,CEN
>,FFCNT=СНТКК
;НАДО ЛИ ПРОВЕРЯТЬ СУММАТОР И ОПЕРАНД (20 РАЗРЯД РР=1)
LOGER1:ENTRY
CJP A=PSLOG,MAP=PE,COND=10,%RCA%
;МАСКУ НА "ДЫРУ" - В WR6
CONT А=MD,DSRC=PROM,ALUS=DZ,Н,ALUD=В,RВ=WR6,FFCNT=CLRJMP
;ПЕРЕХОД, ЕСЛИ 64 РАЗРЯД = 1; ПРОВЕРЯЕМ "ДЫРУ"
CJP А=АВ1,МАР=РЕ,COND=СТ,SТОРС=NN,ALUS=АВ,RА=А,RВ=WR6,FUNC=AND,Н,ALUD=Q,СЕМ
;
;===== 64Р.= 0, "ДЫРА" = ВСЕ "1"?
CONT ALUS=AQ,RА=WR6,FUNC=EXOR,Н,CEN
;"ХЛОП" КОПИЮ А
CJP А=WО,МАР=РЕ,COND=СТ,SТОРС=NZ,ALUS=DZ,Н,ALUD=ВА,RА=WR1,RВ=WR1,ALU
>,DSRC=SHIFT,MPS,PSHF=SН0-4,SHF=ARIPH
;===== 64Р.=0, "ДЫРА" = НЕ ВСЕ "1"! МАЛЕНЬКИЕ ЧИСЛА ЗАМЕНЯЕМ НА 0!
CJP А=WО,МАР=РЕ,ALUS=DZ,FUNC=AND,Н,ALUD=В,RВ=WR1
;
;===== 20Р.РР=1, СУММАТОР И ОПЕРАНД НЕ ПРОВЕРЯЕМ!
;"ХЛОП" КОПИЮ А; ЕСЛИ СЛЕД1=1, ВОЗВРАТ (ОПЕРАНДА НЕТ!)
PSLOG:LDCT A=LCW,MAP=PE,ALUS=DZ,H,ALUD=BA,RA=WR1,RB=WR1,ALU,DSRC=SHIFT
>,MPS,PSHF=SH0-4,SHF=ARIPH,FFCNT=CLRJMP
CRTN COND=TR1,FFCNT=LOGGRP
WAAN:CJP A=WAAN,%WA%
;"ХЛОП" ОПЕРАНД
CJP A=PSNOAN,MAP=PE,ECB,BRA=RG2,DSRC=SHIFT,MPS,PSHF=SH0-4,SHF=ARIPH
>,ALUS=DZ,H,ALUD=B,RB=WR2
;
;С "ДЫРОЙ" У СУMМАТОРА ВСЕ В ПОРЯДКЕ; ТАК ЖЕ ПРОВЕРЯЕМ ОПЕРАНД
WО:PUSH А=LCW,МАР=РЕ,%RСА%,FFCNT=LOGGRP
;ИНВЕРТИРУЕМ МАСКУ НА МЛ.12 РАЗРЯДОВ - WR2
LООР А=МРР,COND=ARBRDY,DSRC=PROM,ALUS=DZ,FUNC=EXNOR,Н,ALUD=В,RВ=WR2
;ЧИТАЕМ ОПЕРАНД В РГ.WR2, ОБНУЛЯЕМ МЛ.12Р.; ВОЗВРАТ, ЕСЛИ ПРОВЕРЯЕМ ТОЛЬКО РГ.А
CRTN COND=TR1,ЕСА,ARA=RG2,ALUS=DА,RА=WR2,FUNC=AND,Н,ALUD=В,RВ=WR2,CEN
;ПРОВЕРЯЕМ "ДЫРУ" НА "0"; ПЕРЕХОД, ЕСЛИ 64Р.= 1
CJP А=ОВ1,МАР=РЕ,COND=СТ,SТОРС=NN,ALUS=АВ,RА=WR2,RВ=WR6,FUNC=AND,Н,ALUD=В,СЕМ
;
;===== 64Р.= 0, "ДЫРА" Д.Б.= ВСЕ "1"
CONT А=MD,DSRC=PROM,ALUS=DА,RА=WR6,FUNC=EXOR,Н,CEN
CRTN COND=СТ,SТОРС=NZ,ALUS=DZ,Н,ALUD=ВА,RА=WR2,RВ=WR2,ALU,DSRC=SHIFT
>,MPS,PSHF=SН0-4,SHF=ARIPH
;
;===== 64Р.= 0, "ДЫРА" = НЕ ВСЕ "1". ЗАМЕНЯЕМ ОПЕРАНД НА 0, ВОЗВРАТ
CRTN ALUS=DZ,FUNC=AND,Н,ALUD=В,RВ=WR2
END
АЕХ:PROG;
;     I=========I
;     I  А Е Х  I
;     I=========I
;
;РЭ:0,15ИР:0,ПИА:0,КОП:0А
;PROM:00А
;===== ЛОГИЧЕСКОЕ СРАВНЕНИЕ (АНАЛОГИЧНО ААХ)
;   СУММАТОР ДО ВЫПОЛНЕНИЯ ОПЕРАЦИИ ПЕРЕСЫЛАЕТСЯ В РМР
;   (ВСЕ 64 РАЗРЯДА НЕЗАВИСИМО ОТ ПРИЗНАКА ГРУПП И РЭ!)
;
EXTERNAL LCW,RDOPER,АЕХ,RDOPER1
;М.А. - В АИСП, ИАОП
LDCT А=LCW,МАР=РЕ,%МА%
;ФИЗ.АДРЕС; ТКК = ПК; ПЕРЕПИСЫВАЕМ А - В РГ.У
;РГ.У МОЖНО ПОРТИТЬ, ДАЖЕ ЕСЛИ БУДЕТ INT, Т.К.ВСЕ РАВНО ТУДА БУДЕМ ПИСАТЬ!
CJS А=RDOPER1,МАР=РЕ,SCI,ICI,%PHAR%,ALUS=ZА,RА=А,Н,ALUD=В,RВ=У,FFCNT=СНТКК
;ПЕРЕХОД ПО КОП, ПМИ; ЛОГИЧЕСКОЕ СРАВНЕНИЕ СУММАТОРА И ОПЕРАНДА
JRP %М%,ЕСА,ARA=RG2,ALUS=DА,RА=А,FUNC=EXOR,Н,ALUD=В,RВ=А,СЕМ,FFCNT=LOGGRP
;
;
AEXF:ENTRY
;РЭ:0,15ИР:1,ПИА:0,КОП:0А
;PROM:20А
;
LDCT А=LCW,МАР=РЕ,ALUS=ZQ,CEN
;ПЕРЕХОД, ЕСЛИ АК НЕ 0; ДЛЯ МАГАЗИННОГО ОБРАЩЕНИЯ МАТ.АДРЕС 15ИР-1 - В АИСП,ИАОП
CJP А=АЕХ,МАР=РЕ,COND=СТ,SТОРС=/NZ,%ARF%,%SSPRG%
;ЧИТАЕМ ОПЕРАНД ИЗ МАГАЗИНА: ФИЗ. АДРЕС - В RG0, ПЕРЕПИСЫВАЕМ А - В РГ.У
CJS А=RDOPER1,МАР=РЕ,SCI,ICI,%PHAR%,ALUS=ZА,RА=А,Н,ALUD=В,RВ=У,FFCNT=СНТКК
;ПЕРЕХОД;НОВЫЙ SР ИЗ RG3 НА МЕСТО В ОЗУ МОД.; ОПЕРАНД СРАВНИВАЕМ С РГ.А
JRP %М%,ЕСА,ARA=RG2,ALUS=DА,RА=А,FUNC=EXOR,Н,ALUD=В,RВ=А,СЕМ,FFCNT=LOGGRP
>,ЕСВ,BRA=RG3,CSM,WЕМ,MNSA=МР,MODNM=SР
END
АЕХЕ:PROG;
;РЭ:1,15ИР:0,ПИА:0,КОП:0А
;PROM:40А
;===== ЛОГИЧЕСКОЕ СРАВНЕНИЕ СУММАТОРА И ОПЕРАНДА В РЭ
;   РГ.А КОПИРУЕТСЯ БЕЗ ИЗМЕНЕНИЯ В РГ.У ПЕРЕД ИСПОЛНЕНИЕМ КОМАНДЫ, ВСЕ 64 РАЗР.
;
EXTERNAL LOGER,PSAAXE,АЕХЕ
;М.А. - В АИСП, ИАОП
CONT %МА%
;КОПИРУЕМ РГ.А - В У, П/У НА П/П ПРОВЕРКИ СУММАТОРА И ОПЕРАНДА
CJS А=LOGER,МАР=РЕ,ALUS=ZА,RА=А,Н,ALUD=В,RВ=У,FFCNT=CLRTR1
;ЛОГ.СРАВНИВАЕМ СДВИНУТЫЕ СУММАТОР И ОПЕРАНД
CJP А=PSAAXE,МАР=РЕ,ALUS=АВ,RА=WR1,RВ=WR2,FUNC=EXOR,Н,ALUD=В
;
;
AEXFE:ENTRY
;РЭ:1,15ИР:1,ПИА:0,КОП:0А
;PROM:60А
;
;ПЕРЕХОД, ЕСЛИ АК НЕ 0
CONT А=М15,DSRC=PROM,ALUS=DQ,FUNC=AND,CEN
CJP А=АЕХЕ,МАР=РЕ,COND=СТ,SТОРС=/NZ
;МАГАЗИННОЕ ЧТЕНИЕ: 15ИР-1 - В АИСП, В ИАОП, В RG3
CONT %ARF%,%SSPRG%
;КОПИРУЕМ РГ.А - В РГ.У, П/У НА П/П ПРОВЕРКИ СУММАТОРА И ОПЕРАНДА
CJS А=LOGER,МАР=РЕ,ALUS=ZА,RА=А,Н,ALUD=В,RВ=У,FFCNT=CLRTR1
;ЛОГ.СРАВНИВАЕМ СДВИНУТЫЕ СУММАТОР И ОПЕРАНД; ПИШЕМ 15ИР
CJP А=PSAAXE,МАР=РЕ,ALUS=АВ,RА=WR1,RВ=WR2,FUNC=EXOR,Н,ALUD=В
>,ЕСВ,BRA=RG3,CSM,WЕМ,MNSA=МР,MODNM=SР
END
ARX:PROG;
;     I=========I
;     I  А R Х  I
;     I=========I
;
;РЭ:0,15ИР:0,ПИА:0,КОП:0В
;PROM:00В
;===== ЦИКЛИЧЕСКОЕ СЛОЖЕНИЕ СУММАТОРА И ОПЕРАНДА
;
EXTERNAL RDOPER,LCW,ARX,RDOPER1
;М.А. - В АИСП, ИАОП
CJS А=RDOPER,МАР=РЕ,%МА%
;СКЛАДЫВАЕМ СУММАТОР И ОПЕРАНД, ЗАПОМИНАЕМ ПЕРЕНОС
CONT ЕСА,ARA=RG2,ALUS=DА,RА=А,FUNC=ADD,Н,ALUD=В,RВ=А,CEN
;ПЕРЕХОД НА СЛЕД.КОМАНДУ, ДОБАВЛЯЕМ ПЕРЕНОС В РЕЗУЛЬТАТУ
JRP %М%,ALUS=ZА,RА=А,FUNC=ADD,Н,CI=CIC,ALUD=В,RВ=А,СЕМ,FFCNT=MULGRP
;
;
ARXF:ENTRY
;РЭ:0,15ИР:1,ПИА:0,КОП:0В
;PROM:20В
;
;ПЕРЕХОД, ЕСЛИ АК НЕ 0; ПИШЕМ М.А.ДЛЯ МАГАЗИННОГО ОБРАЩЕНИЯ
LDCT А=LCW,МАР=РЕ,ALUS=ZQ,CEN
CJP А=ARX,МАР=РЕ,COND=СТ,SТОРС=/NZ,%ARF%,%SSPRG%
;ЧИТАЕМ ОПЕРАНД ИЗ МАГАЗИНА: ФИЗ.АДРЕС, МЕНЯЕМ ТКК
CJS А=RDOPER1,МАР=РЕ,SCI,ICI,%PHAR%,FFCNT=СНТКК
;СКЛАДЫВАЕМ
CONT ЕСА,ARA=RG2,ALUS=DА,RА=А,FUNC=ADD,Н,ALUD=В,RВ=А,CEN
;ПЕРЕХОД НА СЛЕД.КОМАНДУ;НОВЫЙ SР ИЗ RG3 НА МЕСТО В ОЗУ МОД.; ДОБАВЛЯЕМ ПЕРЕНОС
JRP %М%,ALUS=ZА,RА=А,FUNC=ADD,Н,CI=CIC,ALUD=В,RВ=А,СЕМ,FFCNT=MULGRP
>,ЕСВ,BRA=RG3,CSM,WЕМ,MNSA=МР,MODNM=SР
END
ARXE:PROG;
;РЭ:1,15ИР:0,ПИА:0,КОП:0В
;PROM:40В
;===== ЦИКЛИЧЕСКОЕ СЛОЖЕНИЕ В РЭ
;
EXTERNAL LOGER,PSAAXE,ARXE
;М.А. - В АИСП, ИАОП
CJS А=LOGER,МАР=РЕ,%МА%,FFCNT=CLRTR1
;ЦИКЛИЧЕСКИ СКЛАДЫВАЕМ СДВИНУТЫЕ СУММАТОР И ОПЕРАНД ДЛЯ РЭ
CONT ALUS=АВ,RА=WR1,RВ=WR2,FUNC=ADD,Н,ALUD=В,CEN,FFCNT=MULGRP
PSARXE:ENTRY
;ПЕРЕНОС - В МЛ.РАЗРЯД
CONT А=ZER,DSRC=PROM,ALUS=DZ,Н,CI=CIC,ALUD=В,RВ=А
;СДВИГАЕМ ЕГО В 17 РАЗРЯД
CONT ALUS=DZ,Н,ALUD=ВА,RА=А,RВ=А,ALU,DSRC=SHIFT,MPS,PSHF=SН0-16,SHF=LOG
;ДОБАВЛЯЕМ К РЕЗУЛЬТАТУ
CJP А=PSAAXE,МАР=РЕ,ALUS=АВ,RА=А,RВ=WR2,Н,ALUD=В
;
;
ARXFE:ENTRY
;РЭ:1,15ИР:1,ПИА:0,КОП:0В
;PROM:60В
;
;ПЕРЕХОД, ЕСЛИ АК НЕ 0
CONT А=М15,DSRC=PROM,ALUS=DQ,FUNC=AND,CEN
CJP А=ARXE,МАР=РЕ,COND=СТ,SТОРС=/NZ
;МАГАЗИН: 15ИР-1 - В АИСП, В ИАОП, В RG3; ПЕРЕХОД НА АНАЛИЗ СУММАТОРА И ОПЕРАНДА
CJS А=LOGER,МАР=РЕ,%ARF%,%SSPRG%,FFCNT=CLRTR1
;СКЛАДЫВАЕМ СУММАТОР И ОПЕРАНД; ЗАПОМИНАЕМ ПЕРЕНОС, ПИШЕМ 15ИР, П/У НА КОНЕЦ ARX
CJP А=PSARXE,МАР=РЕ,ALUS=АВ,RА=WR1,RВ=WR2,FUNC=ADD,Н,ALUD=В,CEN
>,ЕСВ,BRA=RG3,CSM,WЕМ,MNSA=МР,MODNM=SР,FFCNT=MULGRP
END
АОХ:PROG;
;     I=========I
;     I  А О Х  I
;     I=========I
;
;РЭ:0,15ИР:0,ПИА:0,КОП:0D
;PROM:00D
;===== ЛОГИЧЕСКОЕ СЛОЖЕНИЕ СУММАТОРА И ОПЕРАНДА
;
EXTERNAL RDOPER,LCW,АОХ,RDOPER1
;М.А. - В АИСП, ИАОП
CJS А=RDOPER,МАР=РЕ,%МА%
;ПЕРЕХОД НА СЛЕД.КОМАНДУ, ПМИ, ЛОГИЧЕСКОЕ СЛОЖЕНИЕ СУММАТОРА И ОПЕРАНДА
JRP %М%,ЕСА,ARA=RG2,ALUS=DА,RА=А,FUNC=OR,Н,ALUD=В,RВ=А,СЕМ,FFCNT=LOGGRP
;
;
AOXF:ENTRY
;РЭ:0,15ИР:1,ПИА:0,КОП:0D
;PROM:20D
;
;ПЕРЕХОД, ЕСЛИ АК НЕ 0; М.А. ДЛЯ МАГАЗИННОГО ОБРАЩЕНИЯ
LDCT А=LCW,МАР=РЕ,ALUS=ZQ,CEN
CJP А=АОХ,МАР=РЕ,COND=СТ,SТОРС=/NZ,%ARF%,%SSPRG%
;ЧИТАЕМ ОПЕРАНД ИЗ МАГАЗИНА: Ф.А.+ МЕНЯЕМ ТКК
CJS А=RDOPER1,МАР=РЕ,SCI,ICI,%PHAR%,FFCNT=СНТКК
;ПЕРЕХОД; НОВЫЙ SР ИЗ RG3 НА МЕСТО В ОЗУ МОД.; ДЕЛАЕМ ЛОГ.СЛОЖЕНИЕ
JRP %М%,ЕСА,ARA=RG2,ALUS=DА,RА=А,FUNC=OR,Н,ALUD=В,RВ=А,СЕМ,FFCNT=LOGGRP
>,ЕСВ,BRA=RG3,CSM,WЕМ,MNSA=МР,MODNM=SР
END
АОХЕ:PROG;
;РЭ:1,15ИР:0,ПИА:0,КОП:0D
;PROM:40D
;===== ЛОГИЧЕСКОЕ СЛОЖЕНИЕ СУММАТОРА И ОПЕРАНДА В РЭ
;
EXTERNAL LOGER,PSAAXE,АОХЕ
;М.А. - В АИСП, ИАОП
CJS А=LOGER,МАР=РЕ,%МА%,FFCNT=CLRTR1
;ДЕЛАЕМ ЛОГ.СЛОЖЕНИЕ
CJP А=PSAAXE,МАР=РЕ,ALUS=АВ,RА=WR1,RВ=WR2,FUNC=OR,Н,ALUD=В
;
;
AOXFE:ENTRY
;РЭ:1,15ИР:1,ПИА:0,КОП:0D
;PROM:60D
;
CONT А=М15,DSRC=PROM,ALUS=DQ,FUNC=AND,CEN
CJP А=АОХЕ,МАР=РЕ,COND=СТ,SТОРС=/NZ
;МАГАЗИН: 15ИР-1 - В АИСП,ИАОП,RG3; ПЕРЕХОД НА АНАЛИЗ СУММАТОРА И ОПЕРАНДА
CJS А=LOGER,МАР=РЕ,%ARF%,%SSPRG%,FFCNT=CLRTR1
;ДЕЛАЕМ ЛОГ.СЛОЖЕНИЕ; ПИШЕМ 15ИР
CJP А=PSAAXE,МАР=РЕ,ALUS=АВ,RА=WR1,RВ=WR2,FUNC=OR,Н,ALUD=В
>,ЕСВ,BRA=RG3,CSM,WЕМ,MNSA=МР,MODNM=SР
END
АРХ:PROG;
;     I=========I
;     I  А Р Х  I
;     I=========I
;
;РЭ:0,15ИР:0,ПИА:0,КОП:10
;PROM:010
;===== СБОРКА ПО МАСКЕ: ИЗ РГ.А ВЫБИРАЮТСЯ РАЗРЯДЫ, СООТВЕТСТВУЮЩИЕ "1" ОПЕРАНДА
;   ПОЛУЧЕННЫЙ КОД ПОМЕЩАЕТСЯ В СТАРШИЕ РАЗРЯДЫ РГ.А
;    ------------>       I-------------------------------------------------I
;   ----------------     I    ------------------     -------------------   I
;   I ОПЕРАНД (WR2)I     ---> I РЕЗУЛЬТАТ (WR3)I     I  СУММАТОР (РГ.Q)I---I-->
;   ----------------          ------------------     -------------------
; - ЕСЛИ ВЫДВИНУТЫЙ ИЗ ОПЕРАНДА РАЗРЯД = 1, СУММАТОР СДВИГАЕТСЯ ЦИКЛИЧЕСКИ
;   ВМЕСТЕ С РЕЗ-ТОМ; ВЫДВИНУТЫЙ ИЗ СУММАТОРА БИТ ПОПАДАЕТ В СТ.РАЗРЯДЫ РЕЗ-ТА;
; - ЕСЛИ = 0, ИЗ СУММАТОРА ПРОСТО ВЫТАЛКИВАЕТСЯ 1 РАЗРЯД, РЕЗ-Т НЕ МЕНЯЕТСЯ.
;
EXTERNAL LCW,АРХ,JQ
;М.А. - В АИСП, ИАОП
CONT %МА%
;ФИЗ.АДРЕС - В RG0, "0" - В WR3, SР - В RG3 ДЛЯ APXF
CONT %PHAR%,ALUS=DZ,FUNC=AND,Н,ALUD=В,RВ=WR3,CSM,MNSA=МР,MODNM=SР,ЕСА,WRA
>,ARA=RG3
;ЖДЕМ ГОТ.АРБИТРА; СУММАТОР ПЕРЕСЫЛАЕМ В РГ.Q; MZ=0,ЧТОБ НЕ ВЫЙТИ СРАЗУ ИЗ ЦИКЛА
WА:CJP А=WА,%WА%,ALUS=ZА,RА=А,Н,ALUD=Q,SТОРС=CLRM,СЕМ
;ОПЕРАНД - В WR2; SР - В ОЗУ МОД.,Т.К. INT НЕ БУДЕТ!
LDCT А=LCW,МАР=РЕ,ЕСА,ARA=RG2,ALUS=DZ,Н,ALUD=В,RВ=WR2,ЕСВ,BRA=RG3,CSM
>,WЕМ,MNSA=МР,MODNM=SР,FFCNT=СНТКК
;
АРХС:ENTRY
;ЦИКЛ: СДВИГ ОПЕРАНДА; ВЫХОД, ЕСЛИ ОПЕРАНД = 0 (MZ=1)
CJP А=OUT,МАР=РЕ,COND=СТ,SТОРС=MZ,ALUS=ZВ,RВ=WR2,Н,ALUD=RSB,SHMUX=ASLLS,СЕМ
CJP А=ISC1,МАР=РЕ,COND=СТ,SТОРС=МС,FFCNT=LOGGRP
;МС=0: СДВИГАЕМ ТОЛЬКО СУММАТОР (РГ.Q)
CJP А=АРХС,МАР=РЕ,ALUS=ZQ,Н,ALUD=RSBQ,RВ=WR6,SHMUX=LSLSL
;МС=1: СДВИГАЕМ СУМ.+ РЕЗ-Т; ВЫДВИНУТЫЙ ИЗ СУММАТОРА БИТ ПОПАДАЕТ В СТ.Р.РЕЗ-ТА
ISC1:CJP А=АРХС,МАР=РЕ,ALUS=ZВ,RВ=WR3,Н,ALUD=RSBQ,SHMUX=RDLRDL
;
;КОНЕЦ; ПЕРЕСЫЛАЕМ РЕЗУЛЬТАТ В СУММАТОР
OUT:JRP А=JQ,МАР=РЕ,COND=ТККВ,DDEV=CLRCD,ISE,ALUS=ZА,RА=WR3,Н,ALUD=В,RВ=А
>,СЕМ,FFCNT=CLRJMP
;
;
APXF:ENTRY
;РЭ:0,15ИР:1,ПИА:0,КОП:10
;PROM:210
;
;ПЕРЕХОД, ЕСЛИ АК НЕ 0; ПИШЕМ М.А. ДЛЯ МАГАЗИННОГО ОБРАЩЕНИЯ
CONT ALUS=ZQ,CEN
CJP А=АРХ,МАР=РЕ,COND=СТ,SТОРС=/NZ,%ARF%,%SSPRG%
;ФИЗ. АДРЕС - В RG0, "0" - В WR3
CJP А=WА,МАР=РЕ,%PHAR%,ALUS=DZ,FUNC=AND,Н,ALUD=В,RВ=WR3
END
АРХЕ:PROG;
;РЭ:1,15ИР:0,ПИА:0,КОП:10
;PROM:410
;===== СБОРКА ПО МАСКЕ В РЭ
;
EXTERNAL LOGER,JQ,АРХЕ
;М.А. - В АИСП, ИАОП;  П/У НА П/П ЧТЕНИЯ И ПРОВЕРКИ СУММАТОРА И ОПЕРАНДА
CJS А=LOGER,МАР=РЕ,%МА%,FFCNT=CLRTR1
;0 - В WR3 (РГ.РЕЗУЛЬТАТА)
CONT ALUS=DZ,FUNC=AND,Н,ALUD=В,RВ=WR3
;
;СУММАТОР ПЕРЕСЫЛАЕМ В РГ.Q В МЛ.РАЗРЯДЫ
АРХСС:CONT ALUS=DZ,Н,ALUD=ВА,RА=WR1,RВ=WR1,ALU,DSRC=SHIFT,MPS,PSHF=SН0+16
>,SHF=LOG
CONT ALUS=ZА,RА=WR1,Н,ALUD=Q,SТОРС=CLRM,СЕМ
;СДВИНУТЫЙ НА 16 РАЗР. ОПЕРАНД - В WR2
CONT ALUS=DZ,Н,ALUD=ВА,RА=WR2,RВ=WR2,ALU,DSRC=SHIFT,MPS,PSHF=SН0+16,SHF=LOG
;
;ЦИКЛ: СДВИГ ОПЕРАНДА; ВЫХОД, ЕСЛИ ОПЕРАНД = 0 (MZ=1)
АРХСЕ:CJP А=PSAPXE,МАР=РЕ,COND=СТ,SТОРС=MZ,ALUS=ZВ,RВ=WR2,Н,ALUD=RSB,SHMUX=ASLLS
>,СЕМ
CJP А=ISC1E,МАР=РЕ,COND=СТ,SТОРС=МС
;МС=0: СДВИГАЕМ ТОЛЬКО СУММАТОР (РГ.Q)
CJP А=АРХСЕ,МАР=РЕ,ALUS=ZQ,Н,ALUD=RSBQ,RВ=WR6,SHMUX=LSLSL
;МС=1: СДВИГАЕМ СУМ.+ РЕЗ-Т; ВЫДВИНУТЫЙ ИЗ СУММАТОРА БИТ ПОПАДАЕТ В СТ.Р.РЕЗ-ТА
ISC1E:CJP А=АРХСЕ,МАР=РЕ,ALUS=ZВ,RВ=WR3,Н,ALUD=RSBQ,SHMUX=RDLRDL
;
;КОНЕЦ; РАСДВИГАЕМ РЕЗУЛЬТАТ; ПЕРЕХОД НА СЛЕД.КОМАНДУ
PSAPXE:ENTRY
JRP А=JQ,МАР=РЕ,COND=ТККВ,DDEV=CLRCD,WRD,ISE,ALUS=DZ,Н,ALUD=ВА
>,RА=WR3,RВ=А,СЕМ,ALU,DSRC=SHIFT,MPS,PSHF=SН0+4,SHF=EMULF
;
;
APXFE:ENTRY
;РЭ:1,15ИР:1,ПИА:0,КОП:10
;PROM:610
;
CONT А=М15,DSRC=PROM,ALUS=DQ,FUNC=AND,CEN
CJP А=АРХЕ,МАР=РЕ,COND=СТ,SТОРС=/NZ
;МАГАЗИН: 15ИР-1 - В АИСП,ИАОП,RG3; ПЕРЕХОД НА АНАЛИЗ СУММАТОРА И ОПЕРАНДА
CJS А=LOGER,МАР=РЕ,%ARF%,%SSPRG%,FFCNT=CLRTR1
;0 - В WR3, SР - НА МЕСТО, П/У НА АРХ
CJP А=АРХСС,МАР=РЕ,ALUS=DZ,FUNC=AND,Н,ALUD=В,RВ=WR3,%SPRG%
END
AUX:PROG;
;     I=========I
;     I  А U Х  I
;     I=========I
;
;РЭ:0,15ИР:0,ПИА:0,КОП:11
;PROM:011
;===== РАЗБОРКА ПО МАСКЕ: КОД СТАРШЕГО РАЗРЯДА СУММАТОРА ПОМЕЩАЕТСЯ В РАЗРЯД,
;   СООТВЕТСТВУЮЩИЙ СТАРШЕЙ "1" МАСКИ - КОДА ПО ИСП.АДРЕСУ;
;   КОД СЛЕДУЮЩЕГО РАЗРЯДА СУММАТОРА - СЛЕДУЮЩЕЙ "1" МАСКИ И Т.Д.
;    <------------        I----------------------------------------------------I
;   -----------------     I    ------------------       --------------------   I
;   I ОПЕРАНД (WR2) I     <----I СУММАТОР (WR1) I    <--I РЕЗУЛЬТАТ (РГ.Q) I<--I
;   -----------------          ------------------       --------------------
; - ЕСЛИ ВЫДВИНУТЫЙ ИЗ ОПЕРАНДА РАЗРЯД = 1, СУММАТОР СДВИГАЕТСЯ ЦИКЛИЧЕСКИ
;   ВМЕСТЕ С РЕЗ-ТОМ; ВЫДВИНУТЫЙ ИЗ СУММАТОРА БИТ ПОПАДАЕТ В МЛ.РАЗРЯДЫ РЕЗ-ТА;
; - ЕСЛИ = 0, СУММАТОР НЕ ИЗМЕНЯЕТСЯ, РЕЗУЛЬТАТ СДВИГАЕТСЯ ВЛЕВО ЛОГ. НА 1;
;   И ТАК - 64 РАЗА, ЧТОБ РЕЗ-Т ЦИКЛИЧЕСКИ 1 РАЗ "ОБЕРНУЛСЯ"!
;
EXTERNAL LCW,AUX
;М.А. - В АИСП, ИАОП
CONT %МА%
;ФИЗ.АДРЕС - В RG0, "0" - В РГ.Q, ДЛЯ ANXF ПЕРЕПИСЫВАЕМ SР В RG3 БОИ
CONT %PHAR%,ALUS=DZ,FUNC=AND,Н,ALUD=Q,CSM,MNSA=МР,MODNM=SР,ЕСА,WRA,ARA=RG3
;ЖДЕМ ГОТ.АРБИТРА; СУММАТОР ПЕРЕСЫЛАЕМ В WR1
WА:CJP А=WА,%WА%,ALUS=ZА,RА=А,Н,ALUD=В,RВ=WR1
;ОПЕРАНД - В WR2, SР ПЕРЕПИСЫВАЕМ В ОЗУ МОД.
LDCT А=63,МАР=РЕ,ЕСА,ARA=RG2,ALUS=DZ,Н,ALUD=В,RВ=WR2,ЕСВ,BRA=RG3,CSM,WЕМ
>,MNSA=МР,MODNM=SР,FFCNT=СНТКК
;
AUXC:ENTRY
PUSH IСС
;ЦИКЛ: СДВИГ ОПЕРАНДА
CONT ALUS=ZВ,RВ=WR2,Н,ALUD=LSB,SHMUX=LSLSL,СЕМ,FFCNT=CLRJMP
CJP А=ISC1,МАР=РЕ,COND=СТ,SТОРС=МС,FFCNT=LOGGRP
;МС=0: СДВИГАЕМ ТОЛЬКО РЕЗУЛЬТАТ (РГ.Q); ВЫХОД, ЕСЛИ РГ/СЧ = 0
TWB А=OUT,МАР=РЕ,IСС,ALUS=ZQ,Н,ALUD=LSBQ,RВ=WR6,SHMUX=ASLLS
;МС=1: СДВИГАЕМ СУМ.+ РЕЗ-Т; ВЫДВИНУТЫЙ ИЗ СУММАТОРА БИТ ПОПАДАЕТ В МЛ.Р.РЕЗ-ТА
ISC1:RFCT ALUS=ZВ,RВ=WR1,Н,ALUD=LSBQ,SHMUX=RDRD
;
;КОНЕЦ; ПЕРЕХОД НА ВКС, ЕСЛИ ДЛЯ ЛЕВОЙ; ПЕРЕСЫЛАЕМ РЕЗ-Т В СУММАТОР
OUT:CJP А=LCW,МАР=РЕ,COND=ТККВ,IСС,ALUS=ZQ,Н,ALUD=В,RВ=А,СЕМ,DDEV=CLRCD,ISE
;ПЕРЕХОД ПО КОП, ПМИ, ЧИТАЕМ АК В Q
CJP %МЕ%,%RСА%
;
;
AUXF:ENTRY
;РЭ:0,15ИР:1,ПИА:0,КОП:11
;PROM:211
;
;ПЕРЕХОД, ЕСЛИ АК НЕ 0
CONT ALUS=ZQ,CEN
CJP А=AUX,МАР=РЕ,COND=СТ,SТОРС=/NZ,%ARF%,%SSPRG%
;ЧИТАЕМ ОПЕРАНД ИЗ МАГАЗИНА
;ФИЗ. АДРЕС - В RG0, "0" - В РГ.Q, ПЕРЕХОД
CJP А=WА,МАР=РЕ,%PHAR%,ALUS=DZ,FUNC=AND,Н,ALUD=Q
END
AUXE:PROG;
;РЭ:1,15ИР:0,ПИА:0,КОП:11
;PROM:411
;===== РАЗБОРКА ПО МАСКЕ В РЭ
;
EXTERNAL LOGER,LCW,PSAPXE,AUXE
;М.А. - В АИСП, ИАОП;  П/У НА П/П ЧТЕНИЯ И ПРОВЕРКИ СУММАТОРА И ОПЕРАНДА
CJS А=LOGER,МАР=РЕ,%МА%,FFCNT=CLRTR1
;0 - В РГ.Q (РГ.РЕЗУЛЬТАТА)
CONT ALUS=DZ,FUNC=AND,Н,ALUD=Q
;
AUXCE:PUSH А=47,МАР=РЕ
;ЦИКЛ: СДВИГ ОПЕРАНДА
CONT ALUS=ZВ,RВ=WR2,Н,ALUD=LSB,SHMUX=LSLSL,СЕМ
CJP А=ISC1E,МАР=РЕ,COND=СТ,SТОРС=МС
;МС=0: СДВИГАЕМ ТОЛЬКО РЕЗУЛЬТАТ (РГ.Q)
TWB А=OUTE,МАР=РЕ,IСС,ALUS=ZQ,Н,ALUD=LSBQ,RВ=WR6,SHMUX=ASLLS
;МС=1: СДВИГАЕМ СУМ.+ РЕЗ-Т; ВЫДВИНУТЫЙ ИЗ СУММАТОРА БИТ ПОПАДАЕТ В МЛ.Р.РЕЗ-ТА
ISC1E:RFCT ALUS=ZВ,RВ=WR1,Н,ALUD=LSBQ,SHMUX=RDRD
;
;КОНЕЦ; РЕЗ-Т - В РГ.А В СТАРШИЕ РАЗРЯДЫ, РАСДВИГАЕМ РЕЗ-Т
OUTE:LDCT А=LCW,МАР=РЕ,ALUS=ZQ,Н,ALUD=В,RВ=WR3
CJP А=PSAPXE,МАР=РЕ,ALUS=DZ,Н,ALUD=ВА,RА=WR3,RВ=WR3,ALU,DSRC=SHIFT,MPS
>,PSHF=SН0-16,SHF=LOG
;
;
AUXFE:ENTRY
;РЭ:1,15ИР:1,ПИА:0,КОП:11
;PROM:611
;
CONT А=М15,DSRC=PROM,ALUS=DQ,FUNC=AND,CEN
CJP А=AUXE,МАР=РЕ,COND=СТ,SТОРС=/NZ
;МАГАЗИН: 15ИР-1 - В АИСП, В ИАОП, В RG3; П/У НА АНАЛИЗ СУММАТОРА И ОПЕРАНДА
CJS А=LOGER,МАР=РЕ,%ARF%,%SSPRG%,FFCNT=CLRTR1
;0 - В РГ.Q, SР - НА МЕСТО, П/У НА AUX
CJP А=AUXCE,МАР=РЕ,ALUS=DZ,FUNC=AND,Н,ALUD=Q,%SPRG%
END
АСХ:PROG;
;     I=========I
;     I  А С Х  I
;     I=========I
;
;РЭ:0,15ИР:0,ПИА:0,КОП:12
;PROM:012
;===== ПОДСЧЕТ ЧИСЛА ЕДИНИЦ В СУММАТОРЕ
;   ЗАТЕМ ВЫПОЛНЯЕТСЯ КОМАНДА ЦИКЛИЧЕСКОГО СЛОЖЕНИЯ ARX С КОДОМ ПО ИСП.АДРЕСУ
;
EXTERN LCW,АСХ
;М.А. - В АИСП, ИАОП; П/У НА П/П ПОДСЧЕТА ЧИСЛА ЕДИНИЦ
CJS А=ACXN,МАР=РЕ,%МА%
;ДЕЛАЕМ ЦИКЛИЧЕСКОЕ СЛОЖЕНИЕ
LDCT А=LCW,МАР=РЕ,ЕСА,ARA=RG2,ALUS=DА,RА=WR3,Н,ALUD=В,RВ=А,CEN
;ДОБАВЛЯЕМ ПЕРЕНОС, ПЕРЕХОД НА СЛЕД.КОМАНДУ
JRP %М%,ALUS=ZВ,RВ=А,Н,CI=CIC,ALUD=В,СЕМ,FFCNT=LOGGRP
;
;===== П/П ПОДСЧЕТА ЧИСЛА ЕДИНИЦ СУММАТОРА В НР
;ФИЗ.АДРЕС - В RG0, СУММАТОР - В WR1
ACXN:CONT %PHAR%,ALUS=ZА,RА=А,Н,ALUD=В,RВ=WR1,SТОРС=CLRM,СЕМ,FFCNT=СНТКК
ACXNC:ENTRY
;0 - В WR3
PUSH А=63,МАР=РЕ,ALUS=DZ,FUNC=AND,Н,ALUD=В,RВ=WR3
;СДВИГ ЛОГ.СУММАТОРА - СЕМ; ВЫХОД, ЕСЛИ ОН РАВЕН 0
CJPP А=WAS,МАР=РЕ,COND=СТ,SТОРС=MZ,ALUS=ZВ,RВ=WR1,Н,ALUD=RSB,SHMUX=ASLLS,СЕМ
;ДОБАВЛЯЕМ ПЕРЕНОС (ВЫШЕДШИЙ РАЗРЯД)
RFCT ALUS=ZВ,RВ=WR3,Н,CI=CIC,ALUD=В,SТОРС=MZ
;ПРОВЕРЯЕМ ГОТ.АРБИТРА, ЧИТАЕМ АК В Q, ВОЗВРАТ
WAS:CJP А=WAS,%WА%,%RСА%,FFCNT=CLRJMP
CRTN
;
;
ACXF:ENTRY
;РЭ:0,15ИР:1,ПИА:0,КОП:12
;PROM:212
;
;ПЕРЕХОД, ЕСЛИ АК НЕ 0
CONT ALUS=ZQ,CEN
CJP А=АСХ,МАР=РЕ,COND=СТ,SТОРС=/NZ
;ЧИТАЕМ ОПЕРАНД ИЗ МАГАЗИНА; П/У НА П/П ПОДСЧЕТА ЧИСЛА ЕДИНИЦ
CJS А=ACXN,МАР=РЕ,%ARF%,%SSPRG%
;СКЛАДЫВАЕМ
LDCT А=LCW,МАР=РЕ,ЕСА,ARA=RG2,ALUS=DА,RА=WR3,Н,ALUD=В,RВ=А,CEN
;ДОБАВЛЯЕМ ПЕРЕНОС, SР - НА МЕСТО, ПЕРЕХОД НА СЛЕД.КОМАНДУ
JRP %М%,ALUS=ZВ,RВ=А,Н,CI=CIC,ALUD=В,СЕМ,%SPRG%,FFCNT=LOGGRP
END
АСХЕ:PROG;
;РЭ:1,15ИР:0,ПИА:0,КОП:12
;PROM:412
;===== ПОДСЧЕТ ЧИСЛА ЕДИНИЦ СУММАТОРА В РЭ
;
EXTERNAL LOGER,LCW,PSARXE,АСХЕ
;М.А. - В АИСП, ИАОП;  П/У НА П/П ЧТЕНИЯ И ПРОВЕРКИ СУММАТОРА И ОПЕРАНДА
CJS А=LOGER,МАР=РЕ,%МА%,FFCNT=CLRTR1
;===== ПОДСЧЕТ ЧИСЛА ЕДИНИЦ СУММАТОРА В РЭ
;0 - В WR3
ACXNE:PUSH А=47,МАР=РЕ,ALUS=DZ,FUNC=AND,Н,ALUD=В,RВ=WR3,SТОРС=CLRM,СЕМ
;СДВИГ ЛОГ.СУММАТОРА - СЕМ; ВЫХОД, ЕСЛИ ОН РАВЕН 0
CJPP А=PSACXE,МАР=РЕ,COND=СТ,SТОРС=MZ,ALUS=ZВ,RВ=WR1,Н,ALUD=LSB,SHMUX=LSLSL
>,СЕМ
;ДОБАВЛЯЕМ ПЕРЕНОС
RFCT ALUS=ZВ,RВ=WR3,Н,CI=CIC,ALUD=В,SТОРС=MZ
PSACXE:ENTRY
;СДВИГ ЧИСЛА ЕДИНИЦ НА МЕСТО В РЭ (НАЧИНАЯ С 17 РАЗРЯДА), ИДЕМ НА ЦИКЛ.СЛОЖЕНИЕ
LDCT А=LCW,МАР=РЕ,ALUS=DZ,Н,ALUD=ВА,RА=WR3,RВ=WR1,ALU,DSRC=SHIFT,MPS
>,PSHF=SН0-16,SHF=LOG
CJP А=PSARXE,МАР=РЕ,ALUS=АВ,RА=WR1,RВ=WR2,FUNC=ADD,Н,ALUD=В,CEN
;
;
ACXFE:ENTRY
;РЭ:1,15ИР:1,ПИА:0,КОП:12
;PROM:612
;
;ПЕРЕХОД, ЕСЛИ АК НЕ 0
CONT А=М15,DSRC=PROM,ALUS=DQ,FUNC=AND,CEN
CJP А=АСХЕ,МАР=РЕ,COND=СТ,SТОРС=/NZ
;
;МАГАЗИН: 15ИР-1 - В АИСП, В ИАОП, В RG3; П/У НА АНАЛИЗ СУММАТОРА И ОПЕРАНДА
CJS А=LOGER,МАР=РЕ,%ARF%,%SSPRG%,FFCNT=CLRTR1
;SР - НА МЕСТО (ПРЕР. БОЛЬШЕ НЕ БУДЕТ); П/У НА АСХ В РЭ
CJP А=ACXNE,МАР=РЕ,%SPRG%
END
ANX:PROG;
;     I=========I
;     I  А N Х  I
;     I=========I
;
;РЭ:0,15ИР:0,ПИА:0,КОП:13
;PROM:013
;===== ОПРЕДЕЛЯЕТСЯ НОМЕР СТАРШЕГО РАЗРЯДА СУММАТОРА, СОДЕРЖАЩЕГО "1";
;   В РМР ПОМЕЩАЕТСЯ "ОСТАТОК" СУММАТОРА ПОСЛЕ РАЗРЯДА С "1":
;   1 В 64 РАЗРЯДЕ РГ.А: РЕЗ-Т = 1; РМР = 63-1 РАЗР.РГ.А
;   1 В 63 РАЗРЯДЕ:            = 2; РМР = 62-1
;   1 В  1 РАЗРЯДЕ:            =64; РМР = 0
;   НЕТ ЕДИНИЦ В СУММАТОРЕ:    = 0; РМР = 0
;   ЕСЛИ ЕСТЬ "1", БЛОК "ПЛЕ" ВЫДАЕТ НОМЕР ОТ "0" ДО "63" В МЛ.6 РАЗРЯДАХ,
;   ФЛАГ (7РАЗР.) = 0;  ЕСЛИ НЕТ ЕДИНИЦ, МЛАДШИЕ 7 РАЗРЯДОВ = 7F.
;   ПОЭТОМУ К РЕЗУЛЬТАТУ БЛОКА "ПЛЕ" ДОБАВЛЯЕМ "1" И ОСТАВЛЯЕМ 7 РАЗРЯДОВ,
;   ПОЛУЧАЕТСЯ КАК РАЗ ТО, ЧТО НУЖНО!
;   ЗАТЕМ ВЫПОЛНЯЕТСЯ КОМАНДА ЦИКЛИЧЕСКОГО СЛОЖЕНИЯ ARX С КОДОМ ПО ИСП.АДРЕСУ
;
EXTERN LCW,ANX
;М.А. - В АИСП, ИАОП; П/У НА П/П ВЫЧИСЛЕНИЯ ЛЕВОЙ ЕДИНИЦЫ
CJS А=ANXN,МАР=РЕ,%МА%
;ДОБАВЛЯЕМ ПЕРЕНОС, ПЕРЕХОД ПО КОП
JRP %М%,ALUS=ZВ,RВ=А,Н,CI=CIC,ALUD=В,СЕМ
;
;===== П/П ОПРЕДЕЛЕНИЯ НОМЕРА СТАРШЕЙ ЕДИНИЦЫ СУММАТОРА В НР
;ФИЗ.АДРЕС - В RG0, МАСКУ НА 7 РАЗР.- В WR2
ANXN:CONT А=М7В,DSRC=PROM,ALUS=DZ,Н,ALUD=В,RВ=WR2,%PHAR%,FFCNT=СНТКК
;ЧИСЛО ЕДИНИЦ ИЗ "ПЛЕ"+1 - В РГ.WR3
CONT ALUS=DZ,Н,CI,ALUD=ВА,RА=А,RВ=WR3,ALU,DSRC=LOS
;МАСКА НА 7 РАЗРЯДОВ
CONT ALUS=АВ,RА=WR2,RВ=WR3,FUNC=AND,Н,ALUD=В
;ВЫЧИСЛЯЕМ ПАРАМЕТР СДВИГА ДЛЯ ОПРЕДЕЛЕНИЯ "ОСТАТКА" СУММАТОРА
CONT А=S0,DSRC=PROM,ALUS=DА,RА=WR3,FUNC=SUBS,Н,CI,ALU,YDST=PSHIFT
;СДВИГАЕМ СУММАТОР НА СООТВЕТСТВУЮЩЕЕ ЧИСЛО РАЗРЯДОВ - В РМР
LDCT А=LCW,МАР=РЕ,ALUS=DZ,Н,ALUD=ВА,RА=А,RВ=У,ALU,DSRC=SHIFT,SHF=LOG
;ЖДЕМ ГОТ.АРБИТРА, ЧИТАЕМ АК В Q
WAS:CJP А=WAS,%WА%,%RСА%,FFCNT=CLRJMP
;ДЕЛАЕМ ЦИКЛИЧЕСКОЕ СЛОЖЕНИЕ; ВОЗВРАТ
CRTN ЕСА,ARA=RG2,ALUS=DА,RА=WR3,Н,ALUD=В,RВ=А,CEN,FFCNT=LOGGRP
;
;
ANXF:ENTRY
;РЭ:0,15ИР:1,ПИА:0,КОП:13
;PROM:213
;
;ПЕРЕХОД, ЕСЛИ АК НЕ 0
CONT ALUS=ZQ,CEN
CJP А=ANX,МАР=РЕ,COND=СТ,SТОРС=/NZ
;ЧИТАЕМ ОПЕРАНД ИЗ МАГАЗИНА; П/У НА П/П ПОДСЧЕТА ЧИСЛА ЕДИНИЦ
CJS А=ANXN,МАР=РЕ,%ARF%,%SSPRG%
;ДОБАВЛЯЕМ ПЕРЕНОС, SР - НА МЕСТО, ПЕРЕХОД НА СЛЕД.КОМАНДУ
JRP %М%,ALUS=ZВ,RВ=А,Н,CI=CIC,ALUD=В,СЕМ,%SPRG%
END
ANXE:PROG;
;РЭ:1,15ИР:0,ПИА:0,КОП:13
;PROM:413
;===== ОПРЕДЕЛЕНИЕ НОМЕРА СТАРШЕЙ ЕДИНИЦЫ В РЭ
;
EXTERNAL LOGER,PSACXE,ANXE
;М.А. - В АИСП, ИАОП;  П/У НА П/П ЧТЕНИЯ И ПРОВЕРКИ СУММАТОРА И ОПЕРАНДА
CJS А=LOGER,МАР=РЕ,%МА%,FFCNT=CLRTR1
;
;===== ОПРЕДЕЛЕНИЕ N СТАРШЕЙ ЕДИНИЦЫ СУММАТОРА В РЭ
;ЧИСЛО ЕДИНИЦ ИЗ "ПЛЕ"+1 - В РГ.WR3
ANXNE:CONT ALUS=DZ,Н,CI,ALUD=ВА,RА=WR1,RВ=WR3,ALU,DSRC=LOS
;МАСКА НА 7 РАЗРЯДОВ
CONT А=М7В,DSRC=PROM,ALUS=DА,RА=WR3,FUNC=AND,Н,ALUD=В,RВ=WR3
;ВЫЧИСЛЯЕМ ПАРАМЕТР СДВИГА ДЛЯ ОПРЕДЕЛЕНИЯ "ОСТАТКА" СУММАТОРА
CONT А=S0,DSRC=PROM,ALUS=DА,RА=WR3,FUNC=SUBS,Н,CI,ALU,YDST=PSHIFT
;СДВИГАЕМ СУММАТОР НА СООТВЕТСТВУЮЩЕЕ ЧИСЛО РАЗРЯДОВ - В РМР
CONT ALUS=DZ,Н,ALUD=ВА,RА=WR1,RВ=У,ALU,DSRC=SHIFT,SHF=LOG
;РАСДВИГАЕМ, ДЕЛАЕМ "ДЫРУ" В ПОРЯДКЕ У РМР
CJP А=PSACXE,МАР=РЕ,ALUS=DZ,Н,ALUD=ВА,RА=У,RВ=У,ALU,DSRC=SHIFT,MPS,PSHF=SН0+4
>,SHF=EMULF
;
;
ANXFE:ENTRY
;РЭ:1,15ИР:1,ПИА:0,КОП:13
;PROM:613
;
CONT А=М15,DSRC=PROM,ALUS=DQ,FUNC=AND,CEN
CJP А=ANXE,МАР=РЕ,COND=СТ,SТОРС=/NZ
;
;МАГАЗИН: 15ИР-1 - В АИСП, В ИАОП, В RG3; П/У НА АНАЛИЗ СУММАТОРА И ОПЕРАНДА
CJS А=LOGER,МАР=РЕ,%ARF%,%SSPRG%,FFCNT=CLRTR1
;SР - НА МЕСТО (ПРЕРЫВАНИЙ НЕ ДОЛЖНО БЫТЬ); П/У НА ANX В РЭ
CJP А=ANXNE,МАР=РЕ,%SPRG%
END
ASX:PROG;
;     I=========I
;     I  А S Х  I
;     I=========I
;
;РЭ:0,15ИР:0,ПИА:0,КОП:16
;PROM:016
;===== СДВИГ СУММАТОРА ПО КОДУ ЛОГИЧЕСКИЙ В НР.
;   ВЫДВИНУТЫЕ ИЗ СУММАТОРА РАЗРЯДЫ ПОПАДАЮТ В РМР:
;   ПРИ СДВИГЕ ВЛЕВО - НАЧИНАЯ С МЛ.РАЗРЯДОВ РМР; ВПРАВО - СО СТАРШИХ.
;   РМР ПЕРЕД ОПЕРАЦИЕЙ ОБНУЛЯЕТСЯ.
;   РГ.А,У ДО ОПЕРАЦИИ:
;   I------------II--------------I
;   I////РГ.А////II    РГ.У      I
;   I------------II--------------I
;   РЕЗУЛЬТАТ ПРИ СДВИГЕ ВПРАВО:      ВЛЕВО:
;   I------------II--------------I    I---------------II--------------I
;   I     I//////II////////I     I    I/////I         II    I/////////I
;   I------------II--------------I    I---------------II--------------I
;         РГ.А            РГ.У              РГ.А            РГ.У
;
EXTERNAL LCW,ASX
;М.А. - В АИСП, ИАОП
CONT %МА%
;ФИЗ.АДРЕС - В RG0, СУММАТОР - В РГ.WR1
CONT %PHAR%,ALUS=ZА,RА=А,Н,ALUD=В,RВ=WR1,FFCNT=СНТКК
;ЖДЕМ ГОТ.АРБИТРА; ЧИТАЕМ АК В Q ДЛЯ ПК
WА:CJP А=WА,%WА%,%RСА%
;ОПЕРАНД СДВИГАЕМ В МЛ.РАЗРЯДЫ АРИФМЕТИЧЕСКИ, ЗАПОМИНАЕМ ЗНАК (НАПРАВЛ.СДВИГА)
ASXS:LDCT А=LCW,МАР=РЕ,ЕСВ,BRA=RG2,DSRC=SHIFT,MPS,PSHF=SН0+53,SHF=ARIPH
>,ALUS=DZ,Н,ALUD=В,RВ=WR2,CEN
;ПАРАМЕТР СДВИГА - В РПС; ЧИТАЕМ CONST ДЛЯ ВЫЧИСЛЕНИЯ ПАР-РА СДВИГА ДЛЯ РМР
CONT А=С64,DSRC=PROM,ALUS=DZ,Н,ALUD=ВА,RА=WR2,RВ=WR3,ALU,YDST=PSHIFT
>,FFCNT=CLRJMP
;СДВИГАЕМ СУММАТОР; П/У, ЕСЛИ СДВИГ ВПРАВО
CJP А=RSH,МАР=РЕ,COND=СТ,SТОРС=NN,ALUS=DZ,Н,ALUD=ВА,RА=WR1,RВ=А,ALU
>,DSRC=SHIFT,SHF=LOG,СЕМ,FFCNT=LOGGRP
;СДВИГ СУММАТОРА ВЛЕВО: УВЕЛИЧИВАЕМ ПАР-Р СДВИГА ДЛЯ РМР НА 64
LSH:CONT ALUS=АВ,RА=WR2,RВ=WR3,FUNC=ADD,Н,ALU,YDST=PSHIFT
;СДВИГАЕМ СУММАТОР - В РМР, ПЕРЕХОД ПО КОП
PSSH:JRP %М%,ALUS=DZ,Н,ALUD=ВА,RА=WR1,RВ=У,ALU,DSRC=SHIFT,SHF=LOG
;
;СДВИГ ВПРАВО: УМЕНЬШАЕМ ПАР-Р СДВИГА ДЛЯ РМР
RSH:CJP А=PSSH,МАР=РЕ,ALUS=АВ,RА=WR2,RВ=WR3,FUNC=SUBS,CI,ALU,YDST=PSHIFT
;
;
ASXF:ENTRY
;РЭ:0,15ИР:1,ПИА:0,КОП:16
;PROM:216
;
;ПЕРЕХОД, ЕСЛИ АК НЕ 0; ПИШЕМ М.А.ДЛЯ МАГАЗИННОГО ОБРАЩЕНИЯ
CONT ALUS=ZQ,CEN
CJP А=ASX,МАР=РЕ,COND=СТ,SТОРС=/NZ,%ARF%
;ЧИТАЕМ ОПЕРАНД ИЗ МАГАЗИНА
;ФИЗ. АДРЕС - В RG0, СУММАТОР - В WR1
CONT %PHAR%,ALUS=ZА,RА=А,Н,ALUD=В,RВ=WR1,FFCNT=СНТКК
;ЖДЕМ ГОТ. АРБИТРА, ЧИТАЕМ АК - В РГ.Q
WALF:CJP А=WALF,%WА%,%RСА%
;Т.К.ПРЕРЫВАНИЙ НЕ БУДЕТ - ПИШЕМ SР НА МЕСТО; ПЕРЕХОД
CJP А=ASXS,МАР=РЕ,ALUS=ZА,RА=DADR,ALU,CSM,WЕМ,MNSA=МР,MODNM=SР
END
ASXE:PROG;
;РЭ:1,15ИР:0,ПИА:0,КОП:16
;PROM:416
;
EXTERNAL LOGER,LCW,ASXE
;М.А. - В АИСП, ИАОП;  П/У НА П/П ЧТЕНИЯ И ПРОВЕРКИ СУММАТОРА И ОПЕРАНДА
CJS А=LOGER,МАР=РЕ,%МА%,FFCNT=CLRTR1
;ОПЕРАНД СДВИГАЕМ В МЛ.РАЗРЯДЫ АРИФМЕТИЧЕСКИ, ЗАПОМИНАЕМ ЗНАК (НАПРАВЛ.СДВИГА)
;Т.К."КАЧЕСТВО" ОПЕРАНДА ПРОВЕРЕНО, БЕРЕМ ЕГО ПРЯМО ИЗ БОИ, ГДЕ ОН С "ДЫРОЙ"
ASXSE:ENTRY
CONT ЕСВ,BRA=RG2,DSRC=SHIFT,MPS,PSHF=SН0+53,SHF=ARIPH,ALUS=DZ,Н,ALUD=В
>,RВ=WR2,CEN
;ПАРАМЕТР СДВИГА - В РПС; ЧИТАЕМ CONST ДЛЯ ВЫЧИСЛЕНИЯ ПАР-РА СДВИГА ДЛЯ РМР
CONT А=С48,DSRC=PROM,ALUS=DZ,Н,ALUD=ВА,RА=WR2,RВ=WR3,ALU,YDST=PSHIFT
;СДВИГАЕМ СУММАТОР; П/У, ЕСЛИ СДВИГ ВПРАВО
CJP А=RSHE,МАР=РЕ,COND=СТ,SТОРС=NN,ALUS=DZ,Н,ALUD=ВА,RА=WR1,RВ=А,ALU
>,DSRC=SHIFT,SHF=LOG,СЕМ
;СДВИГ СУММАТОРА ВЛЕВО: УВЕЛИЧИВАЕМ ПАР-Р СДВИГА ДЛЯ РМР
CONT ALUS=АВ,RА=WR2,RВ=WR3,FUNC=ADD,Н,ALU,YDST=PSHIFT
;СДВИГАЕМ СУММАТОР - В РМР
PSSHE:LDCT А=LCW,МАР=РЕ,ALUS=DZ,Н,ALUD=ВА,RА=WR1,RВ=У,ALU,DSRC=SHIFT,SHF=LOG
;ОБНУЛЯЕМ "ХВОСТ" СУММАТОРА И РМР И РАСДВИГАЕМ (ЗАПОЛНЯЕМ ДЫРУ ИНВЕРТ.ЗНАКОМ)
CONT А=MNQ,DSRC=PROM,ALUS=DА,RА=А,FUNC=AND,Н,ALUD=В,RВ=А
CONT А=MNQ,DSRC=PROM,ALUS=DА,RА=У,FUNC=AND,Н,ALUD=В,RВ=У
CONT ALUS=DZ,Н,ALUD=ВА,RА=А,RВ=А,ALU,DSRC=SHIFT,MPS
>,PSHF=SН0+4,SHF=EMULF
JRP %М%,ALUS=DZ,Н,ALUD=ВА,RА=У,RВ=У,ALU,DSRC=SHIFT,MPS
>,PSHF=SН0+4,SHF=EMULF
;
;СДВИГ СУММАТОРА ВПРАВО: УМЕНЬШАЕМ ПАР-Р СДВИГА
RSHE:CJP А=PSSHE,МАР=РЕ,ALUS=АВ,RА=WR2,RВ=WR3,FUNC=SUBS,Н,CI
>,ALU,YDST=PSHIFT
;
;
ASXFE:ENTRY
;РЭ:1,15ИР:1,ПИА:0,КОП:16
;PROM:616
;
;ПЕРЕХОД, ЕСЛИ АК НЕ 0
CONT А=М15,DSRC=PROM,ALUS=DQ,FUNC=AND,CEN
CJP А=ASXE,МАР=РЕ,COND=СТ,SТОРС=/NZ
;
;МАГАЗИН: 15ИР-1 - В АИСП, В ИАОП, В RG3; П/У НА АНАЛИЗ СУММАТОРА И ОПЕРАНДА
CJS А=LOGER,МАР=РЕ,%ARF%,%SSPRG%,FFCNT=CLRTR1
;SР - НА МЕСТО, П/У НА ASX В РЭ
CJP А=ASXSE,МАР=РЕ,ALUS=ZА,RА=DADR,ALU,CSM,WЕМ,MNSA=МР,MODNM=SР
END
ASN:PROG;
;     I=========I
;     I  А S N  I
;     I=========I
;
;РЭ:0,15ИР:0-1,ПИА:0,КОП:16,ДОП.ФОРМАТ
;PROM:816,А16
;===== СДВИГ СУММАТОРА ПО АДРЕСУ ЛОГИЧЕСКИЙ В НР. АНАЛОГИЧНО ASX.
;
EXTERNAL LCW,JQ
;ПАР-Р СДВИГА - В DADR И РПС; ПРОВЕРЯЕМ НАПРАВЛЕНИЕ СДВИГА (11 РАЗРЯД)
LDCT А=RSH,МАР=РЕ,CSM,MNSA=IRA,ALUS=DQ,FUNC=ADD,ALUD=В,RВ=DADR,ALU
>,YDST=PSHIFT
CONT А=В11,DSRC=PROM,ALUS=DА,RА=DADR,FUNC=AND,CEN
;СДВИГАЕМ СУММАТОР - В РГ.WR1; ПЕРЕХОД В ЗАВИСИМОСТИ ОТ НАПРАВЛЕНИЯ СДВИГА
JSRP А=LSH,МАР=РЕ,COND=СТ,SТОРС=NZ,ALUS=DZ,Н,ALUD=ВА,RА=А,RВ=WR1,ALU
>,DSRC=SHIFT,SHF=LOG,FFCNT=CLRJMP
;СДВИГАЕМ А ДЛЯ РМР
LDCT А=LCW,МАР=РЕ,ALUS=DZ,Н,ALUD=ВА,RА=А,RВ=У,ALU,DSRC=SHIFT,SHF=LOG
>,FFCNT=СНТКК
;ПЕРЕПИСЫВАЕМ РЕЗУЛЬТАТ В СУММАТОР
JRP А=JQ,МАР=РЕ,COND=ТККВ,DDEV=CLRCD,ISE,ALUS=ZА,RА=WR1,Н,ALUD=В,RВ=А
>,СЕМ,FFCNT=LOGGRP
;
;ИЗМЕНЯЕМ ПАРАМЕТР СДВИГА ДЛЯ РМР: RSH - ВПРАВО, -64; LSH - ВЛЕВО, +64
RSH:CRTN А=С64,DSRC=PROM,ALUS=DА,RА=DADR,FUNC=SUBR,CI,ALU,YDST=PSHIFT
LSH:CRTN А=С64,DSRC=PROM,ALUS=DА,RА=DADR,FUNC=ADD,ALU,YDST=PSHIFT
END
ASNE:PROG;
;РЭ:1,15ИР:0-1,ПИА:0,КОП:1Е
;PROM:41Е,61Е
;
EXTERNAL LOGER,ASXSE
;П/У НА П/П ПРОВЕРКИ СУММАТОРА, СДВИГАЕМ АК В СТ.РАЗРЯДЫ, ЗАПОМИНАЕМ В БОИ
CONT А=МРР,DSRC=PROM,ALUS=DА,RА=А,FUNC=NOTRS,Н,ALUD=В,RВ=WR1,CEN
CONT CSM,MNSA=IRA,ALUS=DQ,FUNC=ADD,ALUD=В,RВ=DADR,FFCNT=SETTR1
CJS А=LOGER1,МАР=РЕ,ALUS=ZА,RА=DADR,Н,ALU,DSRC=SHIFT,MPS,PSHF=SН0-57,SHF=LOG
>,ЕСА,WRA,ARA=RG3,FFCNT=СНТКК
;"РАСХЛАПЫВАЕМ", ЗАПОМИНАЕМ ЗНАК (НАПРАВЛЕНИЕ СДВИГА); ДАЛЬШЕ - КАК В ASX В РЭ
CJP А=ASXSE,МАР=РЕ,ЕСВ,BRA=RG3,DSRC=SHIFT,MPS,PSHF=SН0+4,SHF=EMULF,ЕСА,WRA
>,ARA=RG2,FFCNT=CLRTR1
END
ATI:PROG;
;      ==========================
;      4.ИНДЕКСНЫЕ КОМАНДЫ БЭСМ-6
;      ==========================
;
;ATI,STI,IТА,ITS,MTJ,J+М(JDM),UТС,WТС,VТМ,UТМ
;
;     I=========I
;     I  А Т I  I
;     I=========I
;
;РЭ:0,15ИР:0-1,ПИА:0,КОП:20,ДОП.ФОРМАТ
;PROM:820,А20
;===== 1-32 РАЗРЯДЫ СУММАТОРА ПЕРЕДАЮТСЯ В МОДИФИКАТОР ПО АИСП.
;
EXTERN LCWISE,JIQ
;ИСПОЛНИТ.АДРЕС (М+Q) - В АИСП
LDCT А=LCWISE,МАР=РЕ,CSM,MNSA=IRA,ALUS=DQ,ALUD=В,RВ=DADR,ALU,YDST=ADRREG
;ПЕРЕПИСЫВАЕМ 32 РАЗР.РГ.А В ОЗУ МОД.ПО АИСП.
JRP А=JIQ,МАР=РЕ,COND=RСВ,IСС,ALUS=ZА,RА=А,ALU,CSM,WЕМ,MNSA=U,FFCNT=СНТКК
END
ATIE:PROG;
;РЭ:1,15ИР:0-1,ПИА:0,КОП:20
;PROM:420,620
;
EXTERNAL LCW
;ИСП.АДРЕС - В АИСП
CONT CSM,MNSA=IRA,ALUS=DQ,FUNC=ADD,ALUD=В,RВ=DADR,ALU,YDST=ADRREG
;СДВИГАЕМ СУММАТОР В МЛ.РАЗРЯДЫ (ВПРАВО НА 12)
LDCT А=LCW,МАР=РЕ,ALUS=ZА,RА=А,Н,ALU,DSRC=SHIFT,MPS,PSHF=SН0+12,SHF=LOG
>,ЕСА,WRA,ARA=RG3,FFCNT=СНТКК
;ПИШЕМ ПО АИСП
JRP %М%,%RСА%,ЕСВ,BRA=RG3,CSM,WЕМ,MNSA=U,FFCNT=CLRJMP
END
STI:PROG;
;     I=========I
;     I  S Т I  I
;     I=========I
;
;РЭ:0-1,15ИР:0-1,ПИА:0,КОП:21,ДОП.ФОРМАТ В НР; ОСН.ФОРМАТ В РЭ
;PROM:821,А21,421,621
;===== ЗАПИСЬ В МОДИФИКАТОР МАГАЗИННАЯ
;   ATI ПО АИСП.
;   15 ХТА
; - В ИНДЕКС-РЕГИСТР ПИШЕМ ПОСЛЕ ПОЯВЛЕНИЯ "ГОТ.АРБИТРА",
;   Т.К. ОН МОЖЕТ УЧАСТВОВАТЬ В ФОРМИРОВАНИИ ИСП.АДРЕСА;
; - ЕСЛИ АИСП.=15, ЧТЕНИЕ ИДЕТ ПО АДРЕСУ, РАВНОМУ СОДЕРЖИМОМУ РГ.А
;   (ВЫЧИТАНИЕ "1" НЕ ПРОИСХОДИТ!)
;
EXTERNAL LCW,RDOPER,STIE
;ИСПОЛН.АДРЕС (М+Q) - В WR5
CONT CSM,MNSA=IRA,ALUS=DQ,FUNC=ADD,ALUD=В,RВ=WR5
;РАВЕН ЛИ ОН 15?
CONT А=С15,DSRC=PROM,ALUS=DА,RА=WR5,FUNC=EXOR,ALUD=В,RВ=WR4
CJP А=STIE,МАР=РЕ,COND=EMLRG,ALUS=DZ,Н,ALUD=ВА,RА=WR4,RВ=WR4,CEN,ALU
>,DSRC=SHIFT,MPS,PSHF=SН0-60,SHF=LOG
;ЕСЛИ АИСП.НЕ 15, ОБЫЧНОЕ МАГАЗИННОЕ ЧТЕНИЕ: 15ИР-1 - В АИСП.,ИАОП, ЧИТАЕМ
CJP А=IА15,МАР=РЕ,COND=СТ,SТОРС=NZ,%ARF%,FFCNT=СНТКК
CONT %PHAR%,%RСА%
;ЖДЕМ ГОТ.А.; АИСП.КОМАНДЫ - В РГ.АИСП.
WALF:CJP А=WALF,%WА%,ALUS=ZА,RА=WR5,ALU,YDST=ADRREG
;ПИШЕМ СУММАТОР ПО АИСП.
LDCT А=LCW,МАР=РЕ,ALUS=ZА,RА=А,ALU,CSM,WЕМ,MNSA=U,FFCNT=CLRJMP
;ПЕРЕХОД НА ДР.КОМАНДУ, НОВЫЙ SР НА МЕСТО, ЧИТАЕМ В СУММАТОР, СБРОС ПИА, ПМИ
JRP %М%,ЕСА,ARA=RG2,ALUS=DZ,Н,ALUD=ВА,RА=DADR,RВ=А,СЕМ,FFCNT=LOGGRP
>,ALU,CSM,WЕМ,MNSA=МР,MODNM=SР
;
;АИСП.=15: АДРЕС ПАМЯТИ = СОДЕРЖИМОМУ РГ.А; ПИШЕМ МАТ.АДРЕС
IА15:CJS А=RDOPER,МАР=РЕ,ALUS=ZА,RА=А,ALUD=В,RВ=DADR,ALU,YDST=ADRREG
>,FFCNT=СНТКК
;ПЕРЕХОД НА СЛЕД.КОМАНДУ, ПМИ, ЧИТАЕМ В СУММАТОР, ПИШЕМ В МОДИФИКАТОР
JRP %М%,ЕСА,ARA=RG2,ALUS=DZ,Н,ALUD=ВА,RА=DADR,RВ=А,СЕМ,FFCNT=LOGGRP
>,ALU,CSM,WЕМ,MNSA=МР,MODNM=SР
END
STIE:PROG;
;===== ПРОДОЛЖЕНИЕ STI В РЭ
;
EXTERNAL LCW,JQ
;СДВИГАЕМ СУММАТОР ВПРАВО НА 12 РАЗРЯДОВ - В WR1
CONT ALUS=DZ,Н,ALUD=ВА,RА=А,RВ=WR1,ALU,DSRC=SHIFT,MPS,PSHF=SН0+12
>,SHF=LOG
;ЕСЛИ АИСП.НЕ 15, ОБЫЧНОЕ МАГАЗИННОЕ ЧТЕНИЕ: 15ИР-1 - В АИСП.,ИАОП, ЧИТАЕМ
CJP А=IА15Е,МАР=РЕ,COND=СТ,SТОРС=NZ,%ARF%,FFCNT=СНТКК
CONT %PHAR%,%RСА%
;АИСП.КОМАНДЫ - В РГ.АИСП
WF:CJP А=WF,%WА%,ALUS=ZА,RА=WR5,ALU,YDST=ADRREG
;ПИШЕМ ПО АИСП
LDCT А=LCW,МАР=РЕ,ALUS=ZА,RА=WR1,ALU,CSM,WЕМ,MNSA=U,FFCNT=CLRJMP
;ПЕРЕХОД ПО КОП, НОВЫЙ SР НА МЕСТО, ЧИТАЕМ В СУММАТОР, СБРОС ПИА, ПМИ
JRP %М%,ЕСА,ARA=RG2,ALUS=DZ
>,Н,ALUD=ВА,RА=DADR,RВ=А,СЕМ,FFCNT=LOGGRP,ALU,CSM,WЕМ,MNSA=МР,MODNM=SР
;
;АИСП.=15: АДРЕС ПАМЯТИ = СОДЕРЖИМОМУ РГ.А (СДВИНУТОМУ В МЛ.РАЗРЯДЫ!)
IА15Е:CONT ALUS=ZА,RА=WR1,ALUD=В,RВ=DADR,ALU,YDST=ADRREG
;ФИЗ.АДРЕС - В RG0
CONT %PHAR%,%RСА%
WAF:CJP А=WAF,%WА%
;ПИШЕМ ПО АИСП (=15)
LDCT А=LCW,МАР=РЕ,ALUS=ZА,RА=WR1,ALU,CSM,WЕМ,MNSA=МР,MODNM=SР,FFCNT=CLRJMP
;ПЕРЕХОД НА СЛЕД.КОМАНДУ, ЧИТАЕМ В СУММАТОР, СБРОС ПИА, ПМИ
JRP %М%,ЕСА,ARA=RG2,ALUS=DZ,Н,ALUD=ВА,RА=DADR,RВ=А,СЕМ,FFCNT=LOGGRP
END
IТА:PROG;
;     I=========I
;     I  I Т А  I
;     I=========I
;
;РЭ:0,15ИР:0-1,ПИА:0,КОП:80,ДОП.ФОРМАТ
;PROM:880,А80
;===== 1-32 РАЗРЯДЫ МОДИФИКАТОРА ПО АИСП. ПЕРЕДАЮТСЯ В СУММАТОР
;   В НР РАЗМНОЖАЕМ ЗНАК МОДИФИКАТОРА В 33-64 РАЗРЯДЫ!
;   ПОЭТОМУ ДЕЛАЕМ АНАЛОГИЧНО ДР.КОМАНДАМ С I-ОПЕРАНДОМ:
;   ДОСТАЕМ МОДИФИКАТОР И П/У НА PS СИММЕТРИЧНОЙ КОМАДЫ С U-ОПЕРАНДОМ!
;
EXTERNAL LCW,PSUTA
;ИСПОЛНИТ.АДРЕС (М+Q) - В АИСП
LDCT А=LCW,МАР=РЕ,COND=EMLRG,CSM,MNSA=IRA,ALUS=DQ,FUNC=ADD,ALUD=В,RВ=DADR
>,ALU,YDST=ADRREG
PSITA:ENTRY
;ЧИТАЕМ МОДИФИКАТОР (32 РАЗРЯДА), ЗАПОМИНАЕМ ЕГО В RG2 БОИ, ПРОВЕРЯЕМ ЗНАК!
CJP А=PSUTA,МАР=РЕ,CSM,MNSA=U,ЕСА,WRA,ARA=RG2,ALUS=DZ,CEN,FFCNT=CLRJMP
END
IТАЕ:PROG;
;РЭ:1,15ИР:0-1,ПИА:0,КОП:22
;PROM:422,622
;
EXTERNAL LCW,JQ
;ИСПОЛНИТ.АДРЕС (М+Q) - В АИСП
CONT CSM,MNSA=IRA,ALUS=DQ,FUNC=ADD,ALUD=В,RВ=DADR,ALU,YDST=ADRREG
;ЧИТАЕМ МОДИФИКАТОР
LDCT А=LCW,МАР=РЕ,CSM,MNSA=U,ALUS=DZ,ALUD=В,RВ=WR1,FFCNT=CLRJMP
;ОСТАВЛЯЕМ ТОЛЬКО 15 МЛ.РАЗРЯДОВ
CONT А=М15,DSRC=PROM,ALUS=DА,RА=WR1,FUNC=AND,Н,ALUD=В,RВ=WR1
;ЧИТАЕМ "ДЫРУ" В ПОРЯДКЕ
CONT А=MD,DSRC=PROM,ALUS=DZ,Н,ALUD=Q,FFCNT=СНТКК
;СДВИГАЕМ МОДИФ.НА МЕСТО, ОТВЕДЕННОЕ ДЛЯ РЭ (13-28 РАЗР.), ДОБАВЛЯЕМ "ДЫРУ"
JRP А=JQ,МАР=РЕ,COND=ТККВ,ALUS=DQ,FUNC=OR,Н,ALUD=ВА,RА=WR1,RВ=А,СЕМ
>,FFCNT=LOGGRP,ALU,DSRC=SHIFT,MPS,PSHF=SН0-12,SHF=LOG,DDEV=CLRCD,WRD,ISE
END
ITS:PROG;
;     I=========I
;     I  I Т S  I
;     I=========I
;
;РЭ:0-1,15ИР:0-1,ПИА:0,КОП:22,23,ОСН.И ДОП.ФОРМАТ
;PROM:822,А22,423,623
;===== 15 АТХ         ЗАПИСЬ СУММАТОРА В МАГАЗИН
;      IТА АИСП.      ЧТЕНИЕ МОДИФИКАТОРА В СУММАТОР ПО ИСП.АДРЕСУ
;   ЕСЛИ АИСП.=15, ЧИТАЕТСЯ НОВОЕ(!) ЗНАЧЕНИЕ 15ИР (Т.Е.УВЕЛИЧЕННОЕ НА 1)
;   ПОЭТОМУ СНАЧАЛА ПИШЕМ СУММАТОР В ПАМЯТЬ И ЖДЕМ ГОТ.А.,ЧТОБ НЕ БЫЛО ПРЕРЫВ.,
;   ЗАТЕМ ПИШЕМ SР НА МЕСТО, А ПОТОМ УЖЕ ЧИТАЕМ МОДИФИКАТОР.
;
EXTERN LCW,ITSE,PSITA
;15 РЕГИСТР - В АИСП, В ИАОП; СУММАТОР - В БОИ ДЛЯ ЗАПИСИ В ОЗУ; ВЫЧИСЛЯЕМ АИСП.
CONT %MAWF%
LDCT А=LCW,МАР=РЕ,%PHAW%
;ЕСЛИ РЭ, ЗАГРУЖАЕМ В РГ/СЧ АДРЕС ПЕРЕХОДА ДЛЯ РЭ!
PUSH А=ITSE,МАР=РЕ,COND=EMLRG,ALUS=ZВ,RВ=WR6,Н,ALUD=ВА,RА=А,ALU,ЕСВ,WRB,BRA=RG3
;ЖДЕМ ГОТ.АРБИТРА, ПИШЕМ АДРЕС В АИСП. ДО ГОТ.АРБИТРА!
LООР COND=ARBRDY,CSM,MNSA=IRA,ALUS=DQ,FUNC=ADD,ALU,YDST=ADRREG
;ПИШЕМ НОВОЕ ЗНАЧЕНИЕ SР
JRP А=PSITA,МАР=РЕ,COND=EMLRG,IСС,ALUS=ZА,RА=DADR,CI,ALU,CSM,WЕМ,MNSA=МР
>,MODNM=SР
END
ITSE:PROG;
;===== ПРОДОЛЖЕНИЕ КОМАНДЫ ITS В РЭ:
;
EXTERNAL LCW
;ЧИТАЕМ МАСКУ НА 15 РАЗРЯДОВ
CONT А=М15,DSRC=PROM,ALUS=DZ,Н,ALUD=В,RВ=WR1
;ЧИТАЕМ МОДИФИКАТОР, ОСТАВЛЯЕМ ТОЛЬКО 15 РАЗРЯДОВ
CONT CSM,MNSA=U,ALUS=DА,RА=WR1,FUNC=AND,Н,ALUD=В,RВ=WR1,FFCNT=CLRJMP
;СДВИГАЕМ ИХ НА МЕСТО, ОТВЕДЕННОЕ ДЛЯ РЭ (13-28 РАЗР.)
LDCT А=LCW,МАР=РЕ,ALUS=DZ,Н,ALUD=ВА
>,RА=WR1,RВ=А,ALU,DSRC=SHIFT,MPS,PSHF=SН0-12,SHF=LOG,FFCNT=СНТКК
;ДОБАВЛЯЕМ "ДЫРУ" В ПОРЯДКЕ, ПЕРЕХОД НА СЛЕД.КОМАНДУ
CONT А=MD,DSRC=PROM,ALUS=DА,RА=А,FUNC=OR,Н,ALUD=В,RВ=А,СЕМ,FFCNT=LOGGRP
JRP %М%,%RСА%
END
MTJ:PROG;
;     I=========I
;     I  М Т J  I
;     I=========I
;
;РЭ:0-1,15ИР:0-1,ПИА:0,КОП:24,ОСН.И ДОП.ФОРМАТ
;PROM:824,А24,424,624
;===== ПЕРЕДАЧА СОДЕРЖИМОГО ИНДЕКС-РЕГИСТРА,ЗАДАВАЕМОГО В ПОЛЕ МОДИФИКАТОРА,
;   В ИНДЕКС-РЕГИСТР ПО ИСП.АДРЕСУ
;
EXTERN LCW
;ЧИТАЕМ МОДИФИКАТОР ПО IRA В RG3
CONT CSM,MNSA=IRA,ЕСА,WRA,ARA=RG3
;ИСПОЛНИТ.АДРЕС (Q) - В АИСП
LDCT А=LCW,МАР=РЕ,ALUS=ZQ,ALUD=В,RВ=DADR,ALU,YDST=ADRREG,FFCNT=СНТКК
;ПЕРЕПИСЫВАЕМ МОДИФ.ИЗ RG3 В ОЗУ МОД.ПО АИСП.,ПЕРЕХОД НА СЛЕД.КОМАНДУ
JRP %М%,%RСА%,ЕСВ,BRA=RG3,CSM,WЕМ,MNSA=U,FFCNT=CLRJMP
END
JDM:PROG;
;     I=========I
;     I  J + М  I
;     I=========I
;
;РЭ:0-1,15ИР:0-1,ПИА:0,КОП:25,ОСН.И ДОП.ФОРМАТ
;PROM:825,А25,425,625
;===== СЛОЖЕНИЕ ИНДЕКС-РЕГИСТРОВ, ЗАДАННЫХ В ПОЛЕ МОДИФИКАТОРА И ПО АИСП.
;   РЕЗ-Т - В РЕГИСТРЕ ПО АИСП.
;
EXTERNAL LCW,JQ
;ЧИТАЕМ МОДИФИКАТОР ПО IRA В RG3, ИСПОЛН.АДРЕС (Q) - В АИСП.
CONT CSM,MNSA=IRA,ЕСА,WRA,ARA=RG3,ALUS=ZQ,ALUD=В,RВ=DADR,ALU,YDST=ADRREG
;ЧИТАЕМ МОДИФИКАТОР ПО АИСП. - В РГ.WR1
LDCT А=LCW,МАР=РЕ,CSM,MNSA=U,ALUS=DZ,ALUD=В,RВ=WR1,FFCNT=СНТКК
;СКЛАДЫВАЕМ МОДИФИКАТОРЫ - ПИШЕМ В ОЗУ МОД., ИДЕМ НА СЛЕД.КОМАНДУ
JRP А=JQ,МАР=РЕ,COND=ТККВ,ЕСА,ARA=RG3,ALUS=DА,RА=WR1,ALU,CSM,WЕМ,MNSA=U
>,DDEV=CLRCD,WRD,ISE,FFCNT=CLRJMP
END
UТС:PROG;
;     I=========I
;     I  U Т С  I
;     I=========I
;
;РЭ:0-1,15ИР:0-1,ПИА:0,КОП:90
;PROM:090,290,490,690
;===== ИСПОЛНИТ.АДРЕС КОМАНДЫ ЗАНОСИТСЯ В С-РЕГИСТР; УСТАНАВЛИВАЕТСЯ ПИА=1
;   В ПОСЛЕДНЕЙ М/К - УСТАНОВКА  ПИА, ПОЭТОМУ ПЕРЕХОД ПО КОП - В СЛЕД.МК!
;   В РЭ И НР КОМАНДА ИСПОЛНЯЕТСЯ ОДИНАКОВО! Т.К.16-32Р.РГ.С СОХРАНЯТЬ
;   БЕСПОЛЕЗНО, ОНИ ВСЕ РАВНО ОБНУЛЯЮТСЯ В КОМАНДАХ ПЕРЕДАЧИ УПРАВЛЕНИЯ,
;   ГДЕ МОЖЕТ ПРОИСХОДИТЬ СМЕНА РЕЖИМА!
;
EXTERNAL LCW,JQ
;ИСПОЛНИТ.АДРЕС (М+Q) - В DADR
CONT CSM,MNSA=IRA,ALUS=DQ,FUNC=ADD,ALUD=В,RВ=DADR
>,FFCNT=CLRJMP
;ПЕРЕПИСЫВАЕМ АИСП.(32 РАЗР.) В РГ.С
LDCT А=LCW,МАР=РЕ,ALUS=ZА,RА=DADR,ALU,CSM,WЕМ,MNSA=МР,MODNM=С,FFCNT=СНТКК
JRP А=JQ,МАР=РЕ,COND=ТККВ,FFCNT=SЕТС,ISE
END
WТС:PROG;
;     I=========I
;     I  W Т С  I
;     I=========I
;
;РЭ:0,15ИР:0,ПИА:0,КОП:3В
;PROM:03В
;===== СОДЕРЖИМОЕ 1-32 РАЗР.СЛОВА ПО ИСПОЛН.АДРЕСУ ЗАНОСИТСЯ В РГ.С,
;   УСТАНАВЛИВАЕТСЯ ПРИЗНАК ПИА=1
;
EXTERN RDOPER,LCW,WТС,JQ,RDOPER1
;М.А. - В АИСП, ИАОП
CJS А=RDOPER,МАР=РЕ,%МА%,FFCNT=CLRJMP
;ПМИ, ПИШЕМ ПРОЧИТАННОЕ В РГ.С
JRP А=JQ,МАР=РЕ,COND=ТККВ,ISE
>,FFCNT=SЕТС,ЕСВ,BRA=RG2,CSM,WЕМ,MNSA=МР,MODNM=С
;
;
WTCF:ENTRY
;РЭ:0,15ИР:1,ПИА:0,КОП:3В
;PROM:23В
;
;ЕСЛИ АИСП.=0, ЧИТАЕМ ЧИСЛО ИЗ МАГАЗИНА: 15-1 -> 15; ЧТЕНИЕ ПО 15ИР.
LDCT А=LCW,МАР=РЕ,ALUS=ZQ,CEN
CJP А=WТС,МАР=РЕ,COND=СТ,SТОРС=/NZ,%ARF%
;МАГАЗИННОЕ ЧТЕНИЕ: 15ИР-1 - В АИСП, В ИАОП, МЕНЯЕМ ТКК
CJS А=RDOPER1,МАР=РЕ,SCI,ICI,%PHAR%,FFCNT=СНТКК
;ПИШЕМ ПРОЧИТАННОЕ В РГ.С
LDCT А=LCW,МАР=РЕ,ЕСВ,BRA=RG2,CSM,WЕМ,MNSA=МР,MODNM=С,FFCNT=SЕТС
;ПЕРЕХОД ПО КОП, НОВЫЙ SР НА МЕСТО,ПМИ
JRP %MED%,FFCNT=CLRJMP,ALUS=ZА,RА=DADR
>,ALU,CSM,WЕМ,MNSA=МР,MODNM=SР
END
WТСЕ:PROG;
;РЭ:1,15ИР:0,ПИА:0,КОП:98
;PROM:498
;
EXTERNAL RDOPER,LCW,WТСЕ,RDOPER1
;М.А. - В АИСП, ИАОП, П/У НА П/П ДЛЯ WТС В РЭ
CJS А=RDOPER,МАР=РЕ,%МА%
;СДВИГАЕМ ПРОЧИТАННОЕ В МЛ.РАЗРЯДЫ
PSWTCE:LDCT А=LCW,МАР=РЕ,ЕСВ,BRA=RG2,DSRC=SHIFT,MPS,PSHF=SН0+12,SHF=LOG
>,ALUS=DZ,Н,ALUD=В,RВ=WR1,FFCNT=SЕТС
;ПИШЕМ В РГ.С, ПЕРЕХОД НА СЛЕД.КОМАНДУ
JRP %MED%,ALUS=ZА,RА=WR1,ALU,CSM,WЕМ,MNSA=МР,MODNM=С
;
;
WTCFE:ENTRY
;РЭ:1,15ИР:1,ПИА:0,КОП:98
;PROM:698
;
;ПЕРЕХОД, ЕСЛИ АК НЕ 0
CONT А=М15,DSRC=PROM,ALUS=DQ,FUNC=AND,CEN
CJP А=WТСЕ,МАР=РЕ,COND=СТ,SТОРС=/NZ,%ARF%
;МАГАЗИНН
CJS А=RDOPER1,МАР=РЕ,SCI,ICI,%PHAR%,FFCNT=СНТКК
CJP А=PSWTCE,МАР=РЕ,ALUS=ZА,RА=DADR,ALU,CSM,WЕМ,MNSA=МР,MODNM=SР
END
VТМ:PROG;
;     I=========I
;     I  V Т М  I
;     I=========I
;
;РЭ:0-1,15ИР:0-1,ПИА:0,КОП:3С,А0
;PROM:03С,23С,4А0,6А0
;===== ИСП.АДРЕС КОМАНДЫ ЗАНОСИТСЯ В ИНДЕКС-РЕГИСТР,УКАЗАННЫЙ В ПОЛЕ МОДИФИК.
;
EXTERNAL LCWISE,JIQ
;ИСПОЛНИТ.АДРЕС (Q) - В ОЗУ МОД.ПО IRA
LDCT А=LCWISE,МАР=РЕ,ALUS=ZQ,ALUD=В,RВ=DADR,ALU,CSM,WЕМ,MNSA=IRA
JRPCH:ENTRY
;МЕНЯЕМ ТКК, ПЕРЕХОД НА ДР.КОМАНДУ
JRP А=JIQ,МАР=РЕ,COND=RСВ,IСС,FFCNT=СНТКК
END
UТМ:PROG;
;     I=========I
;     I  U Т М  I
;     I=========I
;
;РЭ:0-1,15ИР:0-1,ПИА:0,КОП:3D,А8
;PROM:03D,23D,4А8,6А8
;===== ИСП.АДРЕС КОМАНДЫ ДОБАВЛЯЕТСЯ К ИНДЕКС-РЕГИСТРУ,УКАЗАННОМУ В ПОЛЕ МОДИФ.
;
EXTERNAL VТМ
;ВЫЧИСЛЯЕМ ИСП.АДРЕС (М+Q); ДАЛЬШЕ - КАК В VТМ
CJP А=VТМ,МАР=РЕ,CSM,MNSA=IRA,ALUS=DQ,ALUD=Q
END
UZA:PROG;
;      ====================================
;      5.КОМАНДЫ ПЕРЕДАЧИ УПРАВЛЕНИЯ БЭСМ-6
;      ====================================
;
;UZA,UIA,JМР(JАМР),VJM,JMEQ,JMNE,VLM
;
;     I=========I
;     I  U Z А  I
;     I=========I
;
;РЭ:0-1,15ИР:0-1,ПИА:0,КОП:ЕВ В НР; В0 В РЭ
;PROM:0ЕВ,2ЕВ,4В0,6В0
;===== УСЛОВНЫЙ ПЕРЕХОД ПО "0" В ЗАВИСИМОСТИ ОТ ПРИЗНАКОВ ГРУПП
;   РЭ ОТЛИЧАЕТСЯ ТОЛЬКО ТЕМ, ЧТО В РС ЗАПИСЫВАЕТСЯ МЛ.15 РАЗРЯДОВ АИСП.
;   ПРОВЕРКУ РЕЖИМА ДЕЛАЕМ ПРЯМО В LCWJMP, ТАМ И ОБНУЛЯЕМ СТ.РАЗРЯДЫ, ЕСЛИ РЭ
;   СЛ.1 = 0    СЛ.0 = 1
;
EXTERNAL JNNZ
;УСТАНАВЛИВАЕМ СЛ.0 = 1, СЛ.1 = 0; ВЫЧИСЛЯЕМ АИСП.
CONT CSM,MNSA=IRA,ALUS=DQ,ALUD=Q,FFCNT=SETTR0
LDCT А=JNNZ,МАР=РЕ,FFCNT=CLRTR1
;ПЕРЕХОДИМ ПО ПНА ГРУПП, ЧИТАЕМ МАСКУ ДЛЯ РЭ; 0 - В NZ, ЕСЛИ ВСЕ ПРИЗНАКИ = 0
CJP А=М48,МАР=GRP,DSRC=PROM,ALUS=DZ,Н,ALUD=В,RВ=WR1,SТОРС=CLRNZ,CEN
>,FFCNT=CLRJMP
END
UZAPNA:PROG;
EXTERNAL JNZ
;   СИГНАЛЫ, УПРАВЛЯЮЩИЕ ВЫБОРКОЙ ИЗ ПНА ГРУПП:
;   СЛ1, СЛ0, ГС, ГУ, ГЛ
;    0    1    Х   Х   Х  -  ДЛЯ КОМАНДЫ  U Z А
;   ЕСЛИ ОДНОВРЕМЕННО УСТАНОВЛЕНЫ НЕСКОЛЬКО ПРИЗНАКОВ ГРУПП, ТО ПРИОРИТЕТ
;   ИМЕЕТ ГРУППА СЛОЖЕНИЯ (ГС); ЕСЛИ ЕЕ НЕТ - ГРУППА УМНОЖЕНИЯ (ГУ)
;
;===== ГРУППА СЛОЖЕНИЯ (ГС) ДЛЯ КОМАНДЫ UZA - ПЕРЕНЕСЕНА В ВКС
;   ВОЗМОЖНЫЕ КОМБИНАЦИИ ВХОДНЫХ СИГНАЛОВ В1-В5: 0С,0D,0Е,0F
;   ЕСЛИ СТАРШИЕ ВХОДНЫЕ СИГНАЛЫ ПНА ГРУПП В6-В11 НЕ ОПРЕДЕЛЕНЫ,
;   ТО ЭТОТ АДРЕС ЗАНОСИТСЯ И ПО ВСЕМ КОМБИНАЦИЯМ СТАРШИХ СИГНАЛОВ!
;   В12,В13 - НОМЕР ПНА ГРУПП ДЛЯ ЗАГРУЗЧИКА = 3.
;В ВКС: PROM:180С-180F
;
;===== ВСЕ ПРИЗНАКИ ГРУПП = 0 ДЛЯ КОМАНДЫ UZA - ПЕРЕНЕСЕНА В ВКС
;В ВКС: PROM:1808
;
UZAMUL:ENTRY
;===== ГРУППА УМНОЖЕНИЯ (ГУ) ДЛЯ КОМАНДЫ UZA
;   ВОЗМОЖНЫЕ КОМБИНАЦИИ ВХОДНЫХ СИГНАЛОВ:
;   0 1 0 1 0  =  0А Н
;   0 1 0 1 1  =  0В Н
;   ПО ЭТИМ АДРЕСАМ В ПНА ГРУПП Д.БЫТЬ ЗАНЕСЕН АДРЕС СЛЕДУЮЩЕЙ МИКРОКОМАНДЫ!
;PROM:180А,180В
;ПРИ ГУ ПРОИСХОДИТ ПЕРЕДАЧА УПРАВЛЕНИЯ ПО АИСП,
;ЕСЛИ ПОРЯДОК ЧИСЛА НА СУММАТОРЕ БОЛЬШЕ ИЛИ РАВЕН 0 (64 РАЗРЯД = 1)
;ПРОВЕРЯЕМ; П/У НА АНАЛИЗ /NZ
JRP А=В64,DSRC=PROM,ALUS=DА,RА=А,FUNC=AND,Н,CEN,IСС,FFCNT=CLRTR1
;
UZALOG:ENTRY
;===== ЛОГИЧЕСКАЯ ГРУППА (ГЛ) ДЛЯ КОМАНДЫ UZA
;   ВОЗМОЖНЫЕ КОМБИНАЦИИ ВХОДНЫХ СИГНАЛОВ:
;   0 1 0 0 1  =  09 Н
;   ПО ЭТОМУ АДРЕСУ В ПНА ГРУПП Д.БЫТЬ ЗАНЕСЕН АДРЕС СЛЕДУЮЩЕЙ МИКРОКОМАНДЫ!
;PROM:1809
;ПРИ ЛОГИЧЕСКОЙ ГРУППЕ ПРОИСХОДИТ ПЕРЕДАЧА УПРАВЛЕНИЯ ПО АИСП,
;ЕСЛИ В СУММАТОРЕ НУЛЕВОЙ КОД (В НР ПРОВЕРЯЮТСЯ 64 РАЗРЯДА, В РЭ - 48: 64,59-13)
CJP А=JNZ,МАР=РЕ,COND=EMLRG,IСС,ALUS=ZА,RА=А,Н,CEN,FFCNT=CLRTR1
CJP А=JNZ,МАР=РЕ,ALUS=АВ,RА=А,RВ=WR1,FUNC=AND,Н,CEN
END
UIA:PROG;
;     I=========I
;     I  U I А  I
;     I=========I
;
;РЭ:0-1,15ИР:0-1,ПИА:0,КОП-ЕС В НР; В8 В РЭ
;PROM:0ЕС,2ЕС,4В8,6В8
;===== УСЛОВНЫЙ ПЕРЕХОД ПО "1" В ЗАВИСИМОСТИ ОТ ПРИЗНАКОВ ГРУПП
;   СЛ.1 = 1;  СЛ.0 = 0
;
EXTERNAL JNZ
;СЛ.0 = 0, СЛ.1 = 1; ВЫЧИСЛЯЕМ АИСП.
CONT CSM,MNSA=IRA,ALUS=DQ,ALUD=Q,FFCNT=CLRTR0
LDCT А=JNZ,МАР=РЕ,FFCNT=SETTR1
;ПЕРЕХОДИМ ПО ПНА ГРУПП
CJP А=М48,МАР=GRP,DSRC=PROM,ALUS=DZ,Н,ALUD=В,RВ=WR1,FFCNT=CLRJMP
END
UIAPNA:PROG;
EXTERNAL JNNZ
;   СИГНАЛЫ, УПРАВЛЯЮЩИЕ ВЫБОРКОЙ ИЗ ПНА ГРУПП:
;   СЛ1, СЛ0, ГС, ГУ, ГЛ
;    1    0    Х   Х   Х  -  ДЛЯ КОМАНДЫ  U I А
;   ЕСЛИ ОДНОВРЕМЕННО УСТАНОВЛЕНЫ НЕСКОЛЬКО ПРИЗНАКОВ ГРУПП, ТО ПРИОРИТЕТ
;   ИМЕЕТ ГРУППА СЛОЖЕНИЯ (ГС); ЕСЛИ ЕЕ НЕТ - ГРУППА УМНОЖЕНИЯ (ГУ)
;
;===== ГРУППА СЛОЖЕНИЯ (ГС) ДЛЯ КОМАНДЫ UIA - ПЕРЕНЕСЕНА В ВКС
;   ВОЗМОЖНЫЕ КОМБИНАЦИИ ВХОДНЫХ СИГНАЛОВ: 14,15,16,17
;В ВКС: PROM:1814-1817
;
;===== ВСЕ ПРИЗНАКИ ГРУПП = 0 ДЛЯ КОМАНДЫ UIA - ПЕРЕНЕСЕНА В ВКС - П/У ПО АИСП.
;В ВКС: PROM:1810
;
UIAMUL:ENTRY
;===== ГРУППА УМНОЖЕНИЯ (ГУ) ДЛЯ КОМАНДЫ UIA
;   ВОЗМОЖНЫЕ КОМБИНАЦИИ ВХОДНЫХ СИГНАЛОВ:
;   1 0 0 1 0  =  12 Н
;   1 0 0 1 1  =  13 Н
;PROM:1812,1813
;ПРИ ГУ ПРОИСХОДИТ ПЕРЕДАЧА УПРАВЛЕНИЯ ПО АИСП,
;ЕСЛИ ПОРЯДОК ЧИСЛА НА СУММАТОРЕ МЕНЬШЕ 0 (64 РАЗРЯД = 0)
JRP А=В64,DSRC=PROM,ALUS=DА,RА=А,FUNC=AND,Н,CEN,IСС,FFCNT=CLRTR1
;
UIALOG:ENTRY
;===== ЛОГИЧЕСКАЯ ГРУППА (ГЛ) ДЛЯ КОМАНДЫ UIA
;   ВОЗМОЖНЫЕ КОМБИНАЦИИ ВХОДНЫХ СИГНАЛОВ:
;   1 0 0 0 1  =  11 Н
;PROM:1811
;ПРИ ЛОГИЧЕСКОЙ ГРУППЕ ПРОИСХОДИТ ПЕРЕДАЧА УПРАВЛЕНИЯ ПО АИСП,
;ЕСЛИ В РГ.А НЕНУЛЕВОЙ(!) КОД (В НР ПРОВЕРЯЮТСЯ 64 РАЗРЯДА, В РЭ - 48: 64,59-13)
CJP А=JNNZ,МАР=РЕ,COND=EMLRG,IСС,ALUS=ZА,RА=А,Н,CEN,FFCNT=CLRTR1
CJP А=JNNZ,МАР=РЕ,ALUS=АВ,RА=А,RВ=WR1,FUNC=AND,Н,CEN
END
JАМР:PROG;
;     I=========I
;     I  J М Р  I
;     I=========I
;
;РЭ:0-1,15ИР:0-1,ПИА:0,КОП:18 В НР; С0 В РЭ
;PROM:018,218,4С0,6С0
;===== БЕЗУСЛОВНЫЙ ПЕРЕХОД НА ЛК ПО ИСПОЛНИТЕЛЬНОМУ АДРЕСУ
;
EXTERN LCWJMP
;ВЫЧИСЛЯЕМ АИСП.- В Q, ПЕРЕХОД НА ВКС ПО АИСП.
CJP А=LCWJMP,МАР=РЕ,CSM,MNSA=IRA,ALUS=DQ,ALUD=Q
END
VJM:PROG;
;     I=========I
;     I  V J М  I
;     I=========I
;
;РЭ:0-1,15ИР:0-1,ПИА:0,КОП:19,С8
;PROM:019,219,4С8,6С8
;===== В ИНДЕКС-РЕГИСТР,УКАЗАННЫЙ В ПОЛЕ МОДИФИКАТОРА, ЗАНОСИТСЯ АДРЕС ВОЗВРАТА
;   (РС+1), ЗАТЕМ ПРОИЗВОДИТСЯ ПЕРЕДАЧА УПРАВЛЕНИЯ ПО АДРЕСУ КОМАНДЫ
;
EXTERN LCWJMP
;ЗАПОМИНАЕМ АДРЕС ВОЗВРАТА, П/У НА ВКС ПЕРЕХОДА
CJP А=LCWJMP,МАР=РЕ,ALUS=ZА,RА=РС,CI,ALU,CSM,WЕМ,MNSA=IRA
END
JMEQ:PROG;
;     I===========I
;     I  J М Е Q  I
;     I===========I
;
;РЭ:0,15ИР:0-1,ПИА:0,КОП:20
;PROM:020,220
;===== УСЛОВНЫЙ ПЕРЕХОД ПО НУЛЮ МОДИФИКАТОРА
;
EXTERNAL JNZ
;ПРОВЕРЯЕМ МОДИФИКАТОР НА "0", ПЕРЕХОД НА АНАЛИЗ
CJP А=JNZ,МАР=РЕ,CSM,MNSA=IRA,ALUS=DZ,CEN,FFCNT=CLRJMP
END
JMEQE:PROG;
;РЭ:1,15ИР:0-1,ПИА:0,КОП:Е0
;PROM:4Е0,6Е0
;===== УСЛОВНЫЙ ПЕРЕХОД ПО НУЛЮ МОДИФИКАТОРА В РЭ, ПРОВЕРЯЕМ МЛ.15 РАЗРЯДОВ
;
EXTERNAL JNZ
CONT А=М15,DSRC=PROM,ALUS=DZ,Н,ALUD=В,RВ=WR1
CJP А=JNZ,МАР=РЕ,CSM,MNSA=IRA,ALUS=DА,RА=WR1,FUNC=AND,CEN,FFCNT=CLRJMP
END
JMNE:PROG;
;      I===========I
;      I  J М N Е  I
;      I===========I
;
;РЭ:0,15ИР:0-1,ПИА:0,КОП:21
;PROM:021,221
;===== УСЛОВНЫЙ ПЕРЕХОД ПО НЕ НУЛЮ МОДИФИКАТОРА
;
EXTERNAL JNNZ
;ПРОВЕРЯЕМ МОДИФИКАТОР НА "0", ПЕРЕХОД НА П/П АНАЛИЗА NZ
CJP А=JNNZ,МАР=РЕ,CSM,MNSA=IRA,ALUS=DZ,CEN,FFCNT=CLRJMP
END
JMNEE:PROG;
;РЭ:1,15ИР:0-1,ПИА:0,КОП:Е8
;PROM:4Е8,6Е8
;===== УСЛОВНЫЙ ПЕРЕХОД ПО НЕ НУЛЮ МОДИФИКАТОРА В РЭ, ПРОВЕРЯЕМ 15 МЛ.РАЗРЯДОВ
;
EXTERNAL JNNZ
CONT А=М15,DSRC=PROM,ALUS=DZ,Н,ALUD=В,RВ=WR1
CJP А=JNNZ,МАР=РЕ,CSM,MNSA=IRA,ALUS=DА,RА=WR1,FUNC=AND,CEN,FFCNT=CLRJMP
END
VLM:PROG;
;     I=========I
;     I  V L М  I
;     I=========I
;
;РЭ:0,15ИР:0-1,ПИА:0,КОП:1В
;PROM:01В,21В
;===== КОНЕЦ ЦИКЛА С УВЕЛИЧЕНИЕМ ЗНАЧЕНИЯ МОДИФИКАТОРА:
; - ЕСЛИ УКАЗАННЫЙ В ПОЛЕ МОДИФИКАТОРА ИНДЕКС-РЕГИСТР НЕ РАВЕН 0,
;   ПРОИЗВОДИТСЯ П/У ПО АК, А ИР = ИР + 1;
; - ЕСЛИ ИР = 0, ВЫПОЛНЯЕТСЯ СЛЕДУЮЩАЯ КОМАНДА.
;
EXTERN LCWJMP,LCWISE,JIQ
;ПРОВЕРЯЕМ МОДИФИКАТОР НА "0"
LDCT А=LCWISE,МАР=РЕ,CSM,MNSA=IRA,ALUS=DZ,ALUD=В,RВ=WR1,CEN
PSVLM:ENTRY
;ПЕРЕХОД, ЕСЛИ МОДИФИКАТОР НЕ РАВЕН 0
CJP А=VLMJMP,МАР=РЕ,COND=СТ,SТОРС=/NZ,ALUS=ZQ,ALUD=В,RВ=DADR
;НЕТ ПЕРЕХОДА, ИДЕМ ПО ПОРЯДКУ
JRP А=JIQ,МАР=РЕ,COND=RСВ,IСС,FFCNT=СНТКК
;
;МОДИФИКАТОР НЕ РАВЕН 0: УВЕЛИЧЕНИЕ МОДИФИКАТОРА, П/У ПО АК
VLMJMP:CJP А=LCWJMP,МАР=РЕ,ALUS=ZА,RА=WR1,CI,ALU,CSM,WЕМ,MNSA=IRA
END
VLME:PROG;
;РЭ:1,15ИР:0-1,ПИА:0,КОП:F8
;PROM:4F8,6F8
;===== КОНЕЦ ЦИКЛА С УВЕЛИЧЕНИЕМ ЗНАЧЕНИЯ МОДИФИКАТОРА В РЭ, ПРОВЕРЯЕМ 15 РАЗРЯД
;
EXTERNAL LCWISE,PSVLM
;ПРОВЕРЯЕМ 15 РАЗРЯДОВ МОДИФИКАТОРА НА "0"
LDCT А=LCWISE,МАР=РЕ,CSM,MNSA=IRA,ALUS=DZ,ALUD=В,RВ=WR1
CJP А=PSVLM,МАР=РЕ,ALUS=DZ,Н,ALUD=ВА,RА=WR1,RВ=WR6,CEN,ALU,DSRC=SHIFT
>,MPS,PSHF=SН0-49,SHF=LOG
END
XTR:PROG;
;      =============================
;      6.КОМАНДЫ С РЕГИСТРОМ РЕЖИМОВ
;      =============================
;
;XTR,NTR,RТЕ
;
;     I=========I
;     I  Х Т R  I
;     I=========I
;
;РЭ:0-1,15ИР:0,ПИА:0,КОП:DВ,17
;PROM:0DB,417
;===== УСТАНОВКА 1-6 РАЗРЯДОВ РР КОДОМ ЧИСЛА ПО ИСП.АДРЕСУ, НЕ 15ИР;
;   ВЫПОЛНЯЕТСЯ НЕЗАВИСИМО ОТ РЕЖИМА.
;
EXTERN LCWISE,LCW,XTR,JIQ
;М.А. - В АИСП, ИАОП, ПЕРЕХОД НА П/П ДЛЯ XTR
CJS А=SXTR,МАР=РЕ,%МА%
;ОБ'ЕДИНЯЕМ РР С АИСП.,ПИШЕМ НА СВОИ МЕСТА
JRP А=JIQ,МАР=РЕ,COND=ТККВ,ALUS=АВ,RА=WR1,RВ=WR2,FUNC=OR,ALU,YDST=CNT,CSM,WЕМ
>,MNSA=МР,MODNM=RR
;
;ФИЗ.АДРЕС - В RG0, ЧИТАЕМ ПОСТОЯННУЮ ЧАСТЬ РР ИЗ ОЗУ МОД.
SXTR:LDCT А=LCWISE,МАР=РЕ,%PHAR%,CSM,MNSA=МР,MODNM=RR,ALUS=DZ,ALUD=В,RВ=WR1
>,FFCNT=СНТКК
;УБИРАЕМ ЛИШНЕЕ: ПИА=ППК=ППУ=МЛ.6 РАЗРЯДАМ=0, Т.К.В МЛ.6 СЕЙЧАС ЗАПИШЕМ,
;ПИА,ППУ=0 ПОСЛЕ ЭТОЙ КОМАНДЫ, А В ППК СКОПИРУЕТСЯ ИЗ ТКК ПО ПМИ! ТГ.НЕ ЧИТАЕМ!!
CONT А=MXTR,DSRC=PROM,ALUS=DА,RА=WR1,FUNC=AND,ALUD=В,RВ=WR1
;ЧИТАЕМ МАСКУ НА 6 МЛ. РАЗРЯДОВ, МЕНЯЕМ ТКК, Т.К. В НЕГО НЕ ПИШЕТСЯ
CONT А=М6В,DSRC=PROM,ALUS=DZ,ALUD=В,RВ=WR3
;ЖДЕМ ГОТ.А.
WА:CJP А=WА,%WА%,%RСА%
;ЧИТАЕМ ИЗ БОИ, СРАЗУ СДВИГАЕМ В МЛ.РАЗРЯДЫ, УБИРАЕМ МУСОР
CRTN ЕСВ,BRA=RG2,DSRC=SHIFT,MPS,PSHF=SН0+53
>,SHF=LOG,ALUS=DА,RА=WR3,FUNC=AND,ALUD=В,RВ=WR2
;
;
XTRF:ENTRY
;РЭ:0-1,15ИР:1,ПИА:0,КОП:80,17
;PROM:2DB,617
;===== МАГАЗИННОЕ ОБРАЩЕНИЕ К ПАМЯТИ
;
;ПЕРЕХОД, ЕСЛИ АК НЕ 0
CONT ALUS=ZQ,CEN
CJP А=XTR,МАР=РЕ,COND=СТ,SТОРС=/NZ
;МАГАЗИННОЕ ЧТЕНИЕ: 15ИР-1 - В АИСП, В ИАОП, П/У НА ОБЩУЮ П/П
CJS А=SXTR,МАР=РЕ,%ARF%
;ОБ'ЕДИНЯЕМ РР С АИСП.,ПИШЕМ НА СВОИ МЕСТА
LDCT А=LCW,МАР=РЕ,ALUS=АВ,RА=WR1,RВ=WR2,FUNC=OR,ALU,YDST=CNT,CSM,WЕМ
>,MNSA=МР,MODNM=RR
;ПИШЕМ SР НА МЕСТО
JRP %MED%,ALUS=ZА,RА=DADR,ALU,CSM,WЕМ,MNSA=МР,MODNM=SР
END
NTR:PROG;
;     I=========I
;     I  N Т R  I
;     I=========I
;
;РЭ:0-1,15ИР:0-1,ПИА:0,КОП:82,ДОП.ФОРМАТ;1F
;PROM:882,А82,41F,61F
;===== УСТАНОВКА 1-6 РАЗРЯДОВ РР АДРЕСОМ
;
EXTERN LCWISE,JIQ
;ЧИТАЕМ ПОСТОЯННУЮ ЧАСТЬ РР ИЗ ОЗУ МОД.
LDCT А=LCWISE,МАР=РЕ,CSM,MNSA=МР,MODNM=RR,ALUS=DZ,ALUD=В,RВ=WR1
;УБИРАЕМ "УСТАРЕВШИЕ" РАЗРЯДЫ: ППУ,ПИА=0, В ППК СКОПИРУЕТСЯ ИЗ ТКК! ТГ.НЕ ЧИТАЕМ
CONT А=MXTR,DSRC=PROM,ALUS=DА,RА=WR1,FUNC=AND,ALUD=В,RВ=WR1
;У АИСП.ОСТАВЛЯЕМ ТОЛЬКО 6 МЛ.РАЗРЯДОВ
CONT CSM,MNSA=IRA,ALUS=DQ,ALUD=В,RВ=DADR
CONT А=М6В,DSRC=PROM,ALUS=DА,RА=DADR,FUNC=AND,ALUD=Q,FFCNT=СНТКК
;ОБ'ЕДИНЯЕМ С АИСП., ПИШЕМ НА МЕСТО
JRP А=JIQ,МАР=РЕ,COND=ТККВ,ALUS=AQ,RА=WR1,FUNC=OR,ALU,YDST=CNT,CSM,WЕМ
>,MNSA=МР,MODNM=RR
END
RТЕ:PROG;
;     I=========I
;     I  R Т Е  I
;     I=========I
;
;РЭ:0-1,15ИР:0-1,ПИА:0,КОП-83,ДОП.ФОРМАТ;18
;PROM:883,А83,418,618
;===== ВЫДАЧА РР
;
EXTERN LCW,JQ
CONT CSM,MNSA=IRA,ALUS=DQ,FUNC=ADD,ALUD=В,RВ=DADR
CONT А=М6В,DSRC=PROM,ALUS=DА,RА=DADR,FUNC=AND,ALUD=Q
;ЧИТАЕМ ТГ.ПРИЗНАКОВ
CONT DSRC=CNT,ALUS=DZ,ALUD=В,RВ=WR1
;УБИРАЕМ МУСОР ИЗ "ПЕРЕМЕННОЙ" ЧАСТИ РР: ОСТАВЛЯЕМ ТОЛЬКО ПР.ГРУПП
CONT А=Н1С,DSRC=PROM,ALUS=DА,RА=WR1,FUNC=AND,ALUD=В,RВ=WR1
;ЧИТАЕМ ПОСТОЯННУЮ ЧАСТЬ РР ИЗ ОЗУ МОД, ОСТАВЛЯЕМ ТОЛЬКО БЛОКИРОВКИ Б-6
CONT CSM,MNSA=МР,MODNM=RR,ALUS=DZ,ALUD=В,RВ=WR2
CONT А=Н23,DSRC=PROM,ALUS=DА,RА=WR2,FUNC=AND,ALUD=В,RВ=WR2
;ОБ'ЕДИНЯЕМ ИХ
LDCT А=LCW,МАР=РЕ,ALUS=АВ,RА=WR1,RВ=WR2,FUNC=OR,ALUD=В,FFCNT=CLRJMP
;ЛОГИЧЕСКИ УМНОЖАЕМ НА АИСП
CJS А=PSEM,МАР=РЕ,COND=EMLRG,ALUS=AQ,RА=WR2,FUNC=AND,ALUD=В,RВ=WR2,FFCNT=СНТКК
;СДВИГАЕМ, ПИШЕМ В 54-59 РАЗРЯДЫ СУММАТОРА
JRP А=JQ,МАР=РЕ,COND=ТККВ,ALUS=DZ,Н,ALUD=ВА,RА=WR2,RВ=А,СЕМ
>,ALU,DSRC=SHIFT,MPS,PSHF=SН0-53,SHF=LOG,DDEV=CLRCD,WRD,ISE
;
;В РЭ ДОБАВЛЯЕМ "ДЫРУ" ДЛЯ ПОРЯДКА
PSEM:CRTN А=MIND,DSRC=PROM,ALUS=DА,RА=WR2,FUNC=OR,Н,ALUD=В,RВ=WR2
END
RMOD:PROG;
;     ============================
;     17.ПРИВИЛЕГИРОВАННЫЕ КОМАНДЫ
;     ============================
;
;RMOD,WMOD,SETR,CLRR,SEMR,SEMW,SMON,CMON,ТТХ,ТТА,HLT
;IJP - ВМЕСТЕ С ПРЕРЫВАНИЯМИ, RЕТ - С ЭКСТРАКОДАМИ
;
;     I===========I
;     I  R М О D  I
;     I===========I
;
;РЭ:0,15ИР:0-1,ПИА:0,КОП-F0
;PROM:0F0,2F0
;===== ЧТЕНИЕ РЕГИСТРОВ И ПАМЯТИ В СУММАТОР; ТОЛЬКО НР!
;   СЛ.1 = 0    СЛ.0 = 0
;
EXTERNAL EXTDR,LCW
;ВЫЧИСЛЯЕМ АИСП., П/У НА ЭКСТРАКОД, ЕСЛИ НЕ РД
CJP А=EXTDR,МАР=РЕ,COND=DRG,IСС,CSM,MNSA=IRA,ALUS=DQ,FUNC=ADD,ALUD=В,RВ=WR1
>,ALU,YDST=ADRREG,FFCNT=CLRTR1
LDCT А=LCW,МАР=РЕ,FFCNT=CLRTR0,ALUS=ZА,RА=WR1,ALUD=В,RВ=DADR
CONT FFCNT=СНТКК
JМАР %RСА%,FFCNT=CLRJMP
END
WMOD:PROG;
;     I===========I
;     I  W М О D  I
;     I===========I
;
;РЭ:0,15ИР:0-1,ПИА:0,КОП-F1
;PROM:0F1,2F1
;===== ЗАПИСЬ СУММАТОРА В РЕГИСТРЫ И ПАМЯТЬ; ТОЛЬКО НР!
;   СЛ.1 = 0   СЛ.0 = 1
;
EXTERNAL EXTDR,LCW
;ВЫЧИСЛЯЕМ АИСП., П/У НА ЭКСТРАКОД, ЕСЛИ НЕ РД
CJP А=EXTDR,МАР=РЕ,COND=DRG,IСС,CSM,MNSA=IRA,ALUS=DQ,FUNC=ADD,ALUD=В
>,RВ=WR1,ALU,YDST=ADRREG,FFCNT=CLRTR1
LDCT А=LCW,МАР=РЕ,FFCNT=SETTR0,ALUS=ZА,RА=WR1,ALUD=В,RВ=DADR
CONT FFCNT=СНТКК
JМАР %RСА%,FFCNT=CLRJMP
END
SETR:PROG;
;     I===========I
;     I  S Е Т R  I
;     I===========I
;
;РЭ:0,15ИР:0-1,ПИА:0,КОП-F2
;PROM:0F2,2F2
;===== УСТАНОВКА РР АДРЕСОМ; ТОЛЬКО НР
;   ИСПОЛНИТЕЛЬНЫЙ АДРЕС СДВИГАЕТСЯ ВЛЕВО НА 10 РАЗРЯДОВ!
;
EXTERNAL EXTDR,LCWISE,JIQ
;ВЫЧИСЛЯЕМ АИСП., ИДЕМ "СОБИРАТЬ" РР И СДВИГАТЬ АИСП.
CJS А=CHRR,МАР=РЕ,CSM,MNSA=IRA,ALUS=DQ,FUNC=ADD,ALUD=В,RВ=DADR
;УСТАНАВЛИВАЕМ В "1" РАЗРЯДЫ РР, СООТВ.АИСП., ПИШЕМ НА МЕСТО
JRP А=JIQ,МАР=РЕ,COND=ТККВ,ALUS=АВ,RА=WR1,RВ=WR2,FUNC=OR,ALU,YDST=CNT,CSM,WЕМ
>,MNSA=МР,MODNM=RR
;
;ПРОЧИЩАЕМ ТГ.ПРИЗНАКОВ, УБИРАЕМ "УСТАРЕВШИЕ" РАЗРЯДЫ РР
CHRR:ENTRY
;СДВИГ АИСП.НА 10Р.ВЛЕВО, "СОБИРАЕМ" РР
CJPP А=EXTDR,МАР=РЕ,COND=DRG,IСС,ALUS=DZ,Н,ALUD=ВА,RА=DADR,RВ=WR1,ALU
>,DSRC=SHIFT,MPS,PSHF=SН0-10,SHF=LOG
CONT CSM,MNSA=МР,MODNM=RR,ALUS=DZ,ALUD=В,RВ=WR2
CONT А=MCRR,DSRC=PROM,ALUS=DА,RА=WR2,FUNC=AND,ALUD=В,RВ=WR2
LDCT А=LCWISE,МАР=РЕ,DSRC=CNT,ALUS=DZ,ALUD=В,RВ=WR3
CONT А=MFLFL,DSRC=PROM,ALUS=DА,RА=WR3,FUNC=AND,ALUD=В,RВ=WR3
;ОБ'ЕДИНЯЕМ РР И ТГ.ПРИЗНАКОВ (БЕЗ РГ.М, ОН - В МЛ.10 РАЗРЯДАХ,КОТ.НЕ ТРОГАЕМ!)
CRTN ALUS=АВ,RА=WR3,RВ=WR2,FUNC=OR,ALUD=В,FFCNT=СНТКК
END
CLRR:PROG;
;     I===========I
;     I  С L R R  I
;     I===========I
;
;РЭ:0,15ИР:0-1,ПИА:0,КОП-F3
;PROM:0F3,2F3
;===== ГАШЕНИЕ РР АДРЕСОМ; ТОЛЬКО НР
;   ИСПОЛНИТЕЛЬНЫЙ АДРЕС СДВИГАЕТСЯ ВЛЕВО НА 10 РАЗРЯДОВ!
;
EXTERNAL CHRR,JIQ
;ВЫЧИСЛЯЕМ АИСП., ИДЕМ "СОБИРАТЬ" РР И СДВИГАТЬ АИСП.
CJS А=CHRR,МАР=РЕ,CSM,MNSA=IRA,ALUS=DQ,FUNC=ADD,ALUD=В,RВ=DADR
PSCLRR:ENTRY
;ДОБАВЛЯЕМ В АИСП. МЛ.10 РАЗЯДОВ, = 1
CONT А=MWN,DSRC=PROM,ALUS=DА,RА=WR1,FUNC=OR,ALUD=В,RВ=WR1
;ОБНУЛЯЕМ РАЗРЯДЫ, СООТВЕТСТВУЮЩИЕ "0" ИСП.АДРЕСА; ПИШЕМ РР НА МЕСТО
JRP А=JIQ,МАР=РЕ,COND=ТККВ,ALUS=АВ,RА=WR1,RВ=WR2,FUNC=AND,ALU,YDST=CNT,CSM,WЕМ
>,MNSA=МР,MODNM=RR
END
SEMR:PROG;
;     I===========I
;     I  S Е М R  I
;     I===========I
;
;РЭ:0,15ИР:0-1,ПИА:0,КОП-F4
;PROM:0F4,2F4
;===== СЕМАФОРНОЕ ЧТЕНИЕ
; - ЕСЛИ СЕМАФОР НЕ ЗАНЯТ, ПРОИЗВОДИТСЯ "ЗАХВАТ" СЕМАФОРА:
;   В ПАМЯТЬ ПИШЕМ НОМЕР ПРОЦЕССА В 57-64 РАЗР. + 56 РАЗРЯД, РАВНЫЙ "1",
;   ПРИЗНАК С В РГ.М СУСС УСТАНАВЛИВАЕТСЯ В "1" (УДАЧНАЯ ПОПЫТКА);
; - ЕСЛИ СЕМАФОР ЗАНЯТ "СОБОЙ" (57-64Р.= РНП), С ТОЖЕ УСТАНАВЛИВАЕТСЯ В 1.
; - ЕСЛИ ЗАНЯТ ЧУЖИМ ПРОЦЕССОМ, ПРИЗНАК С УСТАНАВЛИВАЕТСЯ В "0".
;   МАГАЗИННОГО ИСПОЛНЕНИЯ КОМАНДЫ НЕТ! РР НЕ МЕНЯЕМ. ВОЗМОЖНЫ ПРЕРЫВАНИЯ.
;
EXTERN EXTDR,LCWISE,JIQ,LCW
;М.А. - В АИСП, ИАОП
CJP А=EXTDR,МАР=РЕ,COND=DRG,IСС,%МА%,SТОРС=MCN,CEN
;ФИЗ.АДРЕС - В RG0, N ПРОЦЕССА - В WR2
PUSH А=JIQ,МАР=РЕ,DSRC=PROCN,ALUS=DА,RА=ВУТЕ,FUNC=AND,Н,ALUD=В,RВ=WR2,ЕСВ,WRB
>,YDEV=PHYSAD,BRA=RG0,ARBI=RDMWR,FFCNT=CLRJMP,SТОРС=CLRNC,CEN
;УСТАНАВЛИВАЕМ МС В "0" НА СЛУЧАЙ, ЕСЛИ СЕМАФОР ЗАНЯТ
LООР А=В56,COND=ARBRDY,DSRC=PROM,ALUS=DZ,Н,ALUD=В,RВ=WR1,SТОРС=MCN,СЕМ
;ЧТО ПРОЧИТАЛИ?
PUSH А=LCWISE,%R%,ЕСА,ARA=RG2,ALUS=DА,RА=WR1,FUNC=AND,Н,CEN,FFCNT=СНТКК
;БЫЛ ЛИ ЗАНЯТ СЕМАФОР? ЕСЛИ НЕТ - ИДЕМ НА ЗАХВАТ; РНП - В СТАРШИЕ РАЗРЯДЫ
CJPP А=ZАХ,МАР=РЕ,COND=СТ,SТОРС=NZ,ALUS=DZ,Н,ALUD=ВА,RА=WR2,RВ=WR2,ALU
>,DSRC=SHIFT,MPS,PSHF=SН0+8,SHF=CICL
;ЗАНЯТ, НО КЕМ?
LООР ЕСА,ARA=RG2,ALUS=DА,RА=WR2,FUNC=EXOR,Н,ALUD=В,RВ=WR3
CONT ALUS=DZ,Н,ALUD=ВА,RА=WR3,RВ=WR3,ALU,DSRC=SHIFT,MPS,PSHF=SН0+56
>,SHF=LOG,CEN
;ВЫХОД, ЕСЛИ N ПРОЦЕССА НЕ СОВПАЛ
JRP А=MIP,МАР=РЕ,COND=СТ,SТОРС=NZ
;N ПРОЦЕССА ТОТ ЖЕ! С = 1, ВЫХОД
MIP:CONT SТОРС=32,DDEV=STATUS,ALUS=DZ,ALUD=В,RВ=WR5
JRP А=В8,IСС,DSRC=PROM,ALUS=DА,RА=WR5,FUNC=OR,ALU,DDEV=STATUS,WRD,SТОРС=0
>,СЕМ
;СВОБОДЕН: ЗАХВАТЫВАЕМ; МС УСТАНАВЛИВАЕМ В "1"; ПРОЧИЩАЕМ СТЕК
ZАХ:PUSH А=LCW,МАР=РЕ,%PHAW%,SТОРС=MCN,CEN
LООР COND=ARBRDY,ALUS=АВ,RА=WR1,RВ=WR2,FUNC=OR,Н,ALU,ЕСВ,WRB,BRA=RG3
>,SТОРС=SETNC,CEN
;ПЕРЕХОД НА СЛЕДУЮЩУЮ КОМАНДУ, ПМИ
JRP %М%,SТОРС=MCN,СЕМ,%RСА%
END
SEMW:PROG;
;     I===========I
;     I  S Е М W  I
;     I===========I
;
;РЭ:0,15ИР:0-1,ПИА:0,КОП-F7
;PROM:0F7,2F7
;===== СЕМАФОРНАЯ ЗАПИСЬ
; - ЕСЛИ СЕМАФОР ЗАНЯТ ТЕК.ПРОЦЕССОМ (57-64Р.= РНП), ОН ОБНУЛЯЕТСЯ!
;   МАГАЗИННОГО ИСПОЛНЕНИЯ КОМАНДЫ НЕТ! РР НЕ МЕНЯЕМ. ВОЗМОЖНЫ ПРЕРЫВАНИЯ.
;
EXTERN EXTDR,LCWISE,JIQ,LCW
;М.А. - В АИСП, ИАОП
CJP А=EXTDR,МАР=РЕ,COND=DRG,IСС,%МА%
;ФИЗ.АДРЕС - В RG0, N ПРОЦЕССА - В WR2
PUSH А=JIQ,МАР=РЕ,DSRC=PROCN,ALUS=DА,RА=ВУТЕ,FUNC=AND,Н,ALUD=В,RВ=WR2
>,YDEV=PHYSAD,ЕСВ,WRB,BRA=RG0,ARBI=DRD,FFCNT=CLRJMP
;РНП - В СТ.РАЗРЯДЫ
LООР COND=ARBRDY,ALUS=DZ,Н,ALUD=ВА,RА=WR2,RВ=WR3,ALU,DSRC=SHIFT,MPS,PSHF=SН0+8
>,SHF=CICL
;СРАВНИВАЕМ N ПРОЦЕССА
PUSH А=LCWISE,%R%,ЕСА,ARA=RG2,ALUS=DА,RА=WR3,FUNC=EXOR,Н,ALUD=В,RВ=WR3
>,FFCNT=СНТКК
LООР ALUS=DZ,Н,ALUD=ВА,RА=WR3,RВ=WR3,ALU,DSRC=SHIFT,MPS,PSHF=SН0+56
>,SHF=LOG,CEN
;ВЫХОД, ЕСЛИ N ПРОЦЕССА НЕ СОВПАЛ
JRP А=MIP,МАР=РЕ,COND=СТ,SТОРС=NZ
;N ПРОЦЕССА СВОЙ! ОБНУЛЯЕМ СЕМАФОР
MIP:PUSH А=LCW,МАР=РЕ,%PHAW%,%RСА%
LООР COND=ARBRDY,ALUS=DZ,FUNC=AND,Н,ALU,ЕСВ,WRB,BRA=RG3
;ПЕРЕХОД НА СЛЕДУЮЩУЮ КОМАНДУ, ПМИ
JRP %М%
END
SMON:PROG;
;     I===========I
;     I  S М О N  I
;     I===========I
;
;РЭ:0,15ИР:0-1,ПИА:0,КОП-F8
;PROM:0F8,2F8
;===== ЗАХВАТ ГЛОБАЛЬНОГО СЕМАФОРА
; _ ЗАПОМИНАЕМ РР, УСТАНАВЛИВАЕМ БЛОКИРОВКИ, ЧИТАЕМ СЕМАФОР ПО МАТ.АДР.80840;
; - ЕСЛИ СЕМАФОР НЕ ЗАНЯТ, ПРОИЗВОДИТСЯ "ЗАХВАТ" СЕМАФОРА:
;   В ПАМЯТЬ ПИШЕМ НОМЕР ПРОЦЕССА В 57-64 РАЗР. + 56 РАЗРЯД, РАВНЫЙ "1",
;   ИСПОЛНЯЕТСЯ КОМАНДА SETR С АИСП. ДЛЯ СТАРОГО РР + БВП + П/У НА СЛЕД.КОМАНДУ;
; - ЕСЛИ СЕМАФОР ЗАНЯТ "СОБОЙ" (57-64Р.= РНП), ТОЖЕ SETR + БВП И П/У ДАЛЬШЕ;
; - ЕСЛИ СЕМАФОР ЗАНЯТ ЧУЖИМ ПРОЦЕССОМ, ЖДЕМ ОСВОБОЖДЕНИЯ СЕМАФОРА!
;   МАГАЗИННОГО ИСПОЛНЕНИЯ КОМАНДЫ НЕТ. ПРЕРЫВАНИЙ НЕ Д.БЫТЬ.
;
EXTERN CHRR,LCWISE
;ЗАПОМИНАЕМ СТАРЫЙ РР - В WR2, СДВИНУТЫЙ АИСП.- В WR1, МЕНЯЕМ ТКК
CJS А=CHRR,МАР=РЕ,CSM,MNSA=IRA,ALUS=DQ,FUNC=ADD,ALUD=В,RВ=DADR
CONT А=CMONO,DSRC=PROM,ALUS=DА,RА=WR2,FUNC=OR,ALU,YDST=CNT,CSM,WЕМ,MNSA=МР
>,MODNM=RR
CONT А=ASEM,DSRC=PROM,ALUS=DZ,ALUD=В,RВ=DADR,ALU,YDST=ADRREG
;ФИЗ.АДРЕС - В RG0, N ПРОЦЕССА - В WR4
РНА:PUSH DSRC=PROCN,ALUS=DА,RА=ВУТЕ,FUNC=AND,Н,ALUD=В,RВ=WR4
>,YDEV=PHYSAD,ЕСВ,WRB,BRA=RG0,ARBI=RDMWR
LООР А=В56,COND=ARBRDY,DSRC=PROM,ALUS=DZ,Н,ALUD=В,RВ=WR3
;ЧТО ПРОЧИТАЛИ?
CONT ЕСА,ARA=RG2,ALUS=DА,RА=WR3,FUNC=AND,Н,CEN
;БЫЛ ЛИ ЗАНЯТ СЕМАФОР? ЕСЛИ НЕТ - ИДЕМ НА ЗАХВАТ; РНП - В СТАРШИЕ РАЗРЯДЫ
CJP А=ZАХ,МАР=РЕ,COND=СТ,SТОРС=NZ,ALUS=DZ,Н,ALUD=ВА,RА=WR4,RВ=WR4,ALU
>,DSRC=SHIFT,MPS,PSHF=SН0+8,SHF=CICL
;ЗАНЯТ, НО КЕМ?
LDCT А=РНА,МАР=РЕ,ЕСА,ARA=RG2,ALUS=DА,RА=WR4,FUNC=EXOR,Н,ALUD=В,RВ=WR5
CONT ALUS=DZ,Н,ALUD=ВА,RА=WR5,RВ=WR5,ALU,DSRC=SHIFT,MPS,PSHF=SН0+56
>,SHF=LOG,CEN
;ЕСЛИ N ПРОЦЕССА НЕ СОВПАЛ - ПОВТОРЯЕМ; ЕСЛИ ТОТ ЖЕ - ИДЕМ НА ЗАПИСЬ В РР
JRP А=WRR,МАР=РЕ,COND=СТ,SТОРС=NZ
;СВОБОДЕН: ЗАХВАТЫВАЕМ
ZАХ:CONT %PHAW%
WAW:CJP А=WAW,%WА%,ALUS=АВ,RА=WR3,RВ=WR4,FUNC=OR,Н,ALU,ЕСВ,WRB,BRA=RG3
;ДАБАВЛЯЕМ К РР БВП(!) И АИСП.,СДВИНУТЫЙ ВЛЕВО НА 10 РАЗРЯДОВ
WRR:CONT А=В18,DSRC=PROM,ALUS=DА,RА=WR1,FUNC=OR,ALUD=В,RВ=WR1
CJP А=LCWISE,МАР=РЕ,COND=ТККВ,IСС,ALUS=АВ,RА=WR1,RВ=WR2,FUNC=OR,ALU
>,YDST=CNT,CSM,WЕМ,MNSA=МР,MODNM=RR
CJP %ML%,FFCNT=CLRJMP,%RСА%
END
CMON:PROG;
;     I===========I
;     I  С М О N  I
;     I===========I
;
;РЭ:0,15ИР:0-1,ПИА:0,КОП-F9
;PROM:0F9,2F9
;===== ОСВОБОЖДЕНИЕ "ГЛОБАЛЬНОГО" СЕМАФОРА
; - НА ВРЕМЯ ИСПОЛНЕНИЯ КОМАНДЫ УСТАНАВЛИВАЮТСЯ РАЗНЫЕ БЛОКИРОВКИ;
; - ЕСЛИ СЕМАФОР (М.АДР.80840) ЗАНЯТ ТЕК.ПРОЦЕССОМ (57-64Р.= РНП), ОН ОБНУЛЯЕТСЯ
; - ПРОИЗВОДИТСЯ ЧТЕНИЕ ВНЕШНИХ ПРЕРЫВАНИЙ В ГРП И ИХ СБРОС И
;   "ВЗВЕДЕНИЕ" ЗАНОВО ТАЙМЕРА БВП!
; - ЗАТЕМ ИСПОЛНЯЕТСЯ КОМАНДА CLRR С ИСПОЛНИТЕЛЬНЫМ АДРЕСОМ И СТАРЫМ РР.
;   МАГАЗИННОГО ИСПОЛНЕНИЯ КОМАНДЫ НЕТ!
;
EXTERN CHRR,LCW,JQ
;ЗАПОМИНАЕМ СТАРЫЙ РР - В WR2; СДВИНУТЫЙ АИСП. - В WR1
CJS А=CHRR,МАР=РЕ,CSM,MNSA=IRA,ALUS=DQ,FUNC=ADD,ALUD=В,RВ=DADR
CONT А=CMONO,DSRC=PROM,ALUS=DА,RА=WR2,FUNC=OR,ALU,YDST=CNT,CSM,WЕМ,MNSA=МР
>,MODNM=RR
CONT А=ASEM,DSRC=PROM,ALUS=DZ,ALUD=В,RВ=DADR,ALU,YDST=ADRREG
;ФИЗ.АДРЕС - В RG0, N ПРОЦЕССА - В WR4
РНА:PUSH DSRC=PROCN,ALUS=DА,RА=ВУТЕ,FUNC=AND,Н,ALUD=В,RВ=WR4
>,YDEV=PHYSAD,ЕСВ,WRB,BRA=RG0,ARBI=DRD
;РНП - В СТАРШИЕ РАЗРЯДЫ
LООР COND=ARBRDY,ALUS=DZ,Н,ALUD=ВА,RА=WR4,RВ=WR5,ALU,DSRC=SHIFT,MPS
>,PSHF=SН0+8,SHF=CICL
;СОВПАДАЮТ ЛИ НОМЕРА ПРОЦЕССА?
CONT ЕСА,ARA=RG2,ALUS=DА,RА=WR5,FUNC=EXOR,Н,ALUD=В,RВ=WR5
CONT ALUS=DZ,Н,ALUD=ВА,RА=WR5,RВ=WR5,ALU,DSRC=SHIFT
>,MPS,PSHF=SН0+56,SHF=LOG,CEN
;ЕСЛИ N ПРОЦЕССА СОВПАЛ - ОБНУЛЯЕМ СЕМАФОР; ЕСЛИ НЕТ - ИДЕМ НА ЗАПИСЬ В РР
CJP А=WRR,МАР=РЕ,COND=СТ,SТОРС=/NZ
;НАШ: ОБНУЛЯЕМ
CONT %PHAW%
WAW:CJP А=WAW,%WА%,ALUS=DZ,FUNC=AND,Н,ALU,ЕСВ,WRB,BRA=RG3
;ЧИТАЕМ ВНЕШНИЕ ПРЕРЫВАНИЯ С ШИНЫ
WRR:CONT А=В18,YDEV=PHYSAD,ARBI=BIRD,DSRC=PROM,ALUS=DА,RА=WR2,FUNC=NOTRS
>,ALUD=В,RВ=WR3
;В РР ПИШЕМ СТАРОЕ ЗНАЧЕНИЕ, НО БЕЗ БВП! ("ВЗВОДИМ" ТАЙМЕР ЗАНОВО!)
WАЕХ:CJP А=WАЕХ,%WА%,ALUS=ZА,RА=WR3,ALU,YDST=CNT,CSM,WЕМ,MNSA=МР,MODNM=RR
;СБРАСЫВАЕМ ПРЕРЫВАНИЯ НА ШИНЕ, МАСКИРУЕМ РАЗРЯДЫ, КОТОРЫЕ ЗАДЕЙСТВОВАНЫ (40,41)
LDCT А=LCW,МАР=РЕ,ЕСА,ARA=RG2,ALUS=DZ,Н,ALUD=В,RВ=WR5,ALU,ЕСВ,WRB,BRA=RG3
PUSH А=PROGR,IСС,YDEV=PHYSAD,ARBI=BICLR,DSRC=PROM,ALUS=DА,RА=WR5,FUNC=NOTRS,Н
>,ALUD=В,RВ=WR5
;ДОБАВЛЯЕМ В АИСП. МЛ.10 РАЗЯДОВ, = 1
LООР А=MWN,COND=ARBRDY,DSRC=PROM,ALUS=DА,RА=WR1,FUNC=OR,ALUD=В,RВ=WR1
;ОБНУЛЯЕМ РАЗРЯДЫ, СООТВЕТСТВУЮЩИЕ "0" ИСП.АДРЕСА; ПИШЕМ РР НА МЕСТО
CONT ALUS=АВ,RА=WR1,RВ=WR2,FUNC=AND,ALU,YDST=CNT,CSM,WЕМ,MNSA=МР,MODNM=RR
JRP А=JQ,МАР=РЕ,COND=ТККВ,DDEV=CLRCD,WRD,ISE
>,ALUS=АВ,RА=WR5,RВ=INTR,FUNC=OR,Н,ALUD=В,FFCNT=CLRJMP
END
ТТХ:PROG;
;     I=========I
;     I  Т Т Х  I
;     I=========I
;
;РЭ:0,15ИР:0,ПИА:0,КОП-F5
;PROM:0F5
;===== ЗАПИСЬ СУММАТОРА С ЗАДАННЫМ ТЕГОМ В ПАМЯТЬ ПО ИСПОЛНИТЕЛЬНОМУ АДРЕСУ
;   ТЕГ ВОССТАНАВЛИВАЕМ ПО СОДЕРЖИМОМУ РГ.РРР! В БОИ ТЕГА - ТОЛЬКО КОПИЯ.
;
EXTERN LCW,EXTDR
;СУММАТОР - В РГ.РЕЗУЛЬТАТА
CJP А=EXTDR,МАР=РЕ,COND=DRG,IСС
>,ALUS=ZА,RА=А,Н,ALU,ЕСВ,WRB,BRA=RG3
CONT %МА%
;ФИЗ.АДРЕС - В RG0
LDCT А=LCW,МАР=РЕ,%PHAW%,FFCNT=СНТКК
;ПИШЕМ ТЕГ ИЗ РГ.А В БОИ
CONT ALUS=ZВ,RВ=WR6,ALUD=ВА,RА=У,ALU,YDEVT=ECBTAG,WRB,BRA=RG3
;ЖДЕМ ГОТ. АРБИТРА; ЧИТАЕМ АК В Q
WW:CJP А=WW,%WА%,%RСА%
;ВОССТАНАВЛИВАЕМ ТЕГ
JRP %М%,CSM,MNSA=МР,MODNM=RRR,ALUS=DZ,ALU,YDEVT=ECBTAG
>,WRB,BRA=RG3,FFCNT=CLRJMP
;
;
TTXF:ENTRY
;РЭ:0,15ИР:1,ПИА:0,КОП-F5
;PROM:2F5
;
;СУММАТОР - В БОИ; М.А. - В АИСП., Т.К.ОН ОДИНАКОВЫЙ!
CJP А=EXTDR,МАР=РЕ,COND=DRG,IСС,ALUS=ZА,RА=А,Н,ALU,ЕСВ,WRB,BRA=RG3
CONT %МА%
;ФИЗ.АДРЕС - В RG0
CONT %PHAW%,ALUS=ZQ,CEN,FFCNT=СНТКК
;ТЕГ ИЗ РГ.А - В БОИ; ПЕРЕХОД, ЕСЛИ АК НЕ 0
CJP А=WW,МАР=РЕ,COND=СТ,SТОРС=/NZ,ALUS=ZВ,RВ=WR6,ALUD=ВА,RА=У,ALU
>,YDEVT=ECBTAG,WRB,BRA=RG3
;ЖДЕМ ГОТ. АРБИТРА; ЧИТАЕМ АК В Q
WWF:CJP А=WWF,%WА%,%RСА%
;ВОССТАНАВЛИВАЕМ ТЕГ
LDCT А=LCW,МАР=РЕ,CSM,MNSA=МР,MODNM=RRR,ALUS=DZ,ALU,YDEVT=ECBTAG,WRB,BRA=RG3
;15+1 - В 15 РЕГИСТР, СБРОС ПИА, ПМИ, ТКК КОПИРУЕТСЯ В ППК
JRP %М%,ALUS=ZА,RА=DADR,FUNC=ADD,CI,ALU
>,CSM,WЕМ,MNSA=МР,MODNM=SР,FFCNT=CLRJMP
END
ТТА:PROG;
;     I=========I
;     I  Т Т А  I
;     I=========I
;
;РЭ:0,15ИР:0,ПИА:0,КОП-F6
;PROM:0F6
;===== ЧТЕНИЕ ЧИСЛА С ТЕГОМ ПО ИСПОЛНИТЕЛЬНОМУ АДРЕСУ В СУММАТОР
;
EXTERN LCW,EXTDR,ТТА
;М.А. - В АИСП, ИАОП
CJP А=EXTDR,МАР=РЕ,COND=DRG,IСС,%МА%
;ФИЗ.АДРЕС - В RG0, ИЗМЕНЯЕМ ТКК
CONT %PHAR%,FFCNT=СНТКК
;ЖДЕМ ГОТ. АРБИТРА, ЧИТАЕМ АК В Q
WА:CJP А=WА,%WА%,%RСА%
;ЧИТАЕМ ТЕГ
LDCT А=LCW,МАР=РЕ,YDEVT=ECBTAG,BRA=RG2,DSRC=SHIFT,MPS,PSHF=SН0,SHF=LOG
>,ALUS=DА,RА=ВУТЕ,FUNC=AND,Н,ALUD=В,RВ=У,FFCNT=CLRJMP
;ПЕРЕХОД ПО КОП, ПМИ, ЧИТАЕМ В СУММАТОР
JRP %М%,ЕСА,ARA=RG2,ALUS=DZ,Н,ALUD=В,RВ=А,СЕМ,FFCNT=LOGGRP
;
;
TTAF:ENTRY
;РЭ:0,15ИР:1,ПИА:0,КОП-F6
;PROM:2F6
;===== ЧТЕНИЕ ЧИСЛА С ТЕГОМ ИЗ МАГАЗИНА: 15-1 -> 15; ЧТЕНИЕ ПО 15ИР.
;
;ПЕРЕХОД, ЕЛИ АК НЕ 0
CJP А=EXTDR,МАР=РЕ,COND=DRG,IСС,ALUS=ZQ,CEN
CJP А=ТТА,МАР=РЕ,COND=СТ,SТОРС=/NZ,%ARF%
;ЧТЕНИЕ ИЗ МАГАЗИНА
CONT %PHAR%,FFCNT=СНТКК
;ЖДЕМ ГОТ. АРБИТРА, ЧИТАЕМ АК В Q
WALF:CJP А=WALF,%WА%,%RСА%
;ЧИТАЕМ ТЕГ
LDCT А=LCW,МАР=РЕ,YDEVT=ECBTAG,BRA=RG2,DSRC=SHIFT,MPS,PSHF=SН0,SHF=LOG
>,ALUS=DА,RА=ВУТЕ,FUNC=AND,Н,ALUD=В,RВ=У,FFCNT=CLRJMP
;НОВЫЙ SР НА МЕСТО, ЧИТАЕМ В СУММАТОР, СБРОС ПИА, ПМИ
JRP %М%,ЕСА,ARA=RG2,ALUS=DZ,Н,ALUD=ВА,RА=DADR,RВ=А,СЕМ,FFCNT=LOGGRP
>,ALU,CSM,WЕМ,MNSA=МР,MODNM=SР
END
HLT:PROG;
;     I=========I
;     I  Н L Т  I
;     I=========I
;
;РЭ:0,15ИР:0-1,ПИА:0,КОП-FF,ДОП.ФОРМАТ
;PROM:8FF,AFF
;===== ОСТАНОВ: 12Р.АИСП.НЕ 0 - ОСТАНОВ + УСТАНОВКА РАЗРЯДА ПП: 2Р.2 РГ.БОПП = 1
;   ЕСЛИ 12 МЛ.РАЗРЯДОВ АИСП.= 0 - ПУСТАЯ КОМАНДА! НЕТ ОСТАНОВА!
;   ЗДЕСЬ ОСТАНОВ "ЛОГИЧЕСКИЙ", А НЕ ЧЕРЕЗ ПРЕРЫВАНИЕ!
;   ТКК ИЗМЕНЯЕМ ДО ПЕРЕДАЧИ СИГНАЛОВ ПП, Т.К.ВО ВРЕМЯ ОСТАНОВА М.Б.ЗАПИСЬ
;   В РГ/ТГ И СООТВЕТСТВЕННО В ТКК!
;   СБРАСЫВАЕМ ТГ. "RUN", УСТАНАВЛИВАЕМ HLT, ПИШЕМ ПРИЗНАКИ КОМАНДЫ "ОСТАНОВ"
;   ВО 2 РГ.БОПП И ЖДЕМ УСТАНОВКИ ТГ."ПУСК" БЕЗ ISE!
;   ЗАТЕМ ИДЕМ НА СЛЕДУЮЩУЮ КОМАНДУ ИЛИ ВКС ПО ОБЩЕМУ ПРАВИЛУ.
;
EXTERN EXTDR,LCWPH
;ЕСЛИ НЕ РД - ЭКСТРАКОД
CJP А=EXTDR,МАР=РЕ,COND=DRG,IСС,FFCNT=CLRJMP
>,CSM,MNSA=IRA,ALUS=DQ,FUNC=ADD,ALUD=В,RВ=DADR
CONT А=МРР,DSRC=PROM,ALUS=DА,RА=DADR,FUNC=AND,ALUD=В,RВ=DADR,CEN
>,FFCNT=СНТКК,DDEV=CLRCD,WRD
CJP А=NOHLT,МАР=РЕ,COND=СТ,SТОРС=NZ
>,YDEV=МРМЕМ,IОМР,MPADR=FСР,%TRSH%,ALUS=DZ,ALUD=В,RВ=WR5
CONT А=В2,DSRC=PROM,ALUS=DА,RА=WR5,FUNC=OR,ALU,YDEV=МРМЕМ,WRY,IОМР
>,MPADR=FСР
FATHLT:ENTRY
CONT FFCNT=CLRRUN
CONT FFCNT=SETHLT
;ЖДЕМ ТГ."RUN"
WRUN:CJP А=WRUN,МАР=РЕ,COND=RUN,IСС
;ПЕРЕХОДИМ НА СЛЕДУЮЩУЮ КОМАНДУ
NOHLT:CJP %МЕ%,COND=ТККВ,ISE,%RСА%
CJP А=LCWPH,МАР=РЕ,ALUS=ZВ,RВ=РС,CI,ALUD=В,ALU,YDST=ADRREG,FFCNT=CLRTR1
END
*END
*END F
*END FILE
